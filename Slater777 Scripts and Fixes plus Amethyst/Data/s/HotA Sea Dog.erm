ZVSE2


; Sea Dogs ability - Accurate Shot
; This ability doesnot work if shooting with obstacle penalty
; This chance is 2% or 3% per Sea Dog in the stack (if shooting with/without range penalty), and is not cumulative
; Accurate shot cannot kill more enemies than are equal to 1% of the number of Sea Dogs in a stack (rounded up). For example:
;    1-50 Sea Dogs may kill 0-1 enemy units with accurate shot
;    51-100 Sea Dogs may kill 0-2 enemy units with accurate shot
;    101-150 Sea Dogs may kill 0-3 enemy units with accurate shot


!?FU(js_BattleStack_DeadShot);
!#VA(shooter:x) (target:x);

!!UN:C(shooter)/56/4/?(shooterHex:y); 0x38
!!UN:C(target)/56/4/?(targetHex:y); 0x38
;!IF:L^shooterHex= %(shooterHex), targetHex= %(targetHex)^;

!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(cmbMgr:y);

    ; if ( BattleMgr_Stack_Has_ShotObstacklePenalty(o_BattleMgr, st_A, hex_ix, st_D->hex_ix) )
    ;   v16 = v16 * 0.5;
!!SN:E4616432/(CALLCONV_THISCALL)/(cmbMgr)/(shooter)/(shooterHex)/(targetHex); 004670F0 BattleMgr_Stack_Has_ShotObstacklePenalty
!!VR(isShotObstacklePenalty:y):Sv1;
;!IF:L^obs= %(isShotObstacklePenalty)^;
!!FU&(isShotObstacklePenalty):E;

    ; if ( BattleMgr_Stack_HasDistancePenalty(o_BattleMgr, st_A, st_D) )
    ;   v16 = v16 * 0.5;
!!SN:E4616672/(CALLCONV_THISCALL)/(cmbMgr)/(shooter)/(target); 004671E0 BattleMgr_Stack_HasDistancePenalty
!!VRv1:&1; Check first byte
!!VR(hasDistancePenalty:y):Sv1;
;!IF:L^dist= %(hasDistancePenalty)^;


!!VR(chance:y):S3;

!!if&(hasDistancePenalty);
  !!VR(chance:y):S2;
!!en;
;!IF:L^chance= %(chance)^;


!!UN:C(shooter)/76/4/?(shooterQty:y); 0x4C
!!VR(maximumKills:y):S(shooterQty) :50;

!!VR(div:y):S(shooterQty) %50;
;!IF:L^div= %(div)^;

!!VR(maximumKills)&(div)>0: +1;
;!IF:L^maximumKills= %(maximumKills)^;


!!VR(kills:y):S0;

!!re i/0/(shooterQty);
  !!br&(kills)>=(maximumKills);

  !!VRr:S0 R100;
  !!VR(kills)&r<=(chance): +1;
!!en;
;!IF:L^kills= %(kills)^;


!!if&(kills);
  !!FU(js_GetStackNumberByStackPtr):P(target)/?(stackNum:y);
  
  !!BM(stackNum):N?(monQty:y);
  !!VR(kills)&(monQty)<(kills):S(monQty);
  
  !!if&i^battle_isVisible^;
    !!SN:T^jsAmetistMod.creature.210.skillLogMsg^/?(logMsg:z)/^number^/(kills);
    !!MM:S(logMsg);
    !!SN:P^DEADSHOT^;
    !!FU(ES_PlayCustomAnimationOnStack):P(stackNum)/^DEADSHOT.def^/1;
  !!en;

  !!BM(stackNum):Nd-(kills);

  !!if&i^battle_isVisible^;
    ; Play WNCE animation
    !!BM(stackNum):K0; костыль, но работает
    !!FU(js_DrawActionPlay):P;
  !!en;
!!en;

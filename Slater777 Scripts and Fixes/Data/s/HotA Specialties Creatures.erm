ZVSE2

; Усилен эффект специализаций по существам, дающий +5% к атаке и защите за n уровней героя, 
; где n — уровень существа: сделано:
; +10% для существ 1 уровня; 
; +30% для Баллисты и 
; +20% для остальных существ

; Логика выше перенесена в HotA_CreatureSpecBonus.era (double нельзя кастить во float в erm)

; Специализация по Водным Элементалям: +2 к атаке -> +12 к атаке
; Специализация по Огненным Элементалям: +1 к атаке, +2 к защите и +2 к урону -> +3 к атаке, +4 к защите и +2 к урону
; Специализация по Земляным Элементалям: +2 к атаке, +1 к защите, +5 к урону -> +3 к атаке, +2 к защите и +5 к урону
; Специализация по Психическим Элементалям: +3 к атаке и +3 к защите -> +5 к атаке и +5 к защите
; Специализация по Чёрным Рыцарям для Хаарта Лича: +5 к атаке, +5 к защите, +10 к урону -> +10 к атаке, +10 к защите и +10 к урону
; Специализация по Дьяволам: +4 к атаке, +2 к защите, +1 к скорости -> +10 к атаке, +10 к защите и +1 к скорости
; Специализация по Чудищам: +5 к атаке, +5 к защите, +10 к урону -> +10 к атаке, +10 к защите и +10 к урону
; Специализация по Драконам: +5 к атаке и +5 к защите -> +10 к атаке и +10 к защите


// Incompatible options
!?FU(js_CheckSpecialtiesCreaturesOptions);
; x1 - result
!!UN:P39/?y1; Enhanced Specialties

!!VRx1:S(FALSE);

!!if&y1; /y2/y3/y4;
  !!VRx1:S(TRUE);
!!en;


!?FU(OnAfterErmInstructions);
!!FU(js_CheckSpecialtiesCreaturesOptions):P?(activeSettings:y);
!!FU&(activeSettings):E;

!!VRi^js_hota_specialties_creatures^:S(TRUE);

!!re i/0/(HERO_LAST_WOG);
  !!HEi:X?(specType:y)/?(specSubtype:y)/d/d/d/d/d;

  !!if&(specType)=1;
    !!if&(specSubtype)=(MON_BALLISTA);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^30^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el;
      !!MA:L(specSubtype)/?(monLvl:y);
      
      !!if&(monLvl)=0;
        !!SN:H^spec^/i/2/?(specDesc:z);
        !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^1^/^10^;
        !!SN:H^spec^/i/2/(newSpecDesc);
      !!el;
        !!SN:H^spec^/i/2/?(specDesc:z);
        !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^1^/^20^;
        !!SN:H^spec^/i/2/(newSpecDesc);
      !!en;
    !!en;
    
  !!el&(specType)=4;
    !!if&(specSubtype)=(MON_WATER_ELEMENTAL);
      !!HEi:Xd/d/12/d/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^12^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_FIRE_ELEMENTAL);
      !!HEi:Xd/d/3/4/2;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^3^/^4^/^2^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_EARTH_ELEMENTAL);
      !!HEi:Xd/d/3/2/5;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^3^/^2^/^5^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_PSYCHIC_ELEMENTAL);
      !!HEi:Xd/d/5/5/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^5^/^5^;
      !!SN:H^spec^/i/2/(newSpecDesc);

    !!el&(specSubtype)=(MON_BLACK_KNIGHT);
      !!HEi:Xd/d/10/10/10;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_DEVIL);
      !!HEi:Xd/d/10/10/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_BEHEMOTH);
      !!HEi:Xd/d/10/10/10;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!en;

  !!el&(specType)=7;
      !!HEi:Xd/d/10/10/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
  !!en;
!!en;

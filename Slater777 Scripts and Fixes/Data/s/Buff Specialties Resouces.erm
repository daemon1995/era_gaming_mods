ZVSE2


; Специализации по ресурсу усилена - за каждые 4 уровня герой приносит дополнительную единицу ресурса (или 350 золота)


!?FU(OnAfterErmInstructions);

!!re i/0/(HERO_LAST_WOG);
  !!HEi:X?(specType:y)/?(specSubtype:y)/d/d/d/d/d;

  !!if&(specType)=2/(specSubtype)>=(RES_WOOD)/(specSubtype)<=(RES_GOLD);
    !!SN:H^spec^/i/2/?(specDesc:z);
    !!VR(specDesc): +^%T(jsMod.secSkill.resource.desc.spec)^;
    !!SN:H^spec^/i/2/(specDesc);
  !!en;
!!en;


!?FU(OnEveryDay)&i^timerOnce^;
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:O?(owner:y);

  !!if&(owner)>(NO_OWNER);
    !!HEi:X?(specType:y)/?(specSubtype:y);

    !!if&(specType)=2/(specSubtype)>=(RES_WOOD)/(specSubtype)<=(RES_GOLD);
      !!HEi:Ed/?(heroLvl:y);
      !!VR(resQty:y):S(heroLvl) :4;

      !!if&(resQty)>0;
        !!if&(specSubtype)=(RES_GOLD);
          !!VR(resQty): *350;
        !!en;

        !!OW:R(owner)/(specSubtype)/d(resQty);

      !!en;
    !!en;
  !!en;
!!en;

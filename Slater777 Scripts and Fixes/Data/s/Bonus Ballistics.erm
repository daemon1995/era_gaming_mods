ZVSE2


; Позволяет участвовать катапульте в обычных битвах (кусок из опции 55 - ус.боев.маш II)
; Баллиста атакует огненным шаром и критует


// Incompatible options
!?FU(js_CheckCatapultOptions);
; x1 - result
!!UN:P(WOG_OPT_ENHANCED_WAR_MACHINES_I)/?y1; ^wog_54_enabled^
!!UN:P(WOG_OPT_ENHANCED_WAR_MACHINES_II)/?y2; ^wog_55_enabled^
!!UN:P(WOG_OPT_ENHANCED_WAR_MACHINES_III)/?y3; ^wog_73_enabled^
!!UN:P193/?y4; warfare

!!VRx1:S(FALSE);

!!if|y1/y2/y3/y4;
  !!VRx1:S(TRUE);
!!en;


!?FU(OnAfterErmInstructions);
!!FU(js_CheckCatapultOptions):P?(activeSettings:y);
!!FU&(activeSettings):E;

!!VRi^js_ballistics_bonus^:S(TRUE);

!!VR(addDesc:z):S^%T(jsMod.secSkill.ballistics.desc.addDesc)^;

!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_BASIC)/?(descBallistics:z);
!!VR(descBallistics): +(addDesc);
!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_BASIC)/(descBallistics);

!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_ADVANCED)/?(descBallistics:z);
!!VR(descBallistics): +(addDesc);
!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_ADVANCED)/(descBallistics);

!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_EXPERT)/?(descBallistics:z);
!!VR(descBallistics): +(addDesc);
!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_EXPERT)/(descBallistics);



************************************************************************************************
!!SN:H^monname^/(MON_CATAPULT)/0/?(single:z) H^monname^/(MON_CATAPULT)/1/?(plural:z);
!!SN:H^monname^/(MON_NOT_USED_2)/0/(single) H^monname^/(MON_NOT_USED_2)/1/(plural);

; Set up flags for Catapult in non-siege
!!MA:X(MON_NOT_USED_2)/132197 F(MON_NOT_USED_2)/650 ; [set NU2 flags {catapult flags (132197) + shooter (4)}    NU2 Fight Value 650  ]
!!MA:A(MON_NOT_USED_2)/10 D(MON_NOT_USED_2)/10 P(MON_NOT_USED_2)/250 N(MON_NOT_USED_2)/24; [NU2 Attack 10 NU2 Defense 10 NU2 HP 250  NU2 Shots 24]

; creature descriptions
!!SN:H^monname^/(MON_CATAPULT)/2/?(desc:z);
!!VR(desc): +^%T(jsMod.creature.145.addDesc)^;
!!SN:H^monname^/(MON_CATAPULT)/2/(desc);

; Stack exp abilities
!!UN:P(WOG_OPT_STACK_EXPERIENCE)/?(stackExp:y); [check if creature experience is enabled]

!!if&(stackExp);
  !!EA(MON_NOT_USED_2):O(MON_CATAPULT);

  *!EA(MON_NOT_USED_2):F105/?(availSlot:y); [NU2 (catapult) - get bonus line index]
  *!EA(MON_NOT_USED_2):B(availSlot)/1/105/61/1/1/1/1/1/1/1/1/1/1/1; [set no range penalty]
  !!EA(MON_NOT_USED_2):F98/?(availSlot:y); [NU2 (catapult) - get bonus line index]
  !!EA(MON_NOT_USED_2):B(availSlot)/1/98/61/50/50/50/50/50/50/50/50/50/50/50; [set ignore defense to 50%]
!!en;


!?FU(js_OnBeforeMagogShoot);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(esi:y) C(esi)/52/4/?(monNum:y); 0x34
!!if&(monNum)=(MON_NOT_USED_2);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/(monNum);
  !!SN:X?y99/0;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4454197; 0043F735
!!en;


; Set up Catapult in non-siege and non-CB battles
!?FU(OnSetupBattlefield)&i^js_ballistics_bonus^;
; Initialise catapult variables
!!re i/(BATTLE_LEFT)/(BATTLE_RIGHT);
  !!VRi^wog_55_catapult_%i^:S(NO_STACK);
!!en;

; Restore the memory value for Arrow Tower Shoot Adjacent
!!UN:C4466213/(UNC_INT)/(MON_ARROW_TOWERS);
!!UN:C4479001/(UNC_INT)/(MON_ARROW_TOWERS);

!!FU(js_PlaceCatapult)&i^wog_isSiege^<>(TRUE)/i^wog_isCBBattle^<>(TRUE):P(BATTLE_LEFT) P(BATTLE_RIGHT);


!?FU(js_PlaceCatapult);
!#VA(side:x);
  
!!if&i^battle_hero_%(side)^>(NO_HERO);
  !!HEi^battle_hero_%(side)^:S(SKILL_BALLISTICS)/?(ballisticsLv:y);

  !!if&(ballisticsLv)>(SKILL_NOT_LEARNED);
    !!if&(side)=(BATTLE_LEFT);
      !!VR(catPos:y):S119;
    !!el;
      !!VR(catPos):S135;
    !!en;

    !!BU:S(MON_CATAPULT)/1/(catPos)/(side)/-1/(FALSE); [add catapults]
    !!BU:E(catPos)/?(catStack:y);

    !!if&(catStack)>(NO_STACK);
      !!BM(catStack):T?(type:y);

      !!if&(type)=(MON_CATAPULT);
        ; Store stack number to be used later after Tactic Phase
        !!VRi^wog_55_catapult_%(side)^:S(catStack);
        ; Set up basic stats except for monster type as setting here would conflict with HD fast troop placements
        !!HEi^battle_hero_%(side)^:F?(heroAtk:y)/d/d/d;

        !!BM(catStack):A?(catAtk:y);
        !!VR(atkDelta:y):S(catAtk) -(heroAtk) -10; //Вычитаем геройскую атаку и базовую (чтобы синхронизировать бонус с колчаном и тележкой)

        !!VR(bonusAtk:y):S(heroAtk) +1;
        ;[2–3 * (атака + 1)] на [2–3 * (атака + 5)]
        !!VR(bonusAtk:y)&i^js_hota_artillery^:S(heroAtk) +5;
        !!VR(bonusAtk):+(atkDelta);

        !!VR(minDamage:y):S2 *(bonusAtk);
        !!VR(maxDamage:y):S3 *(bonusAtk);

        !!BM(catStack):U1/(minDamage) U2/(maxDamage) H250 B1;

        ; Change the memory value of Arrow Tower to Catapult in order to have it shoot adjacent
        !!UN:C4466213/(UNC_INT)/(MON_NOT_USED_2);
        !!UN:C4479001/(UNC_INT)/(MON_NOT_USED_2);
      !!en;
    !!en;
  !!en;
!!en;


; Change catapult ID to 124 - Must be after Tactics Phase due to HD mod compatibility
!?FU(OnAfterTacticsPhase)&i^js_ballistics_bonus^;
!!re i/(BATTLE_LEFT)/(BATTLE_RIGHT);
  !!BMi^wog_55_catapult_%i^&i^wog_55_catapult_%i^>(NO_STACK):T(MON_NOT_USED_2);
!!en;

!!if&i^wog_isSiege^<>(TRUE)/i^wog_isCBBattle^<>(TRUE);
  !!MA:X(MON_CATAPULT)/?f;
  !!VR(aoeShooter:y):Sf &(MON_FLAG_SPLASH_SHOOTER);
  !!MA&(aoeShooter)=0:X(MON_CATAPULT)/d|(MON_FLAG_SPLASH_SHOOTER);
!!en;


!?FU(OnAfterBattleUniversal)&i^js_ballistics_bonus^;
; Restore the memory value for Arrow Tower Shoot Adjacent
!!UN:C4466213/(UNC_INT)/(MON_ARROW_TOWERS);
!!UN:C4479001/(UNC_INT)/(MON_ARROW_TOWERS);

!!MA:X(MON_CATAPULT)/?f;
!!VR(aoeShooter:y):Sf &(MON_FLAG_SPLASH_SHOOTER);
!!MA&(aoeShooter):X(MON_CATAPULT)/d~(MON_FLAG_SPLASH_SHOOTER);


!?FU(OnGameLeave)&i^js_ballistics_bonus^;
!!UN:C4466213/(UNC_INT)/(MON_ARROW_TOWERS);
!!UN:C4479001/(UNC_INT)/(MON_ARROW_TOWERS);

!!MA:X(MON_CATAPULT)/?f;
!!VR(aoeShooter:y):Sf &(MON_FLAG_SPLASH_SHOOTER);
!!MA&(aoeShooter):X(MON_CATAPULT)/d~(MON_FLAG_SPLASH_SHOOTER);


; Critical hit for Catapult in non-siege and non-CB battles
** start of pre-damage trigger
!?FU(OnStackToStackDamage)&i^js_ballistics_bonus^/i^wog_isSiege^<>(TRUE)/x7/x9=0; [x7 = shot, x9 = 0.. only for real damage]
!#VA(attacker:x) (defender:x);

!!BM(attacker):T?(monType:y);

!!if&(monType)=(MON_NOT_USED_2);
  !!BM(defender):N?(defNum:y);

  ;!if&(defNum)>1;                    [Works only when the defending number is greater than 1]
    !!VR(random:y):R0/0/99;           [random roll - 1% chance per level]

    !!BM(defender):Z?(defStack:y);
    !!UN:C(defStack)/120/4/?(monLvl:y); 0x78
    !!FU&(monLvl)<0:E;
    !!VR(chance:y):S6 -(monLvl);
    !!HEi^battle_hero_%i(battle_acting_side)^:S(SKILL_BALLISTICS)/?(skillLvl:y);
    !!VR(chance):+(skillLvl);
    !!VR(chance):*10 F0/100;

    !!if&(random)<=(chance);
      !!BM(defender):Nd-1;            [show animation, set target quantity and reduce target quantity by 1]

      !!if&i^battle_isVisible^;
        !!VR(logMsg:z):Sz155007;
        !!MM:S(logMsg);
        !!BM(defender):V82;
      !!en;
    !!en;
  ;!en;
!!en;


// Катапульта тут не детектится! - и правильно, потому что в функции shoot 3 кейса (магог, лич и все остальные)
// Хук стоит в кейсе "все остальные"

*?FU(js_BattleStack_ShotAbilities)&i^js_ballistics_bonus^/i^wog_isSiege^<>(TRUE);
*!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(shooter:y) C(shooter)/52/4/?(shooterNum:y); 34;
*!IF:L^shooterNum= %(shooterNum)^;
*!FU&(shooterNum)<>(MON_NOT_USED_2):E;

*!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(target:y) C(target)/80/4/?(monQty:y); 50
*!IF:L^monQty= %(monQty)^;
*!FU&(monQty)<=0:E;

*!UN:C(target)/120/4/?(targetLvl:y); 0x78

*!VR(random:y):R0/0/99;
*!VR(chance:y):S6 -(targetLvl);
*!HEi^battle_hero_%i(battle_acting_side)^:S(SKILL_BALLISTICS)/?(skillLvl:y);
*!VR(chance):+(skillLvl);
*!VR(chance):*10 F0/100;

*!if&(random)<=(chance);
  *!FU(js_GetActualStackSideByStackPtr):P(target)/?(side:y);
  *!UN:C(target)/248/4/?(stackNum:y); 0xF8
  *!VR(stackNum)&(side): +(BATTLE_STACKS_PER_SIDE);

  *!BM(stackNum):Nd-1;

  *!if&i^battle_isVisible^;
    *!VR(logMsg:z):Sz155007;
    *!MM:S(logMsg);
    *!BM(stackNum):V82;
  *!en;
*!en;
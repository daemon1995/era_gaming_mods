ZVSE2


; Вся логика вынесена в HotA_SpecBonus.era - тут только замена описания специализаций

; Усилены специализации, дающие +3% к эффекту заклинания за n уровней героя, 
; где n — уровень цели: сделано +5% для Метеоритного Дождя, Цепной Молнии, Воскрешения и Поднятия Мертвецов и +10% для всех остальных заклинаний

// Специализация по Забывчивости теперь усиливает уменьшение атакующих существ в целевых 
отрядах на 10% за n уровней героя, где n — уровень цели (нет героя в игре с такой спецой)

; Специализация по Лечению даёт +10% к эффекту за (8 – n) уровней героя, где n — уровень цели (действует сильнее на сильных существ)
; Специализации по Жажде Крови, Каменной Коже и Точности добавляют +10/8/6/4 к эффекту заклинания для существ 1-2/3-4/5-6/7 уровня
; Специализация по Слабости теперь добавляет +4/6/8/10 к эффекту заклинания для существ 1-2/3-4/5-6/7 уровня (действует сильнее на сильных существ)
; Специализации по Ускорению и Молитве добавляют +3/2/1 к эффекту заклинания для существ 1-4/5-6/7 уровня
; Специализация по Палачу добавляет +20/16/12/8 к эффекту заклинания для существ 1-2/3-4/5-6/7 уровня
; Специализация по Разрушающему Лучу увеличивает эффект заклинания на 10

//TODO:
// Специализация по магии Огня Адриэн увеличивает урон заклинаний Огня на 50% (надо хуковать в месте сферы огня)
; Специализация по Огненной Стене Луны снижена с +100% до +25%


*#VRi^js_hota_specialties_spells^:S(TRUE);


// Incompatible options
!?FU(js_CheckSpecialtiesSpellsOptions);
; x1 - result
!!UN:P39/?y1; Enhanced Specialties

!!VRx1:S(FALSE);

!!if&y1; /y2/y3/y4;
  !!VRx1:S(TRUE);
!!en;


!?FU(OnAfterErmInstructions);
;!IF:M^%i(js_hota_specialties_spells)^;

!!FU(js_CheckSpecialtiesSpellsOptions):P?(activeSettings:y);
!!FU&(activeSettings):E;

*!VRi^js_hota_specialties_spells^:S(TRUE);


!!re i/0/(HERO_LAST_WOG);
  !!HEi:X?(specType:y)/?(specSubtype:y)/d/d/d/d/d;

  !!if&(specType)=3;
    !!if|(specSubtype)=(SPELL_METEOR_SHOWER)/(specSubtype)=(SPELL_CHAIN_LIGHTNING)/(specSubtype)=(SPELL_RESURRECTION)/(specSubtype)=(SPELL_ANIMATE_DEAD);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!VR(specDesc): +^%T(jsMod.spell.23193839.desc.spec)^;
      !!SN:H^spec^/i/2/(specDesc);

    !!el&(specSubtype)=(SPELL_CURE);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.37.desc.spec)^;
    !!el&(specSubtype)=(SPELL_BLOODLUST);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.43.desc.spec)^;
    !!el&(specSubtype)=(SPELL_STONE_SKIN);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.46.desc.spec)^;
    !!el&(specSubtype)=(SPELL_PRECISION);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.44.desc.spec)^;

    !!el&(specSubtype)=(SPELL_WEAKNESS);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.45.desc.spec)^;
    !!el&(specSubtype)=(SPELL_HASTE);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.53.desc.spec)^;
    !!el&(specSubtype)=(SPELL_PRAYER);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.48.desc.spec)^;
    !!el&(specSubtype)=(SPELL_SLAYER);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.55.desc.spec)^;
    !!el&(specSubtype)=(SPELL_DISRUPTING_RAY);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^10^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);

    !!el&(specSubtype)=(SPELL_FIRE_WALL);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^25^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el|(specSubtype)=(SPELL_MAGIC_ARROW)/(specSubtype)=(SPELL_FORTUNE);
      ; do nothing
    !!el;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!VR(specDesc): +^%T(jsMod.spell.default.desc.spec)^;
      !!SN:H^spec^/i/2/(specDesc);
    !!en;
  !!en;
!!en;

ZVSE2

* Big fix: spell dmg of magic arrow, ice bolt and lightning strike was not dependent
* from orbs and sorcery secondary skill


!?FU(OnMagicBasicResistance);                   // Перед расчетом сопротивления
!!BG:A?y1 N?y2;                                 // y1 - тип действия, y2 - номер активного стека
*!IF:M^Тип действия: %Y1^;
!!FU&y1<>10:E;                                  // Выход, если не каст существом
!!BMy2:T?y3;                                    // y3 - тип монстра
*!IF:M^Тип монстра: %Y3^;
!!FU&y3<>134/y3<>258:E;                         // Выход, если не каст Сказочными или Первобытными Сказочными Драконами
!!BG:Q?y1;                                      // y1 - текущая сторона
!!BA:Hy1/?y2;                                   // y2 - герой
;!IF:M^Номер героя: %Y2^;
; -2 - если героя нет
!!FU&y2=-2:E;                                   // Выход, если нет героя
!!MR:S?y3;                                      // y3 - номер заклинания (если существо колдует)
; 15 - волшебная стрела
; 16 - ледяная молния
; 17 - удар молнии
; 19 - цепная молния
; 20 - кольцо холода
; 21 - огненный шар
; 22 - инферно
; 23 - метеоритный дождь

!!MR:D?y4;                                      // y4 - базовый урон заклинания

;Герои с артефактами Сферы: +50% урона
;79 - Сфера Небесного Свода  80 - Сфера Илистого Озера  81 - Сфера Бушующего Огня 82 - Сфера Проливного Дождя
!!HEy2:A2/79/d/?y5;                             // y5 - надетая Сфера Небесного Свода
!!HEy2:A2/80/d/?y6;                             // y6 - надетая Сфера Илистого Озера
!!HEy2:A2/81/d/?y7;                             // y7 - надетая Сфера Бушующего Огня
!!HEy2:A2/82/d/?y8;                             // y8 - надетая Сфера Проливного Дождя
!!VRy9|y5>0/y6>0/y7>0/y8>0:S1;                  // y9 - надета любая из четырех сфер

!!VRy4&y9=1/y3=15:*3 :2;                        // +50% к урону волшебной стрелы, если на герое надета любая из четырех сфер
!!VRy4&y8>0/y3=16:*3 :2;                        // +50% к урону ледяной молнии, если на герое надета Сфера Проливного Дождя
!!VRy4&y5>0/y3=17:*3 :2;                        // +50% к урону удара молнии, если на герое надета Сфера Небесного Свода

;Герои со сборным артефактом Сферы Хаоса: +100% урона
;238 - Сфера Хаоса
!!HEy2:A2/238/d/?y10;                           // y10 - Сфера Хаоса у героя

!!VRy4&y10>0/y3>14/y3<18:*2;                    // +100% к урону волшебной стрелы, ледяной молнии, удара молнии
!!VRy4&y10>0/y3>18/y3<24:*4 :3;                 // +33% доп урона к заклинаниям 20-23, т.к. сфера всё равно работает (???). +33% от 1.5х = +100%
;ВНИМАНИЕ, ЦЕПНАЯ МОЛНИЯ (y3=19) СО СФЕРОЙ ХАОСА БЬЕТ ВСЕ РАВНО ТОЛЬКО НА 50% СИЛЬНЕЕ - ЭТО БАГ

;Герои с навыком Волшебство: до +15% доп урона к заклинаниям волшебная стрела, ледяная молния, удар молнии
!!HEy2:S25/?y11;                                // y9 - Уровень навыка Волшебства героя (0..3)
*!IF:M^Уровень навыка волшебства: %Y11^;
; нет волшебства 0
; базовое волшебство 1
; продвинутое волшебство 2
; экспертное волшебство 3
!!VRy4&y11=1/y3>14/y3<18:*105 :100;             // +5% к урону волшебной стрелы, ледяной молнии, удара молнии - герой изучил Базовое Волшебство
!!VRy4&y11=2/y3>14/y3<18:*11 :10;               // +10% к урону волшебной стрелы, ледяной молнии, удара молнии - герой изучил Продвинутое Волшебство
!!VRy4&y11=3/y3>14/y3<18:*115 :100;             // +15% к урону волшебной стрелы, ледяной молнии, удара молнии - герой изучил Экспертное Волшебство

;Герои со специализацией Волшебство
!!HEy2:X?y12/?y13/d/d/d/d/d;                    // y12=0 (вторичный навык), y13=25 (волшебство) для героя-спеца по Волшебству
!!HEy2:Ed/?y15;                                 // y14-лвл героя

!!VRy16&y12=0/y13=25/y9=1:S25 *y15;             // Добавочный коэффициент: 0,25% усилка к урону за каждый лвл героя
!!VRy17&y12=0/y13=25/y9=1:S10500 +y16;          // Числитель умножения урона, Базовое Волшебство
!!VRy4&y12=0/y13=25/y9=1/y3>14/y3<18:*y17 :10000;// Итоговая прибавка урона для волшебной стрелы, ледяной молнии, удара молнии

!!VRy16&y12=0/y13=25/y9=2:S50 *y15;             // Добавочный коэффициент: 0,5% усилка к урону за каждый лвл героя
!!VRy17&y12=0/y13=25/y9=2:S11000 +y16;          // Числитель умножения урона, Продвинутое Волшебство
!!VRy4&y12=0/y13=25/y9=2/y3>14/y3<18:*y17 :10000;// Итоговая прибавка урона для волшебной стрелы, ледяной молнии, удара молнии

!!VRy16&y12=0/y13=25/y9=3:S75 *y15;             // Добавочный коэффициент: 0,75% усилка к урону за каждый лвл героя
!!VRy17&y12=0/y13=25/y9=3:S11500 +y16;          // Числитель умножения урона, Экспертное Волшебство
!!VRy4&y12=0/y13=25/y9=3/y3>14/y3<18:*y17 :10000;// Итоговая прибавка урона

!!MR:Dy4;                                       // Обновление урона заклинания

**end

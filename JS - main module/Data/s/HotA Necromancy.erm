ZVSE2


; All sources of necromancy power are halfed
; The option is incompatible with WoG-options: necromancy


// Incompatible options
!?FU(js_CheckNecromancyOptions);
; x1 - result
!!UN:P751/?y1; Necromancy

!!VRx1:S(FALSE);

!!if&y1;
  !!VRx1:S(TRUE);
!!en;


!?FU(js_HotA_Necromancy);
!!VR(jsonKey:z):S^jsMod.settings.hotaNecromancy.value^; Check json setting
!!SN:T(jsonKey)/?(value:z);
!!VR(jsOpt:y):V(value);
!!FU&(jsOpt)=0:E;

!!FU(js_CheckNecromancyOptions):P?(activeSettings:y);
!!FU&(activeSettings):E;

!!VRi^js_hota_necromancy^:S(TRUE);

** Secondary skills
!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_BASIC)/?(skillDesc:z);
!!FU(StringReplaceINT):P^%(skillDesc)^/^5^/?(newSkillDesc:z);
!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_BASIC)/(newSkillDesc);

!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_ADVANCED)/?(skillDesc:z);
!!FU(StringReplaceINT):P^%(skillDesc)^/^10^/?(newSkillDesc:z);
!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_ADVANCED)/(newSkillDesc);

!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_EXPERT)/?(skillDesc:z);
!!FU(StringReplaceINT):P^%(skillDesc)^/^15^/?(newSkillDesc:z);
!!SN:H^secskill^/(SKILL_NECROMANCY)/(SKILL_EXPERT)/(newSkillDesc);


** Artifacts
!!SN:H^art^/(ART_AMULET_OF_THE_UNDERTAKER)/1/?(artDesc:z);
!!FU(StringReplaceINT):P^%(artDesc)^/^2.5^/?(newArtDesc:z);
!!SN:H^art^/(ART_AMULET_OF_THE_UNDERTAKER)/1/(newArtDesc);

!!SN:H^art^/(ART_VAMPIRES_COWL)/1/?(artDesc:z);
!!FU(StringReplaceINT):P^%(artDesc)^/^5^/?(newArtDesc:z);
!!SN:H^art^/(ART_VAMPIRES_COWL)/1/(newArtDesc);

!!SN:H^art^/(ART_DEAD_MANS_BOOTS)/1/?(artDesc:z);
!!FU(StringReplaceINT):P^%(artDesc)^/^7.5^/?(newArtDesc:z);
!!SN:H^art^/(ART_DEAD_MANS_BOOTS)/1/(newArtDesc);

!!SN:H^art^/(ART_CLOAK_OF_THE_UNDEAD_KING)/1/^%T(jsMod.artifact.130.desc)^;


** Buildings
; Necromancy Amplifier: 48 = (line number - 1) in BldgSpec.txt
!!FU(gem_GetSpecBuilduingDesc):P48/?(bldDesc:z);
!!FU(StringReplaceINT):P(bldDesc:z)/^5^/?(newBldDesc:z);
!!FU(gem_SetSpecBuilduingDesc):P48/(newBldDesc);
; Soul Prison: 53 = (line number - 1) in BldgSpec.txt
!!FU(gem_SetSpecBuilduingDesc):P53/^%T(jsMod.building.necropolis.grail.desc)^;


!#FU(js_HotA_Necromancy):P;


!?FU(js_Hero_GetNecromancyPower_BeforeReturn);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/?(necromancyPower:e);
!!VR(necromancyPower): :2;
!!UN:C(ebp)/-4/4/(necromancyPower:e);

ZVSE2


; Replace Galthran with Ranloo


!?FU(js_BanGalthran);
!!VR(jsonKey:z):S^jsMod.settings.replaceGalthran.value^; Check json setting
!!SN:T(jsonKey)/?(value:z);
!!VR(jsOpt:y):V(value);

!!FU&(jsOpt)=0:E;

; Hero Spec switch
!!HE(HERO_GALTHRAN):X1/(MON_BALLISTA); Ранлу спец по артиллерии
!!HE(HERO_GALTHRAN):S(SKILL_ARMORER)/?(skillLvl:y);
!!HE(HERO_GALTHRAN):S(SKILL_ARMORER)/0;
!!HE(HERO_GALTHRAN):S(SKILL_ARTILLERY)/(skillLvl:y);

; Hero Spec desc
!!SN:H^spec^/(HERO_CHRISTIAN)/2/?(specArtillery:z);
!!SN:H^spec^/(HERO_GALTHRAN)/2/(specArtillery);

; Change pictures, name and bio
!!HE(HERO_GALTHRAN):L1^HPS071DX.pcx^;
!!HE(HERO_GALTHRAN):L2^HPL071DX.pcx^;
!!HE(HERO_GALTHRAN):B0/^%T(jsMod.heroReplacement.%(HERO_GALTHRAN).name)^;
!!HE(HERO_GALTHRAN):B1/^%T(jsMod.heroReplacement.%(HERO_GALTHRAN).bio)^;

; Change starting army
!!VR(wightQty:y):S4 R2;
!!HE(HERO_GALTHRAN):C0/1/(MON_WIGHT)/(wightQty);
!!HE(HERO_GALTHRAN):C0/2/-1/0;

; Give ballista
!!HE(HERO_GALTHRAN):A1/(ART_BALLISTA)/(ART_SLOT_BALLISTA);

; Change spell
!!HE(HERO_GALTHRAN):M(SPELL_SHIELD)/?(hasSpell:y);
!!HE(HERO_GALTHRAN)&(hasSpell):M(SPELL_SHIELD)/0;
!!HE(HERO_GALTHRAN):M(SPELL_HASTE)/?(hasSpell:y);
!!HE(HERO_GALTHRAN)&(hasSpell)=0:M(SPELL_HASTE)/1;

!#FU(js_BanGalthran):P;

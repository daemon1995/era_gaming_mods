ZVSE2


; Logic has been transferred to HotA_SpecBonus.era (double type cannot cast into float in erm)

; Enhanced the effect of creature-based specializations, giving +5% attack and defense for n hero levels, where n is the creature's Tier: now +10% for Tier 1 creatures; +30% for Ballista and +20% for other creatures
; Water Elementals specialization: +2 to Attack -> +12 to Attack
; Fire Elementals specialization: +1 to Attack, +2 to Defense, and +2 to Damage - > +3 to Attack, +4 to Defense, and +2 to damage
; Earth Elementals specialization: +2 Attack, +1 Defense, +5 Damage -> +3 Attack, +2 Defense, and +5 Damage
; Psychic Elementals specialization: +3 Attack and +3 Defense -> +5 Attack and +5 Defense
; Black Knights specialization for Haart Lich: +5 Attack, +5 Defense, +10 Damage -> +10 Attack, +10 Defense, and +10 Damage
; Devils specialization: +4 to Attack, +2 to Defense, and +1 to Speed - > +10 to Attack, +10 to Defense, and +1 to Speed
; Behemoths specialization: +5 Attack, +5 Defense, +10 Damage -> +10 Attack, +10 Defense, and +10 Damage
; Dragons specialization: +5 Attack and +5 Defense -> +10 Attack and +10 Defense
; The option is incompatible with WoG-options: enhanced specialties


// Incompatible options
!?FU(js_CheckSpecialtiesCreaturesOptions);
; x1 - result
!!UN:P39/?y1; Enhanced Specialties

!!VRx1:S(FALSE);

!!if&y1; /y2/y3/y4;
  !!VRx1:S(TRUE);
!!en;


!?FU(OnAfterErmInstructions);
!!VR(jsonKey:z):S^jsMod.settings.hotaSpecialtiesCreatures.value^; Check json setting
!!SN:T(jsonKey)/?(value:z);
!!VR(jsOpt:y):V(value);
!!FU&(jsOpt)=0:E;

!!FU(js_CheckSpecialtiesCreaturesOptions):P?(activeSettings:y);
!!FU&(activeSettings):E;

!!VRi^js_hota_specialties_creatures^:S(TRUE);

!!re i/0/(HERO_LAST_WOG);
  !!HEi:X?(specType:y)/?(specSubtype:y)/d/d/d/d/d;

  !!if&(specType)=1;
    !!if&(specSubtype)=(MON_BALLISTA);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^30^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el;
      !!MA:L(specSubtype)/?(monLvl:y);
      
      !!if&(monLvl)=0;
        !!SN:H^spec^/i/2/?(specDesc:z);
        !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^1^/^10^;
        !!SN:H^spec^/i/2/(newSpecDesc);
      !!el;
        !!SN:H^spec^/i/2/?(specDesc:z);
        !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^1^/^20^;
        !!SN:H^spec^/i/2/(newSpecDesc);
      !!en;
    !!en;
    
  !!el&(specType)=4;
    !!if&(specSubtype)=(MON_WATER_ELEMENTAL);
      !!HEi:Xd/d/12/d/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^12^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_FIRE_ELEMENTAL);
      !!HEi:Xd/d/3/4/2;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^3^/^4^/^2^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_EARTH_ELEMENTAL);
      !!HEi:Xd/d/3/2/5;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^3^/^2^/^5^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_PSYCHIC_ELEMENTAL);
      !!HEi:Xd/d/5/5/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^5^/^5^;
      !!SN:H^spec^/i/2/(newSpecDesc);

    !!el&(specSubtype)=(MON_BLACK_KNIGHT);
      !!HEi:Xd/d/10/10/10;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_DEVIL);
      !!HEi:Xd/d/10/10/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el&(specSubtype)=(MON_BEHEMOTH);
      !!HEi:Xd/d/10/10/10;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!en;

  !!el&(specType)=7;
      !!HEi:Xd/d/10/10/d;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(js_StringReplaceINT):P(specDesc)/?(newSpecDesc:z)/^10^/^10^;
      !!SN:H^spec^/i/2/(newSpecDesc);
  !!en;
!!en;

Z VSE2


!?FU(gem_CreateERMHook);
!!SN:Ex1/1/5911540/(js_CombatMan_SelectSpellTarget_OnCast); 005A33F4
!!SN:Ex1/1/5911700/(js_CombatMan_SelectSpellTarget_OnCancel); 005A3494


!?FU(js_CombatMan_SelectSpellTarget_OnCast);
;!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(msg:y) C(msg)/0/4/?(msgType:y);
;!IF:L^msgType= %(msgType)^;

;!UN:C6849396/4/?(hexId:y); 00688374
;!IF:L^hexId= %(hexId)^; 32

!!FU(js_AoeShot_Shoot):P;


!?FU(js_CombatMan_SelectSpellTarget_OnCancel);
!!SN:W^Magog_Activated^;
!!UN:C(COMBAT_MANAGER)/4/?(cmbMgr:y) C(cmbMgr)/78560/4/?(cursor:y);
!!UN:R5/2/(cursor);


!?FU(OnKeyPressed_Battle)&x1=(KEY_G)/i^Magog_Activated^=0;
!!FU(js_AoeShot_ShowHintAndChangeCursor):P;


!?FU(js_AoeShot_ShowHintAndChangeCursor);
; Проверка на флаг
!!BG:N?(activeStackNum:y);
!!BM(activeStackNum):F?f;
!!VR(aoeShooter:y):Sf &(MON_FLAG_SPLASH_SHOOTER);
!!FU&(aoeShooter)=0:E;

; Проверка на возможность выстрелить
!!BM(activeStackNum):Z?(shooter:y);
!!SN:E4466192/(CALLCONV_THISCALL)/(shooter)/0; 442610 BattleStack::CanShoot
!!FU&v1=0:E;

; Проверка на валидность хекса
!!CM:D?(mouseHex:y);
!!FU|(mouseHex)<0/(mouseHex)>186:E;

; Проверка на обычную стрельбу (стек на хексе под мышкой)
!!BU:E(mouseHex)/?(stackNum:y);
!!FU&(stackNum)>=0:E;

!!SN:W^Magog_Activated^/1;
!!FU(js_RedrawCursorToAoeSpell):P;


!?FU(js_RedrawCursorToAoeSpell);
; v13 = BattleMgr_MovementShadow; - это настройка в меню битвы (тень перемещения)
*!UN:C(GAME_SETTING_COMBAT_SHADE_LEVEL)/4/?(movementShadowSetting:y); 698814
*!IF:L^movementShadowSetting= %(movementShadowSetting)^;


!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(cmbMgr:y); 
; cmbMgr->ActionParam = vSpell;
!!UN:C(cmbMgr)/64/4/(SPELL_FIREBALL); cmbMgr->ActionParam
; cmbMgr->Action = v15 + 10;
!!UN:C(cmbMgr)/60/4/7; cmbMgr->Action
; v16 = Battle_GetHex_ByCoordinates((int)this, side);
!!CM:D?(mouseHex:y);
; BattleMgr_CursorShadowOnGex(v16);
!!SN:E5896720/(CALLCONV_FASTCALL)/(mouseHex); 59FA10 BattleMgr_CursorShadowOnGex

; WndMgr::ProcessDialog(o_WndMgr, 0, CombatMan_SelectSpellTarget, 0);
!!UN:C(WND_MANAGER)/(UNC_INT)/?(wndMgr:y);
!!SN:E6302432/(CALLCONV_THISCALL)/(wndMgr)/0/5911424/0; 602AE0

; MouseMgr::SetCursor(o_MouseMgr, 6, 2);
!!UN:C(cmbMgr)/78560/4/?(cursor:y);
!!UN:R6/2/(cursor);


!?FU(js_AoeShot_Shoot);
!!SN:W^Magog_Activated^;

!!BG:N?(activeStackNum:y);
!!BM(activeStackNum):Z?(shooter:y);

!!CM:D?(targetHexId:y);
!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(cmbMgr:y);

!!SN:E5898560/(CALLCONV_THISCALL)/(cmbMgr)/(SPELL_FIREBALL)/(targetHexId)/1/-1/1/3; 005A0140 BattleMgr::CastSpell

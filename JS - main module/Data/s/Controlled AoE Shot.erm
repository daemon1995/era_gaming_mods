ZVSE2


!?FU(OnBattleMouseHint)&i^Magog_Activated^;
!!FU(js_AoeShot_ShowHintAndChangeCursor):P;

// TODO: тут при наведении на кнопки настройки боя, автобой и тд курсор должен меняться на
// перечеркнутый круг, и кнопки блокируются


!?FU(OnKeyPressed_Battle)&x1=(KEY_G)/i^Magog_Activated^=0;
!!FU(js_AoeShot_ShowHintAndChangeCursor):P;


!?FU(js_AoeShot_ShowHintAndChangeCursor);
; Проверка на флаг
!!BG:N?(activeStackNum:y);
!!BM(activeStackNum):F?f;
!!VR(aoeShooter:y):Sf &(MON_FLAG_SPLASH_SHOOTER);
!!FU&(aoeShooter)=0:E;

; Проверка на валидность хекса
!!CM:D?(mouseHex:y);
!!FU|(mouseHex)<0/(mouseHex)>186:E;

; Проверка на обычную стрельбу (стек на хексе под мышкой)
!!BU:E(mouseHex)/?(stackNum:y);
!!FU&(stackNum)>=0:E;

; Вывод хинта в зависимости от типа стрелка
!!BM(activeStackNum):T?(shooterNum:y);

!!if|(shooterNum)=(MON_LICH)/(shooterNum)=(MON_POWER_LICH)/(shooterNum)=(MON_DRACOLICH);
  !!SN:T^jsMod.shotAtArea.Lich^/?(msg:z);
!!el;|(shooterNum)=(MON_MAGOG)/(shooterNum)=(MON_NOT_USED_2);
  !!SN:T^jsMod.shotAtArea.Magog^/?(msg:z);
!!en;
!!MM:M(msg);

// TODO: тип курсора должен быть разным - облако или пламя
!!UN:R5/3/0;
!!SN:W^Magog_Activated^/1;


; Отмена режима прицельной аое стрельбы
!?FU(OnBattleScreenMouseClick)&i^Magog_Activated^;
!!CM:F?(mouseFlag:y) S?(mouseAction:y);
;!IF:L^mouseFlag= %(mouseFlag), mouseAction= %(mouseAction)^;
!!FU&(mouseFlag)<>512/(mouseAction)<>14:E; пкм

!!UN:C(COMBAT_MANAGER)/4/?(cmbMgr:y) C(cmbMgr)/78560/4/?(cursor:y);
!!UN:R5/2/(cursor);
;!SN:P^BUTTON^;
!!SN:W^Magog_Activated^;


; Выстрел
!?FU(OnBattleScreenMouseClick)&i^Magog_Activated^;
!!SN:W^Magog_Activated^;

; Проверка на клик в настройки/автобой и т.д.
!!CM:I?(mousePlace:y);
;!IF:L^mousePlace= %(mousePlace)^;
!!FU&(mousePlace)>=2000:E;

; Проверка на лкм
!!CM:F?(mouseFlag:y) S?(mouseAction:y);
;!IF:L^mouseFlag= %(mouseFlag), mouseAction= %(mouseAction)^;
!!FU|(mouseFlag)<>0/(mouseAction)<>12:E;

; Проверка на валидность хекса
!!CM:D?(mouseHex:y);
!!FU|(mouseHex)<0/(mouseHex)>186:E;

; Проверка на обычную стрельбу (стек на хексе под мышкой)
!!BU:E(mouseHex)/?(stackNum:y);
!!FU&(stackNum)>=0:E;

// Далее - стрельба по площади
!!CM:R0;

!!BG:N?(activeStackNum:y);
!!FU(js_GetActualStackSide):P(activeStackNum)/?(side:y);
!!VR(side):X(TRUE); Переворачиваем значение

; Создание говно-крестьяно-стека опп сайда для цели выстрела (решение достойное книги рекордов гинесса)
!!BU:S139/0/(mouseHex)/(side)/-1/0; Суммон крестьянина
!!BU:E(mouseHex)/?(stackNum:y);
!!BM(activeStackNum):G-95/(mouseHex)/d; Наносимый стеком урон (реальный)
!!BM(stackNum):G-100/8/d; -100: Видимость существа (4 - виден, 8 - не виден)
!!BM(stackNum):G-90/1/d; -90: Есть ли цифры кол-ва существ? (0 = есть, 1 = нет)
!!BM(stackNum):F?f;
!!VRf:|(MON_FLAG_SUMMONED);
!!BM(stackNum):Ff;

;!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(cmbMgr:y); C(cmbMgr)/68/4/?(targetHexId:y); 0x44
;!VR(shooter:y):S(activeStackNum)*1352+21708+(cmbMgr);
;!VR(defender:y):S(stackNum)*1352+21708+(cmbMgr);

!!BM(activeStackNum):Z?(shooter:y);
!!BM(stackNum):Z?(defender:y);

// Нужен деструктор указателя на оригинальный KILL-звук
!!UN:C(defender)/384/4/0; 0x180 - зануляем указатель на KILL-звук

!!SN:E4453920/(CALLCONV_THISCALL)/(shooter)/(defender); 43F620 BattleStack::Shoot
!!BU:R;
!!BG:A12; протратить ход

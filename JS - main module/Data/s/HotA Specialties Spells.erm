ZVSE2


; Logic has been transferred to HotA_SpecBonus.era

; Enhanced specializations that add +3% to a spell's effect for n hero levels, where n is the target's Tier: changed to +5% for Meteor Shower, Chain Lightning, Resurrection and Raise Dead; +10% for all other spells
; Bloodlust, Stone Skin, and Precision specializations add +10/8/6/4 to the spell's effect on Tier 1-2/3-4/5-6/7 creatures
; Weakness specialization adds +4/6/8/10 to the spell effect for Tier 1-2/3-4/5-6/7 creatures (affects stronger creatures more)
; Haste and Prayer specializations add +3/2/1 to the spell effect for Tier 1-4/5-6/7 creatures
; Slayer specialization adds +20/16/12/8 to the spell effect for Tier 1-2/3-4/5-6/7 creatures 
; Disrupting Ray Specialization  increases the spell's effect by 10
; Luna: specialization bonus reduced from 100% to 25%
; The option is incompatible with WoG-options: enhanced specialties

// Forgetfulness specialization now increases the reduction of attacking creatures' number in target stacks by 10% for n hero levels, where n is the target's level
// TODO: Adrienne's Fire Magic specialization increases Fire spell damage by 50%


// Incompatible options
!?FU(js_CheckSpecialtiesSpellsOptions);
; x1 - result
!!UN:P39/?y1; Enhanced Specialties

!!VRx1:S(FALSE);

!!if&y1; /y2/y3/y4;
  !!VRx1:S(TRUE);
!!en;


!?FU(OnAfterErmInstructions);
!!VR(jsonKey:z):S^jsMod.settings.hotaSpecialtiesSpells.value^; Check json setting
!!SN:T(jsonKey)/?(value:z);
!!VR(jsOpt:y):V(value);
!!FU&(jsOpt)=0:E;

!!FU(js_CheckSpecialtiesSpellsOptions):P?(activeSettings:y);
!!FU&(activeSettings):E;

!!VRi^js_hota_specialties_spells^:S(TRUE);

!!re i/0/(HERO_LAST_WOG);
  !!HEi:X?(specType:y)/?(specSubtype:y)/d/d/d/d/d;

  !!if&(specType)=3;
    !!if|(specSubtype)=(SPELL_METEOR_SHOWER)/(specSubtype)=(SPELL_CHAIN_LIGHTNING)/(specSubtype)=(SPELL_RESURRECTION)/(specSubtype)=(SPELL_ANIMATE_DEAD);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!VR(specDesc): +^%T(jsMod.spell.23193839.desc.spec)^;
      !!SN:H^spec^/i/2/(specDesc);

    !!el&(specSubtype)=(SPELL_CURE);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.37.desc.spec)^;
    !!el&(specSubtype)=(SPELL_BLOODLUST);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.43.desc.spec)^;
    !!el&(specSubtype)=(SPELL_STONE_SKIN);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.46.desc.spec)^;
    !!el&(specSubtype)=(SPELL_PRECISION);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.44.desc.spec)^;

    !!el&(specSubtype)=(SPELL_WEAKNESS);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.45.desc.spec)^;
    !!el&(specSubtype)=(SPELL_HASTE);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.53.desc.spec)^;
    !!el&(specSubtype)=(SPELL_PRAYER);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.48.desc.spec)^;
    !!el&(specSubtype)=(SPELL_SLAYER);
      !!SN:H^spec^/i/2/^%T(jsMod.spell.55.desc.spec)^;
    !!el&(specSubtype)=(SPELL_DISRUPTING_RAY);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^10^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);

    !!el&(specSubtype)=(SPELL_FIRE_WALL);
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!FU(StringReplaceINT):P(specDesc)/^25^/?(newSpecDesc:z);
      !!SN:H^spec^/i/2/(newSpecDesc);
    !!el|(specSubtype)=(SPELL_MAGIC_ARROW)/(specSubtype)=(SPELL_FORTUNE);
      ; do nothing
    !!el;
      !!SN:H^spec^/i/2/?(specDesc:z);
      !!VR(specDesc): +^%T(jsMod.spell.default.desc.spec)^;
      !!SN:H^spec^/i/2/(specDesc);
    !!en;
  !!en;
!!en;

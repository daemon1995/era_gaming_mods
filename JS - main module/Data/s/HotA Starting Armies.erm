ZVSE2


; Harpy count in starting armies: 4-6 -> 6-8
; Gog count in starting armies: 4-7 -> 5-7
; Hell Hound count in starting armies: 3-4 -> 4-5
; Goblin count in starting armies: 15-25 -> 20-30
; Orc count in starting armies: 4-6 -> 5-6
; Griffin count in starting armies: 2-3 -> 4-5
; Dwarf count in starting armies: 3-5 -> 5-7
; Walking Dead count in starting armies: 4-6 -> 6-8
; Water Elemental count in starting armies: 2-3 / 3-5 + 1-2 -> 3-5 / 3-5 + 2-3

; The option is incompatible with WoG-options: rebalanced starting armies


// Incompatible options
!?FU(js_CheckStartingArmiesOptions);
; x1 - result
!!UN:P199/?y1; rebalanced starting armies

!!VRx1:S(FALSE);

!!if&y1;
  !!VRx1:S(TRUE);
!!en;


!?FU(OnAfterErmInstructions);
!!VR(jsonKey:z):S^jsMod.settings.hotaStartingArmies.value^; Check json setting
!!SN:T(jsonKey)/?(value:z);
!!VR(jsOpt:y):V(value);
!!FU&(jsOpt)=0:E;

!!FU(js_CheckStartingArmiesOptions):P?(activeSettings:y);
!!FU&(activeSettings):E;

; Change starting armies
!!re i/(HERO_ORRIN)/(HERO_LAST_WOG);
  !!re j/0/2; max 3 stacks
    !!HEi:C0/j/?(monType:y)/d;
    ;!IF:M^monType= %(monType)^;

    ; Griffin 4-5
    !!if&(monType)=(MON_GRIFFIN);
      !!VR(monQty:y):S4 R1;
      !!HEi:C0/j/(MON_GRIFFIN)/(monQty);
    !!en;

    ; Dwarf 5-7
    !!if&(monType)=(MON_DWARF);
      !!VR(monQty:y):S5 R2;
      !!HEi:C0/j/(MON_DWARF)/(monQty);
    !!en;    

    ; Gog 5-7
    !!if&(monType)=(MON_GOG);
      !!VR(monQty:y):S5 R2;
      !!HEi:C0/j/(MON_GOG)/(monQty);
    !!en;

    ; Hell Hound 4-5
    !!if&(monType)=(MON_HELL_HOUND);
      !!VR(monQty:y):S4 R1;
      !!HEi:C0/j/(MON_HELL_HOUND)/(monQty);
    !!en;

    ; Walking Dead 6-8
    !!if&(monType)=(MON_WALKING_DEAD);
      !!VR(monQty:y):S6 R2;
      !!HEi:C0/j/(MON_WALKING_DEAD)/(monQty);
    !!en;

    ; Harpy 6-8
    !!if&(monType)=(MON_HARPY);
      !!VR(monQty:y):S6 R2;
      !!HEi:C0/j/(MON_HARPY)/(monQty);
    !!en;

    ; Goblin count in starting armies: 15-25 -> 20-30
    !!if&(monType)=(MON_GOBLIN);
      !!VR(monQty:y):S20 R10;
      !!HEi:C0/j/(MON_GOBLIN)/(monQty);
    !!en;

    ; Orc 5-6
    !!if&(monType)=(MON_ORC);
      !!VR(monQty:y):S5 R1;
      !!HEi:C0/j/(MON_ORC)/(monQty);
    !!en;

    ; Water Elemental 3-5
    !!if&(monType)=(MON_WATER_ELEMENTAL);
      !!VR(monQty:y):S3 R2;
      !!HEi:C0/j/(MON_WATER_ELEMENTAL)/(monQty);
    !!en;    

  !!en;
!!en;

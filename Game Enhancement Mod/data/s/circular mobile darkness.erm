ZVSE2
; Author:   Archer30
; Special thanks to Berserker
; Engine:   ERM 2.0+
; Requires: ERA 3.0+, Era Erm Framework, WoG Scripts

; Darkness (ability) of Darkness Dragons now works in a round shape of area around the hero. The shroud is created on turn end.
; Also works on the upgrade of Darkness Dragon (if applicable)


; ============== SETTINGS ==============
!#VRi^cirdark_radius^:S2;               [Radius of Circular Mobile Darkness]
!#VRi^cirdark_noDarkness^:S(FALSE);     [If True, all the darkness ability from creatures will be disabled, including the original one and circular]
; ============ END SETTINGS ============


!?FU(cirdark_RestrainValues);
!!VRi^cirdark_noDarkness^:F(FALSE)/(TRUE);
!!UN:X?(mapSize:y)/?(mapLevel:y);
!!VRi^cirdark_radius^:F0/(mapSize);

!#FU(cirdark_RestrainValues):P;

// Disable the original mobile darkness
!?FU(OnGameEnter);
!!UN:C7685703/(UNC_INT)/?(patch1:y);
!!UN:C7685961/(UNC_INT)/?(patch2:y);

; Set no darkness global var depending on whether Darkness ability has been banned
!!if|(patch1)<>(MON_DARKNESS_DRAGON)/(patch2)<>(MON_DARKNESS_DRAGON);
  !!VRi^cirdark_noDarkness^:S(TRUE);
!!el;
  !!VRi^cirdark_patch1^:S(patch1);
  !!VRi^cirdark_patch2^:S(patch2);
  !!UN:C7685703/(UNC_INT)/(INT_MAX);
  !!UN:C7685961/(UNC_INT)/(INT_MAX);
!!en;

// Restore the original darkness values
!?FU(OnGameLeave)&i^cirdark_patch1^;
!!UN:C7685703/(UNC_INT)/i^cirdark_patch1^;
!!UN:C7685961/(UNC_INT)/i^cirdark_patch2^;

// Create shround on every turn end
!?FU(WOG_EndOfTurn)&i^cirdark_noDarkness^=(FALSE);
!!OW:C?(player:y);
; Check possible upgrade of Darkness Dragon
!!FU(GetUpgradedMonster):P(MON_DARKNESS_DRAGON)/?(upgDarkDragon:y);

; Loop through all heroes
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:O?(owner:y);

  !!if&(owner)=(player);
    ; Find the first slot with Darkness Dragon / upgrade of Darkness Dragon
    !!re (slotInd:y)/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
      !!HEi:C0/(slotInd)/?(monType:y)/?(monCount:y);

      !!br&(monType)=(MON_DARKNESS_DRAGON);
      !!br&(monType)=(upgDarkDragon)/(monType)<>(NO_MON);
    !!en;

    ; Hide the map for all players except for current if creature with Darkness is found
    !!if&(slotInd)<=(ARMY_SLOT_LAST);
      !!HEi:P?(x:y)/?(y:y)/?(z:y);
      !!UN:H(x)/(y)/(z)/(owner)/i^cirdark_radius^;
    !!en;
  !!en;
!!en;

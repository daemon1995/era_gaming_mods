ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.0+, Era Erm Framework, WoG Scripts (80 base)
; Special thanks to Berserker

; Darkness (ability) of Darkness Dragons now works in a round shape of area around the hero. The shroud is created on turn end.
; Also works on Chasm Dragon (upgrade of Darkness Dragon in Third Upgrade Mod).


; ============== SETTINGS ==============
!#VRi^cirdark_on^:S(TRUE);              [This script will be activated only if true]
!#VRi^cirdark_radius^:S2;               [Radius of Circular Mobile Darkness]
!#VRi^cirdark_onChasmDragon^:S(TRUE);   [If True, Chasm Dragon also grants the Circular Mobile Darkness Ability]
!#VRi^cirdark_noDarkness^:S(FALSE);     [If True, all the darkness ability from creatures will be disabled, including original and circular]
; ============ END SETTINGS ============


!?FU(cirdark_RestrainValues);
!!VRi^cirdark_on^:F(FALSE)/(TRUE);
!!VRi^cirdark_enabledForAi^:F(FALSE)/(TRUE);
!!VRi^cirdark_enabledForChasmDragon^:F(FALSE)/(TRUE);
!!VRi^cirdark_noDarkness^:F(FALSE)/(TRUE);
!!UN:X?(mapSize:y)/?(mapLevel:y);
!!VRi^cirdark_radius^:F0/(mapSize);

!#FU(cirdark_RestrainValues):P;

!?FU(OnGameEnter)|i^cirdark_on^/i^cirdark_noDarkness^;
; disable original mobile darkness
!!UN:C7685703/(UNC_INT)/?i^cirdark_patch1^;
!!UN:C7685961/(UNC_INT)/?i^cirdark_patch2^;

!!if|i^cirdark_patch1^<>(MON_DARKNESS_DRAGON)/i^cirdark_patch2^<>(MON_DARKNESS_DRAGON);
  !!VRi^cirdark_noDarkness^:S(TRUE);
!!en;

!!UN:C7685703/(UNC_INT)/(INT_MAX);
!!UN:C7685961/(UNC_INT)/(INT_MAX);

!?FU(OnGameLeave)|i^cirdark_on^/i^cirdark_noDarkness^;
; restore mobile darkness value
!!UN:C7685703/(UNC_INT)/i^cirdark_patch1^;
!!UN:C7685961/(UNC_INT)/i^cirdark_patch2^;

!?FU(WOG_EndOfTurn)&i^cirdark_on^/i^cirdark_noDarkness^=(FALSE);
!!FU(cirdark_CreateShroud):P;

!?FU(cirdark_CreateShroud);
!!OW:C?(player:y);

; Loop through all heroes
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:O?(owner:y);

  !!co&(owner)<>(player);

  ; Find the first slot with Darkness Dragon / Chasm Dragon
  !!re (slotInd:y)/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
    !!HEi:C0/(slotInd)/?(monType:y)/?(monCount:y);

    !!br&(monType)=(MON_DARKNESS_DRAGON);
    !!br&i^Typhon_Third_Upgrade_Mod_Active^=(TRUE)/i^cirdark_onChasmDragon^=(TRUE)/(monType)=231; 231 = Chasm Dragon in TUM
  !!en;

  ; Hide the map for all players except for current if creature with darkness is found
  !!if&(slotInd)<=(ARMY_SLOT_LAST);
    !!HEi:P?(x:y)/?(y:y)/?(z:y);
    !!UN:H(x)/(y)/(z)/(owner)/i^cirdark_radius^;
  !!en;
!!en;
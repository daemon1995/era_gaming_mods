ZVSE2
;gem_scripts.erm
!#DC(GEM_BATTLE_TYPE_NO_AUTO) = 1;
!#DC(GEM_BATTLE_TYPE_AUTO_MANA) = 2;
!#DC(GEM_BATTLE_TYPE_AUTO_MANA_FREE) = 4;
!#DC(GEM_BATTLE_TYPE_ASK_BEFORE_START) = 8;



**************************************************
**** Game Enhancement Mod Part 1/2 - Archer30 ****
**************************************************


IMPORTANT!!!
This part of code needs to be executed SOONER than GEM. Please don't change the filename of the script without testing
The following functions are executed in gem_settigns.erm

**** Load from ini, default GEM Battle Mode depending on whether Quick Combat/Qucik Combat Battle Option is enabled

!?FU(gem_LoadBattleOptionsFromIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
!!UN:C6916044/4/?(quickCombat:y);
!!FU(ReadIniInts):P(filePath)/^main^/^gem_battleMode^/?i^gem_battleMode_ini^/1;[read from ini, default 1]
!!FU(ReadIniInts)&i^gem_Quick_Combat_Check^:P(filePath)/^main^/^gem_battleMode^/?i^gem_battleMode_ini^/8;[if quick combat battle option is on]
!!FU(ReadIniInts)&i^gem_Quick_Combat_Check^=(FALSE)/(quickCombat)=(TRUE):P(filePath)/^main^/^gem_battleMode^/?i^gem_battleMode_ini^/2;[if quick combat battle option is off and quick combat system option is on]
!!VRi^Current_Battle_Type%i(gem_CurPlay)^:Si^gem_battleMode^;


**** Load GEM settings from ini

!?FU(gem_LoadGemSettingsFromIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
!!FU(ReadIniInts):P(filePath)/^main^/^gem_control_units^/?i^gem_option_1_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_newButtons^/?i^gem_option_2_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_allyHeroScreen^/?i^gem_option_3_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_enemyHeroScreen^/?i^gem_option_4_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_spellResearch^/?i^gem_option_5_ini^/1;

**** Fix Quick Combat Syetem Option reverting changes at turn end

!?FU(OnEveryDay)&i^timerDay^>1/i^timerIsAi^=(FALSE);
!!UN:C6916044/4/?(quickCombat:y);
!!VR(battleType:y):Si^Current_Battle_Type%i(gem_CurPlay)^;
!!if&(battleType)=1/(quickCombat)=(TRUE);[change battle type if system option mismatches]
  !!VRi^Current_Battle_Type%i(gem_CurPlay)^:S2;
!!el&(battleType)=2/(quickCombat)=(FALSE);
  !!VRi^Current_Battle_Type%i(gem_CurPlay)^:S1;
!!el&(battleType)=4/(quickCombat)=(FALSE);
  !!VRi^Current_Battle_Type%i(gem_CurPlay)^:S1;
!!en;


**** End Game Enhancement Mod Part 1/2


____________________________________________________________________________________________________

; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
*                       [ scripts for the battles]
; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
!?FU(OnKeyPressed_AdvMap)&x1=(KEY_W)/999;     

  !!VR(lol:y):Si^Current_Battle_Type%i(timerOwner)^ Sd<<1;
  !!if&(lol)>(GEM_BATTLE_TYPE_ASK_BEFORE_START);
    !!VR(lol):S(GEM_BATTLE_TYPE_NO_AUTO);
  !!en;
  !!VRi^Current_Battle_Type%i(timerOwner)^:S(lol);
  !!FU(gem_Button_Switch):P(lol);                 
  !!FU(gem_Autobattle_Check_And_Sound):P(lol)/(TRUE);

!?FU(gem_Autobattle_Check_And_Sound);
!#VA(battleType:x) (playSound:x);

  !!VR(setAuto:y):S(battleType) -1 F0/1;
  !!UN:C6916044/4/(setAuto);

  !!if&(playSound);
    !!FU(IntLog2):P(battleType)/?(stringId:y);
    !!VR(stringId):+1;
    !!SN:T^gem.map.battle%(stringId)^/?(msg:z);
    !!IF:L^%(msg:z)^;
    !!FU(gem_SaveBattleOptionsToIni):P;     [save options]
  !!en;

**  Disable autobattle vs human players (by daemon_n)
** Spell Points Delete in the autobattle, supports battle replay
!?FU(OnBeforeBattleForThisPcDefender);

  !!OW:Cd/?(currPlayer:y); I(currPlayer)/?(isAi:y); [check current player color]

  !!if&(currPlayer)<>i^timerOwner^/i^gem_several_PC^;
    !!VRi^gem_replay_counter^:S1;
    !!BA:Q0;
    !!FU:E;                             [and exit func]
  !!en;

!?FU(gem_CreateERMHook);
!!SN:Ex1/1/4337680/(gem_AI_CastSpell);

!?FU(gem_AI_CastSpell); // jump
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(activeSide:y);
  !!VR(owner:y):Si^battle_owner_%(activeSide)^;

!!if&i^battle_human_%(activeSide)^/
i^Current_Battle_Type_Choice%(owner)^=(GEM_BATTLE_TYPE_AUTO_MANA_FREE);
  !!SN:X?y99/0;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4337852;
!!en;

; Archer30: Do we still need this?
*?FU(OnBeforeBattle)&i^battle_humanOnly^;
*!BA:Q0;
*!FU:E;                             [and exit func]

; Archer30: We put this as late as possible as we want to decide which mode to battle after knowing all the info
; It would be even better if the script is placed in a script with negative number prefix and get rid of _Quit, so that the script can be quited with SN:Q (Freelancer's Guild)
!?FU(OnBeforeBattleUniversal_Quit)&i^battle_humanOnly^=(FALSE)/i^battle_isNetwork^=(FALSE)/i^battle_aiOnly^=(FALSE);     [before Human battle]
  !!OW:Cd/?(currPlayer:y); I(currPlayer)/?(isAi:y); [check current player color]
  !!if&(currPlayer)<>i^timerOwner^/i^gem_several_PC^;
    !!BA:Q0;
    !!FU:E;                             [and exit func]
  !!en;
  !!UN:C6885977/1/?(isGoSolo:y);
  !!if&(isGoSolo);
    !!BA:Q1;
    !!FU:E; 
  !!en;

  !!UN:C6916044/4/?(isAutoBattleState:y);[СѓР·РЅР°С‚СЊ Р°РІС‚Рѕ]


  !!if&(isAutoBattleState)=(FALSE)/i^Current_Battle_Type%(currPlayer)^>(GEM_BATTLE_TYPE_NO_AUTO)/i^Current_Battle_Type%(currPlayer)^<(GEM_BATTLE_TYPE_ASK_BEFORE_START):;
    !!VRi^Current_Battle_Type%(currPlayer)^:S(GEM_BATTLE_TYPE_NO_AUTO); [to know his choice]
    
    !!BA:Q0;
    !!FU:E;                             [and exit func]
  !!en;

  !!VRi^Current_Battle_Type%(currPlayer)^&(isAutoBattleState)=(TRUE)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_NO_AUTO):S(GEM_BATTLE_TYPE_AUTO_MANA);


  !!if&i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START):;                                 [if choising]
     !!VRi^Current_Battle_Type_Choice%(currPlayer)^&i^Current_Battle_Type_Choice%(currPlayer)^=(NULL):S(GEM_BATTLE_TYPE_NO_AUTO);

     !!SN:T^gem.map.battlelist1^/?z-1;
     !!SN:T^gem.map.battlelist2^/?z-2;
     !!SN:T^gem.map.battlelist3^/?z-3;
     !!SN:T^gem.map.battlelist4^/?z-4;
     !!IF:G1/1/i^Current_Battle_Type_Choice%(currPlayer)^/-1/-2/-3/-4;                  [show message]
     !!SN:W^Current_Battle_Type_Choice%(currPlayer)^/v1;  [store choice]
  !!el;
    !!VRi^Current_Battle_Type_Choice%(currPlayer)^:Si^Current_Battle_Type%(currPlayer)^;
  !!en;

  !!if&i^Current_Battle_Type_Choice%(currPlayer)^=(GEM_BATTLE_TYPE_NO_AUTO)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START)|i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_NO_AUTO):;                   [if choisen]
    !!VRi^gem_replay_counter^&i^gem_OnlyOneBattleReplayIsAllowed^=(TRUE):S1;
    !!BA:Q0;                              [set no auto]
    !!FU:E;                               [and exit func]
  !!en;

  !!if&i^Current_Battle_Type_Choice%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START)|i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA):;                   [if choisen]
    !!VRi^gem_replay_counter^&i^gem_OnlyOneBattleReplayIsAllowed^=(TRUE):S(NULL);
    !!BA:Q1;                              [set auto]
    !!FU:E;                               [end]
  !!en;

  !!if&i^Current_Battle_Type_Choice%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA_FREE)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START)|i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA_FREE):;
    !!VRi^gem_replay_counter^&i^gem_OnlyOneBattleReplayIsAllowed^=(TRUE):S(NULL);
    !!BA:Q1;                              [set auto]
    !!FU:E;                               [end]
  !!en;                                   [final] 
  *!UN:C6916044/4/?(isAutoBattleState:y);[СѓР·РЅР°С‚СЊ Р°РІС‚Рѕ]


//remove extra replay bttns
!?FU(gem_DlgBattleResults_AfterCreate)&i^gem_OnlyOneBattleReplayIsAllowed^/i^gem_replay_counter^/i^battle_humanOnly^=(FALSE)/1000;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/?(dlg:y);
  !!FU(gem_DlgItem_Show):P0/0/0/0/(dlg:y);
  !!FU(gem_DlgItem_Show):P30723/0/0/0/(dlg:y);

!?FU(OnBattleReplay)&i^battle_humanOnly^=(FALSE)/i^gem_OnlyOneBattleReplayIsAllowed^;
  !!VRi^gem_replay_counter^:+1;


!?FU(OnAfterBattle)&i^battle_humanOnly^=(FALSE)/(ERM_FLAG_IS_HUMAN);

  !!if&i^battle_humanOnly^=(FALSE);
    !!if&i^gem_OnlyOneBattleReplayIsAllowed^;
      !!VRi^gem_replay_counter^:S0;
    !!en;

    !!FU(gem_Button_Switch):Pi^Current_Battle_Type%i(timerOwner)^;
  !!en;


!?FU(OnGameEnter)&999;
!!if&i^gem_Quick_Combat_Check^;
  !!re i/0/(PLAYER_LAST);
    !!OW:Ii/?(isAi:y)/?(isDead:y);

    !!if&(isAi)=(FALSE)/(isDead)=(FALSE);
      !!SN:W^Current_Battle_Type%i^/(GEM_BATTLE_TYPE_ASK_BEFORE_START);  [ set "ask -before battle"]
    !!en;
  !!en;

  !!SN:W^gem_Quick_Combat_Check^/(FALSE);
!!en;

!!FU(gem_Button_Switch):Pi^Current_Battle_Type%i(timerOwner)^;

!?FU(OnGameEnter);
  !!re i/(PLAYER_FIRST)/(PLAYER_LAST):;
    !!VRi^Current_Battle_Type%i^&i^Current_Battle_Type%i^=0:S(GEM_BATTLE_TYPE_NO_AUTO);
  !!en:;

  !!FU(gem_Autobattle_Check_And_Sound):Pi^Current_Battle_Type%i(timerOwner)^/(FALSE);

!?FU(OnEveryDay)&1000;

!!FU(gem_Button_Switch):Pi^Current_Battle_Type%i(timerOwner)^;&i^timerDay^>0
!!FU(gem_Autobattle_Check_And_Sound):Pi^Current_Battle_Type%i(timerOwner)^/0;


!?FU(OnAdventureMapLeftMouseClick)&999/i^mouse_action^=(MOUSE_LMB_RELEASED);
!#VA(bttns[4]:x);
!!VR(bttns[0]):Ci^manualf_Button_ID^/i^automf_Button_ID^/i^autowm_Button_ID^/i^choiceB_Button_ID^;
!!re i/0/(bttns[SIZE])/1/-1;
  !!if&(bttns[i])=i^mouse_item^;

    !!VRi^Current_Battle_Type%i(timerOwner)^:S1 Sd<<i;
    !!FU(gem_Button_Switch):Pi^Current_Battle_Type%i(timerOwner)^;
    !!FU(gem_Autobattle_Check_And_Sound):Pi^Current_Battle_Type%i(timerOwner)^/(TRUE);
    !!br;
  !!en;
!!en;


!?FU(gem_Button_Switch)&999;
  !#VA(battleType:x);
  !!UN:J8/1/^gem_interface.pac^; !!FU&-1:E;
  !!SN:F^GetButtonID^/^indicator^; !!FU&v1<1:E;

  !!FU(IntLog2):P(battleType:x)/?(setFrame:y);
  !!VR(setFrame:y):*2;S(battleType:x) -1;i^Current_Battle_Type%i(gem_CurPlay)^;
  !!VR(setFrame)&(setFrame)=(GEM_BATTLE_TYPE_ASK_BEFORE_START):-1;
  !!FU(H3Dlg_GetRootDlg):P?(rootDlg:y);
  !!FU(H3Dlg_GetCurrentDlg):P?(currentDlg:y);

  !!if&(rootDlg)=(currentDlg);
    !!FU(gem_DlgDefProc):Pv1/(setFrame);
    !!SN:D;  
  !!el;
    !!FU(gem_DlgDefProc):Pv1/(setFrame)/d/d/d/(rootDlg);
  !!en;



right-click msg of Lord buttons
!?FU(OnAdventureMapRightMouseClick)&i^mouse_item^=i^mithrilB_Button_ID^;
!!OW:Cd/?(clickedPlayer:y);
!!OW:R(clickedPlayer)/(RES_MITHRIL)/?(resNumber:y);

!!if&(resNumber)>0;
  !!SN:T^gem.settings.line7+^/?z1/^mithril^/(resNumber);
!!el;
  !!SN:T^gem.settings.line7^/?z1/^mithril^/(resNumber);
!!en;
!!CM:R0;
!!IF:M0/4/^%z1^;  


**************************************************
**** Game Enhancement Mod Part 2/2 - Archer30 ****
**************************************************


IMPORTANT!!!
This part of code needs to be executed LATER than GEM. Please don't change the filename of the script without testing


**** Save GEM Battle Mode selection to ini

!?FU(gem_Autobattle_Check_And_Sound);
!!FU(gem_SaveBattleOptionsToIni)&x1=1:P;[inherit x1 from GEM]

!?FU(gem_SaveBattleOptionsToIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_battleMode^/?i^Current_Battle_Type%i(gem_CurPlay)^;
!!FU(SaveIni):P(filePath);


**** Save GEM Settings to ini

!?FU(OnCustomDialogEvent)&v998=505/v999>=30721/v999<=30722/v1000=13;
!!FU(gem_SaveGEMSettingsToIni):P;

!?FU(gem_SaveGEMSettingsToIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
*!SN:L^BattleSave.dll^/?(battleSavePlugin:y);
!!FU(WriteIniInts):P(filePath)/^main^/^gem_control_units^/?i^gem_option_1_%i(gem_CurPlay)^; [save battle save option only when plugin is disabled]
!!FU(WriteIniInts):P(filePath)/^main^/^gem_newButtons^/?i^gem_option_2_%i(gem_CurPlay)^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_allyHeroScreen^/?i^gem_option_3_%i(gem_CurPlay)^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_spellResearch^/?i^gem_option_5_%i(gem_CurPlay)^;
*!FU(WriteIniInts):P(filePath)/^main^/^gem_disableFreeJoin^/?i^gem_option_5_%i(gem_CurPlay)^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_enemyHeroScreen^/?i^gem_option_4_%i(gem_CurPlay)^;
!!FU(SaveIni):P(filePath);


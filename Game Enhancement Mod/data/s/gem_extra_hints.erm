ZVSE2


!?FU(gem_CreateERMHook);

  !!SN:Ex1/1/4274460/(gem_AdvMgr_Get_Object_RightClick_Descr); [0041391C - bebore get obj type switch]
  !!SN:Ex1/1/4283002/(gem_AdvMgr_BeforeRmcPopup);              [00415A7A - after hiding/showing hero and before jump to popup ]

  !!SN:Ex1/1/5206294/(gem_BeforeMultyPicDlg);                  [before !_!IF:N create]
  !!SN:Ex1/1/6229606/(gem_OnUniversityShow);  


  !!SN:Ex1/1/6287289/(gem_BeforeBaseDlgConstruct);                      [before any dlg construct]



!?FU(gem_BeforeBaseDlgConstruct)&i^gem_DlgAtRmc^;                       [Pinning dialog window to object coordinates ]

  !!CM:A?(x:y)/?(y:y);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/8/4/?(xPos:y) C(ebp)/12/4/?(yPos:y)  C(ebp)/16/4/?(width:y) C(ebp)/20/4/?(height:y);
  !!FU(gem_GetRectCenter):P(x)/(y)/(width)/(height)/?(xPos:y)/?(yPos:y);
  !!UN:C(ebp)/8/4/(xPos:y) C(ebp)/12/4/(yPos:y);

!?FU(gem_BeforeMultyPicDlg)&i^gem_DlgAtRmc^;                            [Pinning dialog window to object coordinates ]
  !!CM:A?(x:y)/?(y:y);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/24/4/?(xPos:y) C(ebp)/28/4/?(yPos:y)  C(ebp)/32/4/?(width:y) C(ebp)/36/4/?(height:y);
  !!FU(gem_GetRectCenter):P(x)/(y)/(width)/(height)/?(xPos:y)/?(yPos:y);
  !!UN:C(ebp)/24/4/(xPos:y) C(ebp)/28/4/(yPos:y);


!?FU(gem_AdvMgr_Get_Object_RightClick_Descr);                                               [Replacing CB guard text description with pictures]
  !!CM:P?(x:y)/?(y:y)/?(z:y);
  !!OW:C?y99/?(clickedPlayer:y);

  !!OB(x)/(y)/(z):T?(objType:y) U?(objSubtype:y);
  !!SN:O?(x)/?(y)/?(z);
  !!if|(objType)=(OBJ_CREATURE_BANK)/(objType)=(OBJ_CRYPT)/(objType)=(OBJ_DRAGON_UTOPIA)
  /(objType)=(OBJ_DERELICT_SHIP)/(objType)=(OBJ_SHIPWRECK);
    
    !!CB(x)/(y)/(z):V?(bitMask:y) T?(isPlundered:y);                                        [Get bit mask of object visiting by all players]
    !!VR(playerBit:y):S1 Sd<<(clickedPlayer:y);                                             [1 + offset for each next player]
    !!VR(bitMask):&(playerBit);                                                             [Once visited, reveal the crypt guards to the player]  ; Get the information about CB]
    !!if&(bitMask)/(isPlundered:y)=(FALSE);
      !!VRi^gem_isRmcHint^:S(TRUE);                                                         [Set RMC hint flag]

      !!FU(gem_CreatureBankCalcGuardsAndSetText):P(x)/(y)/(z)/(objType)/(objSubtype);
      ; Jump over standart cb dialog
      !!SN:X?y99/0;                                                                         [it is: return NO_EXEC_DEFAULT]
      !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4282816;                                         [return to "hide hero" part]
    !!en;

  !!el&(objType)=(OBJ_REFUGEE_CAMP);
    !!PO(x)/(y)/(z):O?(owner:y);
    !!if&(owner)<>(NO_PLAYER);

      !!FU(gem_GetRefugeeMonNumByPlayer):P(x)/(y)/(z)/(owner:y)/(clickedPlayer)/?(monNum:y);
      !!if&(monNum)<>-1;
        !!OB(x)/(y)/(z):U?(monType:y);                                                      [G?(growth:y);]
        !!VRi^gem_isRmcHint^:S(TRUE);                                                       [Set RMC hint flag]

        !!VRi^gem_DlgAtRmc^:S(TRUE);
        !!SN:E5577600/(CALLCONV_FASTCALL)/(monType)/?(monNum);
        !!VRi^gem_DlgAtRmc^:S(FALSE);

        !!SN:X?y99/0;                                                                       [it is: return NO_EXEC_DEFAULT]
        !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4282816;                                       [return to "hide hero" part]
      !!en;

    !!en;
  !!el&(objType)=(OBJ_UNIVERSITY);

    !!OB(x)/(y)/(z):C?(cWord:y);
    !!OW:A(clickedPlayer)/?(activeHero:y);
    !!if&(activeHero)<>(NO_HERO);
      !!VR(playerBits:y):S1 Sd<<(clickedPlayer);

      !!VR(cWord):Sd>>5 &(playerBits);
      !!if&(cWord);                       [if object is visited]
        !!VRi^gem_isRmcHint^:S(TRUE);           [Set RMC hint flag]

        !#VA(changeCursorOpps[5]:y);
        
        !!re i/0/(changeCursorOpps[SIZE])/1/-1;
          !!UN:C4890993/i/1/?(changeCursorOpps[i]) C4890993/i/1/(OPCODE_NOP_1); // don't change cursor
        !!en;
        !!FU(gem_AdvMgr_HeroEnterObject):P(x)/(y)/(z)/(activeHero)/d/1; emulate object visit
        !!re i/0/(changeCursorOpps[SIZE])/1/-1;
          !!UN:C4890993/i/1/(changeCursorOpps[i]); // restore values
        !!en;
        !!SN:X?y99/0;                     [it is: return NO_EXEC_DEFAULT]
        !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4282816;                                  [return to "hide hero" part]
      !!en;

    !!en;
  !!en;

!?FU(gem_AdvMgr_BeforeRmcPopup)&i^gem_isRmcHint^; // this part 
  !!SN:X?y99/0;                       [it is: return NO_EXEC_DEFAULT]
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4283123;                                  [return to end of the func - before stack increase]
  !!VRi^gem_isRmcHint^:S(FALSE);           [Reset RMC hint flag]


!$OB(OBJ_UNIVERSITY);
!!FU(gem_AdvMgr_GetMapItem):Pv998/v999/v1000/?(mapItem:y);
!!SN:E5228064/(CALLCONV_THISCALL)/(mapItem)/i^timerOwner^; [set object as visited]



!$OB(OBJ_REFUGEE_CAMP);
!!POv998/v999/v1000:Oi^timerOwner^;
!!OBv998/v999/v1000:C?(monNum:y);
!!FU(PackUnion):Pv998/8/v999/8/v1000/8/?(refugeePacked:y);
!!OW:Ti^timerOwner^/?t;
!!VRi^gem_LastMonsterNumberForTeam%tAt%(refugeePacked)^:S(monNum) +1;


!?FU(gem_GetRefugeeMonNumByPlayer);
!#VA(x:x) (y:x) (z:x) (lastOwner:x) (clickedPlayer:x) (result:x);
  !!VR(result):S-1;
  !!FU(PackUnion):P(x)/8/(y)/8/(z)/8/?(refugeePacked:y);
  !!OW:T(clickedPlayer:x)/?t;
  !!if&i^gem_LastMonsterNumberForTeam%tAt%(refugeePacked)^; [if visited by any from player team this week]
    !!OW:T(lastOwner)/?(ownerTeam:y);
    !!if&t<>(ownerTeam);
      !!VR(result):Si^gem_LastMonsterNumberForTeam%tAt%(refugeePacked)^ -1;
    !!el;
      !!OB(x)/(y)/(z):C?(result:x);
    !!en;
  !!en;

!?FU(OnEveryDay)&i^timerWeekDay^=1/i^timerOnce^;
  !!FU(gem_ResetRefugeeVariables):P D;
  
!?FU(gem_ResetRefugeeVariables);
  !!UN:U(OBJ_REFUGEE_CAMP)/(ANY_OBJ)/?(refugees:y);
  !!VRv2:S-1;
  !!re i/1/(refugees);/1/-1;
    !!UN:U(OBJ_REFUGEE_CAMP)/(ANY_OBJ)/i/2;/?(refugees:y);
    !!re t/0/(PLAYER_LAST);
      !!FU(PackUnion):Pv2/8/v3/8/v4/8/?(refugeePacked:y);
      !!VRi^gem_LastMonsterNumberForTeam%tAt%(refugeePacked)^:S(NULL);
    !!en;
    !!PO2:O(NO_PLAYER);
  !!en;

!?FU(gem_OnUniversityShow)&i^gem_isRmcHint^;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(dlg:y);

  !!FU(H3Dlg_CenterAtMouse):P(dlg:y);
  !!FU(H3Dlg_ShowPopup):P(dlg:y);

  !!SN:X?y99/0;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/6229627;


!?OB(OBJ_CRYPT);                        [Reveal the crypt guards if player visited it]
!!VR(playerBit:y):S1 Sd<<i^timerOwner^; [1 + offset for each next player]
!!CB998:Vd|(playerBit);                 [Add object to player as revealed]
!?OB(OBJ_SHIPWRECK);
!!VR(playerBit:y):S1 Sd<<i^timerOwner^; [1 + offset for each next player]
!!CB998:Vd|(playerBit);                 [Add object to player as revealed]

!?OB(OBJ_DERELICT_SHIP);
!!OB998:T?t;
!!if&t=(OBJ_DERELICT_SHIP);
  !!VR(playerBit:y):S1 Sd<<i^timerOwner^;[1 + offset for each next player]
  !!CB998:Vd|(playerBit);               [Add object to player as revealed]
!!en;




!?FU(gem_GetCreatureBankId);
!#VA(objType:x) (objSubtype:x) (objId:x);

!!if&(objType)=(OBJ_SHIPWRECK);
  !!VR(objId):S7;
!!el&(objType)=(OBJ_DERELICT_SHIP);
  !!VR(objId):S8;
!!el&(objType)=(OBJ_CRYPT);
  !!VR(objId):S9;
!!el&(objType)=(OBJ_DRAGON_UTOPIA);
  !!VR(objId):S10;
!!el;
  !!VR(objId):S(objSubtype);     
!!en;


!?FU(gem_GetCreatureBankNameById);
!#VA(objId:x) (objName:x);
  !!UN:C6750876/4/?(cbTablePtr:y);
  !!VR(offset:y):S(objId) *100 *(UNC_INT) +(UNC_INT);
  !!UN:C(cbTablePtr)/(offset)/4/?(strPtr:y);
  !!SN:B(strPtr)/d/?(objNameStr:z);

  !!VR(objName):Z(objNameStr);


!?FU(gem_CreatureBankCalcGuardsAndSetText);
!#VA(coord[3]:x) (objType:x) (objSubtype:x);
  !!FU(gem_GetCreatureBankId):P(objType)/(objSubtype)/?(objId:y); [Get CB id]
  !!FU(gem_GetCreatureBankNameById):P(objId)/?(objName:z); [Get CB name]
  ; Loop for each stack in CB - calculate sum of each monster type
  !!FU(NewIntArray):P?(types:y) P?(nums:y);[create 2 empty arrays]

  !!re i/0/(ARMY_SLOT_LAST);
    !!CB(coord[0])/(coord[1])/(coord[2]):Gi/?(guardType:y)/?(guardCount:y);
    !!co&(guardType)=(NO_MON);            [if empty guard(-1)] - continue]
    !!FU(Array_IndexOf):P(types)/(guardType)/?(index:y);
    !!if&(index)<>(NO_MON);
      !!SN:M(nums)/(index)/d(guardCount);
    !!el;
      !!FU(Array_Push):P(types:y)/(guardType) P(nums:y)/(guardCount);
    !!en;
  !!en;

  !!SN:M(types)/?(typesSize:y);           [get size of types-array]
  !!FU(NewIntArray):P?(pics:y);

  !!re i/0/(typesSize)/1/-1;
    !!SN:M(types)/i/?t M(nums)/i/?n;
    !!FU(gem_GetCreatureAmountString):Pn/?(guardCountDesc:z);
    ; Building new monster desc
    !!SN:H^monname^/t/1/?s^monName_%t^;
    !!VR(monStringWithNumber:z):S(guardCountDesc) +^ ^ + s^monName_%t^;
    !!SN:H^monname^/t/1/(monStringWithNumber);
    !!FU(Array_Push):P(pics)/(PIC_TYPE_MONSTER)/t;
  !!en;

  !!FU(PrepareMultiPicDialog):P(pics);

  !!if&i^gem_isVisit^;
    ; Get the visit message
    !!FU(gem_GetVisitString):P(objId)/(objName)/?(message:z);
    ; Show a message 
    !!IF:N(MSG_TYPE_QUESTION)/^%(message)^;
  !!en;

  !!if&i^gem_isRmcHint^;
    ; Get the RMC hint message
    !!FU(GetTextFileString):P^genrltxt^/203/?(strGuardedBy:z); [Get "Guarded by" string]
    !!VR(message:z):S^{%(objName)}%T(gem.endl)%T(gem.endl)%(strGuardedBy)^;
    ; Show a message
    !!VRi^gem_DlgAtRmc^:S(TRUE);
    !!IF:N(MSG_TYPE_POPUP)/^%(message)^;
    !!VRi^gem_DlgAtRmc^:S(FALSE);
  !!en;

  ; Return old monster desc and clear global var
  !!re i/0/(typesSize)/1/-1;
    !!SN:M(types)/i/?t;
    !!SN:H^monname^/t/1/s^monName_%t^ W^monName_%t^;
  !!en;


!?FU(gem_GetVisitString);
!#VA(objId:x) (objName:x) (returnedVisitStr:x);

  !!if&(objId)=7;
    !!FU(GetTextFileString):P^advevent^/122/?(message:z); [Shipwreck]
  !!el&(objId)=8;
    !!FU(GetTextFileString):P^advevent^/41/?(message:z); [Derelict Ship]
  !!el&(objId)=9;
    !!FU(GetTextFileString):P^advevent^/119/?(message:z); [Crypt]
  !!el&(objId)=10;
    !!FU(GetTextFileString):P^advevent^/47/?(message:z); [Dragon Utopia]
  !!el;
    !!FU(GetTextFileString):P^advevent^/32/?(message:z); [Common CB]
    !!SN:E6388190/(CALLCONV_CDECL)/?(varStr:z)/(message:z)/^%z(objName)^; [Replace %s in message with z(objName)]
    !!VR(message:z):S^{%z(objName)}%T(gem.endl)%T(gem.endl)%(varStr)^;     
  !!en;

  !!VR(returnedVisitStr):Z^%(message)^;


!?FU(gem_GetCreatureAmountString);
!#VA(totalGuardCnt:x) (returnedStr:x);
  !!SN:E4500144/(CALLCONV_FASTCALL)/(totalGuardCnt)/1; [1 - without "yellow" numbers, 2 - with]
  !!SN:Bv1/d/?(strMonsterCount:z);
  !!VR(returnedStr):Z^%(strMonsterCount)^;

**end

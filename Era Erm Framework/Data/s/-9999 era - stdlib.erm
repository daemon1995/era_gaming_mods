ZVSE2
; Implement new event "OnAfterErmInited", occuring after all instructions and postinstruction triggers are executed
!?FU(OnAfterErmInstructions_Quit);
!!FU(OnAfterErmInited):P;
!!FU(OnStartOrLoad):P(TRUE);

; Implement new "OnStartOrLoad" event, occuring after map start or savegame loaded. Safe alternative to OnGameStart, which is triggered before adventure map display.
!?FU(OnAfterLoadGame_Quit);
!!FU(OnStartOrLoad):P(FALSE);

; Implement new events with the same parameters, as OnKeyPressed, but for particular dialogs.
; OnKeyPressed_AdvMap, OnKeyPressed_Battle, OnKeyPressed_HeroScreen, OnKeyPressed_HeroMeetingScreen, OnKeyPressed_Town
!?FU(OnKeyPressed_Quit);
!#VA(key:x) (preventDefault:x);
!!SN:F^GetGameState^/?(mainDialogs[2]:y);
!!FU(OnKeyPressed_AdvMap)&(mainDialogs[1])=(DLG_ADVMAP):P?(key)/?(preventDefault);
!!FU(OnKeyPressed_Battle)&(mainDialogs[1])=(DLG_BATTLE):P?(key)/?(preventDefault);
!!FU(OnKeyPressed_HeroScreen)&(mainDialogs[1])=(DLG_HERO_SCREEN):P?(key)/?(preventDefault);
!!FU(OnKeyPressed_HeroMeetingScreen)&(mainDialogs[1])=(DLG_HERO_MEETING_SCREEN):P?(key)/?(preventDefault);
!!FU(OnKeyPressed_Town)&(mainDialogs[1])=(DLG_TOWN_SCREEN):P?(key)/?(preventDefault);

!!FU(RestorePrevKeyModsState):P;

; Implement new events with the same parameters, as OnKeyReleased, but for particular dialogs.
; OnKeyReleased_AdvMap, OnKeyReleased_Battle, OnKeyReleased_HeroScreen, OnKeyReleased_HeroMeetingScreen, OnKeyReleased_Town
!?FU(OnKeyReleased_Quit);
!#VA(key:x) (preventDefault:x) (isDown:x);
!!SN:F^GetGameState^/?(mainDialogs[2]:y);
!!FU(OnKeyReleased_AdvMap)&(mainDialogs[1])=(DLG_ADVMAP):P?(key)/?(preventDefault)/(isDown);
!!FU(OnKeyReleased_Battle)&(mainDialogs[1])=(DLG_BATTLE):P?(key)/?(preventDefault)/(isDown);
!!FU(OnKeyReleased_HeroScreen)&(mainDialogs[1])=(DLG_HERO_SCREEN):P?(key)/?(preventDefault)/(isDown);
!!FU(OnKeyReleased_HeroMeetingScreen)&(mainDialogs[1])=(DLG_HERO_MEETING_SCREEN):P?(key)/?(preventDefault)/(isDown);
!!FU(OnKeyReleased_Town)&(mainDialogs[1])=(DLG_TOWN_SCREEN):P?(key)/?(preventDefault)/(isDown);

!!FU(RestorePrevKeyModsState):P;

!?FU(OnAdventureMapRightMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnTownMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnHeroScreenMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnHeroesMeetScreenMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnBattleScreenMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnAdventureMapLeftMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnRecruitDlgMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnTownHallMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnKingdomOverviewMouseClick_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnBattleMouseHint_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnTownMouseHint_Quit);
!!FU(LeaveMouseEvent):P;

!?FU(OnCustomDialogEvent_Quit);
!!FU(RestoreEventData):P?i^dlg_id^;
!!FU(LeaveMouseEvent):P;

!?FU(OnBeforeBattle_Quit);
!!FU(UpdateBattleVars):P;

!?FU(OnBeforeBattleUniversal_Quit);
!!FU(UpdateBattleVars):P;

; Implement new "OnAfterBattleSetup" event, occuring right after "OnBeforeBattle" and "OnBeforeBattleUniversal" events.
; New event allows to to be sure, that basic battle parameters (heroes, quick battle, coordinates) are set up.
!!FU(OnAfterBattleSetup):P;
!!FU(UpdateBattleVars):P;

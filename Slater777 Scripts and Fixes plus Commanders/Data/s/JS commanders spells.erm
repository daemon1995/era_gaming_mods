ZVSE2


; It is only for damaging spells
!?FU(js_GetSpellIdByCommander);
!#VA(type:x) (spellId:x);

!!if&(type)=(MON_PALADIN_TYPE);             Paladin - Destroy Undead
  !!VR(spellId):S(SPELL_DESTROY_UNDEAD);
*!el&(type)=(MON_HIEROPHANT_TYPE);          Hierophant - Resurrect
  *!VR(spellId):S(SPELL_RESURRECTION);
!!el&(type)=(MON_TEMPLE_GUARDIAN_TYPE);     Temple Guardian - Repair
  ;!VR(spellId):S(SPELL_RESURRECTION);
!!el&(type)=(MON_SUCCUBUS_TYPE);            Succubus - Demonology
  ;!VR(spellId):S(SPELL_RESURRECTION);
*!el&(type)=(MON_SOUL_EATER_TYPE);          Soul Eater - Animate Dead
  *!VR(spellId):S(SPELL_ANIMATE_DEAD);
!!el&(type)=(MON_BRUTE_TYPE);               Brute - Implosion
  !!VR(spellId):S(SPELL_IMPLOSION);
!!el&(type)=(MON_OGRE_LEADER_TYPE);         Ogre - Magic Arrow
  !!VR(spellId):S(SPELL_MAGIC_ARROW);
!!el&(type)=(MON_SHAMAN_TYPE);              Shaman - Ice Bolt
  !!VR(spellId):S(SPELL_ICE_BOLT);
!!el&(type)=(MON_ASTRAL_SPIRIT_TYPE);       Astral Spirit - Lighning Bolt
  !!VR(spellId):S(SPELL_LIGHTNING_BOLT);
!!en;


*?FU(OnCombatRound)&v997=0;
*!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST);
  *!BMi:T?(monType:y);
  *!BMi|(monType)=(MON_PALADIN_A)/(monType)=(MON_PALADIN_D):U4/(SPELL_DESTROY_UNDEAD);
*!en;


!?FU(js_SetCommandersSpells);
; 10/7/2 - damaging spell amethyst config 
; Замок (174/183 Паладин)
!!if&i^js_amethyst^;
  !!FU(js_GetSpellIdByCommander):P(MON_PALADIN_TYPE)/?(spellId:y);
  !!MA:W(MON_PALADIN_A)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  !!MA:W(MON_PALADIN_D)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_1.png^/^Data\Defs\JS_npc1.def\0_1.png^;
  // Замена имени спелла
  // Замена описания спелла
!!en;

; Оплот (175/184 Знахарь) - воскрешение (базовая мощность 100 * 1 = 100)
!!if&i^js_amethyst^;
  !!MA:W(MON_HIEROPHANT_A)/^        
    Spell 1=0
    Spell 2=0
    Spell 3=0
  ^;
  !!MA:W(MON_HIEROPHANT_D)/^        
    Spell 1=0
    Spell 2=0
    Spell 3=0
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_3.png^/^Data\Defs\JS_npc1.def\0_3.png^;
  // Замена имени спелла
  // Замена описания спелла
!!en;

; Башня (176/185 Храмовница) - ремонт (базовая мощность 100 * 2 = 200)
!!if&i^js_amethyst^;
  !!MA:W(MON_TEMPLE_GUARDIAN_A)/^        
    Spell 1=0
    Spell 2=10
    Spell 3=0
  ^;
  !!MA:W(MON_TEMPLE_GUARDIAN_D)/^        
    Spell 1=0
    Spell 2=10
    Spell 3=0
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_5.png^/^Data\Defs\JS_npc1.def\0_5.png^;
  // Замена имени спелла
  *!SN:E7827723/(CALLCONV_CDECL)/12/2/(ZNPC00TXT);
  *!SN:Bv1/d/^%T(jsCommanders.176.spell.name)^;
  // Замена описания спелла
  *!SN:E7827723/(CALLCONV_CDECL)/21/2/(ZNPC00TXT);
  *!SN:Bv1/d/^%T(jsCommanders.176.spell.description)^;
!!en;

; Инферно (177/186 Суккуб) - демонение (базовая мощность 50 * 2 = 100)
!!if&i^js_amethyst^;
  !!MA:W(MON_SUCCUBUS_A)/^        
    Spell 1=2
    Spell 2=0
    Spell 3=0
    Demonology=%(MON_DEMON)
  ^;
  !!MA:W(MON_SUCCUBUS_D)/^        
    Spell 1=2
    Spell 2=0
    Spell 3=0
    Demonology=%(MON_DEMON)
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_7.png^/^Data\Defs\JS_npc1.def\0_7.png^;
  // Замена имени спелла
  // Замена описания спелла
!!en;

; Некрополис (178/187 Пожиратель Душ) - анимейт (базовая мощность 100 = 100)
// Фикс решения вог-команды убрать возможность анимейтить мертвый стек
!!if&i^js_amethyst^;
  !!MA:W(MON_SOUL_EATER_A)/^        
    Spell 1=0
    Spell 2=9
    Spell 3=0
  ^;
  !!MA:W(MON_SOUL_EATER_D)/^        
    Spell 1=0
    Spell 2=9
    Spell 3=0
  ^;
  // Замена описания спелла
  *!SN:E7827723/(CALLCONV_CDECL)/23/2/(ZNPC00TXT);
  *!SN:Bv1/d/^%T(jsCommanders.178.spell.description)^;
!!en;

; Темница (179/188 Зверь)
!!if&i^js_amethyst^;
  !!FU(js_GetSpellIdByCommander):P(MON_BRUTE_TYPE)/?(spellId:y);
  !!MA:W(MON_BRUTE_A)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  !!MA:W(MON_BRUTE_D)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_11.png^/^Data\Defs\JS_npc1.def\0_11.png^;
  // Замена имени спелла
  // Замена описания спелла
!!en;

; Цитадель (180/189 Вождь Огров)
!!if&i^js_amethyst^;
  !!FU(js_GetSpellIdByCommander):P(MON_OGRE_LEADER_TYPE)/?(spellId:y);
  !!MA:W(MON_OGRE_LEADER_A)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  !!MA:W(MON_OGRE_LEADER_D)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_13.png^/^Data\Defs\JS_npc1.def\0_13.png^;
  // Замена имени спелла
  // Замена описания спелла
!!en;

; Крепость (181/190 Шаман)
!!if&i^js_amethyst^;
  !!FU(js_GetSpellIdByCommander):P(MON_SHAMAN_TYPE)/?(spellId:y);
  !!MA:W(MON_SHAMAN_A)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  !!MA:W(MON_SHAMAN_D)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_15.png^/^Data\Defs\JS_npc1.def\0_15.png^;
  // Замена имени спелла
  // Замена описания спелла
!!en;

; Сопряжение (182/191 Астральный Дух)
!!if&i^js_amethyst^;
  !!FU(js_GetSpellIdByCommander):P(MON_ASTRAL_SPIRIT_TYPE)/?(spellId:y);
  !!MA:W(MON_ASTRAL_SPIRIT_A)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  !!MA:W(MON_ASTRAL_SPIRIT_D)/^        
    Spell 1=10
    Spell 2=7
    Spell 3=2
    CastsSpell=%(spellId)
  ^;
  // Замена def спелла командира
  !!SN:R^dlg_npc1.def:0_17.png^/^Data\Defs\JS_npc1.def\0_17.png^;
  // Замена имени спелла
  // Замена описания спелла
!!en;


************************************************************************* SPELLS - IMPROVED ANIMATE/REPAIR/RESURRECT
!?FU(js_BS_GetResurrectCount_AfterAmethystCalc);
!!FU(js_BattleMgr_ResurrectArchangelProcessing):Px1/0;

!?FU(js_BattleStack_CanCastAtEmptyHex_AfterCalcMagicPower);
!!FU(js_BattleMgr_ResurrectArchangelProcessing):Px1/1;

!?FU(js_BattleMgr_ResurrectArchangel_OnEnter);
!!FU(js_BattleMgr_ResurrectArchangelProcessing):Px1/2;

!?FU(js_BattleStack_GetMagicResistance_OnCheckMagicPower);
!!FU(js_BattleMgr_ResurrectArchangelProcessing):Px1/3;


************************************************************************* SPELLS - IMPROVED DEMONOLOGY
!?FU(js_BS_GetResurrectCount_AfterAmethystCalc2);
!!FU(js_BattleMgr_ResurrectArchangelProcessing):Px1/10;

!?FU(js_BattleStack_CanCastAtEmptyHex_AfterCalcMagicPower2);
!!FU(js_BattleMgr_ResurrectArchangelProcessing):Px1/11;


************************************************************************* SPELLS - CALCULATIONS FOR DAMAGING SPELLS
!?FU(OnMagicBasicResistance);
!!BG:N?(activeStackNum:y);
!!BM(activeStackNum):T?(monType:y);

!!if&(monType)>=(MON_COMMANDER_FIRST_A)/(monType)<=(MON_COMMANDER_LAST_D);
  !!FU(js_GetActualStackSide):P(activeStackNum)/?(side:y);
  !!BA:H(side)/?(heroId:y);

  !!if&(heroId)>=0;
    !!FU(js_GetCommanderByHeroId):P(heroId)/?(commander:y);
    !!UN:C(commander)/12/4/?(type:y); C
    !!UN:C(commander)/72/4/?(spLvl:y); 48
    !!UN:C(commander)/284/4/?(lvl:y); 11C

    !!HE(heroId):Z?(hero:y);
    !!FU(js_GetSpellIdByCommander):P(type)/?(spellId:y);

  !!el;
    ; 76F2D6
    !!CO-4:T?(type:y);
    !!CO-4:S4/?(spLvl:y);
    !!CO-4:X2/?(lvl:y);
  !!en;

  !!FU(js_RecalcComandersSpPower):P(type)/(spLvl)/(lvl)/?(power:y);
  ;!IF:L^power= %(power)^;

  !!if&(heroId)>=0;
    ; 004E59D0 __thiscall Hero_GetSorceryEffect(_Hero_ *hero, eSpells spellId, signed int damage, _BattleStack_ *target)
    !!SN:E5134800/(CALLCONV_THISCALL)/(hero)/(spellId)/(power)/0;
    !!VR(power):Sv1;
    ;!IF:L^power2= %(power)^;
  !!en;

  !!MR:D(power);
!!en;


************************************************************************* SPELLS - CALCULATIONS FOR RESURRECT TYPE SPELLS
!?FU(js_BattleMgr_ResurrectArchangelProcessing);
!#VA(hook:x) (actionType:x);
; actionType = 0 means it is trigger (mouseover calcs) ON LIVING STACK - ANIMATE/REPAIR/RESURRECT
; actionType = 10 means it is actual spellcasting process (lmc done) - DEMONOLOGY
; actionType = 1 means it is trigger (mouseover calcs) ON AN EMPTY HEX (DEAD STACK) - ANIMATE/REPAIR/RESURRECT
; actionType = 11 means it is trigger (mouseover calcs) ON AN EMPTY HEX (DEAD STACK) - DEMONOLOGY
; actionType = 2 means it is actual spellcasting process (lmc done) - ANIMATE/REPAIR/RESURRECT
; actionType = 3 means it is calculations during magic resistance checks - ANIMATE/REPAIR/RESURRECT

;!IF:L^actionType= %(actionType)^;

!!BG:N?(activeStackNum:y);
!!BM(activeStackNum):T?(monType:y);

!!if&(monType)>=(MON_COMMANDER_FIRST_A)/(monType)<=(MON_COMMANDER_LAST_D);
  !!FU(js_GetActualStackSide):P(activeStackNum)/?(side:y);
  !!BA:H(side)/?(heroId:y);
  !!FU&(heroId)<0:E;

  !!FU(js_GetCommanderByHeroId):P(heroId)/?(commander:y);
  !!UN:C(commander)/12/4/?(type:y); C
  !!UN:C(commander)/72/4/?(spLvl:y); 48
  !!UN:C(commander)/284/4/?(lvl:y); 11C

  // Do math

  !!FU(js_RecalcComandersSpPower):P(type)/(spLvl)/(lvl)/?(power:y);
  ;!IF:L^powerAfterRecal= %(power)^;

  !!if&(actionType)=2; patch spellPower during the cast
    !!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/12/4/(power);

  !!el&(actionType)=10; patch spellPower during the demonology casting
    !!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EAX)/4/(power);
    ;!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/(power);

  !!el&(actionType)=11; patch spellPower during the demonology mouseover empty hex
    !!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/8/4/(power);

  !!el&(actionType)=3; patch spellPower during the magic resistance calcs
    !!UN:C(hook)/(STRUCT_HOOK_CONTEXT_ESI)/4/(power);

  !!el|(actionType)=0/(actionType)=1; patch qty of resurrected creatures
    !!CM:D?(mouseHexId:y);
    !!FU(js_GetStackByHexId):P(mouseHexId)/?(target:y);
    !!FU(js_GetDeadStackByHexId)&(target)=0:P(mouseHexId)/?(target:y);
    !!UN:C(target)/108/4/?(targetHp:y); 6C
    ;!IF:L^targetHp= %(targetHp)^;

    !!if&(power)>=(targetHp);
      !!VR(monResQty:y):S(power) :(targetHp);
      ;!IF:L^monResQty2= %(monResQty)^;
      !!UN&(actionType)=0:C(hook)/(STRUCT_HOOK_CONTEXT_EAX)/4/(monResQty);
      !!UN&(actionType)=1:C(hook)/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/(monResQty);
    !!en;
  !!en;
!!en;


!?FU(js_RecalcComandersSpPower);
!#VA(type:x) (spLvl:x) (lvl:x) (newPower:x);

!!if&(type)=0;             Paladin - Destroy Undead
  !!VR(power:y):S40;
  !!VR(lvlScaling:y):S10;
  !!VR(spScaling:y):S20;
!!el&(type)=1;             Hierophant - Resurrect
  !!VR(power:y):S100;
  !!VR(lvlScaling:y):S10;
  !!VR(spScaling:y):S50;
!!el&(type)=2;             Temple Guardian - Repair
  !!VR(power:y):S200;
  !!VR(lvlScaling:y):S10;
  !!VR(spScaling:y):S50;
!!el&(type)=3;             Succubus - Demonology
  !!VR(power:y):S100;
  !!VR(lvlScaling:y):S10;
  !!VR(spScaling:y):S50;
!!el&(type)=4;             Soul Eater - Animate Dead
  !!VR(power:y):S100;
  !!VR(lvlScaling:y):S10;
  !!VR(spScaling:y):S50;
!!el&(type)=5;             Brute - Implosion
  !!VR(power:y):S50;
  !!VR(lvlScaling:y):S15;
  !!VR(spScaling:y):S30;
!!el&(type)=6;             Ogre - Magic Arrow
  !!VR(power:y):S50;
  !!VR(lvlScaling:y):S15;
  !!VR(spScaling:y):S30;
!!el&(type)=7;             Shaman - Ice Bolt
  !!VR(power:y):S50;
  !!VR(lvlScaling:y):S15;
  !!VR(spScaling:y):S30;  
!!el&(type)=8;             Astral Spirit - Lighning Bolt
  !!VR(power:y):S50;
  !!VR(lvlScaling:y):S15;
  !!VR(spScaling:y):S30;
!!en;

!!VR(bonusForLvl:y):S(lvl) *(lvlScaling);
!!VR(bonusForSpLvl:y):S(spLvl) *(spScaling);
!!VR(newPower):S(power) +(bonusForSpLvl) +(bonusForLvl);


!?FU(js_AI_Battle_GetStackCastSpellMaxValue_BeforeCases);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(caster:y) C(caster)/52/4/?(monType:y); 34
;!IF:L^monType= %(monType)^;

!!if|(monType)=(MON_PALADIN_A)/(monType)=(MON_PALADIN_D);
  ;!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/(monType);
  !!SN:X?y99/0;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4330682; 004214BA
!!en;

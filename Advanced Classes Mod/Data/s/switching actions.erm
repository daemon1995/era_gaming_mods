ZVSE2

!#SN:W^RETURN_SHOTSCASTS_ON^/1;
!#SN:W^X_HIDE_AMMO_STACK^/-1;
!#VRi^switch_enabled^:S(TRUE);

!?FU(OnSetupBattlefield)&i^switch_enabled^;
!!SN:W^X_HIDE_AMMO_SHOTS^/0;
!!SN:W^X_HIDE_AMMO_CASTS^/0;
!!SN:W^X_HIDE_AMMO_STACK^/-1;

!?FU(OnBattleStackObtainsTurn)&i^switch_enabled^;
!#VA(stackSide:x) (stackInd:x);

!!SN:X?y1/?y2;
!!VRy3:Sy1 *21 +y2;
!!BMy3:U3/?y9 E?y10;

!!SN:W^X_HIDE_AMMO_STACK^/y3;

!!BMy3:T?y99;
!!FU|y99=182/y99=191:E;     182 Astral Spirit (attacker) 191  Astral Spirit (defender)

!!SN:W^X_Cast_Move_Old^/?y1;
!!VRy10&y1>0:Sv786;

!!SN:W^X_Cast_Move_New^/?y1;
!!SN:W^Casts_Left_Stack_%Y3^/?y2;
!!VRy10&y1>0:Sy2;

!!SN:W^X_HIDE_AMMO_SHOTS^/y9;
!!SN:W^X_HIDE_AMMO_CASTS^/y10;

*!IF:M^hidden shots/casts set to %Y9/%Y10^;

!?FU(OnBeforeBattleAction)&i^switch_enabled^;
!!BG:A?y6;
!!FU|y6<=1:E;

!!SN:W^X_HIDE_AMMO_SHOTS^/?y9;
!!SN:W^X_HIDE_AMMO_CASTS^/?y10;
!!SN:W^X_HIDE_AMMO_STACK^/?y11;
!!SN:W^X_HIDE_AMMO_STACK^/-1;
*!IF:M^hidden shots/casts/stack = %Y9/%Y10/%Y11 action=%Y6^;

!!FU|y11<(BATTLE_STACK_FIRST)/y11>(BATTLE_STACK_LAST):E;

!!BMy11:T?y99;
!!FU|y99=182/y99=191:E;     182 Astral Spirit (attacker) 191  Astral Spirit (defender)

!!BMy11:T?y7 F?y8 U3/?y3 E?y4;
!!VRy8:&4;
!!BMy11&y6<>7/y9>0/y3=0:U3/y9;
!!BMy11&y6<>10/y10>0/y4=0/y4<>12:Ey10;

!!SN:L^Era.dll^/?y1 Ay1/^GetGameState^/?y2 Ey2/0/?y10;
!!FU|y10<>4205280/y11<>4662240:E;

!?FU(OnKeyPressed_Battle)&i^switch_enabled^;
!#VA(key:x) (preventDefault:x);

!!SN:W^X_HIDE_AMMO_STACK^/?y11;
!!FU|y11<(BATTLE_STACK_FIRST)/y11>(BATTLE_STACK_LAST):E;

!!BMy11:T?y99;
!!FU|y99=182/y99=191:E;     182 Astral Spirit (attacker) 191  Astral Spirit (defender)

!!if&(key)=49;
  !!VR(preventDefault):S(TRUE);
  !!BMy11:U3/0 E0;

!!el&(key)=50;
  !!VR(preventDefault):S(TRUE);
  !!SN:W^X_HIDE_AMMO_SHOTS^/?y9;
  !!BMy11:U3/y9 E0;

!!el&(key)=51;
  !!VR(preventDefault):S(TRUE);
  !!SN:W^X_HIDE_AMMO_CASTS^/?y10;
  !!BMy11:Ey10;
!!en;

ZVSE2
; Author:   Valery, renewed by Archer30
; Special thanks to Hawaiing
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Select your battlefield, mod for Era 2.46 or higher

!?FU(OnKeyPressed_Battle)&i^key^=(KEY_F2);
!!SN:W^Change_terrain^/-1;              [reset]
!!DL448:N^terrain.txt^;
!!DO4122144/2/10/1:P;                   [prepare items 2-10 cadres]
!!DO4122146/32/40/1:P;                  [same]
!!DL448:S1;                             [show dialog]

!?FU4122144;                            [initialize cadres]
!!VR(ind:y):Sx16-2;
!!DL448:Ax16/4/(ind)/1;

!?FU4122146;
!!DL448:Ax16/4/0/1;

!?FU(OnCustomDialogEvent)&i^dlg_id^=448/i^mouse_item^=30722/i^mouse_action^=(MOUSE_LMB_RELEASED); close dialog
!!DL:C1;
!!SN:W^Change_terrain^/?(chosenItem:y); [see if player selected battlefield (if no, will be -1)]

!!if&(chosenItem)>0;
  !!VR(terrain:y):S(chosenItem)-1;      [if player selected top terrain (v999=1/10)]
  !!FU(selbatt_ChangeBackground):P(terrain);

  !!BU&(chosenItem)<11:G(terrain);      [set top terrain]
!!en;

!?FU(OnCustomDialogEvent)&i^dlg_id^=448/i^mouse_item^>1/i^mouse_item^<11/i^mouse_action^=(MOUSE_LMB_RELEASED);
!!DO4122145/2/10/1:P;                   [reset all cadres]
!!DO4122146/32/40/1:P;                  [same]
!!VR(ind:y):Si^mouse_item^+7;
!!DL448:Ai^mouse_item^/4/(ind)/1;
!!VR(ind2:y):Si^mouse_item^+30;
!!DL448:A(ind2)/4/1/1;
!!VR(sound:z):S^Button.wav^;
!!SN:P(sound);
!!SN:W^Change_terrain^/i^mouse_item^;   [store player choice]

!?FU4122145;
!!VR(cadre:y):Sx16-2;                   [set all cadres to disabled]
!!DL448:Ax16/4/(cadre)/1;

!?FU(OnCustomDialogEvent)&i^dlg_id^=448/i^mouse_item^=38/i^mouse_action^=(MOUSE_LMB_RELEASED); randomizer
!!DL:C1;
!!VR(terrain:y):S1 R8;
!!FU(selbatt_ChangeBackground):P(terrain); [Hawaiing - change background immediately]

!!BU:G(terrain);

!?FU(selbatt_ChangeBackground);         [Hawaiing]
!#VA(terrain:x);

!!VR(ert:y):S(terrain) +168000;
!!UN:C(COMBAT_MANAGER)/4/?(value:y);    [Address of the battlefield]
!!VR(add:y):S(value)+78948;
!!SN:E7824928/1/(ert);                  [export the address of ert to v1]
!!UN:C(add)/4/v1;                       [get pcx string from ert]
!!VR(add2:y):S(value)+21432;
!!UN:C(add2)/4/0;
!!SN:E4798576/2/(value);
!!SN:D;
ZVSE2
; Author:   Valery, renewed by Archer30
; Special thanks to Hawaiing
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Select your enhanced battlefields by pressing F2 in the battle


; Open the dialogue by pressing F2
!?FU(OnKeyPressed_Battle)&i^key^=(KEY_F2)/i^battle_humanOnly^=(FALSE);
!!VRi^cusbatt_terrain^:S-1;
!!DL(CUSBATT_MAIN_DLG):N^custom_bf.txt^ S1; [show dialog]

; Close the dialogue by mouse click (or pressing Enter/Esc)
!?FU(OnCustomDialogEvent)&i^dlg_id^=(CUSBATT_MAIN_DLG)/i^mouse_item^=30722/i^mouse_action^=(MOUSE_LMB_RELEASED);
!!DL:C1;

!!if&i^cusbatt_terrain^>-1;
  !!FU(cusbatt_ChangeBackgroundPic):Pi^cusbatt_terrain^;
  !!BU:Gi^cusbatt_terrain^;
!!en;

; Choose a enhanced background
!?FU(OnCustomDialogEvent)&i^dlg_id^=(CUSBATT_MAIN_DLG)/i^mouse_item^>1/i^mouse_item^<11/i^mouse_action^=(MOUSE_LMB_PRESSED);
; Store the previous choice of battlefield and the new one
!!VR(originalTerrain:y):Si^cusbatt_terrain^;
!!VRi^cusbatt_terrain^:Si^mouse_item^ -1;

; Refresh highlight of the backgroudn item
!!if|(originalTerrain)=-1/(originalTerrain)<>i^cusbatt_terrain^;
  !!VR(highlightOri:y):S(originalTerrain) +32;
  !!DL(CUSBATT_MAIN_DLG):A(highlightOri)/(DLG_CMD_SET_DEF_FRAME)/0/1;

  !!VR(highlight:y):Si^mouse_item^ +30;
  !!DL(CUSBATT_MAIN_DLG):A(highlight)/(DLG_CMD_SET_DEF_FRAME)/1/1;

  !!SN:P^Button.wav^;
!!en;

; Randomly choose a background and apply on the battlefield
!?FU(OnCustomDialogEvent)&i^dlg_id^=(CUSBATT_MAIN_DLG)/i^mouse_item^=38/i^mouse_action^=(MOUSE_LMB_RELEASED);
!!DL:C1;
!!VR(terrain:y):R0/1/9;
!!FU(cusbatt_ChangeBackgroundPic):P(terrain);
!!BU:G(terrain);

!?FU(cusbatt_ChangeBackgroundPic);      [Hawaiing - change background immediately]
!#VA(terrain:x);

!!VR(ertIndex:y):S(terrain) +177300;
!!UN:C(COMBAT_MANAGER)/4/?(value:y);    [Address of the battlefield]
!!VR(address:y):S(value) +78948;
!!SN:E7824928/1/(ertIndex);             [Export the address of ert to v1]
!!UN:C(address)/(UNC_INT)/v1;           [Get pcx string from ert]
!!VR(address2:y):S(value) +21432;
!!UN:C(address2)/(UNC_INT)/0;
!!SN:E4798576/2/(value);

!!SN:D;

ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.9+, Era Erm Framework

; Show coordinates
; The following lines are the best executed at the very end of all the scripts. They need to be later than difm - era 3.erm to be compatible with Diff Mod/Mixed Neutrals


!?FU(trainer_SetUpCoordScanner);
!!if&i^trainer_coord_on^;
  !!IF:L^%T(trainer.addCoordOff)^;
  !!VRi^trainer_coord_on^:S(FALSE);
!!el;
  !!SN:P^Military.wav^;
  !!IF:L^%T(trainer.addCoordOn)^;
  !!VRi^trainer_coord_on^:S(TRUE);
!!en;

!?FU(OnAdvMapTileHint)&i^trainer_coord_on^;
!#VA(x:x) (y:x) (z:x);             Object entrance coordinates (SN:O)
!#VA(objType:x) (objSubtype:x);    Type and subtype of object. For active hero it's object under hero.
!#VA(tileX:x) (tileY:x) (tileZ:x); Real tile coordinates.

; Fix object type and subtype in general. Here we got from the real coordinates for fixing object stacking (eg. hero standing at the entrance of a town)
!!OB(tileX)/(tileY)/(tileZ):T?(objType) U?(objSubtype);

; Fix incorrect hero ID
!!MM:M?(existingHint:z);
!!HE(x)/(y)/(z)&(objType)=(OBJ_HERO):N?(objSubtype); [fix subtype of heroes]

!!SN:T^trainer.hintCoord^/?(hintCoord:z)/^x^/(tileX)/^y^/(tileY)^z^/(tileZ)/^objType^/(objType)/^objSubtype^/(objSubtype);

; Special treatment for towns - add town ID at the end of line
!!if&(objType)=(OBJ_TOWN);
  !!CA(x)/(y)/(z):U?(townId:y);
  !!VR(hintCoord):+^ [%(townId)]^;
!!en;

!!MM:M^%(existingHint)%(hintCoord)^;

ZVSE2
; Author:   Hawaiing, edited by Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Press X to execute ERM commands.


!?FU(OnKeyPressed_AdvMap)&i^key^=(KEY_X);
!#VA(key:x) (preventDefault:x) (isDown:x);
!!VR(preventDefault):S(TRUE);
!!FU(execerm_Main):P;

!?FU(OnKeyPressed_Battle)&i^key^=(KEY_X);
!#VA(key:x) (preventDefault:x) (isDown:x);
!!VR(preventDefault):S(TRUE);
!!FU(execerm_Main):P;

!?FU(OnKeyPressed_HeroScreen)&i^key^=(KEY_X);
!#VA(key:x) (preventDefault:x) (isDown:x);
!!VR(preventDefault):S(TRUE);
!!FU(execerm_Main):P;

!?FU(OnKeyPressed_HeroMeetingScreen)&i^key^=(KEY_X);
!#VA(key:x) (preventDefault:x) (isDown:x);
!!VR(preventDefault):S(TRUE);
!!FU(execerm_Main):P;

!?FU(OnKeyPressed_Town)&i^key^=(KEY_X);
!#VA(key:x) (preventDefault:x) (isDown:x);
!!VR(preventDefault):S(TRUE);
!!FU(execerm_Main):P;

!?FU(execerm_Main);
!!VRf:S0;
!!SN:T^execerm.title^/?z2;

!!re y1/0/99/1;
  !!SN&f=0:T^execerm.legend^/?z3;
  !!SN&f>0:T^execerm.legend2^/?z3;
  !!VRz4:S^^;
  !!IF:V2/1;
  !!IF:D2/2/3/4;
  !!IF:E1/2;
  !!VRz1:H1;
  !!IF|-1/v1=-1:V2/0;
  !!VRf&2:+1;
  !!SN&2:W^trainer_execErm.%f^/^%Z1^;
  
  !!br&-2;
!!en;

!!FU&f<1:E;

!!SN:L^Era.dll^/?y2 Ay2/^ExecErmCmd^/?y100;

!!re y1/1/f/1;
  !!SN:W^trainer_execErm.%Y1^/?z1;
  !!UN:P904/?t P904/1 P905/0;
  !!SN:Ey100/1/z1;
  !!UN:P904/t P905/?i;

  !!if&i>0;
    !!SN:T^execerm.error^/?z30/^error^/z1;
    !!IF:M^%z30^;
  !!en;

  !!SN:W^trainer_execErm.%Y1^;
!!en;
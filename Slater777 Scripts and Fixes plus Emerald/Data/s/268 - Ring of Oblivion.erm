ZVSE2


// To-do: Restrain the use of Attract Dead Souls ability


!?FU(OnAfterBattleSetup);
!!VRi^js_%(ART_RING_OF_OBLIVION)_equipped^:S(FALSE);

!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!HEi^battle_hero_%i^:A2/(ART_RING_OF_OBLIVION)/(has:y)/?(equipped:y);

  !!if&(equipped);
    !!VRi^js_%(ART_RING_OF_OBLIVION)_equipped^:S(TRUE);
    !!br;
  !!en;
!!en;


; Get BM:B values
; BM:B values would be restored on battle replay
!?FU(OnSetupBattlefield)&i^js_%(ART_RING_OF_OBLIVION)_equipped^;
!!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST);
  !!BMi:B?i^js_%(ART_RING_OF_OBLIVION)_initNum_%i^;
!!en;


; Disable Necromancy
!?FU(js_OnCalcNecromancyPower)&i^js_%(ART_RING_OF_OBLIVION)_equipped^;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/0;


!?FU(js_OnAfterDoPhysicalDamage)&i^js_%(ART_RING_OF_OBLIVION)_equipped^;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(currentStackCount:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(killCount:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(currentStackPtr:y);

!!UN:C(currentStackPtr)/84/4/d(killCount);
!!UN:C(currentStackPtr)/244/4/?(side:y) C(currentStackPtr)/248/4/?(currentStack:y);

!!VR(currentStack)&(side)=(BATTLE_RIGHT):+(BATTLE_STACKS_PER_SIDE);  [For right side]

; Set beforeBattleMonCount to currentMonCount - it prevents casting resurection/animate and vampire reviving
!!BM(currentStack):B(currentStackCount);


; For spells
!?FU(OnBattleActionEnd)&i^js_%(ART_RING_OF_OBLIVION)_equipped^;
!!BG:A?(action:y);

!!if|(action)=(BATTLE_ACTION_HERO_CAST)/(action)=(BATTLE_ACTION_MONSTER_CAST);
  !!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST);
    !!BMi:N?(num:y) B(num);
  !!en;
!!en;


!?FU(js_OnBeforeBattleResult)&i^js_%(ART_RING_OF_OBLIVION)_equipped^;
!!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST);
  !!BMi:Bi^js_%(ART_RING_OF_OBLIVION)_initNum_%i^ N?(num:y);
  !!BMi&(num)>i^js_%(ART_RING_OF_OBLIVION)_initNum_%i^:Ni^js_%(ART_RING_OF_OBLIVION)_initNum_%i^;

  !!SN:W^js_%(ART_RING_OF_OBLIVION)_initNum_%i^;
!!en;


; Prevent Phoenix resurrects
!?FU(js_OnBattleStackKilled)&i^js_%(ART_RING_OF_OBLIVION)_equipped^;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(stackPtr:y);
!!FU&(stackPtr)=(NULL):E;
!!UN:C(stackPtr)/244/4/?(side:y) C(stackPtr)/248/4/?(stackOfSide:y);
!!VR(stackId:y):S(side) *(BATTLE_STACKS_PER_SIDE) +(stackOfSide);
!!FU|(stackId)<=(NO_STACK)/(stackId)>(BATTLE_STACK_LAST):E;
!!BM(stackId):E0 Fd|(MON_FLAG_SACRIFICED);


!?FU(OnAfterBattle)&i^js_%(ART_RING_OF_OBLIVION)_equipped^;
!!VRi^js_%(ART_RING_OF_OBLIVION)_equipped^:S(FALSE);


; Fix - if player loads the game during the battle
!?FU(OnAfterLoadGame);
!!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST);
  !!SN:W^js_%(ART_RING_OF_OBLIVION)_initNum_%i^;
!!en;

ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor (Idea: Ivor)
** Name          : Mana shrines
** Name rus.     : Святыни маны
** Options       : 749
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7978
** PO-values     : -

** Святилища ежедневно накапливают магическую энергию, которая восстанавливает ману первому посетившему герою.
** Количество восстанавливаемой маны - 5 х Уровень святилища, но не больше максимального запаса героя.

!$OB88;                       [После посещения святыни 1 уровня]
!!UN:P749/?y1;                [проверяем включена ли опция 749 в y1]
!!FU7978&y1>0:Pv998/v999/v1000/5; [вызов функции восполнения маны]

!$OB89;                       [После посещения святыни 2 уровня]
!!UN:P749/?y1;                [проверяем включена ли опция 749 в y1]
!!FU7978&y1>0:Pv998/v999/v1000/10; [вызов функции восполнения маны]

!$OB90;                       [После посещения святыни 3 уровня]
!!UN:P749/?y1;                [проверяем включена ли опция 749 в y1]
!!FU7978&y1>0:Pv998/v999/v1000/15; [вызов функции восполнения маны]

!?FU7978;                     [Функция восполнения маны]
!!VRy2:Sc;                    [y2 - текущий день]
!!SN:W^Manashrine_%X1_%X2_%X3^/?y1 W^Manashrine_%X1_%X2_%X3^/y2; [y1 - день предыдущего посещения святыни]
!!FU&y1=y2:E;                  [выход, если святыня сегодня уже посещалась]
!!HE-1:I?y8/1 Fd/d/d/?y5 Ed/?y6; [y8/y5/y6 - мана/знание/уровень героя]
!!VRy5:*10;                   [y5 базовый макс. запас маны героя]
!!HE-1:S24/?y2;               [y2 - уровень навыка Интеллект [0..3]]
!!HE-1:X?y3/?y4/d/d/d/d/d;    [для специалистов по Интеллекту y3=0,y4=24]
!!VRy7:S0;                    [------------------------------------------]
!!VRy7&y2=1:Sy5 :4;           [                                          ]
!!VRy7&y2=2:Sy5 :2;           [ y7 - бонус запаса маны от Интеллекта     ]
!!VRy7&y2=3:Sy5;              [     и специализации Интеллект            ]
!!VRe99&y3=0/y4=24:Sy6 :20 +1;[                                          ]
!!VRy7&y3=0/y4=24:*e99;       [------------------------------------------]
!!VRy5:+y7;                   [y5 макс. запас маны героя с учетом Интеллекта и специализации]
!!FU&y8>=y5:E;                [выход, если восполнение маны не требуется]
!!VRy5:-y8;                   [макс. кол-во маны для восполнения]
!!VRx4&x4>y5:Sy5;             [x4 - восполняемое кол-во маны]
!!HE-1:Idx4/1;                [восполнение маны]

**end

ZVSE2
** Author orig.  : Algor
** Name          : Peons
** Name rus.     : Ѕатраки
** Options       : 785

; Warning: Currenlty Peons allow the hero to get rid of all the troops, it's better be optimised.


!?FU(OnTownMouseClick)&i^mouse_action^=(MOUSE_LMB_RELEASED)/999;
!!UN:P785/?y1;
!!FU&y1=0:E;                  [выход если опци€ 785 не включена]

!!SN:F^GetButtonID^/^peons^;
!!FU&i^mouse_item^<>v1:E;     [выход, если клик не по кнопке батраков]

!!CM:R0;                      [отмена действи€ по умолчанию]

!!CA(CURRENT_TOWN):O?y2;
!!OW:C?y3;
!!FU&y2<>y3:E; [выход, если город не принадлежит игроку (город союзника)]

!!CA(CURRENT_TOWN):H0/?y6;                [y6 - номер геро€ в гарнизоне]

!!if&y6>(NO_HERO);
  !!HEy6:C0/0/?y10/?y20 C0/1/?y11/?y21 C0/2/?y12/?y22 C0/3/?y13/?y23 C0/4/?y14/?y24 C0/5/?y15/?y25 C0/6/?y16/?y26; [ѕолучаем армию геро€]
  !!CA(CURRENT_TOWN):M2/0/y10/y20 M2/1/y11/y21 M2/2/y12/y22 M2/3/y13/y23 M2/4/y14/y24 M2/5/y15/y25 M2/6/y16/y26; [копируем армию геро€ в гарнизон]
!!en;

!!CA(CURRENT_TOWN):U?y60;
!!VRy4:Si^es_785_peons_%y60^;

; Set the mode as 0 by default (using the garrison)
!!VRy80:S0;

!!CA(CURRENT_TOWN):M2/0/?y10/?y20 M2/1/?y11/?y21 M2/2/?y12/?y22 M2/3/?y13/?y23 M2/4/?y14/?y24 M2/5/?y15/?y25 M2/6/?y16/?y26; [y1х/y2х - тип и количество монстров в слоте х гарнизона]

; Use the visiting hero if there is no troop in the garrison (mode 1)
!!if&y20=0/y21=0/y22=0/y23=0/y24=0/y25=0/y26=0;
  !!CA(CURRENT_TOWN):H1/?y6;

  !!if&y6>(NO_HERO);
    !!VRy80:S1;

    !!HEy6:C0/0/?y10/?y20 C0/1/?y11/?y21 C0/2/?y12/?y22 C0/3/?y13/?y23 C0/4/?y14/?y24 C0/5/?y15/?y25 C0/6/?y16/?y26;
  !!en;
!!en;

!!re i/10/16;
  ; Skip if the creature doesn't exist
  !!VRj:Si +10;
  !!co&yj<=0;

  ; Get the peon power for special creatures
  !!FU(ES_785_GetPeonPower):Pyi/?y70;
  !!VRy70:F1/(INT_MAX);

  ; Calculate how much each creature worth with FV
  !!MA:Fyi/?y71;
  !!VRj:Si +20;
  !!VRyj:Sy71 *y70;
!!en;

!!VRy40&y20>0:Sy30 *y20 :50;  [y40 - доход от монстров в слоте 0 гарнизона FV*n:50 ]
!!VRy41&y21>0:Sy31 *y21 :50;  [y41 - доход от монстров в слоте 1 гарнизона FV*n:50 ]
!!VRy42&y22>0:Sy32 *y22 :50;  [y42 - доход от монстров в слоте 2 гарнизона FV*n:50 ]
!!VRy43&y23>0:Sy33 *y23 :50;  [y43 - доход от монстров в слоте 3 гарнизона FV*n:50 ]
!!VRy44&y24>0:Sy34 *y24 :50;  [y44 - доход от монстров в слоте 4 гарнизона FV*n:50 ]
!!VRy45&y25>0:Sy35 *y25 :50;  [y45 - доход от монстров в слоте 5 гарнизона FV*n:50 ]
!!VRy46&y26>0:Sy36 *y26 :50;  [y46 - доход от монстров в слоте 6 гарнизона FV*n:50 ]
!!UN&y20>0:N3/10/y10/1;        [z1 - наименование монстра в слоте 0 гарнизона]
!!UN&y21>0:N3/11/y11/1;        [z2 - наименование монстра в слоте 1 гарнизона]
!!UN&y22>0:N3/12/y12/1;        [z3 - наименование монстра в слоте 2 гарнизона]
!!UN&y23>0:N3/13/y13/1;        [z4 - наименование монстра в слоте 3 гарнизона]
!!UN&y24>0:N3/14/y14/1;        [z5 - наименование монстра в слоте 4 гарнизона]
!!UN&y25>0:N3/15/y15/1;        [z6 - наименование монстра в слоте 5 гарнизона]
!!UN&y26>0:N3/16/y16/1;        [z7 - наименование монстра в слоте 6 гарнизона]
!!VRv1:S0;                    [обнул€ем v1 - результат выбора отр€дов в диалоге]

!!SN:T^es.785.opt^/?z1/^mon^/z10/^num^/y20/^gold^/y40;
!!SN:T^es.785.opt^/?z2/^mon^/z11/^num^/y21/^gold^/y41;
!!SN:T^es.785.opt^/?z3/^mon^/z12/^num^/y22/^gold^/y42;
!!SN:T^es.785.opt^/?z4/^mon^/z13/^num^/y23/^gold^/y43;
!!SN:T^es.785.opt^/?z5/^mon^/z14/^num^/y24/^gold^/y44;
!!SN:T^es.785.opt^/?z6/^mon^/z15/^num^/y25/^gold^/y45;
!!SN:T^es.785.opt^/?z7/^mon^/z16/^num^/y26/^gold^/y46;

!!SN:T^es.785.dlg1^/?z8/^gold^/y4; [получаем полный заголовок сообщени€]

!!if&y20=0/y21=0/y22=0/y23=0/y24=0/y25=0/y26=0;
  !!SN:T^es.785.dlg2^/?z8/^gold^/y4; [получаем короткий заголовок сообщени€, если в гарнизоне пусто]
  !!IF:Mz8;
  !!FU:E;
!!en;

!!VRy50:S0;                   [обнуление переменной]
!!VRy51:S0;                   [обнуление переменной]
!!VRy52:S0;                   [обнуление переменной]
!!VRy53:S0;                   [обнуление переменной]
!!VRy54:S0;                   [обнуление переменной]
!!VRy55:S0;                   [обнуление переменной]
!!VRy56:S0;                   [обнуление переменной]
!!VRy50&y20>0:S1;             [отображение опции 1 в диалоге, если есть отр€д в слоте 0]
!!VRy51&y21>0:S2;             [отображение опции 2 в диалоге, если есть отр€д в слоте 1]
!!VRy52&y22>0:S3;             [отображение опции 3 в диалоге, если есть отр€д в слоте 2]
!!VRy53&y23>0:S4;             [отображение опции 4 в диалоге, если есть отр€д в слоте 3]
!!VRy54&y24>0:S5;             [отображение опции 5 в диалоге, если есть отр€д в слоте 4]
!!VRy55&y25>0:S6;             [отображение опции 6 в диалоге, если есть отр€д в слоте 5]
!!VRy56&y26>0:S7;             [отображение опции 7 в диалоге, если есть отр€д в слоте 6]
!!IF:G0/1/0/8/y50/y51/y52/y53/y54/y55/y56/0/0/0/0/0; [v1 - результат выбора. ќтображение диалога]

!!re i/0/6;
  !!VRy81:S1 Sd<<i;
  !!VRy5:Sv1 &y81;

  !!if&y5;
    !!VRj:Si +40;
    !!VRy4:+yj;

    !!if&y80=0;
      !!CA(CURRENT_TOWN):M2/i/(NO_MON)/0;
    !!el;
      !!HEy6:C0/i/(NO_MON)/0;
    !!en;
  !!en;
!!en;

!!VRi^es_785_peons_%y60^:Sy4;

; For mode 0, adjust hero/garrison troops
!!if&y80=0/y6>(NO_HERO);
  !!CA(CURRENT_TOWN):M2/0/?y10/?y20 M2/1/?y11/?y21 M2/2/?y12/?y22 M2/3/?y13/?y23 M2/4/?y14/?y24 M2/5/?y15/?y25 M2/6/?y16/?y26; [получаем армию гарнизона]
  !!HEy6:C0/0/y10/y20 C0/1/y11/y21 C0/2/y12/y22 C0/3/y13/y23 C0/4/y14/y24 C0/5/y15/y25 C0/6/y16/y26; [устанавливаем армию гарнизонному герою]
  !!CA(CURRENT_TOWN):M2/0/-1/0 M2/1/-1/0 M2/2/-1/0 M2/3/-1/0 M2/4/-1/0 M2/5/-1/0 M2/6/-1/0; [удал€ем копию армии в гарнизоне]
!!en;

!!SN:D;                       [обновление экрана города]

!?FU(OnCalculateTownIncome);
!#VA(townId:x) (gold:x);

!!UN:P785/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VR(gold):+i^es_785_peons_%(townId)^;

!?FU(ES_785_GetPeonPower);
!#VA(mon:x) (result:x);

!!VR(result):S1;
!!VR(result)&(mon)=(MON_PEASANT):S5;

** end

ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30 and daemon_n
** Name          : Night scouting
** Name rus.     : Ночная разведка
** Options       : 795
** Dialogs       : 77
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7916, FU7997-FU7998,FU8001-FU8009
** PO-values     : -

** Перед началом и в процессе игры на некоторых свободных участках карты размещаются случайные события
** найти и активировать которые может любой герой заночевавший на данном или соседнем участке.
** Герои с навыком Разведка и/или артефактами Телескоп и Подзорная труба во время ночлега
** могут искать события на большем количестве соседних участков.
** За одну ночевку активируется не более одного события (даже если в "радиусе поиска" их несколько).
** После активации событие исчезает не зависимо от того, восползовался им герой или нет.
** ИИ использует события, только если они ему выгодны.


** Сдвиг/скрытие кнопки "Ассасины" (спс igrik)
!?FU(Event_OnUpdateHeroScreen);         // перед обновлением экрана героя
!!UN:P795/?y1;                          // проверка опции
!!UN:C6916808/4/?y2;                    // адрес структуры диалога героя
!!SN:F^GetButtonID^/^assasins^;         // получаем ID кнопки assasins
!!FU&v1<1:E;

!!VRy3:Sv1;                             // перезаписываем ID кнопки
!!SN:E6288816/2/y2/y3;                  // получаем структуру кнопки
!!FU&v1=0:E;

!!VRy4:Sv1;                             // перезаписываем структуру кнопки
!!SN&y1=0:E6286720/2/y4/6/6;            // скрываем кнопку (если опция отключена)
!!SN:L^HD_WOG.dll^/?y5;                 // HD-мод: 0 - значит НЕ включен
!!FU&y5<>0|y1=0:E;                      // выход, если HD включен или опция выключена

!!UN&y4<>0:Cy4/24/2/d40;                // сдвигаем кнопку, если HD нет и опция ВКЛ
!!SN:E6288816/2/y2/111;                 // получаем структуру текста "уволить героя" id=111
!!SN&v1<>0:E6286720/2/v1/6/6;           // прячем текст "уволить героя"

!?FU(OnAfterErmInstructions); [перед началом новой игры]
!!UN:P795/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;   [выход если опция не включена]

** Установка описаний навыка Разведка
!!SN:H^secskill^/(SKILL_SCOUTING)/(SKILL_BASIC)/?(desc:z);
!!SN:T^es.795.desc^/?(desc1:z)/^range^/4;
!!SN:H^secskill^/(SKILL_SCOUTING)/(SKILL_BASIC)/^%(desc)%(desc1)^;

!!SN:H^secskill^/(SKILL_SCOUTING)/(SKILL_ADVANCED)/?(desc:z);
!!SN:T^es.795.desc^/?(desc2:z)/^range^/8;
!!SN:H^secskill^/(SKILL_SCOUTING)/(SKILL_ADVANCED)/^%(desc)%(desc2)^;

!!SN:H^secskill^/(SKILL_SCOUTING)/(SKILL_EXPERT)/?(desc:z);
!!SN:T^es.795.desc^/?(desc3:z)/^range^/12;
!!SN:H^secskill^/(SKILL_SCOUTING)/(SKILL_EXPERT)/^%(desc)%(desc3)^;

** Установка описаний артефактов
!!SN:H^art^/(ART_SPECULUM)/1/?(desc);
!!SN:H^art^/(ART_SPECULUM)/1/^%(desc)%(desc1)^;

!!SN:H^art^/(ART_SPYGLASS)/1/?(desc);
!!SN:H^art^/(ART_SPYGLASS)/1/^%(desc)%(desc1)^;

** Генерация событий
!!UN:X?(size:y)/?(level:y);                          [размер/этажность карты]
!!VR(maxEvents:y):S(level) +1 *(size) *(size) :36;   [макс. количество генерируемых событий (одно на 36 квадратов карты)]
!!VR(maxCoord:y):S(size) -1;                         [макс. координата]
!!DO7916/1/(maxEvents)/1:P(size)/(level);            [генерация события]

!?FU(OnEveryDay)&i^timerDay^>1;         [в начале дня начиная со второго]
!!UN:P795/?y1;
!!FU&y1=0:E;   [выход если опция не включена]

** генерация события (до 1го события за каждого игрока на каждом ходу)
!!UN:X?y1/?y2;                [y1/y2 - размер/этажность карты]
!!VRy1:-1;                    [y1 - макс. координата]
!!FU7916:Py1/y2;              [генерация события]
** обнаружение событий для каждого героя текущего игрока
!!OW:C?y1 Hy1/1/0 Iy1/?y4 Oy1/?y5/d/d/d/d/d/d/d/d;    [y1 - текущий игрок, v1 - кол-во героев текущего игрока, y4 - ИИ, y5 - количество активных героев]
!!VRy2:Sv1;                   [y2 - кол-во героев текущего игрока]
!!SN&y2>y5:W^es_heroes_in_towns^/1;
*!OW:C?y1 Oy1/?y2/?y5 Iy1/?y4;            [y1 - текущий игрок, y2 - кол-во героев текущего игрока, y4 - ИИ] теперь работает только с активными героями @daemon_n
!!UN:X?y3/?i;
!!VRy3:-1;                    [y3 - макс. координата карты]
!!DO7997/1/y2/1&y2>0:Py1/y4/y3; [поиск событий для каждого героя текущего игрока]
!!SN:W^es_heroes_in_towns^/0;

!?FU7997;                     [поиск событий для каждого героя текущего игрока. x1/x2/x3/x16 - игрок/ИИ/макс коорд./номер героя у игрока]
!#VA(player:x) (isAi:y) (size:x);

!!OW:Hx1/1/x16;               [v1 - номер героя]

!!if&i^es_heroes_in_towns^;
  !!VRy10:S0;
  !!OW:Ox1/?y9/?y1/?y2/?y3/?y4/?y5/?y6/?y7/?y8;

  !!re i/1/y9;
    !!if&yi=v1;
      !!VRy10:S1;

      !!br;
    !!en;
  !!en;

  !!FU&y10<>1:E;
!!en;

; Bad fix: Fix the erm error when a hero lose from a night scouting battle event. 
; A known issue: After a hero is defeated from a night scouting battle, the hero ID from v1 would be incorrect for the rest of the heros in the hero list
!!FU|v1<(HERO_FIRST)/v1>(HERO_LAST_WOG):E;

!!VRy7:Sv1;                   [y7 - номер героя]
!!HEy7:P?y1/?y2/?y3 S3/?y4 A2/52/d/?y5 A2/53/d/?y6;  [y1-y3/y4/y5/y6 - коодринаты/уровень Разведки/Телескопы/Подзорные трубы героя]
!!VRy4&y5>0:+1;
!!VRy4&y6>0:+1;               [y4 - сила разведки (0..5)]
!!VRy4:*4 +5;                 [y4 - количество участков для разведки (5..25)]
!!DO7998/1/y4/1:Px1/x2/x3/y7/y1/y2/y3; [поиск события]

!?FU7998;                     [поиск события]
** x1 - игрок, x2 - ИИ, x3 - макс координата, x4 - герой, x5/x6/x7 - координаты героя, x16 - исследуемый квадрат
!!VRy1:Sx5;
!!VRy2:Sx6;
!!VRy3:Sx7;                         [y1/y2/y3 - координаты исследуемого участка]
!!VRy2&x16=2:-1;                    [...]
!!VRy1&x16=3:+1;                    [...]
!!VRy2&x16=4:+1;                    [...]
!!VRy1&x16=5:-1;                    [...]
!!VRy1&x16=6:-1;
!!VRy2&x16=6:-1;                    [...]
!!VRy1&x16=7:+1;
!!VRy2&x16=7:-1;                    [...]
!!VRy1&x16=8:+1;
!!VRy2&x16=8:+1;                    [...]
!!VRy1&x16=9:-1;
!!VRy2&x16=9:+1;                    [...]
!!VRy2&x16=10:-2;                   [...]
!!VRy1&x16=11:+2;                   [...]
!!VRy2&x16=12:+2;                   [...]
!!VRy1&x16=13:-2;                   [...]
!!VRy1&x16=14:+1;
!!VRy2&x16=14:-2;                   [...]
!!VRy1&x16=15:+2;
!!VRy2&x16=15:+1;                   [...]
!!VRy1&x16=16:-1;
!!VRy2&x16=16:+2;                   [...]
!!VRy1&x16=17:-2;
!!VRy2&x16=17:-1;                   [...]
!!VRy1&x16=18:-1;
!!VRy2&x16=18:-2;                   [...]
!!VRy1&x16=19:+2;
!!VRy2&x16=19:-1;                   [...]
!!VRy1&x16=20:+1;
!!VRy2&x16=20:+2;                   [...]
!!VRy1&x16=21:-2;
!!VRy2&x16=21:+1;                   [...]
!!VRy1&x16=22:-2;
!!VRy2&x16=22:-2;                   [...]
!!VRy1&x16=23:+2;
!!VRy2&x16=23:-2;                   [...]
!!VRy1&x16=24:+2;
!!VRy2&x16=24:+2;                   [...]
!!VRy1&x16=25:-2;
!!VRy2&x16=25:+2;                   [...]
!!FU|y1<0/y2<0/y1>x3/y2>x3:E;       [выход, если координаты за пределами карты]

!!SN:W^o795.%Y1.%Y2.%Y3.T^/?y4;     [y4 - тип события]

!!if&y4>0; 
  !!SN:W^o795.%Y1.%Y2.%Y3.P1^/?y5;  [y5-y9 - параметры события]
  !!SN:W^o795.%Y1.%Y2.%Y3.P2^/?y6;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P3^/?y7;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P4^/?y8;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P5^/?y9;  [...]

  !!VRv1:Sy4 +8000;                 [v1 - номер функции обработчика события]
  !!FUv1:Px1/x4/x2/y1/y2/y3/y5/y6/y7/y8/y9; [вызов функции обработчика]

  !!SN:W^o795.%Y1.%Y2.%Y3.T^/?y10;  [y10 - тип события]
  !!VRx16&y10=0:S100500;            [прерывание поиска событий, если событие активировано]
!!en; 

!?FU8097;                           [вызов битвы]
!!HEx2:Tx4/x5/x6/0/1;               [вызов битвы]

!?FU8098;                           [возврат в v1 номера отряда попадающего под критерии отбора]
** x1-номер героя, x2-x4-критерии отбора
** x2=0/1/2/3/4/5/6 - любой/пеший/стрелок/летун/живой/нежить/элементаль
** x3=0/1/2/3/4 - проверяемый параметр Уровень/FV/Численность/Сила(FV*кол-во)/Опыт
** x4=0/1 - максимум/минимум
!!if&x16=0;                         [инициализация переменных при первом запуске]
  !!VRv1:S-1;                       [v1 - найденный отряд]
  !!VRv2&x4=0:S-1;
  !!VRv2&x4=1:S2000000000;          [v2 - оцениваемый параметр]
!!en; 

!!HEx1:C0/x16/?y1/?y2/?y3/2;        [y1/y2/y3 - тип/кол-во/опыт существ]
!!FU|y1<0/y2<1:E;                   [выход, если отряда нет]

!!MA:Xy1/?y4;                       [y4 - флаги существа]
!!VRy5:Sy4 &2;                      [y5 - летун]
!!VRy6:Sy4 &16;                     [y6 - живой]
!!VRy7:Sy4 &262144;                 [y7 - нежить]
!!VRy4:&4;                          [y4 - стрелок]
!!FU&x2=1/y4>0:E;
!!FU&x2=1/y5>0:E;                   [выход если тип отряда не совпадает с требуемым]
!!FU&x2=2/y4=0:E;
!!FU&x2=3/y5=0:E;                   [...]
!!FU&x2=4/y6=0:E;
!!FU&x2=5/y7=0:E;                   [...]
!!FU&x2=6/y6>0:E;
!!FU&x2=6/y7>0:E;                   [...]

!!MA&x3=0:Ly1/?y3;                  [y3 - проверяемый параметр]
!!MA|x3=1/x3=3:Fy1/?y3;             [...]
!!VRy3&x3=2:Sy2;                    [...]
!!VRy3&x3=3:*y2;                    [...]
!!VRv1&y3>v2/x4=0:Sx16;             [обновление максимума/минимума/номера отряда]
!!VRv2&y3>v2/x4=0:Sy3;              [...]
!!VRv1&y3<v2/x4=1:Sx16;             [...]
!!VRv2&y3<v2/x4=1:Sy3;              [...]

!?FU(ES_795_Num2Def);            [возврат в zx2 названия def-файла № x1 для диалогов "Ночной разведки"]
!!VRzx2&x1=0:S^not used^;           [...]
!!VRzx2&x1=1:S^twcrport.def^;       [...]
!!VRzx2&x1=2:S^SpellBon.def^;       [...]
!!VRzx2&x1=3:S^BoRes.def^;          [...]
!!VRzx2&x1=4:S^opt795ps.def^;       [...]
!!VRzx2&x1=5:S^sskilbon.def^;       [...]
!!VRzx2&x1=6:S^artifact.def^;       [...]
!!VRzx2&x1=7:S^opt795he.def^;       [...]

!?FU(ES_795_ShowDialog);         [формирование и вызов диалога]
** x1-герой, x2/x3-тип/номер НПЦ, x4-кол-во картинок (0..3), x5-переменная для возврата результата выбора (1-да, 2-нет),
** x6/x7-тип/номер левой картинки, x8/x9-тип/номер центральной картинки, x10/x11-тип/номер правой картинки, 
** x12 - запрет открытия окна героя, x13 - номер события
** Типы: 0-герой, 1-монстр, 2-заклинание, 3-ресурс, 4-перв.параметр, 5-втор.навык/ранг, 6-артефакт
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
!!VRx5:S0;                    [инициализация]
!!DL77:N^opt795_%X4.txt^;     [подгрузка 77 диалога]

!!if&-1;                      [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                     [и выходим]
!!en;

!!VRx3&x2=1:+2;
!!VRx7&x6=1:+2;
!!VRx9&x8=1:+2;
!!VRx11&x10=1:+2;             [Смещение кадров в стндартном def'е существ]
!!FU(Fun_GetHeroPortrait):Px1/1/30;     // z30 - имя файла с портретом героя
!!DL77:A10/11/z30/1;
!!VRv101:Cx1/x2/x3/x4/x5/x6/x7/x8/x9/x10/x11; [установка номера и портрета героя]
!!FU(ES_795_Num2Def):Px2/12;
!!DL77:A12/9/z12/1 A12/4/x3/1; [установка портрета НПЦ]
!!DL77:A7/3/z7/1;
!!DL77:A8/3/z8/1;             [установка текстов]
!!VRy1:Sx13 -1;
!!DL77:A30/4/y1/1;            [установка шапки]

!!if|x4=2/x4=3;               [отображение левой картинки (для случая 2 или 3 картинок)]
  !!FU(ES_795_Num2Def):Px6/13;
  !!DL77:A14/9/z13/1 A14/4/x7/1; [установка картинки]
  !!DL77:A15/3/z9/1;          [установка подписи]
!!en; 

!!if|x4=1/x4=3;               [отображение средней картинки (для случая 1 или 3х картинок)]
  !!FU(ES_795_Num2Def):Px8/14;
  !!DL77:A17/9/z14/1 A17/4/x9/1; [установка картинки]
  !!DL77:A18/3/z10/1;         [установка подписи]
!!en; 

!!if|x4=2/x4=3;               [отображение левой картинки (для случая 2 или 3 картинок)]
  !!FU(ES_795_Num2Def):Px10/15;
  !!DL77:A20/9/z15/1 A20/4/x11/1; [установка картинки]
  !!DL77:A21/3/z11/1;         [установка подписи]
!!en; 

!!VRv7:Sx12;                  [передача запрета открытия окна героя]
!!VRz31:Sz179096;
!!VRz32:Sz179097;
!!VRz33:Sz179098;             // получение подсказок из ERT
!!DL77:H1/z31 H2/z32 H9/z33;            // установка подсказок в диалог
!!DL77:S?x5;                  [вызов диалога и получение результата выбора (1-да, 2-нет)]

!?FU(OnCustomDialogEvent)&i^dlg_id^=77/i^mouse_action^=(MOUSE_LMB_RELEASED); [обработка диалога 77 (отпущена ЛКМ)]
!!DL77|i^mouse_item^=1/i^mouse_item^=2:C1;                [кнопки "ОК"/"Отмена"]

!!if&i^mouse_item^=9/v7=0;                       [портрет героя, если не запрещено]
  !!UN:C5119174/1/184 C5119175/4/1;     [патч: деактивация кнопки "Уволить"]
  !!SN:E5118576/1/v101/0;               [открыть окно героя ЛКМ]
  !!UN:C5119174/1/161 C5119175/4/6916832; [патч: вернуть исходный код кнопки "Уволить"]
!!en; 

!!if&i^mouse_item^=14/v106=6;                    [клик по левой картинке-артефакту]
  !!UN:C6687592/4/?y12;                 [y12 - ссылка на таблицу артефактов]
  !!VRy13:Sv107 *32 +y12 +16;           [y13 - адрес указателя с описанием артефакта]
  !!UN:Cy13/4/?y14;                     [y14 - получить указатель на описание артефакта]
  !!SN:X?y9 Xy14 X?z3 Xy9;              [z3 - хранит описание артефакта]
  !!IF:Q1/8/v107/4^%Z3^;                [вывод окна с картинкой и названием артефакта]
!!en; 

** Генерация событий

!?FU7916;                     [генерация события, x1/x2 - размер/этажность карты]
!!VRy1:S0 Rx1;
!!VRy2:S0 Rx1;
!!VRy3:S0 Rx2;                [y1/y2/y3 - координаты события]
!!OBy1/y2/y3:T?y4;            [y4 - тип объекта в позиции]
!!TRy1/y2/y3:E?y5 P?y6;       [y5/y6 - "желтый"/"красный" квадрат]
!!TRy1/y2/y3:T?y7/d/d/d/d/d/d/d; [y7 - тип почвы]
!!FU|y4>0/y5=0/y6=0/y7>7:E;   [выход, если квадрат не свободен или морской]

** генерация события
!!VRy10:S0;
!!VRy11:S0;
!!VRy12:S0;
!!VRy13:S0;
!!VRy14:S0;
!!VRy15:S0;                   [инициализация параметров события]
!!VRy10:S1 R8;                [y10 - тип события (1..9)]

** y11-y15 - параметры события
!!if&y10=1;                   [Тайник: P1 = тип ресурса, P2 = Кол-во ресурса, P3 = тип охраны, P4 = кол-во охранников]
  !!VRy11:S0 R6;              [тип ресурса]
  !!VRy12:S5 R8;              [кол-во ресурса 5-13 шт]
  !!VRy13:S0 R7 *14 +y12 -5;  [тип охраны]
  !!VRy14:S15 R10;            [кол-во охранников]
  !!VRy12|y11=0/y11=2:*2;     [дерево и руда х2]
  !!VRy12&y11=6:*200;         [золото х200]
!!el&y10=2;                   [Продавец артефакта: P1 = тип артефакта, P2 = тип ресурса, P3 = Кол-во ресурса]
  !!UN:J6/6/?y11;             [артефакт класса сокровище..ценный]
  !!VRy12:S0 R6;              [тип ресурса]
  !!UN:Ay11/3/?y13;           [класс артефакта (2/4)]
  !!VRy13:*1000;              [стоимость в золоте 2000/4000]
  !!VRy13|y12=0/y12=2::250;   [:250 для стоимости в дереве/руде]
  !!VRy13|y12=1/y12=3/y12=4/y12=5::500; [:500 для стоимости в драг.ресурсах]
!!el&y10=3;                   [Инструктор: P1-P3 = критерии выбора отряда]
  !!VRy11:S0 R3;              [любой/пеший/стрелок/летун]
  !!VRy12:S0 R4;              [Уровень/FV/Численность/Сила(FV*кол-во)/Опыт]
  !!VRy13:S0 R1;              [максимум/минимум]
!!el&y10=4;                   [Наставник: P1 = параметр, P2 = увеличение параметра, P3 = тип ресурса, P4 = кол-во ресурса]
  !!VRy11:S0 R3;              [Параметр 0..3]
  !!VRy12:S1 R2;              [увеличение параметра 1..3]
  !!VRy13:S0 R6;              [тип ресурса]
  !!VRy14:Sy12 *5;            [стоимость 5 ресурса за 1 ед параметра]
  !!VRy14&y13=6: *200;        [х200 для Золота]
  !!VRy14|y13=0/y13=2: *2;    [х2 для Дерева/Руды]
!!el&y10=5;                   [Исчезающий призрак: P1 = требуемое кол-во маны, P2 = тип ресурса, P3 = кол-во ресурса]
  !!VRy11:S1 R4;              [ценность схрона]
!!el&y10=6;                   [Некромант: P1 = повышение ранга]
  !!VRy11:S0 R3;              [критерий выбора отряда 0..3]
  !!VRy12:S1 R2;              [макс.бонус уровня 1..3]
  !!VRy13:S0 R2;              [грейд 0..2]
!!el&y10=7;                   [Барахольщик: P1 множитель цены]
  !!VRy11:S75 R50;            [множитель цены - 75-125%]
!!el&y10=8;                   [Конфликт]
  !!VRy16:S0 R8;              [y16 - город защищающихся]
  !!VRy17:Sy16 +3 R3 %9;      [y17 - город атакующих (другое мировоззрение)]
  !!VRy18:S0 R6;              [y18 - уровень защищающихся]
  !!VRy19:S0 R1;              [y19 - грейд защищающихся]
  !!VRy20:Sy18 +1 R2;         [y20 - уровень атакующих (на 1..3 уровня больше, чем защищающихся)]
  !!VRy20&y20>6:S6;           [... максимум 7й уровень]
  !!VRy21:S0 R1;              [y19 - грейд атакующих]
  !!UN:Ty16/y18/y19/?y11;     [y11 - защищающиеся существа]
  !!UN:Ty17/y20/y21/?y12;     [y12 - атакующие существа существа]
  !!VRy13:S4 R8;              [y13 - 3 х коэфф.превосходства (4..12)]
!!el&y10=9;                   [Ассасины: P1 = размер группы, P2 = стоимость найма]
  !!VRy11:Sc +10 R20;         [размер группы = день размещения события +10..30 юнитов]
  !!VRy12:S50 R50 *y11 :500 *500; [стоимость найма = 50..100 золотых за ассасина, с округлением общей суммы до 500 золотых вниз]
!!en; 
**
!!SN:W^o795.%Y1.%Y2.%Y3.T^/y10;  [установка параметров события]
!!SN:W^o795.%Y1.%Y2.%Y3.P1^/y11; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P2^/y12; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P3^/y13; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P4^/y14; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P5^/y15; [...]

** Обработчики событий - FU80##
** x1 - игрок, x2 - герой, x3 - 0/1=Человек/ИИ, x4-x6 - координаты события, x7-x11 - параметры события

!?FU8001;                     [Тайник]
** Обнаружение тайника с ресурсами
** P1 = тип ресурса, P2 = Кол-во ресурса, P3 = тип охраны, P4 = кол-во охранников
** проверка условия срабатывания
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!OW:Rx1/x7/dx8;            [ИИ получает ресурсы без боя]
!!el;                         [обработчик игрока-человека]
  !!VRy7:S0;
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
  !!VRz7:Sz179307;
  !!VRz8:Sz179308;            [получение описаний события]
  !!VRy1:Sx8 -3 R7;
  !!VRy1&x7=6:Sx8 -600 R1200 :100 *100;
  !!VRy2:Sx10 -5 R11;         [примерные количества ресурса и охраны]
  !!VRz9:S^~ %Y1^;
  !!VRz11:S^~ %Y2^;           [формирование подсказок]
  !!FU(ES_795_ShowDialog):Px2/1/x9/2/?y7/3/x7/0/0/1/x9/0/1; [вызов диалога и получение ответа в y7 (1-да,2-нет)]
  !!FU&y7<>1:E;               [выход, если человек отказался от активации события]

  !!HEx2:Tx4/x5/x6/x9/x10;    [вызов битвы]
  !!HEx2:O?y1;                [y1 - владелец героя]

  !!if&y1<>-1;                [если герой жив]
    !!OW:Rx1/x7/dx8;
    !!UN:R2;                  [игрок получает ресурсы]
    !!VRz1:Sz179309;          [z1 - текст сообщения]
    !!IF:Q1/x7/x8/1/1;        [вывод сообщения]
  !!en; 
!!en; 

!?FU8002;                     [Продавец артефакта]
** Продавец артефакта за ресурсы
** P1 = тип артефакта, P2 = тип ресурса, P3 = Кол-во ресурса
** проверка условия срабатывания
!!OW:Rx1/x8/?y5;              [y5 - кол-во требуемого ресурса у игрока]
!!FU&y5<x9:E;                 [событие не случается, если ресурса недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!HEx2:A2/x7/?y1/?y2;       [y1/y2 - кол-во артефактов предлагаемого типа у героя]
  !!UN:Ax7/2/?y3;             [y3 - позиция предлагаемого артефакта]
  !!HEx2:A1/?y10/0 A1/?y11/1 A1/?y12/2 A1/?y13/3 A1/?y14/4 A1/?y15/5 A1/?y16/6 A1/?y17/7 A1/?y18/8; [Артефакты на герое]
  !!HEx2:A1/?y19/9 A1/?y20/10 A1/?y21/11 A1/?y22/12 A1/?y23/18;                                     [...]
  !!VRy1&y3=1/y10>=0:S1;      [проверка занятости необходимого слота]
  !!VRy1&y3=2/y11>=0:S1;      [...]
  !!VRy1&y3=3/y12>=0:S1;      [...]
  !!VRy1&y3=4/y13>=0:S1;      [...]
  !!VRy1&y3=5/y14>=0:S1;      [...]
  !!VRy1&y3=6/y15>=0:S1;      [...]
  !!VRy1&y3=7/y16>=0/y17>=0:S1; [...]
  !!VRy1&y3=8/y18>=0:S1;      [...]
  !!VRy1&y3=9/y19>=0/y20>=0/y21>=0/y22>=0/y23>=0:S1; [...]
  !!FU|y1>0/y2>0:E;           [выход, если артефакт уже есть у героя или все доступные для него слоты заняты]

  !!VRy5:-x9;                 [вычитание стоимости артефакта]
  !!OW:Rx1/x8/y5;             [обновление ресурсов игрока]
  !!HEx2:A4/x7;               [передача артефакта герою]
!!el;                         [обработчик игрока-человека]
  !!VRy1:Sx8 +179311;         [y1 - индекс переменной с описанием события]
  !!VRz7:Szy1;
  !!VRz8:Sz179318;            [получение описаний события]
  !!VRy1:S3 +x8;              [тип НПЦ в зависимости от ресурса]
  !!VRz9:S^+1^;
  !!VRz11:S^- %X9^;          [формирование подсказок]
  !!FU(ES_795_ShowDialog):Px2/7/y1/2/?y7/6/x7/0/0/3/x8/0/2; [вызов диалога и получение ответа в y7 (1-да,2-нет)]
  !!FU&y7<>1:E;               [выход, если человек отказался от активации события]

  !!VRy5:-x9;                 [вычитание стоимости артефакта]
  !!OW:Rx1/x8/y5;
  !!UN:R2;                    [обновление ресурсов игрока]
  !!HEx2:A4/x7;               [передача артефакта герою]
!!en; 

!?FU8003;                     [Инструктор]
** Максимизация опыта отряда за деньги и время
** P1-P3 = критерии выбора отряда
!!UN:P900/?y1;
!!FU&y1=0:E;                  [выход, если опыт отрядов не включен]

!!DO8098/0/6/1:Px2/x7/x8/x9;  [возврат в v1 номера отряда попадающего под критерии отбора]
!!VRy1:Sv1;
!!FU&y1<0:E;                  [y1 - найденный отряд. Выход, если подходящего отряда нет]

!!HEx2:C0/y1/?y2/?y3/?y4/2;   [y2/y3/y4 - тип/кол-во/опыт отряда]
!!EAy2:L?y5;                  [y5 - опыт 10го ранга]
!!FU&y4>=y5:E;                [выход, если отряд уже имеет 10 ранг опыта]

!!VRy6:Sy5 -y4;               [y6 - необходимое количество опыта]
!!VRy7:Sy4 *100 :y5 -100 *-1; [y7 - % опыта, который надо получить отряду до 10 ранга]
!!MA:Fy2/?y8;                 [y8 - FV существ в отряде]
!!VRy8:*y3;                   [y8 - FV отряда]
!!VRy8:*y7 :500;              [y8 - стоимость тренировки FV/500 за 1% опыта]
!!VRy8&y8<100:S100;           [не менее 100 золотых]
!!OW:Rx1/6/?y9;               [y9 - золото текущего игрока]
!!FU&y9<y8:E;                 [выход, если средств не достаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!VRy10:Sy9 :2;             [y10 - половина золотого запаса ИИ]
  !!FU&y8>y10:E;              [выход, если цена превышает половину имеющегося золота]

  !!MA:Ly2/?y10;              [y10 - уровень существ в отряде (0..6)]
  !!VRy10:*-1 +7 *y10 *4;     [y10 - мин. кол-во сущест для тренировки (98/72/50/32/18/8/2 существ соотв. 1..7 уровня]
  !!HEx2:C0/y1/d/?y11;        [y11 - кол-во существ в тренируемом отряде]
  !!FU&y11<y10:E;             [выход, если существ недостаточно для тренировки]

  !!VRy8:*-1;                 [y8 - золото для списания]
  !!OW:Rx1/6/dy8;             [списание средств]
  !!HEx2:C0/y1/d/d/y5/2 W0/1; [максимизация опыта отряда и трата ОД]
!!el;                         [обработчик игрока-человека]
  !!VRy10:Sx7 +179320;        [y10 - индекс переменной с описанием события]
  !!VRz7:Szy10;
  !!VRz8:Sz179324;            [получение описаний события]
  !!VRz9:S^%Y3^;
  !!VRz10:S^+ %Y6^;
  !!VRz11:S^- %Y8^;           [подписи к картинкам]
  !!VRy11:S10 +x7;            [y11 - Существо-инструктор]
  !!FU(ES_795_ShowDialog):Px2/7/y11/3/?y12/1/y2/4/4/3/6/0/3; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!VRy8:*-1;                 [y8 - золото для списания]
  !!OW:Rx1/6/dy8;
  !!UN:R2;                    [списание средств]
  !!HEx2:C0/y1/d/d/y5/2 W0/1; [максимизация опыта отряда и трата ОД]
!!en; 

!?FU8004;                     [Наставник]
** Тренировка первичного параметра героя за ресурсы
** P1 = параметр, P2 = увеличение параметра, P3 = тип ресурса, P4 = кол-во ресурса
!!OW:Rx1/x9/?y1;              [y1 - кол-во необходимого ресурса у игрока]
!!FU&x10>y1:E;                [выход, если ресурса недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!VRy2:Sy1 :2;              [y2 - половина запаса ресурса у ИИ]
  !!FU&x10>y2:E;              [выход, если цена превышает половину имеющегося ресурса]

  !!VRy8:Sx10 *-1;            [y8 - средства к списанию]
  !!OW:Rx1/x9/dy8;            [списание средств]
  !!HEx2&x7=0:Fdx8/d/d/d;     [повышение первичного параметра героя]
  !!HEx2&x7=1:Fd/dx8/d/d;     [...]
  !!HEx2&x7=2:Fd/d/dx8/d;     [...]
  !!HEx2&x7=3:Fd/d/d/dx8;     [...]
  !!VRy2:Sx8 *-300;           [y2 - стоимость ОД (300 за 1 одно параметра)]
  !!HEx2:Wdy2/1;              [...]
!!el;                         [обработчик игрока-человека]
  !!VRy10:Sx7 +179326;        [y10 - индекс переменной с описанием события]
  !!VRz7:Szy10;
  !!VRz8:Sz179330;            [получение описаний события]
  !!VRz9:S^+ %X8^;
  !!VRz10:S^^;
  !!VRz11:S^- %X10^;          [подписи к картинкам]
  !!VRy11:S14 +x7;            [y11 - Существо-Тренер]
  !!FU(ES_795_ShowDialog):Px2/7/y11/2/?y12/4/x7/0/0/3/x9/0/4; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!VRy8:Sx10 *-1;            [y8 - средства к списанию]
  !!OW:Rx1/x9/dy8;
  !!UN:R2;                    [списание средств]
  !!HEx2&x7=0:Fdx8/d/d/d;     [повышение первичного параметра героя]
  !!HEx2&x7=1:Fd/dx8/d/d;     [...]
  !!HEx2&x7=2:Fd/d/dx8/d;     [...]
  !!HEx2&x7=3:Fd/d/d/dx8;     [...]
  !!VRy2:Sx8 *-300;           [y2 - стоимость ОД (300 за 1 одно параметра)]
  !!HEx2:Wdy2/1;              [...]
!!en; 

!?FU8005;                     [Исчезающий призрак]
** Покупатель маны/пп героя за ресурсы
** P1 = ценность схрона
!!HEx2:I?y1/1;                [y1 - кол-во маны у героя]
!!VRy2:Sx7 *20;               [y2 - требуемое кол-во маны 20*ценность схрона]
!!FU&y2>y1:E;                 [выход, если маны недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!VRy3:Sy2 *-1;               [y3 - отрицательное значение маны]
!!VRy10:S1 Rx7;               [установка случайного кол-ва ресурсов]
!!VRy11:S1 Rx7;               []
!!VRy12:S1 Rx7;               []
!!VRy13:S1 Rx7;               []
!!VRy14:S1 Rx7;               []
!!VRy15:S1 Rx7;               []
!!VRy16:S1 Rx7 *100;          []

; Check if mithril is enabled
!!UN:P36/?y17;                [y17 - статус опции "Мифрил"]
!!VRy17&y17>0:S1 Rx7;         [кол-во мифрила, если включен]

!!if&x3=1;                    [обработчик игрока ИИ]
** ИИ жертвует ману, только если это не больше половины от имеющейся у него в даный момент
  !!VRy3:Sy1 :2;              [y3 - половина маны героя]
  !!FU&y2>y1:E;               [выход, если требуется более половины имеющейся маны]

  !!HEx2:Idy3/1;              [уменьшение маны героя]
  !!OW:Rx1/0/dy10 Rx1/1/dy11 Rx1/2/dy12 Rx1/3/dy13 Rx1/4/dy14 Rx1/5/dy15 Rx1/6/dy16 Rx1/7/dy17; [добавление ресурсов]
!!el;                         [обработчик игрока-человека]
  !!VRz7:Sz179338;
  !!VRz8:Sz179339;            [получение описаний события]
  !!VRz9:S^- %Y2^;
  !!VRz10:S^^;
  !!VRz11:S^???^;             [подписи к картинкам]
  !!FU(ES_795_ShowDialog):Px2/7/18/2/?y12/4/5/0/0/3/8/0/5; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!HEx2:Idy3/1;              [уменьшение маны героя]
  !!OW:Rx1/0/dy10 Rx1/1/dy11 Rx1/2/dy12 Rx1/3/dy13 Rx1/4/dy14 Rx1/5/dy15 Rx1/6/dy16 Rx1/7/dy17; [добавление ресурсов]
  !!UN:R2;                    [обновление строки ресурсов]
  !!VRz1:Sz179340;            [z1 - текст посещения схрона]
  !!IF&y17>0:N1/y11/3/y13/4/y14/5/y15/0/y10/2/y12/6/y16/7/y17; [схрон с мифрилом]
  !!IF&y17=0:N1/y11/3/y13/4/y14/5/y15/0/y10/6/y16/2/y12;       [схрон без мифрила]
  !!IF:N1/1;                  [показ диалога получения ресурсов]
!!en; 

!?FU8006;                     [Некромант]
** Преобразование самого многочисленного живого отряда в нежить
** P1 = критерий выбора отряда, P2 = макс.бонус уровня, P3 = грейд
!!DO8098/0/6/1&x7=0:Px2/4/0/1;[возврат в v1 номера самого низкоуровневого живого отряда]
!!DO8098/0/6/1&x7=1:Px2/4/2/0;[возврат в v1 номера самого многочисленного живого отряда]
!!DO8098/0/6/1&x7=2:Px2/4/3/1;[возврат в v1 номера самого слабого живого отряда]
!!DO8098/0/6/1&x7=3:Px2/4/4/1;[возврат в v1 номера самого неопытного живого отряда]
!!VRy1:Sv1;
!!FU&y1<0:E;                  [выход, если подходящего отряда не найдено]

!!HEx2:C0/y1/?y2/?y3;         [y1/y2/y3 - отряд/тип/кол-во требуемых существ]
!!MA:Ly2/?y4;                 [y4 - уровень требуемых существ.]
!!VRy4:+x8;
!!VRy4&y4>7:S7;               [y4 - уровень преобразованных существ (не более 7. 7 - Кровавые драконы/Драколичи)]
** y5 - вид новых существ (уровень/грейд)
!!VRy5&y4=1/x9=0:S58;
!!VRy5&y4=1/x9=1:S59;
!!VRy5&y4=1/x9=2:S141;        [2й уровень: Мертвецы/Зомби/Мумии]
!!VRy5&y4=2/x9=0:S60;
!!VRy5&y4=2/x9=1:S61;
!!VRy5&y4=2/x9=2:S159;        [3й уровень: Стражи/Призраки/Привидения]
!!VRy5&y4=3/x9=0:S62;
!!VRy5&y4=3/x9=1:S63;
!!VRy5&y4=3/x9=2:S63;         [4й уровень: Вампиры/Л.Вампиры/Л.Вампиры]
!!VRy5&y4=4/x9=0:S64;
!!VRy5&y4=4/x9=1:S65;
!!VRy5&y4=4/x9=2:S65;         [5й уровень: Личи/М.Личи/М.Личи]
!!VRy5&y4=5/x9=0:S66;
!!VRy5&y4=5/x9=1:S67;
!!VRy5&y4=5/x9=2:S172;        [6й уровень: Черн.рыцарь/Р.Смерти/Кошмары]
!!VRy5&y4=6/x9=0:S68;
!!VRy5&y4=6/x9=1:S69;
!!VRy5&y4=6/x9=2:S69;         [7й уровень: Кост.драконы/Призр.драконы/Призр.драконы]
!!VRy5&y4=7/x9=0:S154;
!!VRy5&y4=7/x9=1:S154;
!!VRy5&y4=7/x9=2:S196;        [8й уровень: Кров.Драконы/Драколичи/Драколичи]

!!if&y5=196;                  [доп. требования по здоровью для поднятия Драколича]
  !!MA:Py2/?i P196/?j;
  !!VRi:*y3;                  [i/j - суммарное здоровье жертв/здоровье Драколича]
  !!VRy5&i<j:S154;            [если суммарное здоровье жертв ниже здоровья Драколича, поднимаются Кровавые драконы]
!!en; 

!!MA:Fy2/?y6 Fy5/?y7;         [y6/y7 - FV старого/нового существа]
!!VRy8:Sy3 *y6 :y7 +1;
!!VRy8&y8>y3:Sy3;             [y8 - кол-во новых существ (не менее 1го и не более старого кол-ва)]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!FU&y4<3:E;                [ИИ не соглашается на преобразование ниже Вампиров]

  !!HEx2:C0/y1/y5/y8;         [трансформация существ]
!!el;                         [обработчик игрока-человека]
  !!VRz7:Sz179342;
  !!VRz8:Sz179343;            [получение описаний события]
  !!VRz9:S^- %Y3^;
  !!VRz10:S^^;
  !!VRz11:S^+ %Y8^;           [подписи к картинкам]
  !!FU(ES_795_ShowDialog):Px2/7/2/2/?y12/1/y2/0/0/1/y5/0/6; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]
  !!HEx2:C0/y1/y5/y8;         [трансформация существ]
  !!UN:R1;                    [обновление экрана]
  !!VRz1:Sz179344;            [z1 - текст сообщения]
  !!IF:M1/1;                  [вывод сообщения]
!!en; 

!?FU8007;                               // Барахольщик - предлагает за случайный артефакт в рюкзаке героя наиболее необходимые ему ресурсы по случайно заданному курсу
** x1 - игрок, x2 - герой, x3 - 0/1=Человек/ИИ, x4-x6 - координаты события, x7-x11 - параметры события
** P1 = размер группы, P2 = стоимость групы
** проверка условия срабатывания
!!VRk:S-1;                              // k - количество различных артефактов в рюкзаке героя

!!re i/7/108;                           // перебор интересных Барахольщику артефактов
  !!HEx2:A2/i/?j/?f;
  !!VRj:-f;                             // j - количество артефактов типа i в рюкзаке героя
  !!VRk&j>0:+1;                         // увеличиваем счетчик разных артефактов
!!en; 

!!FU&k<0:E;                             // выход, если в рюкзаке героя нет нужных артефактов

!!SN:W^o795.%X4.%X5.%X6.T^/0;           // удаление события
!!VRl:S0 Rk;
!!VRk:S-1;                              // l - случайный артефакт

!!re i/7/108;                           // перебор интересных Барахольщику артефактов
  !!HEx2:A2/i/?j/?f;
  !!VRj:-f;                             // j - количество артефактов типа i в рюкзаке героя
  !!VRk&j>0:+1;

  !!br&l=k;                             // прерываем поиск, если искомый артефакт найден
!!en; 

!!VRy1:Si;
!!UN:Ay1/1/?y2 Ay1/3/?y3;               // y1/y2/y3 - номер/цена/класс артефакта для покупки
!!VRy4:S0;
!!OW:Rx1/0/?k;
!!VRk::2;                               // y4 - тип ресурса, вкотором игрок максимально нуждается

!!re i/1/6;                             // y10-y16 - количество дерева ... золота у игрока
  !!OW:Rx1/i/?j;                        // ...
  !!VRj|i=0/i=2::2;
  !!VRj&i=6::1000;                      // j  - приведенное количество ресурса
  !!VRy4&k>j:Si;
  !!VRk&k>j:Sj;
!!en;  

!!VRy2:*x7 :100;                        // y2 - цена в золоте с учетом заданного в событии курса, x 1.0 для treasure-артефактов
!!VRy2&y3=4:*5 :4;                      // х 1.25 для minor-артефактов
!!VRy2&y3=8:*6 :4;                      // х 1.5 для major-артефактов
!!VRy2&y3=16:*7 :4;                     // х 1.75 для relict-артефактов
!!VRy2&y4=6::500 *500;                  // y2 - цена для выбранного ресурса, с округлением до 1 рес / 500 злт
!!VRy2|y4=0/y4=2::400;                  // ...
!!VRy2|y4=1/y4=3/y4=4/y4=5::800;        // ...

!!if&x3=1;                              // обработчик игрока ИИ
  !!if&x7>=100;                         // ИИ соглашается на обмен при цене не менее 100% от базовой
    !!HEx2:A3/y1/1/0;                   // удаление артефакта из рюкзака
    !!OW:Rx1/y4/dy2;                    // добавление ресурсов
  !!en;                                 
!!el;                                   // обработчик игрока-человека
  !!VRz7:Sz179414;
  !!VRz8:Sz179415;                      // получение описаний события
  !!VRz9:S^-1^;
  !!VRz10:S^^;
  !!VRz11:S^%Y2^;                       // формирование подсказок
  !!FU(ES_795_ShowDialog):Px2/7/1/2/?y12/6/y1/0/0/3/y4/1/7; // вызов диалога и получение ответа в y12 (1-да,2-нет)

  !!if&y12<>1;                          // при отказе
    !!VRz1:Sz179416;                    // z1 - текст финального сообщения
    !!IF:M1/1;                          // вывод сообщения
  !!el;                                 // при согласии
    !!HEx2:A3/y1/1/0;                   // удаление артефакта из рюкзака
    !!OW:Rx1/y4/dy2;
    !!UN:R2;                            // добавление ресурсов
    !!VRz1:Sz179417;                    // z1 - текст финального сообщения при согласии
    !!IF:M1/1;                          // вывод сообщения
  !!en;     
!!en; 

!?FU8008;                     [Конфликт]
** Схватка 2х неравных по силе сторон с возможностью помочь слабой стороне за вознаграждение
** P1 = Защищающиеся Существа, P2 = Атакующие существа, P3 = утроенный коэффициент превосходства атакующих (4..12),
!!MA:Lx7/?y1 Fx7/?y2;         [y1/y2 - уровень/FV защищающихся]
!!VRy4:Sy1 +1 *y4;            [y4 - делитель = уровень^2]
!!VRy3:Sc *3 :y4 +1;          [y3 - кол-во защищающихся = (день*3)/(уровень^2)+1]
!!VRy4:Sy3 *y2 *x9 :3;        [y4 - общее FV атакующих]
!!MA:Fx8/?y5;                 [y5 - FV атакующего существа]
!!VRy4::y5 +1;                [y4 - кол-во атакующих]
!!VRy5:*y4;                   [y5 - общее FV атакующих]
!!MA:Cx7/6/?y6;
!!VRy6::2;                    [y6 - вознаграждение за каждого спасенного: 1/2 золотой части стоимости]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!HEx2:C0/0/?y11/?y21 C0/1/?y12/?y22 C0/2/?y13/?y23 C0/3/?y14/?y24 C0/4/?y15/?y25 C0/5/?y16/?y26 C0/6/?y17/?y27; [Армия ИИ]
  !!MA&y11>=0:Fy11/?y31;
  !!VRy31:*y21;               [ценности слотов армии ИИ]
  !!MA&y12>=0:Fy12/?y32;
  !!VRy32:*y22;               [...]
  !!MA&y13>=0:Fy13/?y33;
  !!VRy33:*y23;               [...]
  !!MA&y14>=0:Fy14/?y34;
  !!VRy34:*y24;               [...]
  !!MA&y15>=0:Fy15/?y35;
  !!VRy35:*y25;               [...]
  !!MA&y16>=0:Fy16/?y36;
  !!VRy36:*y26;               [...]
  !!MA&y17>=0:Fy17/?y37;
  !!VRy37:*y27;               [...]
  !!VRy31:+y32 +y33 +y34 +y35 +y36 +y37; [y31 - FV армии ИИ]
  !!VRy32:Sy31 :2;            [y32 - 1/2 силы армии ИИ]
  !!VRy33:Sy31 :5;            [y33 - 1/5 силы армии ИИ]
  !!FU&y32<y5:E;              [выход, если армия ИИ не более чем в 2 раза сильнее атакующих]

  !!if&y33<y5;                [если армия ИИ сильнее атакующих от 2 до 5 раз, ИИ получает ресурсы за половину "спасенных"]
    !!VRy34:Sy3 :2 *y6;       [y34 - вознаграждение за половину "спасенных"]
    !!OW:Rx1/6/dy34;          [вознаграждение ИИ]
  !!el;                       [если армия ИИ сильнее атакующих более чем в 5 раз, ИИ получает ресурсы/наемников за 100% "спасенных"]
    !!VRy34:S-1;
    !!HEx2&y34<0/y11<0:C0/0/x7/y3;
    !!FU&y34<0/y11<0:E;       [добавление "спасенных" в первый сводобный слот армии ИИ]
    !!HEx2&y34<0/y12<0:C0/1/x7/y3;
    !!FU&y34<0/y12<0:E;       [...]
    !!HEx2&y34<0/y13<0:C0/2/x7/y3;
    !!FU&y34<0/y13<0:E;       [...]
    !!HEx2&y34<0/y14<0:C0/3/x7/y3;
    !!FU&y34<0/y14<0:E;       [...]
    !!HEx2&y34<0/y15<0:C0/4/x7/y3;
    !!FU&y34<0/y15<0:E;       [...]
    !!HEx2&y34<0/y16<0:C0/5/x7/y3;
    !!FU&y34<0/y16<0:E;       [...]
    !!HEx2&y34<0/y17<0:C0/6/x7/y3;
    !!FU&y34<0/y17<0:E;       [...]
    !!DO8098/0/6/1:Px2/0/3/1; [возврат в v1 номера самого слабого отряда ИИ]
    !!VRy37:Sv1;              [y37 - номер самого слабого отряда ИИ]
    !!VRy34:Sv1 +11;
    !!VRy35:Sy34 +10;         [y34/y35 - индексы y-переменных с типом/кол-вом существ в отряде]
    !!MA:Fyy34/?y36;          [y36 - FV существа в отряде]
    !!VRy36:*yy35 *3;         [y36 - утроенное FV самого слабого отряда]
    !!VRy38:Sy3 *y2;          [y38 - общее FV "спасенных"]
    !!HEx2&y36<y38:C0/y37/x7/y3; [если FV спасенных минимум втрое превышает FV самого слабого отряда, ИИ заменяет ими самый слабый отряд]
    !!FU&y36<y38:E;           [выход, если существа присоединены]
    !!VRy34:Sy3 *y6;          [y34 - вознаграждение за 100% "спасенных"]
    !!OW:Rx1/6/dy34;          [вознаграждение ИИ]
  !!en; 
!!el;                         [обработчик игрока-человека]
  !!UN:N3/1/x7/1;
  !!UN:N3/2/x8/1;             [z1/z2 - наименования существ (мн.ч.)]
  !!VRz7:Sz179346;
  !!VRz8:Sz179347;            [получение описаний события]
  !!VRy31:Sy3 -1 R2;
  !!VRy31&y31<1:S1;           [y31 - примерное число защищающихся (не менее 2)]
  !!VRy32:Sy4 -1 R2;
  !!VRy32&y32<1:S1;           [y32 - примерное число атакующих (не менее 2)]
  !!VRz9:S^~ %Y31^;
  !!VRz10:S^^;
  !!VRz11:S^~ %Y32^;          [подписи к картинкам]
  !!FU(ES_795_ShowDialog):Px2/4/0/3/?y12/1/x7/4/0/1/x8/0/8; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!SN:W^opt795.10.dt^/x7;    [установка параместров конфликта]
  !!SN:W^opt795.10.dc^/y3;    [...]
  !!SN:W^opt795.10.at^/x8;    [...]
  !!SN:W^opt795.10.ac^/y4;    [...]
  !!HEx2:R4/?y35 R4/0;        [y35 - статус Тактики героя, запрет тактической расстановки]
  !!FU8097:Px1/x2/x3/x4/x5/x6;[вызов боя с сохранением значений переменных]
  !!HEx2:R4/y35;              [восстановление статуса Тактики героя]
  !!SN:W^opt795.10.dc^/?y31 W^opt795.10.dc^/0;     [y31 - кол-во защищающихся существ / сброс кол-ва]
  !!HEx2:O?y30;               [y30 - принадлежность героя (-1 - проиграл бой)]
  !!FU|y30<0/y31<1:E;         [выход, если герой не победил в стражении или никого не удалось спасти]

  !!VRy32:Sy31 *y6;           [y32 - вознаграждение]
  !!UN:N3/1/x7/0;             [z1 - название существа (ед.ч.)]

  !!if&y31<>y3;               [если удалось спасти не всех]
    !!VRz2:Sz179348;          [z2 - текст сообщения]
    !!IF:Q1/6/y32/1/2;        [показ сообщения]
    !!OW:Rx1/6/dy6;           [добавление золота]
    !!UN:R2;                  [обновление строки ресурсов]
  !!el;                       [если удалось спасти всех]
    !!VRz2:Sz179349;          [z2 - текст сообщения]
    !!VRy33:Sy3 *65536 +x7;   [y33 - кол-во существ в подписи]
    !!IF:Q1/21/y33/6/y32/7/2; [показ сообщения]

    !!if&1;                   [если выбраны существа]
      !!HEx2:C2/x7/y3/1;      [добавление существ герою]
      !!UN:R1;                [обновление экрана]
    !!el;                     [если выбрано золото]
      !!OW:Rx1/6/dy6;         [добавление золота]
      !!UN:R2;                [обновление строки ресурсов]
    !!en; 
  !!en; 
!!en; 

!?FU(OnBeforeBattleUniversal);
!!SN:W^opt795.10.dc^/?y2;     [y2 - кол-во защищающихся существ в конфликте]
!!FU&y2=0:E;                  [выход, если не конфликт]

!!BA:Q0;                      [отключениe быстрой битвы]
!!SN:W^opt795.10.at^/?y4 W^opt795.10.ac^/?y5; [y4/y5 - тип/кол-во атакующих]
!!VRy12:Sy5 :2 *2;
!!VRy13:Sy5 :3 *3;            [y9 - количество атакующих отрядов (1..3)]
!!VRy9:S1;
!!VRy9&y12=y5:S2;
!!VRy9&y13=y5:S3;             [...]
!!VRy10:Sy5 :y9;              [y10 - кол-во существ в атакующих отрядах]
!!BA:M1/0/y4/y10;             [установка атакующих существ]
!!BA&y9>1:M1/1/y4/y10;        [...]
!!BA&y9>2:M1/2/y4/y10;        [...]

!?FU(OnSetupBattlefield);
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2; [y1/y2 - тип/кол-во атакующих]
!!FU&y2=0:E;                  [выход, если не конфликт]

!!BF:C;                       [удаление преград]
!!BU:Sy1/y2/94/0/-1/1 E94/?y3;[призыв защищающихся существ y3 - номер отряда]
!!SN:W^opt795.10.ds^/y3;      [сохранение номера защищающегося отряда]

!?FU(OnAfterBattleAction);
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2 W^opt795.10.ds^/?y3;
!!FU&y2=0:E;                  [выход, если не конфликт]

!!BU:C?y6;                    [y6=1, если бой завершен]
!!FU&y6=0:E;                  [выход, если бой не завершен]

!!BMy3:N?y4 T?y5 G-81/?y6/d;  [y4/y5/y6 - кол-во/тип/безвозвратные потери]
!!VRy4:-y6;
!!VRy4&y4<0:S0;               [y4 - реальное кол-во защищавшихся]
!!VRy4&y5<>y1:S0;             [обнуление кол-ва защищавшихся, если отряд тругого типа]
!!SN:W^opt795.10.dc^/y4;      [обновление кол-ва защищавшихся]

!?FU(OnBattleStackObtainsTurn);
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2 W^opt795.10.ds^/?y3;
!!FU&y2=0:E;                  [выход, если не конфликт]

!!SN:X?y4/?y5;
!!VRy4:*21 +y5;               [y4 - номер отряда]
!!BMy4:T?y5;                  [y5 - тип отряда]
!!FU|y5<>y1/y4<>y3:E;         [выход, если отряд не "защищающийся"]

!?FU8009;                     [Ассасины]
** Приобретение ассасинов, принимающих участие в боях героя
** x1 - игрок, x2 - герой, x3 - 0/1=Человек/ИИ, x4-x6 - координаты события, x7-x11 - параметры события
** P1 = размер группы, P2 = стоимость групы
** проверка условия срабатывания
!!OW:Rx1/6/?y1;               [y1 - золото игрока]
!!FU&y1<x8:E;                 [выход, если золота недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!VRx8:*-1 :2;              [x8 - отрицательная сумма для списания (половина стоимости для ИИ)]
  !!OW:Rx1/6/dx8;             [списание средств]
  !!SN:W^opt795.11.%X2^/dx7;  [ИИ всегда берет ассасинов]
!!el;                         [обработчик игрока-человека]
** x1-герой, x2/x3-тип/номер НПЦ, x4-кол-во картинок (0..3), x5-переменная для возврата результата выбора (1-да, 2-нет)
** x6/x7-тип/номер левой картинки, x8/x9-тип/номер центральной картинки, x10/x11-тип/номер правой картинки
** Типы: 0-герой, 1-монстр, 2-заклинание, 3-ресурс, 4-перв.параметр, 5-втор.навык/ранг, 6-артефакт
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
  !!VRz7:Sz179403;
  !!VRz8:Sz179404;            [получение описаний события]
  !!VRz9:S^%X7^;
  !!VRz10:S^^;
  !!VRz11:S^- %X8^;           [формирование подсказок]
  !!FU(ES_795_ShowDialog):Px2/7/0/2/?y12/1/143/0/0/3/6/0/9; [вызов диалога и получение ответа в y12 (1-да,2-нет)]

  !!if&y12<>1;                [при отказе]
    !!VRz1:Sz179405;          [z1 - текст финального сообщения]
    !!IF:M1/1;                [вывод сообщения]
  !!el;                       [при согласии]
    !!VRx8:*-1;               [x8 - отрицательная сумма для списания]
    !!OW:Rx1/6/dx8;           [списание средств]
    !!SN:W^opt795.11.%X2^/dx7;[добавление ассасинов герою]
    !!VRz1:Sz179406;          [z1 - текст финального сообщения при согласии]
    !!IF:M1/1;                [вывод сообщения]  
  !!en;     
!!en; 

!?FU(OnAfterLoadGame);                  // при загрузке игры
!!SN:W^auto.a^/0;                       // сброс параметров автобоя

!?FU(Event_AfterStackObtainsTurn);  [триггер: после фазы регенерации; проверки на мораль, страх; и после установки активного стека]
!!SN:W^auto.a^/?y10 W^auto.a^/0 W^auto.b^/?y11 W^auto.c^/?y12 W^auto.s^/?y13; [получение настроек автобоя]
!!UN&y10<>0:Cy10/1/y11 C6916068/1/y12 C6916072/1/y13;                         [восстановление параметров автобоя]
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2 W^opt795.10.ds^/?y3;             [y1/y2/y3 - тип/кол-во/номер отряда защищающихся существ]
!!BG:N?y4;
!!BMy4:T?y5;                        [y4/y5 - номер/тип активного стека]

!!if|y2=0/y3<>y4;                   [если нет защищающегося отряда или не его ход, получаем данные по ассасинам]
  ** &y2=0 [y1/y2/y3 - тип/кол-во/номер отряда ассасинов]
  !!VRy1:S143;
  !!SN:W^opt795.11.an^/?y2 W^opt795.11.as^/?y3;
!!en; 

!!FU|y2=0/y4<>y3/y5<>y1:E;          [выход, если нет или не ходит защищающийся отряд/ассасин или если тип отряда не совпадает]

!!UN:C6919200/4/?y10;               [y10 - combatmanager]
!!VRy1:Sy10 +78532;                 [y1 - адрес статуса автобоя]
!!UN:Cy1/1/?y11;                    [y11 - статус автобоя]
!!UN:C6916068/1/?y12 C6916068/1/1;  [y12 - статус автобоя - существа, существа ходят автоматически]
!!UN:C6916072/1/?y13 C6916072/1/0;  [y13 - статус автобоя - заклинания, заклинания НЕ кастуются автоматически]
!!SN:W^auto.a^/y1 W^auto.b^/y11 W^auto.c^/y12 W^auto.s^/y13; [сохранение настроек автобоя]
!!SN:E4683792/2/y10;                [включение автобоя]

!?FU(OnBeforeBattleUniversal);          // перед боем сброс боевых переменных ассасинов
!!UN:P795/?y1;
!!FU&y1=0:E;                            // выход если опция не включена

!!SN:W^opt795.11.br0^/-1 W^opt795.11.br1^/-1;
!!SN:W^opt795.11.as^/-1 W^opt795.11.an^/0;
!!SN:W^opt795.11.tf^/1;                 // установка флага "Тактическая фаза"

!?FU(OnBattleReplay);                   // перед переигрываемой битвой сброс боевых переменных ассасинов
!!UN:P795/?y1;
!!FU&y1=0:E;                            // выход если опция не включена

!!SN:W^opt795.11.br0^/-1 W^opt795.11.br1^/-1;
!!SN:W^opt795.11.as^/-1 W^opt795.11.an^/0;
!!SN:W^opt795.11.tf^/1;                 // установка флага "Тактическая фаза"

!?FU(OnAfterTacticsPhase);              // После тактической фазы
!!SN:W^opt795.11.tf^/0;                 // Сброс флага "Тактическая фаза"

!?FU(OnBattleRegeneratePhase);          [на фазе регенераци не в тактической фазе появляются ассасины соотв. героя]
!!UN:P795/?y1;
!!FU&y1=0:E;                            [выход если опция не включена]

!!SN:W^opt795.11.tf^/?y1;
!!FU&y1=1:E;                            [выход если тактическая фаза]

** исчезновение ассасинов совершивших действие
!!SN:W^opt795.11.an^/?y1 W^opt795.11.as^/?y2;

!!if&y2>=0;                             [если есть отряд ассасинов на поле]
  !!VRy3:Sy2 :21;
  !!BA:Hy3/?y4;                         [y3/y4 - сторона/герой-владелец походившего отряда ассасинов]
  !!BMy2:N?y5;
  !!SN:W^opt795.11.%Y4^/y5;             [сохраняем герою количество выживших после действия ассасинов]

  !!if&y5>0;                            [убираем отряд, если остались выжившие ассасины]
    !!UN:C6919200/4/?y11;               [Менеджер битвы]
    !!VRy12:Sy2 *1352 +21708 +y11 +234;
    !!UN:Cy12/1/1;                      [указываем, что отряд умер]
    !!BMy2:F?y6 P?y7;                   [y6/y7 - флаги/позиция отряда ассасинов]
    !!VRy6:|268435456 |1073741824 |64;  [ставим флаги "принесён в жертву", "серый цвет", "БМ"]
    !!BMy2:Fy6;                         [обновляем флаги]
    !!BA:Q?f;                           [f - статус битвы]

    !!if&f=0;                           [текст + звук + анимация №50 перед исчезновением для не-быстрой битвы]
      !!VRz1:Sz179410;
      !!MM:Sz1;                         [вывод текста исчезновения в лог битвы]
      !!SN:P^CLONE^;                    [вывод звука]
      !!UN:C6919200/4/?y8;
      !!SN:E4810128/2/y8/50/y7/40/0;    [вывод анимации]
    !!en; 

    !!BMy2:N0 B0;                       [обнуляем численность]
    !!SN:E4621680/2/y11/51/1;           [функция "убрать тело", если мертв]

    !!SN:H^monname^/(MON_ROGUE)/0/s^es_795_singRogueName^; [Возвращаем название "Головорезов"]
    !!SN:H^monname^/(MON_ROGUE)/1/s^es_795_plurRogueName^;

    !!SN:E4797616/2/y11/0/1;            [обновление сетки поля боя]
  !!en; 

  !!SN:W^opt795.11.an^/0 W^opt795.11.as^/-1; [обнуляем данные об ассасинах на поле]
  !!FU:E;                               [ассасины не могут быть призваны сразу после исчезновения]
!!en; 
**
!!UN:P758/?y1;
!!SN:W^opt758s^/?y2;
!!FU&y1>0/y2>=0:E;                      [появление ассасинов не прерывает повторный ход головорезов]

!!BG:N?y1 Q?y2;                         [y1/y2 - номер активного отряда/сторона (0/1)]
!!BA:Hy2/?y3;
!!FU&y3<0:E;                            [y3 - герой-владелец активного отряда, выход, если героя нет]

!!SN:W^opt795.11.f%Y3^/?y4;
!!FU&y4=1:E;                            [y4 - запрет появления ассасинов у героя. выход, если запрещено]

!!SN:W^opt795.11.%Y3^/?y4;
!!FU&y4<1:E;                            [y4 - кол-во ассасинов у героя. выход, если ассасинов нет]

!!SN:W^opt795.11.br%Y2^/?y5;            [y5 - раунд, в котором призывались ассасины]
!!FU&i^battle_round^<=y5:E;                        [выход, если ассасины героя уже появлялись в текущем раунде]

!!VRi:R0/0/3;
!!FU&i>0:E;                             [шанс появления ассасинов в раунде - 25% и увеличивается за каждый отряд в армии героя]

** поиск позиции для призыва, натрезвую разбираться в этом не советую
!!VRy8:S0;
!!VRy6:Sy2 -1 *-1 *21;
!!VRy7:Sy6+20;                          [инициализация переменных для поиска вражеских отрядов]

!!re i/y6/y7;
  !!BMi:N?y9;
  !!VRy8&y9>0:+1;                       [y8 - кол-во вражеских отрядов]
!!en; 

!!VRy8:-1;
!!VRj:S1 Ry8;
!!VRy8:S0;                              [j - случайный отряд, рядом с которым появятся ассасины]

!!re i/y6/y7;
  !!BMi:N?y9;
  !!VRy8&y9>0:+1;                       [y8 - номер найденного вражеского отряда]

  !!br&y8=j;                            [i - номер отряда, рядом с которым появятся ассасины]
!!en; 

!!VRi&i>41:S41;
!!BMi:P?y10;                            [y10 позиция отряда, рядом с которым появятся ассасины]
** debug !_!IF:M^Target:%Vi Position:%Y10^;
!!VRy11:Sy10 :17;
!!VRy12:Sy11 %2;                        [y11/y12 - номер/четность ряда отряда, рядом с которым появятся ассасины]
!!VRi:S0;                               [i - номер заполняемого элемента массива]
!!SN:M-1/18/0/0 W^opt795.ar^/v1;        [Mi^opt795.ar^ - массив возможных позиций появления ассасинов]

!!re l/-35/33/68;                       [проверяем дальние ряды (+-2)]
  !!VRy13:Sy10 +l +1 :17;               [y13 - проверяемый ряд]
  !!br|y13<0/y13>10;                    [прерываем проверку, если ряд вне поля]
  
  !!VRj:Sy10 +l;
  !!VRk:Sj +2;                          [j/k - диапазон проверяемых позиций]

  !!re y14/j/k;                         [y14 - проверяемая позиция]
    !!VRy15:Sy14 :17;
    !!VRy16:Sy14 %17;                   [y15/y16 - проверяемая позиция/ряд/номер в ряде]

    !!if&y13=y15/y16<>0/y16<>16/y14>0/y14<186;  [если проверяемая позиция в нужном ряде и не за пределами поля]
      !!BU:Dy14/?y17 Oy14/?y18;         [y17=-2 - в позиции живой отряд, y18<>0 - в позиции препятствие]

      !!if&y17<>-2/y18=0;               [если позиция доступна]
        !!SN:Mi^opt795.ar^/i/y14;       [сохраняем ее номер в массив]
        !!VRi:+1;                       [и увеличиваем счетчик]
      !!en; 
    !!en; 
  !!en; 
!!en; 

!!VRy21:S-19 +y12;
!!VRy22:S15 +y12;                       [y21/y22 - смещения позиций для проверки ближних рядов]

!!re l/y21/y22/34;                      проверяем ближние ряды (+-1)]
  !!VRy13:Sy10 +l +1 :17;               [y13 - проверяемый ряд]
  !!br|y13<0/y13>10;                    [прерываем проверку, если ряд вне поля]

  !!VRj:Sy10 +l;
  !!VRk:Sj +3;                          [j/k - диапазон проверяемых позиций]

  !!re y14/j/k;                         [y14 - проверяемая позиция]
    !!VRy15:Sy14 :17;
    !!VRy16:Sy14 %17;                   [y15/y16 - проверяемая позиция/ряд/номер в ряде]

    !!if&y13=y15/y16<>0/y16<>16/y14>0/y14<186;  [если проверяемая позиция в нужном ряде и не за пределами поля]
      !!BU:Dy14/?y17 Oy14/?y18;         [y17=-2 - в позиции живой отряд, y18<>0 - в позиции препятствие]

      !!if&y17<>-2/y18=0;               [если позиция доступна]
        !!SN:Mi^opt795.ar^/i/y14;       [сохраняем ее номер в массив]
        !!VRi:+1;                       [и увеличиваем счетчик]
      !!en; 
    !!en; 
  !!en; 
!!en; 

!!VRj:Sy10 -2;
!!VRk:Sj +4;                            [j/k - диапазон проверяемых позиций в ряде цели]

!!re y14/j/k;                           [y14 - проверяемая позиция]
  !!VRy15:Sy14 :17;
  !!VRy16:Sy14 %17;                     [y15/y16 - проверяемая позиция/ряд/номер в ряде]

  !!if&y13=y15/y16<>0/y16<>16/y14>0/y14<186;  [если проверяемая позиция в нужном ряде и не за пределами поля]
    !!BU:Dy14/?y17 Oy14/?y18;           [y17=-2 - в позиции живой отряд, y18<>0 - в позиции препятствие]

    !!if&y17<>-2/y18=0;                 [если позиция доступна]
      !!SN:Mi^opt795.ar^/i/y14;         [сохраняем ее номер в массив]
      !!VRi:+1;                         [и увеличиваем счетчик]
    !!en; 
  !!en; 
!!en; 
**призыв ассасинов
!!FU&i=0:E;                             [отмена призыва ассасинов, если не найдена свободная позиция в радиусе 2х гексов от цели]

!!SN:W^opt795.11.br%Y2^/v997;           [сохраняем раунд призыва ассасинов]
!!VRi:-1;
!!VRy19:S0 Ri;
!!SN:Mi^opt795.ar^/y19/?y20;            [y20 - случайная доступная позиция для призыва]
!!BA:Q?f;

  !!if&f=0;                             [текст + звук + анимация №50 перед появлением для не-быстрой битвы]
    !!VRz1:Sz179409;
    !!MM:Sz1;                           [вывод текста появления в лог битвы]
    !!VRz1:S^CLONE^;
    !!SN:P1;                            [вывод звука]
    !!UN&f=0:C6919200/4/?y25;
    !!SN&f=0:E4810128/2/y25/50/y20/40/0; [вывод анимации]
  !!en; 

!!BU:S(MON_ROGUE)/y4/y20/y2/-1/1 Ey20/?y23;     [y23 - номер отряда появившихся ассасинов]
!!FU|y23<0/y23>41:E;                    [выход, если призыв неудачен]

!!SN:W^opt795.11.as^/y23 W^opt795.11.an^/y4; [сохраняем номер и численность отряда появившихся ассасинов]
!!BMy23:A10 D10 H20 L0 S10 U1/5 U2/7;   [параметры ассасинов 10ат/10зщ/20зд/10ск/5-7ур]
!!BMy23:F?y24;                          [y24 - флаги отряда ассасинов]
!!VRy24:|33554432 |65536 |131072;       [нет ответа, нет морали, не может ждать]
!!BMy23:Fy24;                           [обновляем флаги отряда ассасинов]

!!SN:H^monname^/(MON_ROGUE)/0/?s^es_795_singRogueName^;
!!SN:H^monname^/(MON_ROGUE)/1/?s^es_795_plurRogueName^;
!!SN:H^monname^/(MON_ROGUE)/0/^%T(es.795.singName)^;
!!SN:H^monname^/(MON_ROGUE)/0/^%T(es.795.plurName)^;[установка новых названий ассасинов]

!!SN:Mi^opt795.ar^;                     // удаление временного массива

*!SN:Q;                                 [прерывание фазы регенерации] - Do we realy need this?

!?FU(OnBattleStackObtainsTurn);         [при получении отрядом хода передаем ход появившимся ассасинам]
!!UN:P795/?y1;
!!FU&y1=0:E;                            [выход если опция не включена]

!!SN:W^opt795.11.as^/?y1;               [y1 - номер отряда ассасинов]
!!FU&y1<0:E;                            [выход, если ассасинов нет]

!!VRy2:Sy1 :21;
!!VRy1&y1>20:-21;                       [y2/y1 - сторона и номер отряда для SN:X]
!!SN:Xy2/y1;                            [передача хода ассасинам]

!?FU(OnHeroScreenMouseClick);           [Клик в окне героя]
!!UN:P795/?y1;
!!FU&y1=0:E;                            [выход если опция 795 не включена]

!!CM:I?y1 F?y2 S?y3;                    [y1-y3 - параметры клика]
!!SN:F^GetButtonID^/^assasins^;
!!VRy4:Sv1;                             [y4 - ID кнопки "assasins"]
!!FU|y1<>y4/y2<>0/y3<>13:E;             [выход если не отпущена ЛКМ на кнопке ассасинов]

!!HE(CURRENT_HERO):N?y1;
!!SN:W^opt795.11.%Y1^/?y2;              [y2 - количество ассасинов у героя]

!!if&y2<1;                              [вывод информации об ассасинах героя и запрос роспуска, если ассасины есть]
  !!VRz2:Sz179412;                      // 
  !!IF:M1/z2;                           // 
!!el;                                   // 
  !!SN:W^opt795.11.f%Y1^/?y3;           // y3 - запрет участия ассасинов в битвах
  !!VRz2:Sz179411;
  !!VRz2&y3=1:Sz179192;                 // z2 - текст сообщения
  !!IF:Q1/z2;                           // вывод кол-ва ассасинов и запрос роспуска
  !!SN&1:W^opt795.11.f%Y1^/0;           // разрешение участия в битве при положительном ответе
  !!SN&-1:W^opt795.11.f%Y1^/1;          // запрет участия в битве при отрицательно ответе
!!en;                                   // 

!?FU(OnBeforeBattleAction);             // перед действием в бою
!!UN:P795/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!BG:A?y1;
!!SN:W^opt795.11.a^/y1;                 // сохраняем последнее выполненное действие

!?FU(OnAfterBattleUniversal);
!!UN:P795/?y1;
!!FU&y1=0:E;                            [выход если опция 795 не включена]

!!SN:H^monname^/(MON_ROGUE)/0/s^es_795_singRogueName^;[возвращаем оригинальные названия Воров]
!!SN:H^monname^/(MON_ROGUE)/1/s^es_795_plurRogueName^;

!!BA:H0/?y1 H1/?y2;
!!FU&y2<1:E;                            // y1/y2 - атакующий/защищающийся герои, выход, если не битва двух героев

!!SN:W^opt795.11.a^/?y3;
!!FU|y3=4/y3=5:E;                       // выход, если герой сбежал/откупился

!!HEy1:O?y3;!!HEy2:O?y4;                // y3/y4 - владельцы атакующеего/защищающегося героя
!!FU&y3<0/y4<0:E;                       // выход, если оба героя погибли
!!FU&y3>=0/y4>=0:E;                     // выход, если оба героя выжили (вербовка бандита)

!!if&y3<0;                              // если победил атакующий
  !!VRy4:Sy1;
  !!VRy1:Sy2;
  !!VRy2:Sy4;                           // y1/y2 - победитель/побежденный
!!en; 
!!SN:W^opt795.11.%Y2^/?y3 W^opt795.11.%Y2^/0; // y3 - количество ассасинов у побежденного героя, обнуление
!!SN:W^opt795.11.%Y2.rebirth^/?y4 W^opt795.11.%Y2.rebirth^/0; // y4 - количество ассасинов при перерождении, обнуление
!!VRy3&y3=0/y4>0:Sy4;                   // корректировка ассасинов героя при перерождении
!!FU&y3<1:E;                            // выход, если у побежденного героя нет выживших ассасинов

!!HEy1:O?y4;
!!OW:Iy4/?y6;                           // y6 - ИИ признак победителя

!!if&y6=1;                              // ИИ всегда присоединяет ассасинов
  !!SN:W^opt795.11.%Y1^/?y5;
  !!VRy5:+y3;
  !!SN:W^opt795.11.%Y1^/y5;             // ...
!!el;                                   // Человеку дается выбор ассасины/золото
  !!SN:H^monname^/(MON_ROGUE)/0/^%T(es.795.singName)^;// установка новых названий ассасинов
  !!SN:H^monname^/(MON_ROGUE)/0/^%T(es.795.plurName)^;

  !!VRy7:Sy3 *200;                      // y7 - компенсация в золоте 200 за ассасина
  !!VRz1:Sz179418;                      // z1 - текст присоединения ассасинов
  !!VRy5:Sy3 *65536 +(MON_ROGUE);       // y5 - количество существ для отображения в диалоге
  !!IF:Q1/6/y7/21/y5/7^%Z1^;            // вывод вопроса о присоединении

  !!if&1;                               // если выбрано золото
    !!OW:Ry4/6/dy7;
    !!UN:R2;                            // добавляем золото с обновлением строки ресурсов
  !!el;                                 // иначе
    !!SN:W^opt795.11.%Y1^/?y5;
    !!VRy5:+y3;
    !!SN:W^opt795.11.%Y1^/y5;           // добавляем ассасинов
  !!en;                                 // ...

  !!SN:H^monname^/(MON_ROGUE)/0/s^es_795_singRogueName^;// возвращаем оригинальные названия Воров
  !!SN:H^monname^/(MON_ROGUE)/1/s^es_795_plurRogueName^;
!!en;                                   // ...

** end

ZVSE2
** Author        : Algor
** Updated by    : Archer30
** Name          : Bounty Hunting
** Name rus.     : Охота за головами
** Option        : 725

** При наличии незадействаванных игроков на карте могут появляться банды героев-отступников,
** живущие без городов на какой-то небольшой территории и промышляющие разбоем.
** Со временем банды таких "героев" разрастаются, а сами они получают ресурсы, опыт и снаряжение.

** Для борьбы с бандами Гильдия Охотников За Головами объявляет награды за головы героев-бандитов.
** После победы над бандитом игрок может либо казнить его и получить причитающееся вознаграждение,
** либо завербовать, однако в этом случае награды он не получит.

** ИИ игроки предпочитают вербовать героя, только если его он не ниже 15 уровня или опытнее героя-победителя.

** Если на территории банды есть город и банда смогла его захватить, все активные бандиты объединяются
** в новую фракцию и Гильдия Охотников За Головами отзывает все награды за их головы.
** После появления такой фракции, новые бандиты появляются на карте уже под другим свободным флагом
** и, в свою очередь, также могут захватить город на "своей" территории и создать новую фракцию.

** Если игрок победил всех оппонентов, бандиты разбегаются и их не требуется убивать для завершения игры.


!?FU(OnAfterErmInstructions);           // перед началом игры
!!UN:P725/?y1;
!!FU&y1<1:E;                            // выход, если опция не включена

!!UN:X?y1/?y2;
!!VRy2:+1 *y1 *y1;                      // y2 - количество квадратов карты
!!VRy1:Sy2 :5000 +1;
!!VRy1&y1>8:S8;                         // y1 - макс. количество банд (1..8) в зависимости от размера карты

!!SN:M-1/y1/0/1 W^opt725.b^/v1;         // Mi^opt725.b^ - сохраняемый массив героев-бандитов
!!SN:M-1/y1/0/1 W^opt725.g^/v1;         // Mi^opt725.g^ - сохраняемый массив наград за головы героев-бандитов
!!SN:M-1/8/0/1 W^opt725.n^/v1;          // Mi^opt725.n^ - сохраняемый массив объявлений гильдии
!!VRy1:-1;                              // инициализация массива бандитов и наград (-1)

!!re i/0/y1;                            // ...
  !!SN:Mi^opt725.b^/i/-1;               // ...
  !!SN:Mi^opt725.g^/i/-1;               // ...
!!en;                                   // ...

!!re i/0/7;                             // инициализация массива объявлений (-1)
  !!SN:Mi^opt725.n^/i/-1;               // ...
!!en;                                   // ...

!!SN:W^opt725.p^/-1;                    // номер игрока для героев-бандитов не задан

!?FU(OnEveryDay);                       // ежедневно
!!UN:P725/?y1;
!!FU&y1<1:E;                            // выход, если опция не включена

** Проверка на создание новой фракции
!!SN:W^opt725.p^/?y11;                  // y11 - номер игрока для героев-бандитов

!!if&y11>=0;                            // если игрок-бандит установлен, проверяем создание новой фракции
  !!OW:Ny11/0/?y12;                     // y12 - город в 0-слоте игрока-бандита

  !!if&y12>=0;                          // если у игрока-бандита появился город
    !!SN:Mi^opt725.b^/?y13;
    !!VRy13:-1; // y13 - номер последнего элемента массива бандитов

    !!re i/0/y13;                       // для всех "бывших" бандитов
      !!SN:Mi^opt725.b^/i/?y15;         // y15 - номер выжившего бандита
      !!HEy15:U?k/?k/-1;                // убираем для бандитов ограничение пна перемещения по карте
      !!SN:Mi^opt725.b^/i/-1;           // удаляем бандитов из массива бандитов
      !!SN:Mi^opt725.g^/i/-1;           // удаляем бандитов из массива наград
    !!en;                               // ...

    !!re i/0/7;                         // для каждой фракции создаем уведомление о появлении новой фракции и об отзыве всех заказов
      !!VRy14:Sy11 *-1 -10;             // y14 - номер фракции в уведомлении (-10...-17)
      !!SN:Mi^opt725.n^/i/y14;          // установка уведомления
    !!en;                               // ...

    !!SN:W^opt725.p^/-1;                // сброс номера игрока для героев-бандитов
    !!OW:Ty11/-1 Dy11/255;              // игрок-бандит не в команде и НЕ может существовать без городов
    !!VRy11:S-1;
    !!SN:W^opt725.p^/-1;                // удаление признака бандит у игрока
    !!VRs:Sc +3;
    !!SN:W^opt725.d^/s;                 // спавн банд возможен не ранее чем через 3 дня после объелинения предыдущих банд во фракцию
  !!el;                                 // Если города не появилось, проверяем активность бандитов и наград

  ** проверяем актуальность бандитов и наград
    !!VRo:S0;
    !!SN:Mi^opt725.b^/?j;
    !!VRj:-1;                           // поиск награды за голову героя-бандита в массиве наград

    !!re i/0/j;                         // ...
      !!SN:Mi^opt725.b^/i/?k;           // k - проверяемый герой
      !!HEk&k>=0:O?l;                   // l - текущий владелец проверяемого героя

      !!if&k>=0/l<>y11;                 // если герой бандит неактивен
        !!SN:Mi^opt725.b^/i/-1;         // удаление из списка бандитов и отзыв награды если владелец сменился или отсутствует
        !!SN:Mi^opt725.g^/i/-1;         // ...

        !!re m/0/7;                     // удаление из объявлений
          !!SN:Mi^opt725.n^/m/?n;       // n - значение текущего объявления
          !!SN&n=k:Mi^opt725.n^/m/-1;   // очистка объявления, если в нем проверяемый герой
        !!en;                           // ...

      !!el; 
        !!VRo:+1;                       // подсчитываем количество активных бандитов
      !!en;                             // ...
    !!en;                               // ...

    !!VRy11&o<1:S-1;                    // если все бандиты уничтожены, снимаем с игрока бандита признак "бандит"
    !!SN&o<1:W^opt725.p^/-1;            // ...
  !!en;                                 // ...
!!en;                                   // ...

** ежедневный рост опыта и армий бандитов
!!OW:C?n;                               // n - текущий игрок

!!if&n=y11;                             // если ход игрока-бандита
  !!SN:Mi^opt725.b^/?j;
  !!VRj:-1;                             // поиск активных бандитов

  !!re i/0/j;                           // ...
    !!SN:Mi^opt725.b^/i/?k;             // k - проверяемый бандит

    !!if&k>=0;                          // если бандит есть
      !!OW:Rn/0/d2 Rn/1/d1 Rn/2/d2 Rn/3/d1 Rn/4/d1 Rn/5/d1 Rn/6/d1000; // каждый активный бандит пополняет казну игрока-бандита
      !!HEk:Ed300;                      // бандит получает 300 опыта в день

      !!re l/0/6;                       // бандит имеет шанс получить прирост к отрядам существ 1-4 уровней
        !!HEk:C0/l/?g/?h;               // g/h - тип/кол-во существ в слоте
        !!co|g<0/h<1;                   // пропуск пустого слота

        !!MA:Lg/?m;
        !!VRm:-4 *-1;
        !!VRm&m<0:S0;
        !!VRg:S0 Rm;                    // g - прирост отряда (0..4 для 1го уровня, 0..3 для 2го ...  0..1 для 4го)
        !!HEk&g>0:C0/l/d/dg;            // прирост отряда
      !!en;   
    !!en; 
  !!en; 
!!en; 

** Ликвидация бандитов, если если все вражеские фракции побеждены
!!if&y11>=0;                            // если игрок-бандит существует
  !!OW:C?y20 Ty20/?y21;
  !!VRy22:S0;                           // y20/y21/y22 - текущий игрок/команда текущего игрока/кол-во враждебных фракций

  !!re i/(PLAYER_FIRST)/(PLAYER_LAST);                           // перебор всех игроков
    !!OW:Ii/?j/?k Ti/?l;
    !!VRl&l=-1:S77;                     // k/l - активность/команда игрока
    !!VRy22&i<>y20/l<>y21/i<>y11/k=0:+1;// если игрок жив, не бандит, не текущий игрок и не союзник, увеличиваем счетчик врагов
  !!en;

  !!if&y22=0;                           // если активные вражаеские фракции не найдены
    !!SN:Mi^opt725.b^/?j;

    !!re i/0/j/1/-1;                    // ищем всех активных бандитов
      !!SN:Mi^opt725.b^/i/?k;           // k - бандит в слоте 

      !!if&k>(NO_HERO);
        !!HEk:K;                        // ликвидируем банду
        !!SN:Mi^opt725.b^/i/-1;         // удаляем бандитов из массива бандитов
      !!en;

      !!SN:Mi^opt725.g^/i/-1;           // удаляем бандитов из массива наград
    !!en;                               // ...

    !!SN:T^opt725.e^/?z1;
    !!IF:M^%Z1^;                        // выводим сообщение о распаде бандформирований
    !!FU:E;                             // и выходим
  !!en;
!!en;

** Показ объявлений для текущего игрока
!!OW:C?y8 Iy8/?y9/?y10;                 // y8/y9/y10 - текущий игрок/ИИ-признак/активность
!!SN:Mi^opt725.n^/y8/?y7;               // y7 - объявление для текущего игрока

!!if&y9=0/y10=0/y7<>-1;                 // если ход активного игрока-человека и для него есть объявление
  !!if&y7>=0;                           // если объяление о новой банде
    !!HEy7:O?p;                         // p - игрок-хозяин бандита

    !!if&p=y11&y11>=0;                  // если бандит не убит и не завербован
      !!FU(ES_725_ShowDialog):P0/y7; // показ объявления
      !!HEy7:P?y4/?y5/?y6;              // координаты бандита
      !!UN:Sy4/y5/y6/y8/3 Ly4/y5/y6/5000;// открываем и показываем бандита на карте (радиус открытого пятна - 3, задержка "взгляда"- 5 сек)
    !!en;    

    !!SN:Mi^opt725.n^/y8/-1;            // отмечаем объявление как показанное
  !!el;                                 // если объяление о новой фракции
    !!VRy7:+10 *-1;                     // y7 - номер новой фракции
    !!FU(ES_725_ShowDialog):P1/y7;   // показ объявления о новой фракции
    !!SN:Mi^opt725.n^/y8/-1;            // отмечаем объявление как показанное
  !!en;                                 // 
!!en; 

!!if&y9=1/y10=0/y7>=0;                  // если ход активного ИИ-игрока и для него есть объявление о новой банде
  !!HEy7:P?y4/?y5/?y6;                  // координаты бандита
  !!UN:Sy4/y5/y6/y8/3;                  // показываем бандита на карте (радиус открытого пятна - 3)
  !!SN:Mi^opt725.n^/y8/-1;              // отмечаем объявление как показанное
!!en; 

** Появление бандитов
!!VRy31:S-1;                            // поиск свободного слота для добавления банды
!!SN:Mi^opt725.b^/?j;
!!VRj:-1;                               // ...

!!re i/0/j;                             // ...
  !!SN:Mi^opt725.b^/i/?k;               // k - проверяемый герой

  !!if&k<0;                             // если слот свободен
    !!VRy31:Si;                         // y31 - номер слота банды

    !!br;                               // ...
  !!en;                                 // ...
!!en;                                   // ...

!!FU&y31<0:E;                           // выход, если нет свободного слота для добавления банды

!!VRy1:Sc;
!!FU&y1<8:E;                            // первый спавн банды не ранее 2й недели 

!!SN:W^opt725.d^/?y2;
!!FU&y1<y2:E;                           // выход, если недавно был спавн или объединение во фракцию

!!OW:C?y3 Iy3/?y4/?y5;
!!FU|y4>0/y5>0:E;                       // выход, если ход ИИ

!!VRy3:S1 R99;
!!FU&y3>25:E;                           // выход, если шанс спавна (25%) не выпал

!!if&y11=-1;                            // если игрока-бандита нет, ищем свободного игрока
  !!re i/0/7; 
    !!OW:Ii/?y16/?y17;                  // y17=1, если игрок неактивен

    !!if&y17=1;                         // если игрок неактивен
      !!OW:Ii/1/1;                      // передаем игрока под контроль ИИ
      !!SN:W^opt725.p^/i;               // помечаем игрока как бандита
      !!VRy11:Si;                       // y11 - новый игрок-бандит

      !!br;                             // прерываем поиск
    !!en;                               // ...
  !!en;                                 // ...
!!en;                                   // ...

!!FU&y11<0:E;                           // Выход, если нет игрока-бандита и свободного игрока

!!FU(ES_GetRandomUnoccupiedHero):P?y7;
!!FU&y7=(NO_HERO):E;

** поиск места спавна
!!UN:X?y33/?y34;
!!VRy33:-3;                             // y33/y34 - доступные размеры карты

!!re i/1/30;                            // попытка найти подходящее место для банды (30 попыток)
  !!VRy35:S1 Ry33;
  !!VRy36:S1 Ry33;
  !!VRy37:S0 Ry34;                      // y35/y36/y37 - проверяемые координаты
  !!VRm:S0;                             // m - кол-во подходящих квадратов на площадке

  !!re j/-1/1;                          // проверяем площадку 3х3 с центром в проверяемых координатах
    !!re k/-1/1;                        // ...
      !!VRy38:Sy35 +j;
      !!VRy39:Sy36 +k;                  // y38/y39/y37 - проверяемый квадрат
      !!TRy38/y39/y37:T?l/d/d/d/d/d/d/d;// l - тип почвы

      !!br 2&l>7/j=0/k=0;               // площадка не подходит, если центральный квадрат с водой или скалой
      !!co&l>7;                         // квадрат с водой или скалой не подходит

      !!TRy38/y39/y37:E?l P?n;          // l=0 для желтого, k=0 для красного квадратов

      !!br 2|l=0/n=0;                   // площадка не подходит, если центральный квадрат желтый или красный
      !!co|l=0/n=0;                     // желтые и красные квадраты не подходят

      !!VRm:+1;                         // учет квадрата, как подходящего
    !!en;                               // ...
  !!en;                                 // ...

  !!br&m>=7;                            // выход, если найдена подходящая площадка (минимум 7 из 9 квадратов пустые, включая центральный)
!!en;                                   // ...

!!FU&m<7:E;                             // выход, если для банды не найдено подходящей территории

** Появление банды
** Герой-бандит
!!FU(ES_788_Rebirth):Py7;               // перерождение героя-бандита, если доступна опция "смертные герои" (не обязательно включена)
!!VRy2:Sy1 *300;
!!HEy7:Edy2;                            // Повышение уровня героя (300 опыта в день)

** заклинания
!!HEy7:X?y2/?y3/d/d/d/d/d Fd/d/d/?y4 S(SKILL_WISDOM)/?y5; // y2/y3/y4/y5 - тип.спец./специализация/Знание/Мудрость
!!VRy3&y2<>3:S-1;                       // y3 - заклинание, на котором специализируется герой (-1, если не спец по заклинанию)
!!HEy7&y4>3:A1/0/17;
!!HEy7:A1/?y2/17;                       // бандиты со Знанием 4+ автоматически получают Книгу

!!if|y2=0/y3>0;                         // выдаем заклинания у героя Книга или герой - спец. по заклинанию.
  !!HEy7&y3>=0:My3/1;                   // Специалисты по заклинанию гарантированно получают профильное заклинание.
  !!VRy2:Sy4 :3 +1;                     // y2 - кол-во бонусных заклинаний до (1 + 1 за каждые 3 Знания) (только боевые заклинания)
  !!VRy5:+2;
  !!VRy5&y5>y4:Sy4;                     // y5 - maximum level of available spells (from Wisdom and Knowledge)

  !!re i/1/y2;                          // Teach a spell
    !!FU(Fun_GetRandomSpell)&y5<3:P0/0/2/2/2/0/0/0/0/1/0; // v1 - случайное боевое заклинание не выше заданного уровня
    !!FU(Fun_GetRandomSpell)&y5=3:P0/0/0/2/2/0/0/0/0/1/0; // ...
    !!FU(Fun_GetRandomSpell)&y5=4:P0/0/0/0/2/0/0/0/0/1/0; // ...
    !!FU(Fun_GetRandomSpell)&y5=5:P0/0/0/0/0/0/0/0/0/1/0; // ...
    !!HEy7&v1>(NO_SPELL):Mv1/1;         // герой получает выпавшее заклинание
  !!en; 
!!en; 

!!HEy7:I?y5/1;
!!VRy6:Sy4 *10;
!!HEy7&y5<y6:Iy6/1;                     // даем герою 10 маны за единицу Знания, если маны менее этого количества

** артефакты
!!VRy2:Sy1 -1 :28 +1;                   // y2 - кол-во артефактов у бандита (1 за месяц)

!!re i/1/y2;                            // даем бандиту артефакты
  !!VRj:S1 R1 *2;                       // j - класс артефакта (только treasure и minor)
  !!UN:J6/j/?k;                         // k - случайный артефакт нужного класса
  !!HEy7:A4/k;                          // бандит получает артефакт
!!en; 

** генерация банды
!!VRy2:Sy1 *1300;                       // y2 - сила армии (1300 FV в день)
!!VRy3:Sy1 -1 :7 +1 *750;               // y3 - награда за голову бандита - 750 золотых за неделю

!!re i/0/3;                             // подбор армии (максимум 4 слота)
  !!VRy10:S0 R9;                        // y10 - фракция отряда (9 - нейтралы)
  !!VRy12:S0 R9;                        // y12 - уровень/грейд существа (максимум 5up)
  !!VRy13:Sy10 *14 +y12;                // y13 - номер существа
  !!VRy13&y13>=126:+11;                 // корректировка существ Сопряжения и нейтралов
  !!VRy13&y13=120:S123;                 // ...
  !!VRy13&y13=121:S127;                 // ...
  !!VRy13&y13=145:S125;                 // ...
  !!VRy13&y13=146:S129;                 // ...
  !!MA:Fy13/?y14;                       // y14 - FV существа
  !!VRy15:S4 -i;
  !!VRy16:Sy2 :y15;                     // y16 - FV оряда (примерное)
  !!VRy17:Sy16 :y14;
  !!VRy17&y17<1:S1;                     // y17 - численность отряда
  !!VRy16:Sy14 *y17;
  !!VRy2:-y16;                          // y2 - нераспределенное FV армии
  !!HEy7:C0/i/y13/y17;                  // установка отряда герою-бандиту
!!en; 

** спавн героя-бандита y7 игрока y11 в координатах y35/y36/y37 (слот банды y31, награда y3)
!!OW:Iy11/d/?i;                         // i=1, если игрок еще не активирован, 
!!OW&i=1:Iy11/1/0 Ry11/0/30 Ry11/1/15 Ry11/2/30 Ry11/3/15 Ry11/4/15 Ry11/5/15 Ry11/6/30000; // активируем игрока-бандита и даем начальный капитал игрока-бандита
!!HEy7:Py35/y36/y37 Oy11 Uy35/y36/10;   // помещаем героя на карту, меняем владельца и задаем радиус патрулироания (10)
!!OW:Ay11/y7 Dy11/127 Ty11/-1;          // активируем героя, жизнь вне городов 127 дней, нет союзов

!!re i/0/7;                             // создаем объявление о новой банде для всех игроков
  !!SN:Mi^opt725.n^/i/y7;               // ...
!!en;                                   // ...

!!SN:Mi^opt725.b^/y31/y7 Mi^opt725.g^/y31/y3; // добавляем банду и награду в списки 
!!VRi:Sc +3;
!!SN:W^opt725.d^/i;                     // Сохраняем минимально возможный день появления новой банды (не ранее, чем через 3 дня)

!?FU(OnEveryDay);                       // ежедневно
!!UN:P725/?y1;
!!FU&y1<1:E;                            // выход, если опция не включена

!!SN:W^opt725.p^/?y1;
!!OW:C?y2;
!!FU&y1<>y2:E;                          // выход, если не ход героя-бандита

!!re i/0/7;                             // просмотр уведомлений для всех игроков
  !!SN:Mi^opt725.n^/i/?y1;              // y1 - номер уведомления
  !!HEy1&y1>=0:W0/1;                    // обнуляем очки движения бандита, если уведомление о нем еще не показано всем игрокам
!!en;                                   // ...

!?FU(ES_725_ShowDialog);                // Показ диалога
!!UN:C6885977/1/?y12; if gosolo

!!if&y12;
  !!FU:E;
!!en;
** x1 - тип диалога. 0 - новая банда, 1 - новая фракция, 2 - найм/казнь
** x2 - номер героя-главаря/номер новой фракции/номер банды
** x3 - игрок победитель для диалога найма/казни (x1=2)
!!DL76&x1=0:N^opt725b.txt^;             // подгрузка 76 диалога
!!DL76&x1=1:N^opt725f.txt^;             // подгрузка 76 диалога
!!UN:C6129162/1/?y31;                   // y31 - текущий лимит героев
!!OW:Ox3/?y32/d/d/d/d/d/d/d/d;          // y32 - количество активных героев победителя

!!if&x1=2;
  !!if&y32<y31;
    !!DL76:N^opt725r.txt^;              // подгрузка 76 диалога найм/казнь
  !!el;
    !!DL76:N^opt725r2.txt^;             // подгрузка 76 диалога только казнь, если у игрока уже максимум активных героев
  !!en;
!!en;

!!if&-1;                                // если диалог не найден
  !!IF:M0/4/^ERROR: Dialog not found!^; // выводим сообщение об ошибке
  !!FU:E;                               // и выходим
!!en; 

** диалог новой банды
!!if&x1=0;                              // диалог новой банды
  !!SN:T^opt725.b.t1^/?z3;              // получение текстов диалога
  !!HEx2:B0/?z5;
  !!SN:T^opt725.b.t2^/?z4/^HeroName^/z5;
  !!SN:T^opt725.b.t3^/?z5; // получение текстов диалога
  !!DL76:A12/3/z3/1 A13/3/z4/1  A15/3/z5/1; // установка текстов диалога
  !!HEx2:F?y1/?y2/?y3/?y4;              // y1-y4 - параметры героя-бандита
  !!VRz7:S^%Y1^;
  !!VRz8:S^%Y2^;
  !!VRz9:S^%Y3^;
  !!VRz10:S^%Y4^; // z6-z9 - параметры героя-бандита (текст)
  !!DL76:A31/3/z7/1 A32/3/z8/1 A33/3/z9/1 A34/3/z10/1; // установка параметров героя-бандита в диалог
  !!HEx2:C0/0/?y11/?y21 C0/1/?y12/?y22 C0/2/?y13/?y23 C0/3/?y14/?y24;         // y11-y14/y21-y24 - тип/количество существ в 1-4 слотах
  !!VRz31:S^%Y21^;
  !!VRz32:S^%Y22^;
  !!VRz33:S^%Y23^;
  !!VRz34:S^%Y24^;                      // z31-z34 - количества существ (текст)
  !!VRz31&y21<1:S^^;
  !!VRz32&y22<1:S^^;
  !!VRz33&y23<1:S^^;
  !!VRz34&y24<1:S^^;                    // ...
  !!DL76:A51/3/z31/1 A52/3/z32/1 A53/3/z33/1 A54/3/z34/1;                     // установка количества в диалог
  !!VRy11:+2;
  !!VRy12:+2;
  !!VRy13:+2;
  !!VRy14:+2;                           // y11-y14 - номера кадров для дефа существ
  !!VRy11&y21<1:S0;
  !!VRy12&y22<1:S0;
  !!VRy13&y23<1:S0;
  !!VRy14&y24<1:S0;                     // y11-y14 - номера кадров для дефа существ
  !!DL76:A41/4/y11/1 A42/4/y12/1 A43/4/y13/1 A44/4/y14/1;                     // установка номеров кадров существ
  !!DL76&y21<1:E41/0;
  !!DL76&y22<1:E42/0;
  !!DL76&y23<1:E43/0;
  !!DL76&y24<1:E44/0;                   // не показывать пустые отряды
  !!VRl:S0;                             // поиск награды за голову героя-бандита в массиве наград
  !!SN:Mi^opt725.b^/?j;
  !!VRj:-1;                             // ...

  !!re i/0/j;                           // ...
    !!SN:Mi^opt725.b^/i/?k;             // k - проверяемый герой
    !!SN&k=x2:Mi^opt725.g^/i/?l;        // l - награда за голову
  !!en;                                 // ...

  !!SN:T^opt725.b.t4^/?z30/^Bounty^/^%Vl^; // z30 - текст награды
  !!DL76:A16/3/z30/1;                   // установка текста награды в диалог
  !!FU(Fun_GetHeroPortrait):Px2/1/36;   // z36 - большой портрет героя
  !!DL76:A20/11/z36/1;                  // портрета в диалог
  !!SN:T^opt725.b.h1^/?z35;
  !!DL76:H1/z35;                        // установка подсказки на кнопку ок
!!en; 

** диалог новой фракции
!!if&x1=1;                              // диалог новой фракции
  !!SN:T^opt725.f.t1^/?z2;
  !!SN:T^opt725.f.t2^/?z3;              // получение текстов диалога
  !!DL76:A12/3/z2/1 A13/3/z3/1;         // установка текстов диалога
  !!DL76:A20/4/x2/1;                    // установк кадра с флагом новой фракции
  !!SN:T^opt725.f.h1^/?z7;
  !!DL76:H1/z7;                         // установка подсказки на кнопку ок
!!en; 

** диалог вербовки/награды
!!if&x1=2;                              // диалог вербовки/награды
  !!SN:T^opt725.r.t1^/?z1;
  !!SN:T^opt725.r.t2^/?z2;              // получение текстов диалога

    ; Set up text according to whether the player can recruite more heroes
  !!if&y32<y31;
    !!VRz2:+^%T(opt725.r.t3)^;
  !!el;
    !!VRz2:+^%T(opt725.r.t4)^;
  !!en;

  !!DL76:A10/3/z1/1 A11/3/z2/1;         // установка текстов диалога
  !!SN:Mi^opt725.b^/x2/?y11 Mi^opt725.g^/x2/?y12; // y11/y12 - Герой/Награда
  !!HEy11:B0/?z5;
  !!DL76:A90/3/z5/1;                    // установка имени героя
  !!VRz6:S^%Y12^;
  !!DL76:A91/3/z6/1;                    // установка размера вознаграждения
  !!FU(Fun_GetHeroPortrait):Py11/1/7;   // z7 - большой портрет героя
  !!DL76:A80/11/z7/1;                   // установка портрета в диалог
  !!SN:T^opt725.r.h1^/?z8;
  !!DL76:H70/z8;                        // установка подсказки на кнопку героя
  !!SN:T^opt725.r.h2^/?z9;
  !!DL76:H71/z9;                        // установка подсказки на кнопку золота
  !!SN:T^opt725.r.h3^/?z22;
  !!DL76:H3/z22;                        // установка подсказки на кнопку вербовки 
  !!SN:T^opt725.r.h4^/?z23;
  !!DL76:H4/z23;                        // установка подсказки на кнопку награды
  !!VRp:Sy11;                           // p - номер героя для передачи в диалог
!!en; 

!!DL76:S;                               // показ диалога

** обработка действий в диалоге
!?DL&v998=76;                           // обработка диалога 77 
!!DL76&v999=1/v1000=13:C1;              // выход (отпущена ЛКМ на кнопке 1)

; Open a pop-up type hero screen for the hero p
!!if&v999=70/v1000=(MOUSE_RMB_PRESSED);
  !!UN:C5119174/1/184 C5119175/4/1;     // патч: деактивация кнопки "Уволить"
  !!SN:E5118576/1/p/1;                  // открыть окно героя
  !!UN:C5119174/1/161 C5119175/4/6916832; // патч: вернуть исходный код кнопки "Уволить"
!!en;                                   // 

!!if&v999=3/v1000=13;                   // выбор вербовки героя
  !!VRi^es_dlgRes^:S1;                  // 
  !!DL76:C1;                            // 
!!en;                                   // 

!!if&v999=4/v1000=13;                   // выбор награды за голову
  !!VRi^es_dlgRes^:S2;                  // 
  !!DL76:C1;                            // 
!!en;                                   // 

** победа над бандитом, получение награды или вербовка
!?FU(OnBeforeBattleUniversal);          // перед боем
!!UN:P725/?y1;
!!FU&y1<1:E;                            // выход, если опция не включена

!!BA:H0/?y1 H1/?y2;
!!FU&y2<0:E;                            // y1/y2 - сражавшиеся герои, выход если не битва 2х героев

!!HEy1:P?y11/?y12/?y13;                 // y11..y13/y21..y23 - позиции героев на карте
!!HEy2:P?y21/?y22/?y23;                 // ...
!!SN:W^opt725.h1.x^/y11 W^opt725.h1.y^/y12 W^opt725.h1.l^/y13; // сохранение позиций
!!SN:W^opt725.h2.x^/y21 W^opt725.h2.y^/y22 W^opt725.h2.l^/y23; // ...

!?FU(OnAfterBattleUniversal);           // после боя
!!UN:P725/?y1;
!!FU&y1<1:E;                            // выход, если опция не включена

!!BA:H0/?y1 H1/?y2;
!!FU&y2<0:E;                            // y1/y2 - сражавшиеся герои, выход если не битва 2х героев

!!HEy1:O?y3;
!!HEy2:O?y4;                            // y3/y4 - владельцы героев
!!VRy5:S-1;
!!VRy5&y3>=0:Sy1;
!!VRy5&y4>=0:Sy2;
!!FU&y5<0:E;                            // y5 - победитель, выход, если победителя нет

!!VRy6:S-1;
!!VRy6&y3<0:Sy1;
!!VRy6&y4<0:Sy2;
!!FU&y6<0:E;                            // y6 - проигравший, выход, если проигравшего нет

!!SN&y6=y1:W^opt725.h1.x^/?y11 W^opt725.h1.y^/?y12 W^opt725.h1.l^/?y13; // y11-y13 - позиция проигравшего героя
!!SN&y6=y2:W^opt725.h2.x^/?y11 W^opt725.h2.y^/?y12 W^opt725.h2.l^/?y13; // ...
!!VRy7:S-1;                             // поиск проигравшего героя в списке бандитов
!!SN:Mi^opt725.b^/?j;
!!VRj:-1;                               // ...

!!re i/0/j;                             // ...
  !!SN:Mi^opt725.b^/i/?k;               // k - проверяемый герой
  !!VRy7&k=y6:Si;                       // ...

  !!br&k=y6;                            // ...
!!en;                                   // ...

!!FU&y7<0:E;                            // выход, если побежден не бандит

!!HEy5:O?y3;
!!OW:Iy3/?y4;                           // y3/y4 - игрок победитель/ИИ признак
!!SN:Mi^opt725.g^/y7/?y8;               // y8 - награда за голову

!!if&y4=1;                              // ИИ
  !!HEy5:Ed/?y9;
  !!HEy6:Ed/?y10;                       // y9/y10 - уровни победителя/побежденного
  !!UN:C6129162/1/?y31;                 // y31 - текущий лимит героев
  !!OW:Oy3/?y32/d/d/d/d/d/d/d/d;        // y32 - количество активных героев победителя

  !!if&y32<y31;                         // если количество активных героев у ИИ=игрока еще не максимально
    !!if|y10>y9/y10>=15;                // если бандит 15+ уровня или более высокого уровня, чем победитель, ИИ его вербует
      !!HEy6:Oy3 Py11/y12/y13;          // вербовка героя
    !!el;                               // иначе ИИ забирает награду
      !!OW:Ry3/6/dy8;                   // ...
    !!en;                               // ...
  !!en;                                 // 
!!el;                                   // Человек
  !!VRi^es_dlgRes^:S2;                  // i^es_dlgRes^ - результат выбора по умолчанию - награда
  !!FU(ES_725_ShowDialog):P2/y7/y3;  // показ диалога вербовки или награды

  !!if&i^es_dlgRes^=1;                  // если выбрана вербовка
    !!HEy6:Oy3 Py11/y12/y13;            // вербовка героя
  !!el;                                 // иначе
    !!OW:Ry3/6/dy8;                     // получение награды
  !!en;                                 // ...
!!en; 

!!SN:Mi^opt725.b^/y7/-1 Mi^opt725.g^/y7/-1; // удаление банды и награды из списков

** end

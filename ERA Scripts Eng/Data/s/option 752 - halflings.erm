ZVSE2
** Author        : Algor
** Updated by    : Archer30
** Name          : Halflings
** Name rus.     : ’оббиты-уклонисты
** Options       : 752

** Ћовкость ’оббитов позвол€ет им уклон€тьс€ от атаки
** или заклинани€ урона не направленного непосредственно на них.
** Ёффекты обездвиживани€ или изменени€ сознани€ отмен€ют способность к уклонению.
** FV ’оббитов повышено на 5%.


!?FU(OnAfterErmInstructions);
!!UN:P752/?(wogOption:y);     [провер€ем включена ли опци€ 752]
!!FU&(wogOption)<>(TRUE):E;   [выход если опци€ не включена]

!!SN:H^monname^/(MON_HALFLING)/2/?(desc:z);
!!VR(newDesc:z):S(desc) +^%T(es.752.desc)^;
!!SN:H^monname^/(MON_HALFLING)/2/(newDesc:z);

!!MA:F(MON_HALFLING)/?(fv:y); [FV хоббитов]
!!VR(fv):*105 :100;           [увеличиваем FV на 5%]
!!MA:F(MON_HALFLING)/(fv);    [обновл€ем FV]

!?FU(OnMonsterPhysicalDamage);
!!UN:P752/?y1;                [провер€ем включена ли опци€ 752 в y1]
!!FU&y1=0:E;                  [выход если опци€ не включена]

!!BG:D?y1;                    [y1 - атакуемый гекс]
!!MF:W?y2 N?y3;               [y2/y3 - тип урона/отр€д получающий урон]
!!BMy3:T?y4 P?y5 N?y6;        [y4/y5/y6 - тип/позици€/численность отр€да, получающего урон]
!!BG:N?y7;                    [y7 - активный отр€д]
!!FU|y2<>0/y4<>(MON_HALFLING)/y1=y5/y3=y7:E; [выход, если урон рва/башен, урон получают не ’оббиты, урон пр€мой или ответна€ атака]

!!BMy3:G59/?y11/d G60/?y12/d G61/?y13/d G62/?y14/d G70/?y15/d G72/?y16/d G74/?y17/d; [y11-y17 - длительность заклинаний обездвиживани€/разума]
!!FU|y11>0/y12>0/y13>0/y14>0/y15>0/y16>0/y17>0:E; [выход, если на ’оббитах есть заклинание обездвиживани€/разума]

!!MF:E0;                      [отмена урона]

; Show battle log and animation
!!if&i^battle_isVisible^;
  !!BMy4:V86;

  !!if&y6=1;
    !!SN:H^monname^/(MON_HALFLING)/0/?(halfingName:z);
  !!el;
    !!SN:H^monname^/(MON_HALFLING)/1/?(halfingName);
  !!en;

  !!SN:T^es.752.battleLog^?(battleLog:z)/^halfling^/(halfingName);
  !!MM:S(battleLog);
!!en;

!?FU(OnMagicCorrectedResistance);
!!UN:P752/?y1;                [провер€ем включена ли опци€ 752 в y1]
!!FU&y1=0:E;                  [выход если опци€ не включена]

!!MR:M?y1;                    [y1 - тип существа получающего урон]
!!FU&y1<>(MON_HALFLING):E;               [выход, если урон получают не ’оббиты]
!!BG:A?y1 D?y2;               [y1/y2 - действие/атакуемый гекс]
!!FU&y1<>1/y1<>10:E;          [выход, если не каст заклинани€]

!!UN:C42231940/4/?y11;        [спасибо за код Berserker'у]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y3;              [y3 - гекс отр€да, получающего урон]
!!FU|y3<0/y3>186:E;           [выход, если гекс за пределами пол€]

!!BU:Ey3/?y4;                 [y4 - номер отр€да на позиции]
!!FU&y4<0:E;                  [выход, если живого отр€да нет]

!!BMy4:N?y6;                  [y6 - численность отр€да, получающего урон]]
!!FU|y2=y3/y6<1:E;            [выход, если урон пр€мой или отр€д мертв]

!!BMy4:G59/?y11/d G60/?y12/d G61/?y13/d G62/?y14/d G70/?y15/d G72/?y16/d G74/?y17/d; [y11-y17 - длительность заклинаний обездвиживани€/разума]
!!FU|y11>0/y12>0/y13>0/y14>0/y15>0/y16>0/y17>0:E; [выход, если на ’оббитах есть заклинание обездвиживани€/разума]

!!MR:F0;                      [обнуление магического урона]

; Show battle log and animation
!!if&i^battle_isVisible^;
  !!BMy4:V86;

  !!if&y6=1;
    !!SN:H^monname^/(MON_HALFLING)/0/?(halfingName:z);
  !!el;
    !!SN:H^monname^/(MON_HALFLING)/1/?(halfingName);
  !!en;

  !!SN:T^es.752.battleLog^?(battleLog:z)/^halfling^/(halfingName);
  !!MM:S(battleLog);
!!en;

**end

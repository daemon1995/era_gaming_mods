ZVSE2
** Author orig.  : Algor
** Name          : Third class
** Name rus.     : Третий класс
** Options       : 733
** Dialogs       : 78
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7873-7877,7880-FU7884
** PO-values     : -

** Герои условно разделяются не на 2 класса воин-маг, а на 3: воин-странник-маг - на основании специализации.
** К воинам относятся специалисты по существам и боевым вторичным навыкам.
** К магам относятся специалисты по заклинаниям и магическим вторичным навыкам.
** К странникам относятся специалисты по добыче ресурсов, вторичным навыкам странников и нестандартными специализациями.
** При повышении уровня герой получает 1 первичный параметр и на выбор до 3х вторичных навыков.
** И пп и втор.навыки предлагаемые при повышении уровня зависят от расы и класса героя.
** Втор. навыки предлагаются на выбор из следующих:
** Первый из продвинутых навыков героя, далее - из базовых, далее - из новых.
** Второй из базовых навыков героя, далее - из новых, далее - из продвинутых.
** Третий из новых навыков, далее - из базовых, далее - из продвинутых.
**
** Влияние класса и расы на пп и втор.навыки (Атака\Защита\Сила\Знание Воин\Странник\Маг)
** Воин       3\3\1\1 6\4\2
** Странник   2\2\2\2 4\4\4
** Маг        1\1\3\3 2\4\6
** Замок      3\3\3\3 3\2\3
** Оплот      2\4\3\3 3\3\2
** Башня      1\2\4\5 2\2\4
** Инферно    4\3\3\2 3\3\2
** Некрополь  2\2\4\4 2\3\3
** Темница    3\2\4\3 3\2\3
** Цитадель   5\3\2\2 3\3\2
** Крепость   1\5\3\3 3\2\3
** Сопряжение 2\3\3\4 2\4\2
** где каждая единица соответствует 5% шансу выпадения.
** Т.о. Маг (1\1\3\3 2\4\6) Некрополя (2\2\4\4 2\3\3) получит (3\3\7\7 4\7\9),
** что означает следующие шансы выпадения в процентах 15/15/35/35 20/35/45.
** Из распределения видно, что пп на 60% определяет раса и на 40% - класс.
** Вторичный навык наоборот зависит на 40% от расы и на 60% от класса героя.
** Указанные параметры задаются в ini-файле и могут быть изменены.

; Note: The three skills level up dialgoue would be disabled by New Level Up Screen mod


!#UN:P733/?(wogOption:y);
!#UN&(wogOption):P881/(FALSE);     [Отключение опции 881 "Измененная прокачка героев", если включена опция 733 "Третий класс"]
!#VRi^es_733_thirdClass_enabled^&(wogOption):S(TRUE);
!#VRi^es_733_threeSkillsDlg_enabled^&(wogOption):S(TRUE);

!?FU7884;                          [загрузка параметров из ini-файла]
** x1 - тип параметра (0 - типы втор. навыков, 1 - расовые коэффициенты, 2 - классовые коэффициенты)
!!if&x1=0;                         [типы втор. навыков]
  !!UN:N6/3/x16/2/1;
  !!VRy1:Vz3;   [y1 - числовое значение параметра]
  !!SN:W^es_733_st%X16^/y1;        [сохранение значения параметра]
!!en;

!!if|x1=1/x1=2;
  !!UN:N6/3/x16/2/1;               [z3 - текстовое значение параметра]
  !!VRz4:S^^ M1/z3/0/1;
  !!VRy4:Vz4;[y4 - числовое значение a-коэффициента]
  !!VRz4:S^^ M1/z3/1/1;
  !!VRy5:Vz4;[y5 - числовое значение d-коэффициента]
  !!VRz4:S^^ M1/z3/2/1;
  !!VRy6:Vz4;[y6 - числовое значение p-коэффициента]
  !!VRz4:S^^ M1/z3/3/1;
  !!VRy7:Vz4;[y7 - числовое значение k-коэффициента]
  !!VRz4:S^^ M1/z3/4/1;
  !!VRy8:Vz4;[y8 - числовое значение w-коэффициента]
  !!VRz4:S^^ M1/z3/5/1;
  !!VRy9:Vz4;[y9 - числовое значение s-коэффициента]
  !!VRz4:S^^ M1/z3/6/1;
  !!VRy10:Vz4;[y10 - числовое значение m-коэффициента]

  !!if&x1=1;                       [расовые коэффициенты]
    !!SN:W^es_733_rr.a.%X16^/y4;   [сохранение значений коэффициентов]
    !!SN:W^es_733_rr.d.%X16^/y5;   [...]
    !!SN:W^es_733_rr.p.%X16^/y6;   [...]
    !!SN:W^es_733_rr.k.%X16^/y7;   [...]
    !!SN:W^es_733_rr.w.%X16^/y8;   [...]
    !!SN:W^es_733_rr.s.%X16^/y9;   [...]
    !!SN:W^es_733_rr.m.%X16^/y10;  [...]
  !!el&x1=2;                       [классовые коэффициенты]
    !!VRy3:Sx16 -9;                [y3 - номер класса]
    !!SN:W^es_733_cr.a.%Y3^/y4;    [сохранение значений коэффициентов]
    !!SN:W^es_733_cr.d.%Y3^/y5;    [...]
    !!SN:W^es_733_cr.p.%Y3^/y6;    [...]
    !!SN:W^es_733_cr.k.%Y3^/y7;    [...]
    !!SN:W^es_733_cr.w.%Y3^/y8;    [...]
    !!SN:W^es_733_cr.s.%Y3^/y9;    [...]
    !!SN:W^es_733_cr.m.%Y3^/y10;   [...]
  !!en;

!!el&x1=3;                         [классовые навыки]
  !!VRy1:Sx16 :3;                  [y1 - номер расы (0..8)]
  !!VRy2:Sx16 %3;                  [y2 - номер класса (0..2)]
  !!VRy3:Sy1 +1 *10 +y2;           [y3 - номер параметра (10..92)]
  !!UN:N6/3/y3/2/1;
  !!VRy4:Vz3;                      [y4 - числовое значение параметра]
  !!SN:W^es_733_cs.%Y1.%Y2^/y4;    [сохранение классового навыка]
!!el&x1=4;                         [запретные навыки]
  !!VRy1:Sx16 :3;                  [y1 - номер расы (0..8)]
  !!VRy2:Sx16 %3;                  [y2 - номер класса (0..2)]
  !!VRy3:Sy1 +1 *10 +y2;           [y3 - номер параметра (10..92)]
  !!UN:N6/3/y3/2/1;
  !!VRy4:Vz3;                      [y4 - числовое значение параметра]
  !!SN:W^es_733_fs.%Y1.%Y2^/y4;    [сохранение запретного навыка]
!!en;

!?FU(OnHeroGainLevel)&i^es_733_threeSkillsDlg_enabled^; [при получении уровня героем]
!!UN:P733/?y1;
!!FU&y1=0:E;                       [Выход, если опция 733 не включена]
!!UN:C6885977/1/?y1; if gosolo
!!FU&y1=1:E;                       [выход, если gosolo]

!!HE(CURRENT_HERO):N?y1 O?y2;      [y1/y2 - номер героя/игрока]
!!OW&y2>=0:Iy2/?y3/?y4;            [y3/y4 - ИИ/alive статус игрока]
!!VRy3&y2<0:S1;                    [y3 - ИИ, если герой не нанят]
!!FU7883:Py1/?y5/?y6/?y7/?y8/?y9/?y10/?y11; [y5-y11 - навыки/уровни предлагаемые герою]


!!if&y3=0;                         [для героя-человека]
  !!HL:Sy5/-1/-1;                  [y5 - бонус к пп, втор.навыков нет]

** определение макс. уровня которого достигнет герой на полученном опыте
  ; Archer30: Is this needed? y22/y24/y25 are used nowhere
  !!HE(CURRENT_HERO):E?y21/?y22/1; [y21/y22 - опыт героя после всех повышений/текущий повышаемый уровень]
  !!UN:J1/?y23/d;                    [сохранение ограничений уровня героев]

  [:es_733_loop1];
  *!VRy24:Sy22 +1;
  *!UN:J1/y24/?y25;                  [y25 - опыт следующего уровня за получаемым]

  *!if&y21>=y25;
    *!VRy22:+1;                         [если полученный опыт выше опыта следующего уровня, добавляем уровень...]
    *!SN:G[es_733_loop1];               [...и повторяем проверку]
  *!en;

  *!UN:J1/y23/d;                     [восстановление ограничений уровня героев]
**
  !!SN:W^es_733_h^/y1;             [сохранение номера игрока]
  !!SN:W^es_733_l^/y23;            [сохранение макс.уровня героя]
  !!SN:W^es_733_pp^/y5;            [сохранение бонуса пп]
  !!SN:W^es_733_s1^/y6;            [сохранение 1го навыка]
  !!SN:W^es_733_s2^/y7;            [сохранение 2го навыка]
  !!SN:W^es_733_s3^/y8;            [сохранение 3го навыка]
  !!SN:W^es_733_l1^/y9;            [сохранение уровня 1го навыка]
  !!SN:W^es_733_l2^/y10;           [сохранение уровня 2го навыка]
  !!SN:W^es_733_l3^/y11;           [сохранение уровня 3го навыка]
  !!SN:W^es_733_HumanLevelUp^/1;   [флаг получения уровня игроком-человеком]
  !!HL:Sy5/-1/-1;                  [тип диалога - без навыков]
!!el;                              [для ИИ-героя]
  !!VRy12:S6 R2;                   [y12 - индекс переменной с номером навыка]

  !!if&yy12>=0;
    !!HL:Sy5/yy12/-1;                   [y5 - бонус к пп, 1 втор.навык (если есть)]
  !!el;
    !!if&y6>=0;
      !!HL:Sy5/y6/-1;                   [...]
    !!el;
      !!HL:Sy5/-1/-1;                   [...]
    !!en;
  !!en;
!!en;

!?FU7883;                          [для героя x1 возврат в x2/x3..x5/x6..x8 предлагаемых пп/втор.навыков/уровней втор. навыков]
!!SN:W^es_733_hr.%X1^/?y1 W^es_733_hс.%X1^/?y2; [y1/y2 - раса/класс героя]
!!SN:W^es_733_rr.a.%Y1^/?y11 W^es_733_cr.a.%Y2^/?y21; [y11-y17/y21-y27 - расовые/классовые коэффициенты]
!!SN:W^es_733_rr.d.%Y1^/?y12 W^es_733_cr.d.%Y2^/?y22; [...]
!!SN:W^es_733_rr.p.%Y1^/?y13 W^es_733_cr.p.%Y2^/?y23; [...]
!!SN:W^es_733_rr.k.%Y1^/?y14 W^es_733_cr.k.%Y2^/?y24; [...]
!!SN:W^es_733_rr.w.%Y1^/?y15 W^es_733_cr.w.%Y2^/?y25; [...]
!!SN:W^es_733_rr.s.%Y1^/?y16 W^es_733_cr.s.%Y2^/?y26; [...]
!!SN:W^es_733_rr.m.%Y1^/?y17 W^es_733_cr.m.%Y2^/?y27; [...]
!!VRy11:+y21;
!!VRy12:+y22;
!!VRy13:+y23;
!!VRy14:+y24;
!!VRy15:+y25;
!!VRy16:+y26;
!!VRy17:+y27;                                         [y11-y17 - суммарные коэффициенты]

** выбор пп
!!VRy3:R0/0/99 %19 +1;
!!VRy4:S-1;       [y3 - случайное число 1..20]
!_!IF&x1=113:M^DEBUG: %Y3/%Y11+%Y12+%Y13+%Y14^; 8633 = 5322 + 3311
!!VRy4&y4=-1/y3<=y11:S0;
!!VRy3&y4=-1:-y11; [y4 - выпавший пп (0..3)]
!!VRy4&y4=-1/y3<=y12:S1;
!!VRy3&y4=-1:-y12; [...]
!!VRy4&y4=-1/y3<=y13:S2;
!!VRy4&y4=-1:S3;   [...]
!!VRx2:Sy4;                        [возврат пп]
**
!!UN:C4881872/1/?y11;              [y11 - разрешенное количество втор.навыков у героя]
*!HEx1:S?y12;
!!FU(ES_CalcNumberOfSecSkills):Px1/?y12;[y12 - общее кол-во навыков у героя]    

** выбор типа нового втор. навыка
!!VRy3:R0/0/99 %19 +1;
!!VRy4:S-1;       [y3 - случайное число 1..20]
!!VRy4&y4=-1/y3<=y15:S0;
!!VRy3&y4=-1:-y15; [y4 - выпавший тип втор.навыка (0..2)]
!!VRy4&y4=-1/y3<=y16:S1;
!!VRy4&y4=-1:S2;   [...]

; Initialise variable if Warfare is enabled
!!VRi^es_733_WarfarePicked^&i^wog_193_enabled^:S(FALSE);

** Choose a secondary skill in the first slot
!!VRy5:S-1;
!!VRy8:S-1;                     [инициализация номера/ранга навыка]
!!DO7881/0/27/1:Px1/2/-1/-1/-1/?y5/?y8/0;   [y8 - кол-во подходящих продв. навыков]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&v11>=0:Px1/2/-1/-1/-1/?y5/?y8/y3;                [y5/y8 - номер/ранг случайного продв. навыка]
!!DO7881/0/27/1&y5<0:Px1/1/-1/-1/-1/?y5/?y8/0;                  [y8 - кол-во подходящих базовых навыков, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y5<0/v11>=0:Px1/1/-1/-1/-1/?y5/?y8/y3;           [y5/y8 - номер/ранг случайного базового навыка, если навык еще не выбран]
!!DO7881/0/27/1&y5<0/y12<y11:Px1/0/y4/-1/-1/?y5/?y8/0;          [y8 - кол-во подходящих новых. навыков выбранного типа, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y5<0/v11>=0/y12<y11:Px1/0/y4/-1/-1/?y5/?y8/y3;   [y5/y8 - номер/ранг случайного нового навыка выбранного типа, если навык еще не выбран]
!!DO7881/0/27/1&y5<0/y12<y11:Px1/0/-1/-1/-1/?y5/?y8/0;          [y8 - кол-во подходящих новых навыков любого типа, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y5<0/v11>=0/y12<y11:Px1/0/-1/-1/-1/?y5/?y8/y3;   [y5/y8 - номер/ранг случайного нового навыка любого типа, если навык еще не выбран]

** Chance to get class skill in 1st slot
!!SN:W^es_733_ClassSkillChance^/?y30;       [y30 - Шанс получить расовый навык]
!!VRy31:R0/0/99;                            [y31 - случайное число от 0 до 99]

!!if&y31<y30;                               [если выпал шанс получить расовый навык]
  !!SN:W^es_733_cs.%Y1.%Y2^/?y32;           [y32 - классовый навык]
  !!HEx1:Sy32/?y33;                         [y33 - ранг выпавшего навыка у героя]
  !!VRy5&y33>0/y33<3:Sy32;
  !!VRy8&y33>0/y33<3:Sy33 +1; [Замена навыка в первом слоте классовым, если он уже есть, но недокачан]
  !!VRy5&y33=0/y12<y11:Sy32;
  !!VRy8&y33=0/y12<y11:S1;  [Замена навыка в первом слоте классовым, если у героя его еще нет, но есть свободные слоты]
!!en;

; Set variable if Warfare is enabled and Warfare skill is proposed
!!if&i^wog_193_enabled^;
  !!if|y5=(SKILL_BALLISTICS)/y5=(SKILL_ARTILLERY)/y5=(SKILL_FIRST_AID);
    !!VRi^es_733_WarfarePicked^:S(TRUE);
  !!en;
!!en;

** Select the type of new secondary skill
!!VRy3:R0/0/99 %19 +1;
!!VRy4:S-1;       [y3 - случайное число 1..20]
!!VRy4&y4=-1/y3<=y15:S0;
!!VRy3&y4=-1:-y15; [y4 - выпавший тип втор.навыка (0..2)]
!!VRy4&y4=-1/y3<=y16:S1;
!!VRy4&y4=-1:S2;   [...]

** Select a secondary skill in the second slot
!!VRy6:S-1;
!!VRy9:S-1;            [инициализация номера/ранга навыка]
!!DO7881/0/27/1:Px1/1/-1/y6/-1/?y6/?y9/0;   [y6/y9 - номер/ранг случайного баз. навыка]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&v11>=0:Px1/1/-1/y5/-1/?y6/?y9/y3;                [y6/y9 - номер/ранг случайного баз. навыка]
!!DO7881/0/27/1&y6<0/y12<y11:Px1/0/y4/y5/-1/?y6/?y9/0;          [y6/y9 - номер/ранг случайного нового навыка выбранного типа, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y6<0/v11>=0/y12<y11:Px1/0/y4/y5/-1/?y6/?y9/y3;   [y6/y9 - номер/ранг случайного нового навыка выбранного типа, если навык еще не выбран]
!!DO7881/0/27/1&y6<0:Px1/2/-1/y5/-1/?y6/?y9/0;                  [y6/y9 - номер/ранг случайного продв. навыка, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y6<0/v11>=0:Px1/2/-1/y5/-1/?y6/?y9/y3;           [y6/y9 - номер/ранг случайного продв. навыка, если навык еще не выбран]
!!DO7881/0/27/1&y6<0/y12<y11:Px1/0/-1/y5/-1/?y6/?y9/0;          [y6/y9 - номер/ранг случайного нового навыка любого типа, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y6<0/v11>=0/y12<y11:Px1/0/-1/y5/-1/?y6/?y9/y3;   [y6/y9 - номер/ранг случайного нового навыка любого типа, если навык еще не выбран]

; Set variable if Warfare is enabled and Warfare skill is proposed
!!if&i^wog_193_enabled^;
  !!if|y6=(SKILL_BALLISTICS)/y6=(SKILL_ARTILLERY)/y6=(SKILL_FIRST_AID);
    !!VRi^es_733_WarfarePicked^:S(TRUE);
  !!en;
!!en;

** Selection of the type of new secondary skill
!!VRy3:R0/0/99 %19 +1;
!!VRy4:S-1;                             [y3 - случайное число 1..20]

!!if&y4=-1;
  !!VRy4&y3<=y15:S0;
  !!VRy3:-y15;                          [y3, y4 - выпавший тип втор.навыка (0..2)]
  !!VRy4&y3<=y16:S1;
  !!VRy4:S2;                            [y4, ...]
!!en;

** Choosing a secondary skill in the third slot
!!VRy7:S-1;
!!VRy10:S-1;           [инициализация номера/ранга навыка]
!!DO7881/0/27/1&y12<y11:Px1/0/y4/y5/y6/?y7/?y10/0;              [y7/y10 - номер/ранг случайного нового навыка выбранного типа]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&v11>=0/y12<y11:Px1/0/y4/y5/y6/?y7/?y10/y3;      [y7/y10 - номер/ранг случайного нового навыка выбранного типа]
!!DO7881/0/27/1&y7<0:Px1/1/-1/y5/y6/?y7/?y10/0;                 [y7/y10 - номер/ранг случайного базового навыка, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y7<0/v11>=0:Px1/1/-1/y5/y6/?y7/?y10/y3;         [y7/y10 - номер/ранг случайного базового навыка, если навык еще не выбран]
!!DO7881/0/27/1&y7<0:Px1/2/-1/y5/y6/?y7/?y10/0;                 [y7/y10 - номер/ранг случайного продв. навыка, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y7<0/v11>=0:Px1/2/-1/y5/y6/?y7/?y10/y3;         [y7/y10 - номер/ранг случайного продв. навыка, если навык еще не выбран]
!!DO7881/0/27/1&y7<0/y12<y11:Px1/0/-1/y5/y6/?y7/?y10/0;         [y7/y10 - номер/ранг случайного нового навыка любого типа, если навык еще не выбран]
!!VRv11:-1;
!!VRy3&v11>=0:S1 Rv11;          [y3 - номер подходящего навыка для поиска]
!!DO7881/0/27/1&y7<0/v11>=0/y12<y11:Px1/0/-1/y5/y6/?y7/?y10/y3; [y7/y10 - номер/ранг случайного нового навыка любого типа, если навык еще не выбран]

** Return of secondary skills
!!VRx3:Sy5;
!!VRx6:Sy8;            [возврат номера/ранга первого навыка]
!!VRx4:Sy6;
!!VRx7:Sy9;            [возврат номера/ранга второго навыка]
!!VRx5:Sy7;
!!VRx8:Sy10;           [возврат номера/ранга третьего навыка]

!?FU7880;                               [setting hero race/class x16 0..8/0..2 in es_733_hr.%X16/es_733_hc.%X16]
!!HEx16:B2/?y1 X?y3/?y4/d/d/d/d/d;      [y1/y3/y4 - hero parameters]

!!if&y3=0/y4=(SKILL_FIRST_AID);         [for First Aid specialists]
  !!HEx16:A3/0/1/1;                     [deleting a book]
  !!VRy5:S0;                            [y5 - current spell number]

  ; For First Aid specialist, unlearn all the spells from the start of the map
  !!re i/(SPELL_FIRST)/(SPELL_LAST_WOG);
    !!HEx16:Mi/(FALSE);
  !!en;
!!en;

!!VRy1::2;                              [y1 - hero race]

; If the third class is enabled
!!if&i^es_733_thirdClass_enabled^;
  !!VRy2:S1;                            [y2 - hero class, wanderer by default]
  !!VRy2|y3=1/y3=4/y3=6/y3=7:S0;        [y2, creature specialists are warriors]
  !!VRy2|y3=3:S2;                       [y2, spell specialists are mages]
  !!SN&y3=0:W^es_733_st%Y4^/?y2;        [y2, for secondary skill specialists, the class is determined by the skill class]
; If there are only 2 classes
!!el;
  !!VRy2:Sy1 %2;
  !!VRy2&y2=2:S2;                       [Mage is 2]
!!en;

!!SN:W^es_733_hr.%X16^/y1 W^es_733_hс.%X16^/y2; [saving the hero's race and class]

!?FU7881;                          [для героя x1 возврат навыка/ранга в x6/x7 случайного навыка ранга x2, типа x3, за исключением навыков x4 и x5]
** x8=0 - подсчитать в x7 кол-во подходящих навыков, x8>0 - вернуть в x6/x7 x8-й подходящий навык
!!VRv11&x16=0:S0;                  [сброс счетчика в начале поиска]
!!VRx6:S-1;
!!VRx7:S-1;                        [по умолчанию возврат -1]
!!SN:W^es_733_hr.%X1^/?y1 W^es_733_hс.%X1^/?y2; [y1/y2 - раса/класс героя]

!!if&x2=0;
  !!FU&y1<>4/x16=12:E;             [не рассматривать Некромантию в качестве нового навыка для героев не из Некрополя]
  !!FU&y1=4/x16=6:E;               [не рассматривать Лидерство в качестве нового навыка для героев Некрополя]
!!en;

!!SN:W^es_733_fs.%Y1.%Y2^/?y3;     [y3 - запретный навык для класса]
!!FU&x2=0/x16=y3:E;                [не рассматривать запретный навык для класса в качестве нового навыка]
!!FU|x16=x4/x16=x5:E;              [не рассматривать навыки, уже предложенные в другие слоты]
!!FU&x16=(SKILL_NAVIGATION)/x2=0/i^es_773_landMode^:E; [не рассматривать Навигацию в качестве нового навыка для безводных карт с включенной опцией "Сухопутная навигация"]

; Skip Warfare skill if any Warfare skill is proposed
!!if&i^wog_193_enabled^/i^es_733_WarfarePicked^;
  !!FU|x16=(SKILL_BALLISTICS)/x16=(SKILL_ARTILLERY)/x16=(SKILL_FIRST_AID):E;
!!en;

!!SN:W^es_733_st%X16^/?y1;         [y1 - тип втор.навыка]
!!FU&x3>=0/y1<>x3:E;               [выход, если ищется навык конкретного типа и тип не совпадает]

!!HEx1:Sx16/?y1;                   [y1 - ранг навыка у героя]
!!FU&y1<>x2:E;                     [выход, если ранг не совпадает с требуемым]

!!VRv11:+1;                        [инкремент счетчика]
!!VRx6&x8=v11:Sx16;
!!VRx7&x8=v11:Sy1 +1;              [возврат навыка и следующего ранга, если навык найден]
!!VRx16&x8=v11:S30;

!?FU7882;                          [возвращает в x2 общее кол-во занятых навыков героя x1]
!!VRv11&x16=0:S0;                  [сброс счетчика]
!!HEx1:Sx16/?y1;
!!VRv11&y1>0:+1;                   [инкремент счетчика, если у героя есть навык]
!!VRx2&x16=27:Sv11;                [возврат общего кол-ва занятых навыков]

** Диалог повышения уровня
!?FU(ES_BeforeHeroLevelUpDialog0); // 4DACF9. dialogue for increasing the hero's level without choosing secondary skills 
!!UN:P733/?y1;
!!FU&y1=0:E;                       [Выход, если опция 733 не включена]

!!SN:W^es_733_HumanLevelUp^/?y1;
!!FU&y1=0:E;                       [выход, если уровень получает не герой человека]

!!UN:C6885977/1/?y1; if gosolo
!!FU&y1=1:E;                       [выход, если gosolo]

!!SN:W^es_733_HumanLevelUp^/0;     [сброс флага получения уровня игроком-человеком]
!!SN:X?y99/0;
!!VRy99:+32;
!!UN:Cy99/4/5091078;               [отмена стандартного диалога]
!!DL78:N^opt733.txt^;              [файл с настройкаим диалога]
!!SN:W^es_733_h^/?y1;              [получение номера героя]
!!SN:W^es_733_l^/?y21;             [получение макс. получаемого уровня]
!!SN:W^es_733_pp^/?y2;             [получение бонуса пп]
!!SN:W^es_733_s1^/?y3;             [получение 1го навыка]
!!SN:W^es_733_s2^/?y4;             [получение 2го навыка]
!!SN:W^es_733_s3^/?y5;             [получение 3го навыка]
!!SN:W^es_733_l1^/?y6;             [получение уровня 1го навыка]
!!SN:W^es_733_l2^/?y7;             [получение уровня 2го навыка]
!!SN:W^es_733_l3^/?y8;             [получение уровня 3го навыка]
!!SN:W^es_733_hr.%Y1^/?y9 W^es_733_hс.%Y1^/?y10; [y9/y10 - раса/класс героя]
!!HEy1:B0/?z22;
!!SN:T^es.733.newLv^/?z30/^hero^/z22;   [z30 - надпись "ИМЯ поднимается на новый уровень"]
 
!!FU(GetTextFileString):P^priskill^/y2/?z2;
!!SN:T^es.733.ps^/?z31/^ps^/z2;         [z31 - надпись "ПАРАМЕТР: +1"]

!!if&y3>=0;
  !!SN:T^es.733.newSkill^/?z32;         [z32 - надпись "Навыки для изучения:"]
!!el;
  !!SN:T^es.733.noSkill^/?z32;
!!en;

!!VRy11:Sy9 *3 +y10 +179369;            [y3 - номер ert-переменной с именем класса]
!!VRz33:Szy11;

!!HEy1:E?i/?y11/1;

!!if&y11>=y21; 
  !!SN:T^es.733.lv1^/?z34/^lv^/y11;     [z34 - надпись "Уровень: X" или "Уровень: X/Y"]
!!el;
  !!SN:T^es.733.lv2^/?z34/^lv^/y11/^maxLv^/y21;
!!en;

!!FU(Fun_GetHeroPortrait):Py1/1/35;     [z35 - название большого портрета героя]

; Get skill level string
!!VRz36:S^^;
!!FU(ES_733_GetSkillLevelString)&y6>0:Py1/y3/y6/?z36;
!!VRz37:S^^;
!!FU(ES_733_GetSkillLevelString)&y7>0:Py1/y4/y7/?z37;
!!VRz38:S^^;
!!FU(ES_733_GetSkillLevelString)&y8>0:Py1/y5/y8/?z38;

!!VRz39:S^^;
!!UN&y3>=0:N4/39/y3;                    [z39-z41 - наименоания навыков]
!!VRz40:S^^;
!!UN&y4>=0:N4/40/y4;                    [...]
!!VRz41:S^^;
!!UN&y5>=0:N4/41/y5;                    [...]

; Get skill icon index
!!VRy13:S0;
!!FU(ES_733_GetSkillIconIndex)&y3>=0/y6>=1:Py1/y3/y6/?y13;
!!VRy14:S0;
!!FU(ES_733_GetSkillIconIndex)&y4>=0/y7>=1:Py1/y4/y7/?y14;
!!VRy15:S0;
!!FU(ES_733_GetSkillIconIndex)&y5>=0/y8>=1:Py1/y5/y8/?y15;

!!DL78:A22/11/z35/1;                            [установка портрета]
!!DL78:A3/3/z30/1;                              [установка надписи "ИМЯ получает новый уровень"]
!!DL78:A13/3/z31/1;                             [установка надписи "ПАРАМЕТР: +1"]
!!DL78:A40/3/z32/1;                             [установка надписи "Навыки для изучения:"]
!!DL78:A11/3/z34/1;                             [установка надписи "уровень: x"]
!!DL78:A32/3/z33/1;                             [установка надписи "Класс"]
!!DL78:A23/4/y2/1;                              [установка изображения первичного параметра]
!!DL78:A51/4/y13/1 A52/4/y14/1 A53/4/y15/1;     [установка изображений навыков]
!!DL78:A41/3/z36/1 A42/3/z37/1 A43/3/z38/1;     [установка рангов навыков]
!!DL78:A61/3/z39/1 A62/3/z40/1 A63/3/z41/1;     [установка наименований навыков]

!!SN:T^es.733.rmb^/?z8;
!!SN:T^es.733.lmb^/?z9;                 [z8/z9 - подсказка по герою/навыкам]

!!DL78:H22/z8 H51/z9 H52/z9 H53/z9;             [установка подсказок]
!!DL78:S?v1;                                    [вызов диалога]

!?FU(ES_733_GetSkillLevelString);
!#VA(hero:x) (skill:x) (level:x) (result:x);

!!VR(strInd:y):S(level) -1;
!!FU(GetTextFileString):P^skilllev^/(strInd)/?(skillLevelStr:z);
!!VR(result):Z(skillLevelStr);

!?FU(ES_733_GetSkillIconIndex);
!#VA(hero:x) (skill:x) (level:x) (result:x);

!!VR(result):S(skill) *3 +2 +(level);

!?FU(OnCustomDialogEvent)&i^dlg_id^=78/i^mouse_action^=(MOUSE_LMB_PRESSED);                          [в диалоге нажата ЛКМ (12)]
!!SN&i^mouse_item^=51:W^es_733_s1^/?y1 W^es_733_l1^/?y2; [y1/y2 - выбранный навык/ранг]
!!SN&i^mouse_item^=52:W^es_733_s2^/?y1 W^es_733_l2^/?y2; [y1/y2 - выбранный навык/ранг]
!!SN&i^mouse_item^=53:W^es_733_s3^/?y1 W^es_733_l3^/?y2; [y1/y2 - выбранный навык/ранг]

!!if&y1>=0/y2>0/i^mouse_item^>=51/i^mouse_item^<=53;              [если навык выбран]
  !!HEi^es_733_h^:Sy1/y2;                       [дать герою новый уровень втор.навыка]
  !!SN:P^button^;
  !!DL:C1;
  !!UN:R1;                                      [закрыть диалог и обновить экран]
!!en;

!!SN:W^es_733_s1^/?y1 W^es_733_s1^/?y2 W^es_733_s1^/?y3;

!!if&y1<0/y2<0/y3<0;                            [если нет навыков для выбора]
  !!SN:P^button^;
  !!DL:C1;
  !!UN:R1;                                      [закрыть диалог и обновить экран]
!!en;

!?FU(OnCustomDialogEvent)&i^dlg_id^=78/i^mouse_action^=(MOUSE_RMB_PRESSED);                          [в диалоге нажата ПКМ (14)]
!!SN|i^mouse_item^=22/i^mouse_item^=5/i^mouse_item^=6/i^mouse_item^=7/i^mouse_item^=8:W^es_733_h^/?y1 E5118576/3/y1/1/0/1; [@igrik SN:E5118576/3/<номер_героя>/<кнопка_уволить_вкл_или_откл(0..1)>/0/<ЛКМ_или_ПКМ(0..1)>;]

!!if|i^mouse_item^=51/i^mouse_item^=52/i^mouse_item^=53;                   [подсказка по втор.навыку]
  !!VRy3:Si^mouse_item^ -50;
  !!SN:W^es_733_s%Y3^/?y1 W^es_733_l%Y3^/?y2;   [y1/y2 - навык/ранг навыка в слоте]
  !!SN:H^secskill^/y1/y2/?z1;
  !!IF&y1>=0:M0/4/1;                            [показ описания втор.навыка]
!!en;

** Имена классов (с)пасибо igrik'у
; Here we create the hook in this file instead of using FU(ES_CreateERMHook) for script compatibility
!?FU(OnStartOrLoad)&i^es_733_thirdClass_enabled^;
!!SN:L^erm_hooker.era^/?(ermHooker:y);
!!FU&(ermHooker)=0:E;

!!SN:A(ermHooker)/^SetHook^/?(setHook:y);

!!SN:E(setHook)/1/5083661/7875;
!!SN:E(setHook)/1/5093760/7874;
!!SN:E(setHook)/1/5094903/7874;
!!SN:E(setHook)/1/5119462/7873;

!?FU7875;
!!SN:X?y1/0;
!!VRy2:Sy1 +24;
!!FU7876:Py2/?y4;
!!VRy3:Sy1 +28;
!!UN:Cy3/4/y4;
!!VRy5:Sy1 +32;
!!UN:Cy5/4/5083660;

!?FU7874;
!!SN:X?y1;
!!VRy2:Sy1 +24;
!!FU7876:Py2/?y4;
!!VRy3:Sy1 +28;
!!UN:Cy3/4/y4;

!?FU7873;
!!SN:X?y1;
!!VRy2:Sy1 +28;
!!FU7876:Py2/?y4;
!!VRy3:Sy1 +24;
!!UN:Cy3/4/y4;

; Use only strings with static address here
!?FU7876;
!!UN:Cx1/4/?y1;
!!VRy1:+26;
!!UN:Cy1/4/?x1;                         [x1 - номер героя]
!!SN:W^es_733_hr.%X1^/?y1 W^es_733_hс.%X1^/?y2; [y1/y2 - раса и класс героя]
!!VRy3:Sy1 *3 +y2 +179369;                      [y3 - номер ert-переменной с именем класса]
!!SN:E7824928/1/y3;
!!VRx2:Sv1;                             [возврат ссылки на название класса]

!?FU(ES_733_Initialization);
!!UN:P733/?(wogOption:y);               [состояние опции 733]
!!FU&(wogOption)<>(TRUE):E;

!!VRz1:S^Data\s\option 733 - third class.ini^; [z1 - ini-file name]
!!VRz2:S^ss types^;                     [z2 - ini-file section]
!!DO7884/0/27/1:P0;                     [Loading third skill types]
!!VRz2:S^rates^;                        [z2 - ini-file section]
!!DO7884/0/8/1:P1;                      [Loading racial coefficients]
!!DO7884/9/11/1:P2;                     [Loading class coefficients]
!!VRz2:S^class skills^;                 [z2 - ini-file section]
!!UN:N6/3/1/2/1;
!!VRv4:Vz3;                             [v4 - numerical value of the parameter 1 - class skill drop chance]
!!SN:W^es_733_ClassSkillChance^/v4;     [...]
!!DO7884/0/26/1:P3;                     [Loading class skills]
!!VRz2:S^forbidden skills^;             [z2 - ini file section]
!!DO7884/0/26/1:P4;                     [Loading forbidden skills]
!!DO7880/0/155/1:P; [setting races, classes and hero class names]

!#FU(ES_733_Initialization):P;

; Turn off option 733 if both sub-options are disabled
!?FU(OnAfterErmInited);
!!UN&i^es_733_thirdClass_enabled^<>(TRUE)/i^es_733_threeSkillsDlg_enabled^<>(TRUE):P733/(FALSE);

** end

ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author orig.  : Algor
** Name          : Devils sacrificing
** Name rus.     : Дьявольское пожертвование
** Options       : 759
** Dialogs       : -
** Variables     : v7928-v7929,z787-z788,z179099-z179101,z179204
** Tmp variables : v1,v2,z1,z2
** Timers        : -
** Functions     : FU7968-7969
** PO-values     : -

*** Архидьяволы и Бароны Ада получают способность 1 раз за бой (Бароны Ада - 2 раза)принести в жертву дружественный отряд
*** живых непризванных существ восстановив свое здоровье/численность на сумму здоровья принесенного в жертву отряда.
*** Количество Архидьяволов/Баронов Ада после восстановления не может превышать начальной численности отряда.

*** ИИ при возможности применяет новую способность если отряд Архидьяволов/Баронов Ада потерял не менее 30% численности.
*** При этом выбирается жертвенный отряд, здоровье которого не более чем на 10% превышает максимально необходимое здоровье
*** для восстановления всех Архидьяволов/Баронов Ада и не менее необходимого для восстановления хотя бы одного Архидьявола/Барона.
*** Если таких отрядов у ИИ несколько, выбирается отряд с наименьшим значением FV/восстанавливаемое существо.

!?PI;                                      [перед показом карты]
!!VRv7928:C-1/-1;                          [сброс признака хода Архидьяволов и номера жертвенного отряда]
!!UN:P759/?y1;                             [проверяем включена ли опция 759 в y1]
!!FU&y1=0:E;                               [выход если опция не включена]
!!MA:B55/1 B153/2;                         [Архидьяволы имеют одно заклинание, Бароны Ада имеют два заклинания]
!!VRz787:Sz179100;                         [z787 - новое описание Архидьяволов из ert-файла]
!!VRz788:Sz179101;                         [z788 - новое описание Баронов Ада из ert-файла]
!!UN:G1/55/2/787 G1/153/2/788;             [установка описаний]

!?FU(OnBattleStackObtainsTurn);            [на фазе получения хода отрядом в бою]
!!UN:P759/?y1;                             [проверяем включена ли опция 759 в y1]
!!FU&y1=0:E;                               [выход если опция не включена]
!!VRv7928:C-1/-1;                          [сброс признака хода Архидьяволов и номера жертвенного отряда]
!!SN:X?y1/?y2;                             [y1/y2 - сторона/номер отряда]
!!VRy1:*21 +y2;                            [y1 - номер отряда]
!!BMy1:T?y2 E?y3 N?y4 B?y5;                [y2/y3/y4/y5 - тип отряда/количество заклинаний/текущая численность/начальная численность]
!!FU&y2<>55/y2<>153:E;                     [выход, если ходит не отряд Архидьяволов/Баронов Ада]
!!VRv7928&y3>0/y4<y5:Sy1;                  [если у ходящего отряда есть неиспользованные заклинания и не максимальная численность, признак хода равен номеру отряда]

!?MM0&v7928>=0;                            [при движении мыши по гексам поля боя, если ходит отряд Архидьяволов, который можно восстановить]
!!VRv7929:S-1;                             [сброс номера жертвенного отряда]
!!MM:D?y1;                                 [y1 - позиция курсора]
!!FU|y1<0/y1>186:E;                        [выход, если курсор за полем]
!!BU:Ey1/?y2;                              [y2 - номер живого отряда в позиции]
!!FU|y2=-1/y2=v7928:E;                     [выход, если живого отряда в позиции нет или курсор наведен на ходящий отряд Архидьяволов]
!!FU&y2<21/v7928>20:E;                     [выход, если курсор наведен на вражеский отряд]
!!FU&y2>20/v7928<21:E;                     [...]
!!BMy2:F?y3 T?y6 N?y7;                     [y3/y6/y7 - флаги/тип/количество существ отряда под курсором]
!!VRy4:Sy3 &16;                            [y4=0, если существа отряда под курсором не живые]
!!VRy5:Sy3 &4194304;                       [y5>0, если существа отряда под курсором призванные]
!!FU|y4=0/y5>0:E;                          [выход, если существа отряда под курсором не живые или призванные]
!!VRv7929:Sy2;                             [установка номера жертвенного отряда]
!!UN:R5/2/18;                              [установка вида курсора "Жертва"]
!!UN&y7=1:N3/z2/y6/0;                      [z2 - название существ жертвенного отряда (ед.ч.)]
!!UN&y7>1:N3/z2/y6/1;                      [z2 - название существ жертвенного отряда (мн.ч.)]
!!VRz1:Sz179099;                           [z1 - подсказка при наведении на жертву из ert-файла]
!!MM:Mz1;                                  [подмена стандартной подсказки]

!?CM4&v7928>=0/v7929>=0;                   [при клике на жертвенном отряде, если ходит отряд Архидьяволов, который можно восстановить]
!!CM:F?y1 S?y2;                            [y1/y2 - тип/подтип клика]
!!FU&y1<>0:E;                              [выход, если не ЛКМ]
!!CM:R0;                                   [отключение стандартного действия ЛКМ]
!!FU&y2<>13:E;                             [выход, если ЛКМ нажата (не отпущена). Срабатывание "Жертвы" только на отпускание ЛКМ]
!!FU7968:Pv7928/v7929;                     [Восстановление Архидьяволов за счет жертвенного отряда]

!?FU7968;                                  [Восстановление Архидьяволов за счет жертвенного отряда: x1/x2 - номер отряда Архидьяволов,Баронов/жертвенного отряда]
!!BMx2:N?y1 L?y2 H?y3 T?y21;               [y1/y2/y3/y21 - текущая численность/потерянное здоровье/макс.здоровье/тип существ в жертвенном отряде]
!!VRy9:Sy1 *y3 -y2;                        [y9 - "пожертвованное" здоровье]
!!BMx1:N?y1 L?y2 H?y3 B?y4 T?y22;          [y1/y2/y3/y4/y22 - текущая численность/потерянное здоровье/макс.здоровье/начальная численность/тип существ в отряде Архидьяволов/Баронов]
!!VRy5:Sy1 *y3 -y2 +y9;                    [y5 - максимально возможное новое здоровье отряда Архидьяволов/Баронов]
!!VRy6:Sy5 :y3;                            [y6 - максимально возможное новое количество Архидьяволов/Баронов в отряде]
!!VRy7:Sy5 %y3;                            [y7 - количество здоровья у раненного Архидьявола/Барона]
!!VRy7&y6>=y4:S0;                          [...]
!!VRy6&y7>0/y6<y4:+1;                      [...]
!!VRy6&y6>y4:Sy4;                          [...]
!!VRy8:Sy3 -y7;                            [y8 - потерянное здоровье нового отряда Архидьяволов/Баронов]
!!BMx2:Fd268435456 Fd536870912 Fd8388608;  [добавление флагов "Принесен в жертву", "Клон", "Не меняет цвет при клонироании" жертвенному отряду]
!!UN:C6919200/4/?y10;                      {y10 - combatmanager]
!!VRy11:Sx1 *1352 +21708 +y10;             [y11 - отряд Архидьяволов/Баронов для атаки]
!!VRy12:Sx2 *1352 +21708 +y10;             [y12 - жертвенный отряд для атаки]
!!BA:Q?f;                                  [f-статус быстрой битвы]
!!UN&f=0:C4462626/1/1;                     [анимациия атаки "подманивание"]
!!VRz1&f=0:S^SACRIF1.wav^;                 [z1 - звук "жертвы"]
!!SN&f=0:Pz1;                              [воспроизведение звука]
!!BMx2&f=0:V51;                            [анимация "Жертва" на жертвенном отряде]
!!SN&f=0:E4461360/2/y11/y12/2;             [Атака Архидьяволов/Баронов]
!!VRz1&f=0:S^RESURECT.wav^;                [z1 - звук "воскрешения"]
!!SN&f=0:Pz1;                              [воспроизведение звука]
!!BMx1&f=0:V50;                            [анимация на отряде Архидьяволов]
!!BMx1:Ny6 Ly8 Ed-1;                       [восстановление Архидьяволов с уменьшением количества заклинаний отряда]
!!UN&f=0:C4462626/1/13;                    [анимациия атаки по умолчанию]
!!UN&f=0:N3/z2/y21/1;                      [z2 - название существ жертвенного отряда (мн.ч.)]
!!UN&f=0:N3/z3/y22/1;                      [z3 - название существ восстанавливаемого (мн.ч.)]
!!VRy20&f=0:Sy6 -y1;                       [y20 - восстановленное количество существ]
!!VRz1&f=0:Sz179204;                       [z1 - сообщение о жертвоприношении для вывода в лог боя]
!!MM&f=0:Sz1;                              [добавление сообщения в лог боя]
!!BG:A12;                                  [пропуск хода отрядом]

*** ИИ

!?BG0&v7928>=0;                            [перед действием отряда Архидяволов, который можно восстановить]
!!BMv7928:I?y1 G59/?y2/d G60/?y3/d N?y4 B?y5; [y1/y2/y3/y4/y5 - принадлежность отряда/продолжительность Берсерка/Гипноза/текущая/макс. численность]
!!VRy4:*100 :y5;                           [y4 - численность отряда в процентах от начальной]
!!FU&y4>70:E;                              [выход, если Архидьяволы/Бароны Ада понесли менее 30% потерь]
!!BA:O?y4/?y5;                             [y4/y5 - атакующий/защищающийся игроки]
!!VRy6&y1=0:Sy4;                           [y6 - номер игрока-владельца отряда]
!!VRy6&y1=1:Sy5;                           [...]
!!OW&y6>=0:Iy6/?y7;                        [y7=1, если отряд под контролем ИИ]
!!FU&y6>=0/y7=0:E;                         [выход, если отряд под контролем человека]
!!BMv7928:N?y11 B?y12 L?y13 H?y14;         [y11/y12/y13/y14 - текущее количество/начальное количество/потерянное здоровье/макс.здоровье отряда]
!!VRy10:Sy12 -y11 *y14 +y13 *11 :10;       [y10 - здоровье отряда, которое можно восстановить "жертвой" (+10% допустимый "перебор")]
!!VRv1:C-1/-1;                             [инициализация v1,v2]
!!DO7969/0/20/1&y1=0:Py13/y10/y14;         [поиск отряда наиболее подходящего для жертвы в v1]
!!DO7969/21/41/1&y1=1:Py13/y10/y14;        [...]
!!FU7968&v1>=0:Pv7928/v1;                  [Восстановление Архидьяволов за счет жертвенного отряда, если подходящая жертва найдена]

!?FU7969;                                  [поиск наиболее подходящего отряда на роль жертвы]
** x16 - проверяемый отряд, x1/x2/x3 - минимальное здоровье жертвы/максимальное здоровье жертвы/здоровье восстанавливаемого существа
** v1 - прошлый найденный отряд, v2 - FV/кол-во восстановленных существ для прошлого найденного отряда
!!BMx16:T?y2 N?y3;                         [y2/y3 - тип/количество существ потенциального жертвенного отряда]
!!FU|x16=v7928/y2<0/y3<1:E;                [выход, если приверяемый и восстанавливаемый отряды совпадают или проверяемый отряд не существует]
!!BMx16:F?y1 H?y4 L?y5;                    [y1/y4/y5 - флаги/макс.здоровье/потерянное здоровье существ потенциального жертвенного отряда]
!!VRy6:Sy3 &16;                            [y6=0, если существа отряда под курсором не живые]
!!VRy7:Sy3 &4194304;                       [y7>0, если существа отряда под курсором призванные]
!!FU|y6=0/y7>0:E;                          [выход, если существа отряда не живые или призванные]
!!VRy6:Sy3 *y4 -y5;                        [y6 - общее здоровье жертвы]
!!FU|y6<x1/y6>x2:E;                        [выход, если здоровья не достаточно для восстановления хотя бы одного существа или более чем на 10% больше, чем необходимое для полного восстановления отряда]
!!VRy7:Sy6 -x1 :x3 +1;                     [y7 - количество восстанавливаемых существ]
!!VRy8:Sy6 -x1 %x3;                        [...]
!!VRy7&y8>0:+1;                            [...]
!!MA:Fy2/?y8;                              [y8 - значение "FV/кол-во восстановленных существ" для приносимого в жертву отряда]
!!VRy8:*y3 :y7;                            [...]
!!VRv1&y8<v2:Sx16;                         [если проверяемы отряд принести в жертву выгоднее чем предыдущий найденный...]
!!VRv2&y8<v2:Sy8;                          [...выбираем текущий отряд]
!!VRv2&v1=-1:Sy8;                          [если более подходящего отряда еще не было...]
!!VRv1&v1=-1:Sx16;                         [...выбираем текущий отряд]

*** end

ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author orig.  : Algor
** Name          : Siege mastery
** Name rus.     : Осадное дело
** Options       : 756
** Dialogs       : -
** Variables     : z186
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

!?PI;                         [пост-инструкция: установка описаний]
!!UN:P756/?y1;                [проверяем включена ли опция 756 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz186:Sz179247;            [получаем название навыка в z186]
!!UN:G0/10/0/186;             [Устанавливаем название навыка]
!!VRz110:+^%Z179248^;         [получаем базовое описание навыка в z110]
!!UN:G0/10/1/110;             [Устанавливаем базовое описание навыка]
!!VRz111:+^%Z179249^;         [получаем продвинутое описание навыка в z111]
!!UN:G0/10/2/111;             [Устанавливаем продвинутое описание навыка]
!!VRz112:+^%Z179250^;         [получаем экспертное описание навыка в z112]
!!UN:G0/10/3/112;             [Устанавливаем экспертное описание навыка]

!?MF1;                        [при получении урона]
!!UN:P756/?y1;                [проверяем включена ли опция 756 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!MF:W?y1;                    [y1 - тип урона (1/2 - ров/башня)]
!!BA:H1/?y2;                  [y2 - защищающийся герой]
!!FU|y1=0/y2=-2:E;            [выход, если не урон рва/башни или нет героя-защитника]
!!HEy2:S10/?y1;               [y1 - навык Осадного дела у героя-защитника]
!!FU&y1=0:E;                  [выход, если Осадного дела у героя-защитника нет]
!!MF:F?y2;                    [y2 - получаемый урон]
!!VRy3:Sy1 *10 +100 *y2 :100; [y3 - получаемый урон +10% за уровень Осадного дела]
!!MF:Fy3;                     [обновление получаемого урона]

**end

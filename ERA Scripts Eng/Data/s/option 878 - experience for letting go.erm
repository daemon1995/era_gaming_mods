ZVSE2
** Author orig.  : DracoLich
** Name          : Experience for letting go
** Name orig     : Enhancing the experience
** Name rus.     : Опыт за отпускание монстров (ориг.: Расширение опыта)
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] вынос коэффициентов в ini-файл и разделение их по уровню сложности
**                 [Algor] запрет изменения коэффициентов во время игры
** Options       : 878
** Dialogs       :
** Variables     : v7909,v7924-v7926
** Tmp variables : z1-z4
** Timers        : -
** Functions     : FU7953
** PO-values     : -


!?FU(OnAfterErmInstructions); [пост-инструкция: определение коэффициента опыта за отпускание]
!!UN:P878/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!UN:J2/?y1;                  [y1 - выбранный уровень сложности (0..4)]
!!VRy1:+1;                    [y1 - номер параметра в ini-файле]
!!VRz1:S^Data\s\option 878 - experience for letting go.ini^; [z1 - имя ini-файла]
!!VRz2:S^option 878^;         [z2 - секция ini-файла]
!!UN:N6/3/y1/2/1;             [z3 - текстровые значение коэффициента опыта за отпускание]
!!VRv7909:Vz3;                [v7909 - коэффициент опыта за отпускание]

!?OB(OBJ_MONSTER)&1000;       [перед нападением]
!!UN:P878/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!OB998:U?v7924;              [v7924 - тип существ в отпускаемом отряде]
!!MO998:G?v7925;              [v7925 - количество существ в отпускаемом отряде]
!!FU7953:Pv7924/?v7926;       [v7926 - количество аналогичных существ в армии героя перед боем]
!!UN:P878/2;                  [выставляем флаг отпускания]

!?FU(OnBeforeBattle)&1000;    [в начале боя]
!!UN:P878/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!UN:P878/1;                  [сброс флага отпускания, если началась битва]

!$OB(OBJ_MONSTER)&1000;       [после нападения]
!!UN:P878/?y1;                [проверяем включена ли опция в y1]
!!FU&y1<2:E;                  [выход, если опция не включена или флаг отпускания не установлен]

!!UN:P878/1;                  [сбрасываем флаг отпускания]
!!FU7953:Pv7924/?y1;          [y1 - кол-во существ нужного типа у героя после битвы]
!!FU&y1<>v7926:E;             [выход, если существа присоединились]

!!MA:Pv7924/?y2;              [y2 - здоровье одного отпущенного существа]
!!VRy4:Sv7925 *y2 *v7909 :100;[y4 - опыт за отпускание]
!!FU&y4<=0:E;                 [выход, если опыт отсутствует]

!!HE-1:S21/?y10 X?y11/?y12/d/d/d/d/d Ed/?y13/1; [y10-y12 - параметры навыка и специализации героя на Обучении, y13 - уровень героя]

!!if&y10>0;                   [если у героя есть навык обучения]
  !!VRy10:*4 +6548056; !!UN:Cy10/4/?y14; [y10 - вещественное значение бонуса обучения]
  !!SN:X?y15 Xy14 X?e1 Xy15; !!VRe1:*100; !!VRy10:Se1; [y10 - процентное значение бонуса обоучения]

  !!if&y11=0/y12=21;          [для специалиста по обучению]
    !!VRy13:*5 +100;          [y13 - бонус от специализации за уровень героя]
    !!VRy10:*y13 :100;        [y10 - увеличенный специализацией бонус обучения]
  !!en; 

  !!VRy10:+100;               [y10 - бонус обучения 1хх%]
  !!VRy4:*y10 :100;           [y4 - получаемый опыт с учетом бонуса навыка и специализации обучение]
!!en; 

!!VRy1:S0 R2;                 [y1-y3 - случайные параметры сообщения]
!!VRy2:S0 R2;                 [...]
!!VRy3:S0 R2;                 [...]
!!VRz1&y1=0:Sz179102;         [z1-z3 - случайные части сообщения]
!!VRz1&y1=1:Sz179134;         [...]
!!VRz1&y1=2:Sz179135;         [...]
!!VRz2&y2=0:Sz179136;         [...]
!!VRz2&y2=1:Sz179137;         [...]
!!VRz2&y2=2:Sz179138;         [...]
!!VRz3&y3=0:Sz179139;         [...]
!!VRz3&y3=1:Sz179140;         [...]
!!VRz3&y3=2:Sz179141;         [...]
!!VRz4:Sz179142;              [z4 - сгенерированное сообщение]
!!IF:Q2/17/y4/1/z4;           [вывод сообщения]
!!HE-1:Edy4;                  [добавление опыта герою]

!?FU7953;                     [Подсчет и возврат в x2 количества существ типа x1 в армии текущего героя]
!!HE-1:C0/0/?y1/?y11 C0/1/?y2/?y12 C0/2/?y3/?y13 C0/3/?y4/?y14 C0/4/?y5/?y15 C0/5/?y6/?y16 C0/6/?y7/?y17; [y1-y7/y11-y17 - типы и количества войск в армии героя]
!!VRy10&y1=x1:+y11;           [подсчет количества интересующих существ в y10]
!!VRy10&y2=x1:+y12;           [...]
!!VRy10&y3=x1:+y13;           [...]
!!VRy10&y4=x1:+y14;           [...]
!!VRy10&y5=x1:+y15;           [...]
!!VRy10&y6=x1:+y16;           [...]
!!VRy10&y7=x1:+y17;           [...]
!!VRx2:Sy10;                  [возврат количества в x2]

** end

ZVSE2
** Author orig.  : Algor
** Update        : Archer30
** Name          : Experience exchange
** Name rus.     : Обмен опытом
** Options       : 774
** Dialogs       : -
** Variables     : w100


!?FU(OnAfterErmInstructions); [Пост-инструкция - установка описаний]
!!UN:P774/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;   [выход если опция 774 не включена]

; Change secondary skill descriptions
!!SN:H^secskill^/(SKILL_SCHOLAR)/(SKILL_BASIC)/?(basicDesc:z);
!!SN:H^secskill^/(SKILL_SCHOLAR)/(SKILL_BASIC)/^%(basicDesc)%T(es.774.desc1)^;

!!SN:H^secskill^/(SKILL_SCHOLAR)/(SKILL_ADVANCED)/?(advancedDesc:z);
!!SN:H^secskill^/(SKILL_SCHOLAR)/(SKILL_ADVANCED)/^%(advancedDesc)%T(es.774.desc2)^;

!!SN:H^secskill^/(SKILL_SCHOLAR)/(SKILL_EXPERT)/?(expertDesc:z);
!!SN:H^secskill^/(SKILL_SCHOLAR)/(SKILL_EXPERT)/^%(expertDesc)%T(es.774.desc3)^;

!?FU(OnHeroGainLevel);        [при получении опыта любым героем]
!!UN:P774/?y1; !!FU&y1=0:E;   [выход если опция 774 не включена]

!!HE(CURRENT_HERO):S(SKILL_SCHOLAR)/?y1; [y1 - уровень навыка Грамотность героя (0-3)]
!!FU&y1=0:E;                  [выход, если Грамотности нет]

!!HE(CURRENT_HERO):Ed/?y2;    [y2 - уровень героя]
!!VRy2:*25 *y1;               [y2 - запас опыта (25*Уровень*Грамотность(1-3))]
!!IF:W(CURRENT_HERO);         [выбор текущего героя]
!!VRw100:+y2;                 [увеличиваем запас опыта]

!?FU(OnBeforeHeroInteraction);[встреча героев]
!!UN:P774/?y1; !!FU&y1=0:E;   [выход если опция 774 не включена]

!!SN:X?x1/?x2;                [x1/x2 - встречающиеся герои]
!!HEx1:O?y1;                  [y1 - владелец героя-посетителя (Г1)]
!!HEx2:O?y2;                  [y2 - владелец Г2]
!!FU&y1<>y2:E;                [выход, если герои разных игроков]

!!IF:Wx1;                     [выбор Г1]
!!VRy3:Sw100;                 [y3 - запас опыта Г1]
!!IF:Wx2;                     [выбор Г2]
!!VRy4:Sw100;                 [y4 - запас опыта Г2]
!!FU&y3=0/y4=0:E;             [выход, если нет запасов опыта]

!!HEx1:W?y5;                  [y5 - movepoints Г1]
!!HEx2:W?y6;                  [y6 - movepoints Г2]
!!HEx1:S18/?y7;               [y7 - уровень Грамотности Г1]
!!HEx2:S18/?y8;               [y8 - уровень Грамотности Г2]
!!OW:Iy1/?y9;                 [y9=0 для человека, 1 для ИИ]
!!VRy9:+1;                    [y9=1 для человека, 2 для ИИ]
!!VRy10:Sy5 *y7 *y9;          [y10 - макс. количество опыта к-е Г1 может]
!!VRy10&y5>y6:Sy6 *y7 *y9;    [  передать Г2 с учетом затрат movepoints ]
!!VRy11:Sy3;                  [y11 - количество передаваемого]
!!VRy11&y3>y10:Sy10;          [       опыта от Г1 к Г2       ]
!!VRy10&y11>0:Sy11 :y7 :y9;   [y10 - количество затрачиваемых на обучение movepoints (:2 для ИИ)]
!!FU(ES_774_TransferExperience)&y11>0:Px1/x2/y11/y10/y9; [вызов функции передачи опыта Г1/Г2/опыт/movepoints/Чел-ИИ]
!!HEx1:W?y5;                  [y5 - movepoints Г1]
!!HEx2:W?y6;                  [y6 - movepoints Г2]
!!VRy10:Sy5 *y8 *y9;          [     y10 - макс. количество опыта к-е Г2 может      ]
!!VRy10&y5>y6:Sy6 *y8 *y9;    [ передать Г1 с учетом затрат movepoints (*2 для ИИ) ]
!!VRy11:Sy4;                  [y11 - количество передаваемого]
!!VRy11&y4>y10:Sy10;          [       опыта от Г2 к Г1       ]
!!VRy10&y11>0:Sy11 :y8 :y9;   [y10 - количество затрачиваемых на обучение movepoints (:2 для ИИ)]
!!FU(ES_774_TransferExperience)&y11>0:Px2/x1/y11/y10/y9; [вызов функции передачи опыта Г1/Г2/опыт/movepoints/Чел-ИИ(1-2)]

!?FU(ES_774_TransferExperience); [передача опыта x1=Г1, x2=Г2, x3=опыт, x4=movepoints, x5=Чел-ИИ(1-2)]
; If human, show up dialogue to ask (x5=1)
!!if&x5=1;
  !!HEx1:B0/?z1;           [z1 - имя Г1]
  !!HEx2:B0/?z2;           [z2 - имя Г2]
  !!HEx1:R2/?y1;           [y1 - пол Г1]

  !!if&y1=0;
    !!SN:T^es.774.maleDlg^/?z3/^hero1^/z1/^hero2^/z2/^movement^/x4/^exp^/x3; [z3 - Текст сообщения (Г1 - муж)]
  !!el;
    !!SN:T^es.774.femaleDlg^/?z3/^hero1^/z1/^hero2^/z2/^movement^/x4/^exp^/x3; [z3 - Текст сообщения (Г1 - жен)]
  !!en;

  !!IF:Q1/z3;              [вывод диалога]
  !!FU&-1:E;               [выход при отрицательном ответе]
!!en;

!!HEx1:W?y1;                  [уменьшаем]
!!VRy1:-x4;                   [movepoint]
!!HEx1:Wy1;                   [   у Г1  ]
!!HEx2:W?y1;                  [уменьшаем]
!!VRy1:-x4;                   [movepoint]
!!HEx2:Wy1;                   [   у Г2  ]
!!IF:Wx1;                     [выбираем Г1]
!!VRw100:-x3;                 [y1 - уменьшаем запас опыта Г1]
!!HEx2:E?y1;                  [y1 - текущий опыт Г2]
!!VRy1:+x3;                   [y1 - увеличенный опыт Г2]
!!HEx2:Ey1;                   [обновляем опыт Г2]

** end

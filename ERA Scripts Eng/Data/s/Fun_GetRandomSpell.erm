ZVSE2

** ver 1.0
; Currently, this function is used in option 725 - bounty hunting.erm only.

!?FU(Fun_GetRandomSpell);               // get random non-forbidden spell by selection criteria in v1 (-1 if not)
; Acceptable selection criteria values 0/1/2 (others are considered as 0)
; 0 - selection criterion is not taken into account (analogous to "or" for several criteria)
; 1 - spell must match criterion (analogous to "and" for several criteria)
; 2 - spell must NOT match criterion (analogous to "and not" for several criteria)
; Selection criteria:
; x1 - Level 1
; x2 - Level 2
; x3 - Level 3
; x4 - Level 4
; x5 - Level 5
; x6 - School of Air
; x7 - School of Water
; x8 - School of Fire
; x9 - School of Earth
; x10 - Combat spell
; x11 - specific set of flags, for example: 528 (512+16) - Spell with damage, target - 1 squad (0 - do not take flags into account)
; Example: find a random spell (1st or 3rd or 5th level) that belongs to (Air AND Earth) and is (Battle), flag set does not matter
; FU(Fun_GetRandomSpell):P0/2/0/2/0/1/0/0/1/1/0;
; In the standard magic layout, only "Magic Arrow" meets these criteria
!!SN:M-1/70/0/0 W^fun.grs^/v1;          // Mi^es.fun.grs^ - временный массив заклинаний
!!VRy1:S-1;                             // y1 - количество подходящих заклинаний

!!re y2/0/69;                           // y2 - проверяемое заклинание
  !!UN:J0/y2/?y3; !!co&y3=1;            // пропуск запрещенных заклинаний
  !!SSy2:L?y3 S?y4 F?y5;                // y3/y4/y5 - уровень/школы/флаги заклинания
  !!VRy6:Sy4 &1; !!VRy7:Sy4 &4; !!VRy8:Sy4 &2; !!VRy9:Sy4 &8; // y6..y8 - принадлежность Школам Воздуха..Земли
  !!VRy10:Sy5 &1;                       // y10 - боевое заклинание
  !!co&x1=1/y3<>1;  !!co&x1=2/y3=1;     // пропуск заклинания не подходящего по критериям уровня
  !!co&x2=1/y3<>2;  !!co&x2=2/y3=2;     // ...
  !!co&x3=1/y3<>3;  !!co&x3=2/y3=3;     // ...
  !!co&x4=1/y3<>4;  !!co&x4=2/y3=4;     // ...
  !!co&x5=1/y3<>5;  !!co&x5=2/y3=5;     // ...
  !!co&x6=1/y6=0;   !!co&x6=2/y6>0;     // пропуск заклинания не подходящего по критериям Школы
  !!co&x7=1/y7=0;   !!co&x7=2/y7>0;     // ...
  !!co&x8=1/y8=0;   !!co&x8=2/y8>0;     // ...
  !!co&x9=1/y9=0;   !!co&x9=2/y9>0;     // ...
  !!co&x10=1/y10=0; !!co&x10=2/y10>0;   // пропуск заклинания не подходящего по критерию Боевое/Походное
  !!co&x11>0/x1<>y5;                    // пропуск заклинания не подходящего по флагам

  !!VRy1:+1; !!SN:Mi^fun.grs^/y1/y2;    // добавление заклинания в массив подходящих
!!en;                                   // ...

!!VRv1:S-1;                             // инициализация v1 значением по-умолчанию (подходящее заклинание не найдено)

!!if&y1>=0;                             // если найдено хотя бы одно подходящее заклинание
  !!VRy2:S0 Ry1;                        // выбор случайного заклинания из найденных
  !!SN:Mi^fun.grs^/y2/?v1;              // в переменную v1
!!en;                                   // ...

!!SN:Mi^fun.grs^;                       // удаление временного массива

** end
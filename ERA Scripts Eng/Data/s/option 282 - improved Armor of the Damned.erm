ZVSE2
** Author orig.  : Jim Vogan
** Rewritten by  : Archer30
** Name          : Improved Armor of the Damned
** Name rus.     : Усиленные Доспехи проклятого
** Changes rus.  : [Algor] вынос опции в отдельный erm-файл для мода ERA
**               : [Algor] вынос текстов в ert-файл
** Options       : 282


!?FU(OnAfterErmInstructions);
; disable this script if option disabled
!!UN:P282/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!SN:H^art^/(ART_ARMOR_OF_THE_DAMNED)/1/?(desc:z);
!!VR(newDesc:z):S(desc) +^%T(es.282.desc)^;
!!SN:H^art^/(ART_ARMOR_OF_THE_DAMNED)/1/(newDesc);


// artifact #(ART_ARMOR_OF_THE_DAMNED) put on
!?FU(OnEquipArt)&v998=(ART_ARMOR_OF_THE_DAMNED);
; disable this script if option disabled
!!UN:P282/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; increase primary skills by Lvl/5 if v998=(ART_ARMOR_OF_THE_DAMNED)
!!HE(CURRENT_HERO):Ed/?(lv:y); get exp. level
!!VR(bonus:y):S(lv) :5;
!!HE(CURRENT_HERO):Fd(bonus)/d(bonus)/d(bonus)/d(bonus);


// artifact #(ART_ARMOR_OF_THE_DAMNED) taken off
!?FU(OnUnequipArt)&v998=(ART_ARMOR_OF_THE_DAMNED);
; disable this script if option disabled
!!UN:P282/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; decrease primary skills by Lvl/5 if v998=(ART_ARMOR_OF_THE_DAMNED)
!!HE(CURRENT_HERO):Ed/?(lv:y); get exp. level
!!VR(bonus:y):S(lv) :5;
!!HE(CURRENT_HERO):Fd-(bonus)/d-(bonus)/d-(bonus)/d-(bonus);


// any hero gets level increase
!?FU(OnHeroGainLevel);
; disable this script if option disabled
!!UN:P282/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; check for AoD equipped
!!HE(CURRENT_HERO):A2/(ART_ARMOR_OF_THE_DAMNED)/?(num:y)/?(equipped:y);
!!FU&(equipped)=0:E;

!!HE(CURRENT_HERO):Ed/?(lv:y); get exp. level

; Calculate and deduct old level bonus
!!VR(oldLv:y):S(lv) -1;
!!VR(oldBonus:y):S(oldLv) :5;
!!HE(CURRENT_HERO):Fd-(oldBonus)/d-(oldBonus)/d-(oldBonus)/d-(oldBonus);

!!VR(bonus:y):S(lv) :5;
!!HE(CURRENT_HERO):Fd(bonus)/d(bonus)/d(bonus)/d(bonus);

** end

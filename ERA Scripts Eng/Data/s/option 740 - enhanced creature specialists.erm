ZVSE2
** Author        : igrik, Algor (sponsor: swallowmee)
** Updated by    : Archer30
** Name          : Enhanced creature specialists
** Name rus.     : Измененные специалисты по существам
** Options       : 740

** Существа под командованием соответствующего специалиста получают единовременный бонус +1 к скорости,
** а также +1 к атаке за каждый нечётный уровень героя и +1 к защите за каждый чётный.

; This script disables the monster bonuses from option 39 Hero Specialty Boost.


// Disable Monster Specialty bonuses from Hero Specialty Boost
!#UN:P740/?v1;                          [проверяем включена ли опция 740]
!#VRi^wog_39_monsterSpecialtiesEnabled^&v1:S(FALSE);

!?FU(OnAfterErmInited);
!!UN:P740/?(wogOption:y);               [проверяем включена ли опция 740]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Loop though all the non-special heroes
!!re i/(HERO_FIRST)/(HERO_GRINDAN);
  ; Standarise all the creature specialists to type 1
  !!HEi:X?(spec:y)/?(spec2:y)/d/d/d/d/d;

  !!HEi&(spec)=4:X1/(spec2)/d/d/d/d/d;

  !!SN&(spec)=1/(spec2)<>(MON_BALLISTA):H^spec^/i/2/^%T(es.740.spec)^;
!!en;

!?FU(ES_CreateERMHook);
!#VA(address:x);

!!UN:P740/?(wogOption:y);               [проверяем включена ли опция 740]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!SN:E(address)/1/5137685/(ES_Hero_GetSpecMonster);[0x004E6515]


!?FU(ES_Hero_GetSpecMonster);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/?(hero:y); [структура героя]
!!UN:C(hero)/85/2/?(heroLevel:y);

; [бонус от специальности]
!!VR(attackBonus:y):S(heroLevel) +1 :2 F0/(INT_MAX);
!!VR(defenseBonus:y):S(heroLevel) :2 F0/(INT_MAX);

; [устанавливаем бонусы]
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(creatureInfo:y);
!!UN:C(creatureInfo:y)/84/4/d(attackBonus:y);                         [бонус к атаке]
!!UN:C(creatureInfo:y)/88/4/d(defenseBonus:y);

!!SN:X?y99/0;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/(UNC_INT)/5137828; [возврат в 0x004E65A4]
** end

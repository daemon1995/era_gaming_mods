ZVSE2
** Author orig.  : Algor
** Name          : Improvements of neutrals
** Name rus.     : Улучшения нейтралов
** Options       : 775
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7912-FU7913


!?OB(OBJ_CREATURE_GENERATOR_1)/73;      [Тростниковая лачуга (Хоббиты)]
!!UN:P775/?(wogOption:y);               [проверяем включена ли опция 775]
!!FU7912&(wogOption):P73/(MON_PEASANT)/25/-1/-1/-1/-1/(MON_HALFLING)/15/0/0; [вызов функции замены]

!?OB(OBJ_CREATURE_GENERATOR_1)/78;      [Убежище воров (Воры)]
!!UN:P775/?(wogOption:y);               [проверяем включена ли опция 775]
!!FU7912&(wogOption):P78/(MON_PEASANT)/25/(MON_HALFLING)/15/-1/-1/(MON_ROGUE)/5/5/0; [вызов функции замены]

!?OB(OBJ_CREATURE_GENERATOR_1)/77;      [Палатка кочевников (Кочевники)]
!!UN:P775/?(wogOption:y);               [проверяем включена ли опция 775]
!!FU7912&(wogOption):P77/(MON_ROGUE)/8/(MON_BOAR)/8/-1/-1/(MON_NOMAD)/4/6/0; [вызов функции замены]

!?OB(OBJ_CREATURE_GENERATOR_1)/79;      [Мост троллей (Тролли)]
!!UN:P775/?(wogOption:y);               [проверяем включена ли опция 775]
!!FU7912&(wogOption):P79/(MON_BOAR)/8/(MON_NOMAD)/7/-1/-1/(MON_TROLL)3/3/0; [вызов функции замены]

!?OB(OBJ_CREATURE_GENERATOR_1)/76;      [Могила проклятых (Мумии)]
!!UN:P775/?(wogOption:y);               [проверяем включена ли опция 775]
!!FU7912&(wogOption):P76/(MON_PEASANT)/25/(MON_HALFLING)/15/(MON_ROGUE)/8/(MON_MUMMY)/3/3/4; [вызов функции замены]

!?OB(OBJ_CREATURE_GENERATOR_1)/98;      [Воющая пустота (Оборотни)]
!!UN:P775/?(wogOption:y);               [проверяем включена ли опция 775]
!!FU7912&(wogOption):P98/(MON_PEASANT)/25/(MON_HALFLING)/15/(MON_ROGUE)/8/(MON_WEREWOLF)/3/3/4; [вызов функции замены]

!?FU7912;                     [x1-номер ert-переменной диалога, x2/x3-x6/x7 номера/базовые количества 1-3го заменяемых существ, x8/x9-x11 -номер/базовые кол-ва новых существ]
!!HE(CURRENT_HERO):N?y1;      [y1 - номер героя]
!!FU7913:Py1/x2/?y2/?y3;      [y2/y3 - количество и слот 1х существ]
!!FU7913:Py1/x4/?y4/?y5;      [y4/y5 - количество и слот 2х существ]
!!FU7913:Py1/x6/?y6/?y7;      [y6/y7 - количество и слот 3х существ]
!!VRe1:Sy2 *x9 :x3;           [e1 - количество получаемых существ за 1х существ]
!!VRe2:Sy4 *x10 :x5;          [e2 - количество получаемых существ за 2х существ]
!!VRe3:Sy6 *x11 :x7;          [e2 - количество получаемых существ за 3х существ]
!!VRe1:+e2 +e3;               [e1 - общее количество получаемых существ]
!!VRy10:Se1;                  [у10 - общее количество получаемых существ (целое)]
!!FU&y10=0:E;                 [выход если герой ничего не получит]

!!if&y3=-1;
  !!VRy3:Sy5;                 [y3 - слот в который  ]
  !!VRy3:Sy7;                 [добавлять новых существ]
!!en;

!!HEy1:O?y9;                  [y9 - владелец героя]
!!OW:Iy9/?y9;                 [y9=0/1 - человек/ИИ]

!!if&y9=0;
  !!VRy11:S65536 *y2 +x2;  [y11 - количество 1х существ для отображения]
  !!VRy12:S65536 *y4 +x4;  [y12 - количество 2х существ для отображения]
  !!VRy13:S65536 *y6 +x6;  [y13 - количество 3х существ для отображения]
  !!HEy1:B0/?z1;           [z1 - имя героя]
  !!UN:N3/2/x8/1;          [z2 - название получаемого существа]
  !!SN:T^es.775.dwell%x1^/?z3/^hero^/z1/^num^/y10/^mon^/z2; [z3 - Текст сообщения]     

  !!IF&y2>0/y4>0/y6>0:Q1/(PIC_TYPE_MONSTER)/y11/(PIC_TYPE_MONSTER)/y12/(PIC_TYPE_MONSTER)/y13/2/z3; [вывод диалога 123]
  !!IF&y2>0/y4>0/y6=0:Q1/(PIC_TYPE_MONSTER)/y11/(PIC_TYPE_MONSTER)/y12/2/z3; [вывод диалога 12]
  !!IF&y2>0/y4=0/y6>0:Q1/(PIC_TYPE_MONSTER)/y11/(PIC_TYPE_MONSTER)/y13/2/z3; [вывод диалога 13]
  !!IF&y2=0/y4>0/y6>0:Q1/(PIC_TYPE_MONSTER)/y12/(PIC_TYPE_MONSTER)/y13/2/z3; [вывод диалога 23]
  !!IF&y2>0/y4=0/y6=0:Q1/(PIC_TYPE_MONSTER)/y11/2/z3; [вывод диалога 1]
  !!IF&y2=0/y4>0/y6=0:Q1/(PIC_TYPE_MONSTER)/y12/2/z3; [вывод диалога 2]
  !!IF&y2=0/y4=0/y6>0:Q1/(PIC_TYPE_MONSTER)/y13/2/z3; [вывод диалога 3]
  !!FU&-1:E;              [выход при отказе от обмена]
!!en;

!!HEy1:C1/x2/-1/0;           [удаление 1х существ]
!!HEy1:C1/x4/-1/0;           [удаление 1х существ]
!!HEy1:C1/x6/-1/0;           [удаление 1х существ]
!!HEy1:C0/y3/x8/y10;         [добавление новых существ]

!?FU7913;                     [x1-номер героя, x2-номер существа, x3-возвращает количество найденых существ в армии героя, x4 - номер последнего слота с найденными существами]
!!VRx3:S0;
!!VRx4:S-1;
!!HEx1:C0/0/?y1/?y2;          [y1/y2 - тип и количество монстров в 1м слоте]
!!VRx3&y1=x2:+y2;             [если монстр нужного типа увеличиваем счетчик существ]
!!VRx4&y1=x2:S0;              [x4 - номер слота с найденными монстрами]
!!HEx1:C0/1/?y1/?y2;          [y1/y2 - тип и количество монстров в 2м слоте]
!!VRx3&y1=x2:+y2;             [если монстр нужного типа увеличиваем счетчик существ]
!!VRx4&y1=x2:S1;              [x4 - номер слота с найденными монстрами]
!!HEx1:C0/2/?y1/?y2;          [y1/y2 - тип и количество монстров в 3м слоте]
!!VRx3&y1=x2:+y2;             [если монстр нужного типа увеличиваем счетчик существ]
!!VRx4&y1=x2:S2;              [x4 - номер слота с найденными монстрами]
!!HEx1:C0/3/?y1/?y2;          [y1/y2 - тип и количество монстров в 4м слоте]
!!VRx3&y1=x2:+y2;             [если монстр нужного типа увеличиваем счетчик существ]
!!VRx4&y1=x2:S3;              [x4 - номер слота с найденными монстрами]
!!HEx1:C0/4/?y1/?y2;          [y1/y2 - тип и количество монстров в 5м слоте]
!!VRx3&y1=x2:+y2;             [если монстр нужного типа увеличиваем счетчик существ]
!!VRx4&y1=x2:S4;              [x4 - номер слота с найденными монстрами]
!!HEx1:C0/5/?y1/?y2;          [y1/y2 - тип и количество монстров в 6м слоте]
!!VRx3&y1=x2:+y2;             [если монстр нужного типа увеличиваем счетчик существ]
!!VRx4&y1=x2:S5;              [x4 - номер слота с найденными монстрами]
!!HEx1:C0/6/?y1/?y2;          [y1/y2 - тип и количество монстров в 7м слоте]
!!VRx3&y1=x2:+y2;             [если монстр нужного типа увеличиваем счетчик существ]
!!VRx4&y1=x2:S6;              [x4 - номер слота с найденными монстрами]

** end

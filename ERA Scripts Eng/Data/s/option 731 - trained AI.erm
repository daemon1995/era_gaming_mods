ZVSE2
** Author orig.  : Algor (идея: igrik)
** Update        : Archer30
** Name          : trained AI
** Name rus.     : тренированный ИИ
** Options       : 731

** Каждый ИИ-герой при повышении уровня имеет шанс 0/25/50/75/100% (в зависимости от выбранного уровня сложности)
** дополнительно повысить на 1 профильный первичный параметр. Профильность параметров определяется классом героя.


!?FU(OnHeroGainLevel)&-1000;            [при повышении уровня ИИ-героем]
!!UN:P731/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход, если не включена опция 731]

!!UN:J2/?(diff:y);
!!VR(random:y):R0/0/3;
!!FU&(diff)<=(random):E;                [выход, если тренировка не сработала]

!!HE(CURRENT_HERO):O?(owner:y);
!!FU&(owner)=(NO_OWNER):E;              [выход, если герой не нанят]

!!HE(CURRENT_HERO):B2/?(class:y);
!!VR(random):R0/0/5;                    [класс героя, случайное число (0..5)]
!!VR(bonusAtk:y):S0;
!!VR(bonusDef:y):S0;
!!VR(bonusPwr:y):S0;
!!VR(bonusKnl:y):S0;                    [инициализация]

** выбор повышаемого параметра (bonusAtk)..(bonusKnl) в зависимости от класса героя
!!if&(class)=0;                         [2 2 1 1 Рыцарь]
  !!VR(bonusAtk)|(random)=0/(random)=1:S1;
  !!VR(bonusDef)|(random)=2/(random)=3:S1;
  !!VR(bonusPwr)|(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=1;                         [1 1 2 2 Священник]
  !!VR(bonusAtk)|(random)=0:S1;
  !!VR(bonusDef)|(random)=1:S1;
  !!VR(bonusPwr)|(random)=2/(random)=3:S1;
  !!VR(bonusKnl)|(random)=4/(random)=5:S1;
!!el&(class)=2;                         [1 3 1 1 Рейнджер]
  !!VR(bonusAtk)|(random)=0:S1;
  !!VR(bonusDef)|(random)=1/(random)=2/(random)=3:S1;
  !!VR(bonusPwr)|(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=3;                         [0 2 1 3 Друид]
  !!VR(bonusDef)|(random)=0/(random)=1:S1;
  !!VR(bonusPwr)|(random)=2:S1;
  !!VR(bonusKnl)|(random)=3/(random)=4/(random)=5:S1;
!!el&(class)=4;                         [1 1 2 2 Алхимик]
  !!VR(bonusAtk)|(random)=0:S1;
  !!VR(bonusDef)|(random)=1:S1;
  !!VR(bonusPwr)|(random)=2/(random)=3:S1;
  !!VR(bonusKnl)|(random)=4/(random)=5:S1;
!!el&(class)=5;                         [0 1 2 3 Маг]
  !!VR(bonusDef)|(random)=0:S1;
  !!VR(bonusPwr)|(random)=1/(random)=2:S1;
  !!VR(bonusKnl)|(random)=3/(random)=4/(random)=5:S1;
!!el&(class)=6;                         [2 2 1 1 Демон]
  !!VR(bonusAtk)|(random)=0/(random)=1:S1;
  !!VR(bonusDef)|(random)=2/(random)=3:S1;
  !!VR(bonusPwr)|(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=7;                         [1 1 2 2 Еретик]
  !!VR(bonusAtk)|(random)=0:S1;
  !!VR(bonusDef)|(random)=1:S1;
  !!VR(bonusPwr)|(random)=2/(random)=3:S1;
  !!VR(bonusKnl)|(random)=4/(random)=5:S1;
!!el&(class)=8;                         [1 2 2 1 Рыцарь Смерти]
  !!VR(bonusAtk)|(random)=0:S1;
  !!VR(bonusDef)|(random)=1/(random)=2:S1;
  !!VR(bonusPwr)|(random)=3/(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=9;                         [1 0 2 3 Некромант]
  !!VR(bonusAtk)|(random)=0:S1;
  !!VR(bonusPwr)|(random)=1/(random)=2:S1;
  !!VR(bonusKnl)|(random)=3/(random)=4/(random)=5:S1;
!!el&(class)=10;                        [2 2 1 1 Лорд]
  !!VR(bonusAtk)|(random)=0/(random)=1:S1;
  !!VR(bonusDef)|(random)=2/(random)=3:S1;
  !!VR(bonusPwr)|(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=11;                        [1 0 3 2 Чернокнижник]
  !!VR(bonusAtk)|(random)=0:S1;
  !!VR(bonusPwr)|(random)=1/(random)=2/(random)=3:S1;
  !!VR(bonusKnl)|(random)=4/(random)=5:S1;
!!el&(class)=12;                        [4 0 1 1 Варвар]
  !!VR(bonusAtk)|(random)=0/(random)=1/(random)=2/(random)=3:S1;
  !!VR(bonusPwr)|(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=13;                        [2 1 2 1 Боевой маг]
  !!VR(bonusAtk)|(random)=0/(random)=1:S1;
  !!VR(bonusDef)|(random)=2:S1;
  !!VR(bonusPwr)|(random)=3/(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=14;                        [0 4 1 1 Хозяин Зверей]
  !!VR(bonusDef)|(random)=0/(random)=1/(random)=2/(random)=3:S1;
  !!VR(bonusPwr)|(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=15;                        [0 2 2 2 Ведьма]
  !!VR(bonusDef)|(random)=0/(random)=1:S1;
  !!VR(bonusPwr)|(random)=2/(random)=3:S1;
  !!VR(bonusKnl)|(random)=4/(random)=5:S1;
!!el&(class)=16;                        [3 1 1 1 Путешественник]
  !!VR(bonusAtk)|(random)=0/(random)=1/(random)=2:S1;
  !!VR(bonusDef)|(random)=3:S1;
  !!VR(bonusPwr)|(random)=4:S1;
  !!VR(bonusKnl)|(random)=5:S1;
!!el&(class)=17;                        [0 0 3 3 Элементалист]
  !!VR(bonusPwr)|(random)=0/(random)=1/(random)=2:S1;
  !!VR(bonusKnl)|(random)=3/(random)=4/(random)=5:S1;
!!en;                                   

** повышение первичного параметра
!!HE(CURRENT_HERO):Fd(bonusAtk)/d(bonusDef)/d(bonusPwr)/d(bonusKnl); [повышение выбранного первичного параметра герою];

** end

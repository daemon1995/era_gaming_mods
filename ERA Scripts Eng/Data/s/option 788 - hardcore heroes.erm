ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30 and deamon_n
** Name          : Hardcore heroes
** Name rus.     : Смертные герои
** Options       : 788

; To-do: Fix the compatibility with Hero Specialty Boost


!?FU(OnBeforeBattleAction);             // перед действием в бою
!!UN:P788/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             // выход, если опция не включена

!!BG:A?i^es_788_lastBattleAction^;      // сохраняем последнее выполненное действие

; This function is executed in 1000 es - stdlib in order to be sooner than random heroes (option 72 from WoG Scripts)
!?FU(ES_788_Rebirth);                   // Перерождение героя
!#VA(hero:x);
!#VA(usedY[30]:y);

; Exit if the hero is not neutral
; Double check to make sure there is no crash when FU(ES_788_Rebirth) is called by external scripts by any chance
!!HE(hero):O?(owner:y);
!!FU&(owner)<>(NO_OWNER):E;

; Exit if the defeated hero is Asmodeus
!!FU&(hero)=i^wog_106_Asmodeus^:E;

; Reset the hero
!!HE(hero):Z?(heroPtr:y);
!!SN:E5081488/(CALLCONV_THISCALL)/(heroPtr)/(hero);
; Reset the hero's exp (this is not done with SN:E5081488)
!!HE(hero):E0;
; Restore the original starting army
!!UN:C(GAME_MANAGER)/(UNC_INT)/?(gameManager:y);
!!SN:E5018608/(CALLCONV_THISCALL)/(gameManager)/(hero)/0/0;
; Restore Movement Points
!!SN:E5132288/(CALLCONV_THISCALL)/(heroPtr);
!!HE(hero):Gv1 Wv1/1;

; Unlearn all the spells
!!re i/(SPELL_FIRST)/(SPELL_LAST_WOG);  // для каждого заклинания
  !!HE(hero):Mi/0;                      // удаляем заклинание
!!en;

; Remove war machines
!!HE(hero):A-(ART_BALLISTA) A-(ART_FIRST_AID_TENT);

!!HE(hero):B2/?y2;
!!VRy1:Sy2 :2;                          // y1 - раса героя 0..8
!!VRy2:%2 *2 -1;
!!VRi:R0/0/2;
!!VRy2&i=2:S0;                          // y2 - класс героя -1/0/1 = Воин/Странник/Маг

** установка первичных параметров
!!VRy11:R0/1/2 -y2;
!!VRy12:R0/1/2 -y2;                      // y11/y12/y13/y14 - первичные параметры нового героя
!!VRy13:R0/2/3 +y2;
!!VRy14:R0/2/3 +y2;                      // ...
!!HE(hero):Fy11/y12/y13/y14;                // Установка первичных параметров

** выбор специализации
!!VRy3:R0/0/1;
!!VRy4:R0/0/1;                           // y3 - тип специализации 0 - втор.навык, 1 - существо/ресурс/заклинание, y4=1 для альтернативной специализации (50% шанс)

; Set up i values and give Ballista if lucky
; Warriors
!!if&y2=-1;
  !!if&y3=0;                        // воин со вторичным навыком
    !!VRi:R0/0/3;                          // i - случайное число 0..3
  !!el;                            // воин с существом
    !!VRk:R0/0/7;

    ; 1/8 chance to become Ballista Specialist
    !!if&k<=6;             
      !!UN:Ty1/k/0/?i;                  // i - случайное негрейженное существо расы героя
    !!el;
      !!VRi:S(MON_BALLISTA);
    !!en;
  !!en;
; Adventurers
!!el&y2=0;
  !!if&y3=0;                         // странник со вторичным навыком
    !!VRi:R0/0/3;                          // i - случайное число 0..3
  !!el;                         // странник с ресурсом
    !!VRi:R0/(RES_FIRST)/(RES_LAST_SOD); // i - случайное число
  !!en;
; Mages
!!el&y2=1;
  !!if&y3=0;                         // маг со вторичным навыком
    !!VRi:R0/0/3;                          // i - случайное число 0..3
  !!el;                         // маг с заклинанием
    !!VRi:R0/0/10;                         // i - случайное число 0..10
  !!en;
!!en;

; Set Up new specialty (give Ballista for Ballista specialists)
!!FU(ES_788_SetUpNewSpecialty):P(hero)/y2/y3/y4/i;

; Add in spell book if the hero is a Mage
; By default the spell book is given with internal function so only Alchemists and Death Knights start with a spell book
!!HE(hero)&y2=1:A1/(ART_SPELL_BOOK)/17;                 // выдам катапульту и книгу всем героям

; Set up reborn flags (set the last 16 bit to 788 to ensure the variable is not 0)
!!VRy30:Sy2 +1;                         [Set hero type to positive number in order to be packed]
!!FU(PackUnion):Py30/2/y3/1/y4/1/i/12/788/16/?i^es_788_rebornFlags_%(hero)^;

; Reset secondary skills and teach the specialty skill
; Warning: This script does not include checks of banned skills, idealy should use FU(ES_GameMgr_Skill_isBanned)
; Teach Estates if the hero is a resources master
!!re i/(SEC_SKILL_FIRST)/(SEC_SKILL_LAST); // для каждого втор.навыка
  !!HE(hero):Si/(SKILL_NOT_LEARNED);        // удаляем все вторичные навыки героя
!!en; 

!!HE(hero):X?j/?k/d/d/d/d/d;                // j/k - параметры специализации героя
!!HE(hero)&j=0:Sk/(SKILL_ADVANCED);         // для спецов по втор.навыку даем этот навык на продвинутом уровне
!!HE(hero)&j=2:S(SKILL_ESTATES)/(SKILL_BASIC); // для спецов по ресурсам даем базовое Имущество

!!if&j=3;                               // для спецов по заклинаниям первым навыком даем профильную Школу Магии
  !!SSk:S?l;
  !!VRy10:S4;                           // y10 - навык профильной Школы. По умолчалию или для мультипрофильных заклинаний - Вода
  !!VRy11:Sl &1;
  !!VRy10&y11>0:S15;                    // ...
  !!VRy11:Sl &2;
  !!VRy10&y11>0:S14;                    // ...
  !!VRy11:Sl &8;
  !!VRy10&y11>0:S17;                    // ...
  !!VRy11:Sl &4;
  !!VRy10&y11>0:S16;                    // ...
  !!HE(hero):Sy10/1;                        // даем базовый навык профильной Школы
!!en; 

!!if|j=1/j=6;                           // для спецов по существам первым навыком даем Нападение/Тактику/Стрельбу для пеших/летунов/стрелков
  !!MA:Xk/?l;
  !!VRy10:S22;                          // y10 - профильный навык. По-умолчанию - Нападение
  !!VRy11:Sl &2;
  !!VRy10&y11>0:S19;                    // для летающих существ - Тактика
  !!VRy11:Sl &4;
  !!VRy10&y11>0:S1;                     // для стреляющих существ - Стрельба
  !!HE(hero):Sy10/1;                        // даем базовый профильный навык
!!en; 

!!if&j<>0;                              // для спецов не по втор.навыку подбираем второй базовый навык
  !!if&y2=-1;                           // для Воинов
    !!VRi:R0/0/5;
    !!HE(hero)&i=0/y1<>4:S6/1;              // Лидерство
    !!HE(hero)&i=0/y1=4:S12/1;              // Некромантия
    !!HE(hero)&i=1:S9/1;                    // Удача
    !!HE(hero)&i=2:S10/1;                   // Баллистика
    !!HE(hero)&i=3:S20/1;                   // Артиллерия
    !!HE(hero)&i=4:S23/1;                   // Доспехи
    !!HE(hero)&i=5:S27/1;                   // Первая помощь
  !!el&y2=0;                            // для Странников
    !!VRi:R0/0/6;
    !!HE(hero)&i=0:S0/1;                    // Поиск Пути
    !!HE(hero)&i=1:S2/1;                    // Логистика
    !!HE(hero)&i=2:S3/1;                    // Разведка
    !!HE(hero)&i=3:S4/1;                    // Дипломатия
    !!HE(hero)&i=4:S11/1;                   // Зоркость
    !!HE(hero)&i=5:S18/1;                   // Грамотность
    !!HE(hero)&i=6:S21/1;                   // Обучение
  !!el&y2=1;                            // для Магов
    !!VRi:R0/0/4;
    !!HE(hero)&i=0/y1<>4:S7/1;              // Мудрость
    !!HE(hero)&i=0/y1=4:S12/1;              // Некромантия
    !!HE(hero)&i=1:S8/1;                    // Мистицизм
    !!HE(hero)&i=2:S24/1;                   // Интеллект
    !!HE(hero)&i=3:S25/1;                   // Волшебство
    !!HE(hero)&i=4:S26/1;                   // Сопротивление
  !!en; 
!!en; 

; Reset global variables for Achievementes and Assassins (Night Scouting)
** удаление достижений
!!re i/0/14;                            // для каждого достижения
  !!SN:W^opt760.h%(hero)a%Vir^/0;           // обнуляем достижение
  !!SN:W^opt760.h%(hero)a%Vic^/0;           // обнуляем счетчик достижения
!!en; 

** удаление ассасинов
!!SN:W^opt795.11.%(hero)^/?y15 W^opt795.11.%(hero).rebirth^/y15 W^opt795.11.%(hero)^/0; // удаление ассасинов

** очистка командира/артефактов командира
!!UN:P(WOG_OPT_DISABLE_COMMANDERS)/?i P(WOG_OPT_COMMANDERS_NEED_HIRING)/?j P51/?k;                // i/j/k - статус опций Командиры / Обязательный найм командиров / Измененные командиры

!!if&i=(FALSE);                               // если командиры разрешены
  !!CO(hero):A4/-1/0/-1/0/-1/0/-1/0/-1/0/-1/0;         // удаление артефактов командира
  !!CO(hero):B0/0 D0 Ty1 X0/1000 X1/0 X2/1;            // удаление способностей, уровня, опыта, оживление и сброс расы командира
  !!CO(hero)&k=0:P0/5 P1/5 P2/40 P3/9 P4/1 P5/4 P6/5;  // сброс первичных параметров командира
  !!CO(hero)&k=1:P0/5 P1/5 P2/30 P3/10 P4/3 P5/5 P6/0; // сброс первичных параметров для "измененного" командира
  !!CO(hero):S0/0 S1/0 S2/0 S3/0 S4/0 S5/0 S6/0;       // сброс вторичных навыков командира

  !!CO(hero)&j:E0;                          // если командиры должны быть наняты, удаляем командира

  !!if&k;                             // если включены измененные командиры, раздаем расовые бонусные навыки
    !!CO(hero)&y1=0:B1/5/0;                 // paladin         - endless retaliation 
    !!CO(hero)&y1=1:B1/7/0;                 // heirophant      - magic mirror (fire shield)
    !!CO(hero)&y1=2:B1/4/0;                 // temple guardian - shoot 
    !!CO(hero)&y1=3:B1/14/0;                // succubus        - fly 
    !!CO(hero)&y1=4:B1/1/0;                 // soul eater      - fear
    !!CO(hero)&y1=5:B1/3/0;                 // brute           - no retaliation 
    !!CO(hero)&y1=6:B1/8/0;                 // ogre leader     - block 
    !!CO(hero)&y1=7:B1/12/0;                // shaman          - 50% poison (death stare)
    !!CO(hero)&y1=8:B1/0/0;                 // astral spirit   - ignore 50% defense  
  !!en;
!!en;

** обнуление благословений/проклятий
!!HE(hero):Y0/0/0/3;                        // удаление всех благословений/проклятий

; Reset all the w-vars, special treatments for war machiens and henchmen
!!IF:W(hero);                               // w-переменные текущего героя

!!re i/1/200;
  !!VRwi:S0;
!!en;

; Enhanced War Machines
!!UN:P73/?(wm3:y);

!!if&(wm3);
  !!HE(hero):A2/(ART_BALLISTA)/?(has:y)/?(equipped:y);
  !!VRw81&(equipped)>0:S1;
  !!HE(hero):A2/(ART_FIRST_AID_TENT)/?(has)/?(equipped:y);
  !!VRw83&(equipped)>0:S1;
!!en;

; Henchmen
!!UN:P49/?(hench:y);
!!VRw118&(hench):S(NO_MON);

; Adventure Cave
!!UN:P30/?(advCave:y);

!!if&(advCave);
  !!VRw62:S-1; [hero paying for Rogue attack]
  !!VRw63:S-1; [artifact stolen by Rogues]
!!en;

; Reset known global vars
; Enhanced War Machines I
!!UN:P54/?(wm1:y);

!!if&(wm1);
  !!re (wmArt:y)/(ART_BALLISTA)/(ART_AMMO_CART);
    !!VRi^wog_54_%(hero)_disable%(wmArt)^:S0;
  !!en;
!!en;

; Estates II
!!VRi^wog_191_dailyResPlusOne_%(hero)^:S0;

// Set up new specialty
!?FU(ES_788_SetUpNewSpecialty);
!#VA(hero:x) (heroType:x) (random1:x) (random2:x) (subtype:x);

; Set up variable to determine whether to set hero spec desc
!!VR(isNotValid:y):S(FALSE);

!!HE(hero):B2/?(class:y);
!!VR(faction:y):S(class) :2;

; Warriors
!!if&(heroType)=-1;
  ; Skill Master (Warrior)
  !!if&(random1)=0;            // воин со вторичным навыком
    !#VA(skillsList[4]:y);

    !!VR(skillsList):C(SKILL_ARCHERY)/(SKILL_OFFENCE)/(SKILL_ARMORER)/(SKILL_FIRST_AID);
    !!VR(skillsList[3])&(faction)=(TOWN_NECROPOLIS):S(SKILL_NECROMANCY); [Necropolis heroes won't learn First Aid, only Necromancy]
    !!VR(skill:y):S(skillsList[subtype]);

    ; Check if the skill is banned
    !!FU(ES_GameMgr_Skill_isBanned):P(skill)/?(isNotValid:y);

    !!if&(isNotValid)=(FALSE);
      !!HE(hero):X0/(skill)/0/0/0/0/0;
      !!SN:T^es.788.skillMaster^/?(spec:z);
      !!HE(hero)&(skill)=(SKILL_FIRST_AID):A1/(ART_FIRST_AID_TENT)/15;
    !!en;
  !!el;                                                  // воин с существом
    ; Creature Specialist
    ; Question: What about the compatibility with Hero Specialty Boost (WoG)?
    !!HE(hero):X1/(subtype)/0/0/0/0/0;                   // специалист по существам
    !!HE(hero)&(subtype)=(MON_BALLISTA):A1/(ART_BALLISTA)/13;      [Give Ballista]

    !!UN:P740/?(enhCreaSpec:y);         [Enhanced Creature specialty (ES)]

    !!if&(enhCreaSpec);
      !!SN:T^es.740.spec^/?(spec);
    !!el;
      !!SN:T^es.788.creaSpec^/?(spec);
    !!en;

    ; Creature Trainer
    !!if&(random2)=1;                                                // специалисты по лучшению существ
      !!if&(subtype)=(MON_MONK);
        !!VR(mon2:y):S(MON_ZEALOT);
        !!VR(mon3:y):S(MON_WAR_ZEALOT);
      !!el&(subtype)=(MON_GREMLIN);
        !!VR(mon2):S(MON_MASTER_GREMLIN);
        !!VR(mon3):S(MON_SANTA_GREMLIN);
      !!el&(subtype)=(MON_STONE_GOLEM);
        !!VR(mon2):S(MON_IRON_GOLEM);
        !!VR(mon3):S(MON_GOLD_GOLEM);
      !!el&(subtype)=(MON_WALKING_DEAD);
        !!VR(mon2):S(MON_ZOMBIE);
        !!VR(mon3):S(MON_MUMMY);
      ; Scholar of Death
      !!el&(subtype)=(MON_CAVALIER);
        !!VR(mon2):S(MON_CHAMPION);
        !!VR(mon3):S(MON_DREAD_KNIGHT);
      !!el&(subtype)=(MON_UNICORN);
        !!VR(mon2):S(MON_WAR_UNICORN);
        !!VR(mon3):S(MON_NIGHTMARE);
      !!el&(subtype)=(MON_GREEN_DRAGON);
        !!VR(mon2):S(MON_GOLD_DRAGON);
        !!VR(mon3):S(MON_BLOOD_DRAGON);
      !!el&(subtype)=(MON_RED_DRAGON);
        !!VR(mon2):S(MON_BLACK_DRAGON);
        !!VR(mon3):S(MON_BLOOD_DRAGON);
      !!en;

      !!if|(subtype)=(MON_MONK)/(subtype)=(MON_GREMLIN)/(subtype)=(MON_STONE_GOLEM)/(subtype)=(MON_WALKING_DEAD)/(subtype)=(MON_CAVALIER)/(subtype)=(MON_UNICORN)/(subtype)=(MON_GREEN_DRAGON)/(subtype)=(MON_RED_DRAGON);
        !!HE(hero):X6/(subtype)/0/0/0/(mon2)/(mon3);
        !!SN:H^monname^/(subtype)/1/?(mon1Name:z) H^monname^/(mon2)/1/?(mon2Name:z) H^monname^/(mon3)/1/?(mon3Name:z);

        !!if|(subtype)=(MON_MONK)/(subtype)=(MON_GREMLIN)/(subtype)=(MON_STONE_GOLEM)/(subtype)=(MON_WALKING_DEAD);
          !!SN:T^es.788.creaTrainer^/?(spec)/^mon1^/(mon1Name)/^mon2^/(mon2Name)/^upg^/(mon3Name);
        !!el;
          !!SN:T^es.788.scholarOfDeath^/?(spec)/^mon1^/(mon1Name)/^mon2^/(mon2Name)/^upg^/(mon3Name);
        !!en;
      !!en;
    !!en; 
  !!en;

; Adventurers
!!el&(heroType)=0;
  ; Skill Master (Adventurer)
  !!if&(random1)=0;                                      // странник со вторичным навыком
    !!VR(skillsList):C(SKILL_LOGISTICS)/(SKILL_NAVIGATION)/(SKILL_EAGLE_EYE)/(SKILL_ESTATES);
    !!VR(skillsList[1])&i^es_773_landMode^:S(SKILL_LOGISTICS);
    !!VR(skill):S(skillsList[subtype]);

    ; Check if the skill is banned
    !!FU(ES_GameMgr_Skill_isBanned):P(skill)/?(isNotValid:y);

    !!if&(isNotValid)=(FALSE);
      !!HE(hero):X0/(skill)/0/0/0/0/0;
      !!SN:T^es.788.skillMaster^/?(spec:z);
    !!en;
  ; Supplier
  !!el;                                                  // странник с ресурсом
    !!HE(hero):X2/(subtype)/0/0/0/0/0;                   // специалист по ресурсам

    !!if&(subtype)<>(RES_GOLD);
      !!SN:T^es.788.supplier0^/?(spec);
    !!el;
      !!SN:T^es.788.supplier1^/?(spec);
    !!en;
  !!en;

; Mages
!!el&(heroType)=1;
  ; Skill Master (Mage)
  !!if&(random1)=0;                                      // маг со вторичным навыком
    !!VR(skillsList):C(SKILL_MYSTICISM)/(SKILL_INTELLIGENCE)/(SKILL_SORCERY)/(SKILL_RESISTANCE);
    !!VR(skill):S(skillsList[subtype]);

    ; Check if the skill is banned
    !!FU(ES_GameMgr_Skill_isBanned):P(skill)/?(isNotValid:y);

    !!if&(isNotValid)=(FALSE);
      !!HE(hero):X0/(skill)/0/0/0/0/0;
      !!SN:T^es.788.skillMaster^/?(spec:z);
    !!en;

  !!el;                                               // маг с заклинанием
    ; Aura of Curse
    ; Note: Aura of Curse specialists are also spell specialists
    !!if&(random2)=1/(subtype)>=7/(subtype)<=10;      // специалисты по аурам-проклятиям
      !#VA(spellsList[11]:y);

      !!VR(spellsList):C45/47/42/54;
      !!VR(curseType:y):S(subtype) -7;
      !!VR(spell:y):S(spellsList[curseType]);

      !!UN:J0/(spell)/?(isNotValid);

      !!if&(isNotValid)=(FALSE);
        !!FU10932:P(spell)/?(isNotValid); [WoG legacy. same function as FU(WOG_CHECK_SPELL_BAN)]

        !!if&(isNotValid)=(FALSE);
          !!HE(hero):X3/(spell)/0/0/0/0/0;
          !!HE(hero):M(spell)/(TRUE);
          !!SN:T^es.788.auraOfCurse%(curseType)^/?(spec);
        !!en;
      !!en;
    ; Battlemage
    !!el;
      ; Check if the spell is disabled (map editor) or banned (wog options)
      ; Note that here we use the same variable name (isNotValid) for the convenience of scripting
      !#VA(spellsList[11]:y);

      !!VR(spellsList):C15/16/17/20/21/23/22/18/19/26/25;
      !!VR(spellsList[10])&(faction)=(TOWN_NECROPOLIS):S24;
      !!VR(spell):S(spellsList[subtype]);

      !!UN:J0/(spell)/?(isNotValid);

      !!if&(isNotValid)=(FALSE);
        !!FU10932:P(spell)/?(isNotValid); [WoG legacy. same function as FU(WOG_CHECK_SPELL_BAN)]

        !!if&(isNotValid)=(FALSE);
          !!HE(hero):X3/(spell)/0/0/0/0/0;
          !!HE(hero):M(spell)/(TRUE);
          !!SN:T^es.788.battlemage^/?(spec);
        !!en;
      !!en;
    !!en;
  !!en;
!!en;

; Set up specailty text if the specialty is valid
!!if&(isNotValid)=(FALSE);
  !!SN:H^spec^/(hero)/2/^%(spec)%T(es.788.reborn)^;
; Reroll if the specialty is not valid
!!el;
  !!FU(ES_788_Rebirth):P(hero);
!!en;

!?FU(ES_788_SetUpAuraOfCurseSpec);
!#VA(hero:x) (subtype:x) (specInd:x);

!!HE(hero)&(subtype)=7:X3/45/0/0/0/0/0;         // Слабость
!!HE(hero)&(subtype)=8:X3/47/0/0/0/0/0;         // Разрушение
!!HE(hero)&(subtype)=9:X3/42/0/0/0/0/0;         // Проклятье
!!HE(hero)&(subtype)=10:X3/54/0/0/0/0/0;        // Медлительность

!!VR(curseType:y):S(subtype) -6;
!!SN:T^es.788.auraOfCurse%(curseType)^/?(spec:z);
!!VR(specInd):Z(spec);


** Подержка специализаций на заклинаниях перерожденных героев
!?FU(OnMagicCorrectedResistance);       // при нанесении урона заклинанием
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!BG:A?y1;
!!FU&y1<>1:E;                           // выход, если не каст героя

!!BG:H?y1;
!!FU&i^es_788_rebornFlags_%y1^=0:E;                            // выход, если герой-кастер не перерожденный

!!HEy1:X?i/?j/d/d/d/d/d;
!!FU&i<>3:E;                            // выход, если герой не спец по заклинанию

!!MR:S?y2;
!!FU&y2<>j:E;                           // выход, если каст непрофильного заклинания

!!MR:F?y2;
!!HEy1:E?k/?l/1;                        // y2/l - наносимый урон/уровень героя
!!VRy3:Sy2 *l *5 :100 +y2;
!!MR:Fy3;                               // установка бонуса к урону в размере 5% за уровень героя

** Подержка аур-проклятий перерожденных героев
!?FU(OnBeforeBattleUniversal);
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!SN:W^opt788.a1^/0 W^opt788.a2^/0 W^opt788.a3^/0 W^opt788.a4^/0; // сброс флагов аур-проклятий

; Aura of Curse - Must be executed later than anything else to have a wider range of effect
!?FU(OnBattleRound_Quit)&i^battle_round^=0;
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!BA:H0/?y1 H1/?y2;                     // y1/y2 - номера сражающихся героев
!!VRy11:C0/0/0/0;                       // y11..y14 - действующие ауры
!!HEy1:X?y3/?y4/d/d/d/d/d;              // y3/y4 - специализация атакующего героя

!!if&y3=3/i^es_788_rebornFlags_%y1^<>0;              [флаг перерождения атакующего героя]
  !!VRy11&y4=45:S1;                // y11 - активация ауры Слабости
  !!VRy12&y4=47:S1;                // y11 - активация ауры Разрушения
  !!VRy13&y4=42:S1;                // y11 - активация ауры Проклятья
  !!VRy14&y4=54:S1;                // y11 - активация ауры Медлительности
!!en;

!!if&y2>=0;                             // если есть защищающийся герой
  !!HEy2:X?y3/?y4/d/d/d/d/d;            // y3/y4 - специализация защищающегося героя

  !!if&y3=3/i^es_788_rebornFlags_%y2^<>0;            [флаг перерождения защищающегося героя]
    !!VRy11&y4=45:S1;              // y11 - активация ауры Слабости
    !!VRy12&y4=47:S1;              // y11 - активация ауры Разрушения
    !!VRy13&y4=42:S1;              // y11 - активация ауры Проклятия
    !!VRy14&y4=54:S1;              // y11 - активация ауры Медлительности
  !!en;
!!en;                                   // 

!!FU&y11=0/y12=0/y13=0/y14=0:E;         // выход, если ни одна аура-проклятье не действует

!!if&i^battle_isVisible^;
  !!FU(Fun_SpellAnimationOnStacks)&y11=1:P-1/?y21; // y21 = слот для массовой анимации Слабости
  !!FU(Fun_SpellAnimationOnStacks)&y12=1:P-1/?y22; // y22 = слот для массовой анимации Разрушения
  !!FU(Fun_SpellAnimationOnStacks)&y13=1:P-1/?y23; // y23 = слот для массовой анимации Проклятия
  !!FU(Fun_SpellAnimationOnStacks)&y14=1:P-1/?y24; // y24 = слот для массовой анимации Медлительности
!!en;

!!re i/0/41;                            // для всех отрядов Защищающегося
  !!BMi:N?y3;
  !!co&y3<1;                            // пропуск пустых отрядов

  !!BMi:A?y3 D?y4 U1/?y5 U2/?y6 S?y7;   // y3/y4/y5/y6/y7 - атака/защита/мин.урон/макс.урон/скорость отряда
  !!VRy3&y11=1::2;                      // снижение атаки для Слабости
  !!VRy4&y12=1::2;                      // снижение защиты для Разрушения
  !!VRy6&y13=1:Sy5;                     // макс. урон снижается до мин.урона для Проклятия
  !!VRy7&y14=1/y7>1::2;                 // снижение скорости для Медлительности, если скорость больше 1

  !!if&i^battle_isVisible^;
    !!FU(Fun_SpellAnimationOnStacks)&y11=1:Pi/y21; // добавление отряда в массовую анимацию Слабости
    !!FU(Fun_SpellAnimationOnStacks)&y12=1:Pi/y22; // добавление отряда в массовую анимацию Разрушения
    !!FU(Fun_SpellAnimationOnStacks)&y13=1:Pi/y23; // добавление отряда в массовую анимацию Проклятия
    !!FU(Fun_SpellAnimationOnStacks)&y14=1:Pi/y24; // добавление отряда в массовую анимацию Медлительности
  !!en;

  !!BMi:Ay3 Dy4 U2/y6 Sy7;              // обновляем параметры отряда
!!en;                                   // 

!!if&f=0;
  !!SN&y11=1:W^opt788.a1^/y21;        // выставление флага анимации Слабости
  !!SN&y12=1:W^opt788.a2^/y22;        // выставление флага анимации Разрушения
  !!SN&y13=1:W^opt788.a3^/y23;        // выставление флага анимации Проклятия
  !!SN&y14=1:W^opt788.a4^/y24;        // выставление флага анимации Медлительности
!!en;

; Play animations
!!if&i^battle_isVisible^;
  !!SN:W^opt788.a1^/?y1;                  // y1 - аура Слабости

  !!if&y1<>0;                             // если аура есть
    !!SN:T^es.788.battleLog^/?z1;
    !!MM:Sz1;                             // вывод текста в лог битвы
    !!SN:P^WEAKNESS^;                     // вывод звука слабости
    !!FU(Fun_SpellAnimationOnStacks):Py1/56/0/1; // проигрывание массовой амимации "Слабость" без анимации урона с очисткой памяти
  !!en;                                   // 

  !!SN:W^opt788.a2^/?y1;                  // y1 - аура Разрушения

  !!if&y1<>0;                             // если аура есть
    !!SN:T^es.788.battleLog^/?z1;
    !!MM:Sz1;                             // вывод текста в лог битвы
    !!SN:P^DISRUPTR^;                     // вывод звука разрушения
    !!FU(Fun_SpellAnimationOnStacks):Py1/14/0/1; // проигрывание массовой амимации "Разрушение" без анимации урона с очисткой памяти
  !!en;                                   // 

  !!SN:W^opt788.a3^/?y1;                  // y1 - аура Проклятия

  !!if&y1<>0;                             // если аура есть
    !!SN:T^es.788.battleLog^/?z1;
    !!MM:Sz1;                             // вывод текста в лог битвы
    !!SN:P^CURSE^;                        // вывод звука проклятия
    !!FU(Fun_SpellAnimationOnStacks):Py1/40/0/1; // проигрывание массовой амимации "Проклятие" без анимации урона с очисткой памяти
  !!en;                                   // 

  !!SN:W^opt788.a4^/?y1;                  // y1 - аура Медлительности

  !!if&y1<>0;                             // если аура есть
    !!SN:T^es.788.battleLog^/?z1;
    !!MM:Sz1;                             // вывод текста в лог битвы
    !!SN:P^MUCKMIRE^;                     // вывод звука медлительности
    !!FU(Fun_SpellAnimationOnStacks):Py1/19/0/1; // проигрывание массовой амимации "Медлительность" без анимации урона с очисткой памяти
  !!en;                                   // 
!!en;

; Set up new specialty campaign carry over for reborn heroes
!?FU(OnWinGame);
!!UN:P788/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!SN:F^IsCampaign^;
!!FU&v1<>(TRUE):E;

!!VR(filePath:z):S^Runtime/mortal heroes.ini^;

!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!if&i^es_788_rebornFlags_%i^<>0;
    !!VR(packedUnion:y):Si^es_788_rebornFlags_%i^;
    !!FU(WriteIniInts):P(filePath)/^rebornHero^/^%i^/(packedUnion);
  !!en;
!!en;

!!FU(SaveIni):P(filePath);

!?FU(OnTransferHero);
!#VA(hero:x);

!!UN:P788/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VR(filePath:z):S^Runtime/mortal heroes.ini^;
!!FU(ReadIniInts):P(filePath)/^rebornHero^/^%(hero)^/?i^es_788_rebornFlags_%(hero)^/0;

!!if&i^es_788_rebornFlags_%(hero)^<>0;
  !!FU(UnpackUnion):Pi^es_788_rebornFlags_%(hero)^/?(heroType:y)/2/?(random1:y)/1/?(random2:y)/1/?(subtype:y)/12/?(dummy:y)/16;
  !!VR(heroType):-1;                    [correct hero type from unpacked value]
  !!FU(ES_788_SetUpNewSpecialty):P(hero)/(heroType)/(random1)/(random2)/(subtype);
!!en;

; Remove ini OnAfterErmInstructions as there is no OnTransferHero on the victory of the the last zone of a campaign
!?FU(OnAfterErmInstructions);
!!VR(filePath:z):S^Runtime/mortal heroes.ini^;
!!FU(FileExists):P(filePath)/?(result:y);

!!if&(result);
  !!FU(ClearIniCache):P(filePath);
  !!FU(DeleteFile):P(filePath)/?(result);
!!en;

** end

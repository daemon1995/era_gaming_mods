ZVSE2
*** Автор         : Algor
*** Functions     : FU7982-FU7984, FU7890-FU7891, FU7988-FU7989


TEMPORARY REMOVED by Archer30 as it's been reported buggy, stopping eligible casts
*** Отмена заклинания Берсерк, если в область каста не попадают восприимчивые отряды

*?BG0;                                 [действие в бою]
*!BG:A?y1 S?y2 Q?y3 D?y4;              [y1-y4 - параметры действия]
*!FU|y1<>1/y2<>59:E;                   [выход, если не каст Берсерка]
*!VRy3:+1 *-10;                        [y3 (-10/-20) номер героя]
*!HEy3:S14/?y5;                        [y5 - уровень Школы Огня героя]
*!BA:P?y6/?y7/?y8;                     [y6-y8 - координаты битвы]
*!TRy6/y7/y8:G?y9;                     [y9 - тип бонусной земли]
*!VRy5|y9=226/y9=46:S3;                [макс. уровень Школы Огня на Огненных полях или Полянах магов]
*!VRy10:Sy4 %17;                       [y10 (0-16) - x-координата поля]
*!VRy11:Sy4 :17;                       [y11 (0-10) - y-координата поля]
*!VRy12:Sy11 +1 %2;                    [y12 (0-1) - смещение +1 для эпицентра в четном ряду]
*!SS59:L?y13;                          [y13 - уровень заклинания Берсерк]
*!HE-10:A2/(ART_PENDANT_OF_DISPASSION)/d/?y14 A2/(ART_ORB_OF_VULNERABILITY)/d/?y16;    [y14>0/y16>0 если на атакующем Брелок Бесстрастия/Медаль Уязвимости]
*!HE-10&y14=0:A2/(ART_BADGE_OF_COURAGE)/d/?y14;    [y14>0/y16>0 если на атакующем Брелок Бесстрастия/Медаль Уязвимости]

*!BA:H1/?y18;                          [y18 - номер защищающегося героя]
*!VRy15:S0;                            [обнуление переменных]
*!VRy17:S0;                            [...]
*!HE-20&y18>=0:A2/100/d/?y15 A2/93/d/?y17;  [y15>0/y17>0 если на защищающемся Брелок Бесстрастия/Медаль Уязвимости]
*!HE-20&y18>=0/y15=0:A2/49/d/?y15;  [y15>0/y17>0 если на защищающемся Брелок Бесстрастия/Медаль Уязвимости]


*!VRy16&y17>0:S1;                      [y16>0, если действует Медаль Уязвимости]
** Проверка позиций на наличие восприимчивых отрядов
*!FU7988&y5>2/y10>1/y11>1:Py5/y10/y11/0/-1/-2/y13/y14/y15/y16/?y5;    [ряд -2]
*!FU7988&y5>2/y11>1:Py5/y10/y11/0/0/-2/y13/y14/y15/y16/?y5;           [...]
*!FU7988&y5>2/y10<15/y11>1:Py5/y10/y11/0/1/-2/y13/y14/y15/y16/?y5;    [...]
*!FU7988&y5>2/y10>2/y11>0:Py5/y10/y11/y12/-2/-1/y13/y14/y15/y16/?y5;  [ряд -1]
*!FU7988&y5>1/y10>1/y11>0:Py5/y10/y11/y12/-1/-1/y13/y14/y15/y16/?y5;  [...]
*!FU7988&y5>1/y11>0:Py5/y10/y11/y12/0/-1/y13/y14/y15/y16/?y5;         [...]
*!FU7988&y5>2/y10<14/y11>0:Py5/y10/y11/y12/1/-1/y13/y14/y15/y16/?y5;  [...]
*!FU7988&y5>2/y10>2:Py5/y10/y11/0/-2/0/y13/y14/y15/y16/?y5;           [ряд 0]
*!FU7988&y5>1/y10>1:Py5/y10/y11/0/-1/0/y13/y14/y15/y16/?y5;           [...]
*!FU7988&y5>-1:Py5/y10/y11/0/0/0/y13/y14/y15/y16/?y5;                 [...]
*!FU7988&y5>1/y10<15:Py5/y10/y11/0/1/0/y13/y14/y15/y16/?y5;           [...]
*!FU7988&y5>2/y10<14:Py5/y10/y11/0/2/0/y13/y14/y15/y16/?y5;           [...]
*!FU7988&y5>2/y10>2/y11<10:Py5/y10/y11/y12/-2/1/y13/y14/y15/y16/?y5;  [ряд +1]
*!FU7988&y5>1/y10>1/y11<10:Py5/y10/y11/y12/-1/1/y13/y14/y15/y16/?y5;  [...]
*!FU7988&y5>1/y11<10:Py5/y10/y11/y12/0/1/y13/y14/y15/y16/?y5;         [...]
*!FU7988&y5>2/y10<14/y11<10:Py5/y10/y11/y12/1/1/y13/y14/y15/y16/?y5;  [...]
*!FU7988&y5>2/y10>1/y11<9:Py5/y10/y11/0/-1/2/y13/y14/y15/y16/?y5;     [ряд +2]
*!FU7988&y5>2/y11<9:Py5/y10/y11/0/0/2/y13/y14/y15/y16/?y5;            [...]
*!FU7988&y5>2/y10<15/y11<9:Py5/y10/y11/0/1/2/y13/y14/y15/y16/?y5;     [...]
*!FU(Fun_RedrawShadowAfterAction)&y5>=0:P; *!BG&y5>=0:A0;             [отмена действия с перерисовкой тени, если в области каста не найдены восприимчивые отряды]

*?FU7988;                              [возврат в x11=-1, если в гексе x2+x4+x5/x3+x6 Берсерк-восприимчивый отряд]
** x7 - уровень Берсерка, x8/x9 - Брелок Бесстрастия у атакующего/защищающегося, x10 - действие Медали Уязвимости
*!VRx11:Sx1;
*!VRy1:Sx3 +x6 *17 +x2 +x4 +x5;        [y1 - проверяемая позиция]
*!BU:Ey1/?y2;                          [y2 - номер отряда в позиции]
*!FU&y2<0:E;                           [выход, если отряда нет]
*!BMy2:F?y3 M34/?y4/?y5 T?y7;          [y3 - флаги отряда, y4/y5 - длительность/сила антимагии, y7 - тип существ]
** магищеские защиты/артефакты/БМ
*!FU&y2<21/x8>0:E;                     [выход, если на отряд действует Брелок Бесстрастия]
*!FU&y2>20/x9>0:E;                     [...]
*!if&y4>0;                             [Проверка Антимагии]
  *!SS34:Ey5/?y6;                      [y6 - мин. действующий уровень магии на отряд под Антимагией]
  *!FU&x7<y6:E;                        [выход, если Антимагия блокирует Берсерка]
*!en; 
*!VRy8:Sy3 &64;                        [y8>0, если отряд - БМ]
*!FU&y8>0:E;                           [выход, если отряд - БМ]
** приобретенные с опытом невосприимчивости
*!VRy9:Sy2 +1 *-1;                     [y9 - номер отряда для EA-ресивера]
*!EAy9:E?y10/2/d/d;                    [y10 - опыт отряда]
*!SN:E7503648/1/y7/y10;                [v1 - ранг отряда (0-10), спс. gamecreator'у (0x727F20: int cdecl GetRank(int cr,int exp))]
*!VRy12:Sv1 +1;                        [y12 - ранг отряда (1-11)]
*!DO7989/6/19/1&y13=0:P?y13/y9/y12/102/73;        [Иммун к Огню]
*!DO7989/6/19/1&y13=0:P?y13/y9/y12/102/80;        [Иммун к Разуму]
*!DO7989/6/19/1&y13=0/x7<2:P?y13/y9/y12/119/54;   [Иммун к уровням заклианний (все)]
*!DO7989/6/19/1&y13=0/x7<3:P?y13/y9/y12/119/55;   [...]
*!DO7989/6/19/1&y13=0/x7<4:P?y13/y9/y12/119/56;   [...]
*!DO7989/6/19/1&y13=0/x7<5:P?y13/y9/y12/119/57;   [...]
*!DO7989/6/19/1&y13=0/x7<6:P?y13/y9/y12/119/58;   [...]
*!DO7989/6/19/1&y13=0:P?y13/y9/y12/119/79;        [Иммун к огненным заклинаниям]
*!DO7989/6/19/1&y13=0:P?y13/y9/y12/119/75;        [Иммун к Берсерку]
*!BG&y13=0:Q?y14;                                 [y14 - ходящая сторона]
*!BMy2&y13=0:I?y15;                               [y15 - принадлежность отряда]
*!if&y14<>y15/y13=0;                              [проверка на иммуны к враждебным заклинаниям]
  *!DO7989/6/19/1&y13=0:P?y13/y9/y12/119/70;      [Иммун к враждебным заклинаниям Огня]
  *!DO7989/6/19/1&y13=0:P?y13/y9/y12/119/109;     [Иммун к враждебным заклинаниям Разума]
  *!DO7989/6/19/1&y13=0/x7<2:P?y13/y9/y12/119/49; [Иммун к уровням заклианний (враждебные)]
  *!DO7989/6/19/1&y13=0/x7<3:P?y13/y9/y12/119/50; [...]
  *!DO7989/6/19/1&y13=0/x7<4:P?y13/y9/y12/119/51; [...]
  *!DO7989/6/19/1&y13=0/x7<5:P?y13/y9/y12/119/52; [...]
  *!DO7989/6/19/1&y13=0/x7<6:P?y13/y9/y12/119/53; [...]
*!en; 
*!FU&y13>0:E;                          [выход, если есть приобретенная с опытом невосприимчивость]
** Медаль уязвимости
*!VRx11&x10>0:S-1;                     [прерывание проверок и возврат -1 в x11, если действует Медаль Уязвимости]
*!FU&x10>0:E;                          [...]
** естественные невосприимчивости
*!VRy8:Sy3 &16;                        [y8>0, если отряд - живое существо]
*!FU&y8=0:E;                           [выход, если отряд не "живой"]
*!VRy8:Sy3 &1024;                      [y8>0, если отряд невосприимчив к заклинаниям "разума"]
*!FU&y8>0:E;                           [выход, если отряд невосприимчив к "магии разума"]
*!VRy8:Sy3 &16384;                     [y8>0, если отряд невосприимчив к заклинаниям Огня]
*!FU&y8>0:E;                           [выход, если отряд невосприимчив к заклинаниям Огня]
*!FU&x7<4/y7=26:E;                     [Зел.Дракон]
*!FU&x7<5/y7=27:E;                     [Зол.Дракон]
*!FU&x7<4/y7=82:E;                     [Кр.Дракон]
*!FU&x7<6/y7=83:E;                     [Черн.Дракон]
*!FU&x7<6/y7=155:E;                    [Темн.Дракон]
*!FU&x7<4/y7=132:E;                    [Лаз.Дракон]
*!FU|y7=52/y7=53/y7=130/y7=131/y7=158:E; [Ифриты/Фениксы]
*!VRx11:S-1;                           [возврат -1 в x11, если отряд восприимчив]

*?FU7989;                              [возврат x1>0, если для существа x2 ранга x3 действует бонус опыта x4/x5]
*!VRx1:S0;
*!EAx2:Bx16/?y12/?y13/?y14/?y1/?y2/?y3/?y4/?y5/?y6/?y7/?y8/?y9/?y10/?y11; [y1-y14 - параметры линии опыта]
*!VRx1&y12=1/y13=x4/y14=x5:Syx3;
*!VRx16&x1>0:S20;

*** Фикс бага Забывчивости, оригинальная действует на все отряды уже на Продвинутом уровне.

!#SS(SPELL_FORGETFULNESS):F5221;

*** Фикс боевых машин
*** исправляет вылет игры при лечении Палаткой Первой Помощи
*** отменяет автоматический пропуск хода при попытке воздействовать Палаткой Первой Помощи на вражеский отряд
*** запрещает стрельбу баллисты/катапульты с нулевым боезапасом
*** автоматически пропускает ход баллисты/катапульты с нулевым боезапасом



*** Фикс блокирования командира
*** исправляет баг WoG при котором защита командира при срабатывании его способности блокировать урон
*** могла понижаться из-за некорректного выставления флага 134217728 "отряд в защитном положении"

!?BA52;                               [перед началом битвы]
!!SN:W^algor_cbf_a^/0 W^algor_cbf_d^/0;[обнуляем переменные состояний командиров]

!?BG0;                                [перед действием в битве]
!!BG:N?y1 A?y2;                       [y1 - номер ходящего отряда, y2 - действие]
!!BMy1:T?y3;                          [y3 - тип ходящего отряда]
!!FU|y3<174/y3>191:E;                 [выход, если ходит не командир]
!!SN&y3>=174/y3<=182:W^algor_cbf_a^/y2;[сохраняем последнее действие атакующего командира]
!!SN&y3>=183/y3<=191:W^algor_cbf_d^/y2;[сохраняем последнее действие защищающегося командира]

!?FU(OnBattleRegeneratePhase);        [фаза регенерации]
!!SN:X?y1;                            [y1 - номер отряда]
!!BMy1:T?y3 F?y2;                     [y3/y2 - тип/флаги отряда]
!!FU|y3<174/y3>191:E;                 [выход, если ходит не командир]
!!VRy4:Sy2 &134217728;                [y4>0, если у командира выставлен флаг "отряд в защитном положении"]
!!FU&y4=0:E;                          [выход, если флаг не выставлен]
!!SN:W^algor_cbf_a^/?y5 W^algor_cbf_d^/?y6;[y5/y6 - последние действия командиров]
!!VRy2&y3>=174/y3<=182/y5<>3:-134217728;[снимаем флаг с атакующего командира, если он не должен быть выставлен]
!!VRy2&y3>=183/y3<=191/y6<>3:-134217728;[снимаем флаг с защищающегося командира, если он не должен быть выставлен]
!!BMy1:Fy2;                           [обновляем флаги командира]

*** Фикс стрельбы командира
*** исправляет баг, когда командир получая навык стрельбы терял способность дважды атаковать в ближнем бою

!?BA52;                               [перед началом битвы]
!!SN:W^algor_csf_stack^/-1 W^algor_csf_cmndr^/-1; [сброс переменных]

!?BA53;                               [после окончания битвы]
!!SN:W^algor_csf_stack^/?y1 W^algor_csf_cmndr^/?y2; [y1/y2 - номер отряда/командира с удаленной стрельбой]
!!COy2&y1>-1/y2>-1:B1/4/1;            [восстановление навыка стрельбы у командира]
!!SN:W^algor_csf_stack^/-1 W^algor_csf_cmndr^/-1; [сброс переменных]

!?BG1;
!!SN:W^algor_csf_stack^/?y1 W^algor_csf_cmndr^/?y2; [y1/y2 - номер отряда/командира с удаленной стрельбой]
!!if&y1>-1/y2<>-1; 
  !!COy2:B1/4/1;                      [восстановление навыка стрельбы у командира]
  !!BMy1:F?y3;                        [...и у отряда]
  !!VRy3:|4;                          [...]
  !!BMy1:Fy3;                         [...]
  !!SN:W^algor_csf_stack^/-1 W^algor_csf_cmndr^/-1; [сброс переменных]
!!en; 
!!BG:N?y2;
!!VRy1:Sy2 :21;                       [y1 - сторона (0/1)]
!!VRy3:S-1;                           [тип отряда по умолчанию]
!!BMy2&y2>=0:T?y3 P?y4;               [y3/y4 - тип/позиция активного отряда]
!!FU|y3<174/y3>191:E;                 [выход, если не командир]
!!VRy9:S0;                            [y9 - соседние отряды атакующего]
!!VRy10:S0;                           [y10 - соседние отряды защищающегося]
!!VRy5:Sy4 :17;                       [y5 - линия]
!!VRy6:Sy5 %2;                        [y6 - четность линии]
!!VRy7:Sy4 %17;                       [y7 - позиция в линии]
!!if&y5>0/y7>y6;                      [1я соседняя позиция]
  !!VRy8:Sy4 -17 -y6;                 [y8 - номер позиции]
  !!BU:Ey8/?y11;                      [y11 - номер отряда в позиции]
  !!VRy9&y11>=0/y11<=20:+1;           [счетчик атакующих]
  !!VRy10&y11>=21:+1;                 [счетчик защищающихся]
!!en; 
!!if|y7<15/y6=1;                      [2я соседняя позиция]
!!if&y5>0;                            [...]
  !!VRy8:Sy4 -16 -y6;                 [y8 - номер позиции]
  !!BU:Ey8/?y11;                      [y11 - номер отряда в позиции]
  !!VRy9&y11>=0/y11<=20:+1;           [счетчик атакующих]
  !!VRy10&y11>=21:+1;                 [счетчик защищающихся]
!!en; 
!!en; 
!!if&y7<15;                           [3я соседняя позиция]
  !!VRy8:Sy4 +1;                      [y8 - номер позиции]
  !!BU:Ey8/?y11;                      [y11 - номер отряда в позиции]
  !!VRy9&y11>=0/y11<=20:+1;           [счетчик атакующих]
  !!VRy10&y11>=21:+1;                 [счетчик защищающихся]
!!en; 
!!if|y7<15/y6=1;                      [4я соседняя позиция]
!!if&y5<10;                           [...]
  !!VRy8:Sy4 +18 -y6;                 [y8 - номер позиции]
  !!BU:Ey8/?y11;                      [y11 - номер отряда в позиции]
  !!VRy9&y11>=0/y11<=20:+1;           [счетчик атакующих]
  !!VRy10&y11>=21:+1;                 [счетчик защищающихся]
!!en; 
!!en; 
!!if&y5<10/y7>y6;                     [5я соседняя позиция]
  !!VRy8:Sy4 +17 -y6;                 [y8 - номер позиции]
  !!BU:Ey8/?y11;                      [y11 - номер отряда в позиции]
  !!VRy9&y11>=0/y11<=20:+1;           [счетчик атакующих]
  !!VRy10&y11>=21:+1;                 [счетчик защищающихся]
!!en; 
!!if&y7>1;                            [6я соседняя позиция]
  !!VRy8:Sy4 -1;                      [y8 - номер позиции]
  !!BU:Ey8/?y11;                      [y11 - номер отряда в позиции]
  !!VRy9&y11>=0/y11<=20:+1;           [счетчик атакующих]
  !!VRy10&y11>=21:+1;                 [счетчик защищающихся]
!!en; 
!!BA:Hy1/?y12;                        [y12 - номер командира]
!!VRy12&y12<0:Sy1 +3 *-1;             [...]
!!VRy15:S0;                           [y15 = 0/1 если рядом нет/есть враги]
!!VRy15&y1=0/y10>0:S1;                [...]
!!VRy15&y1=1/y9>0:S1;                 [...]
!!HEy12&y12>=0:A2/137/?i/?y17;        [y17 - наличие экипированного Лука Снайпера у героя]
!!VRy15&y12>=0/y17>0:S0;              [при наличии Лука Снайпера у героя считаем, что врагов рядом нет]
!!COy12:B1/4/?y13;                    [y13 - значение навыка стрельбы у командира]
!!BMy2:U3/?y16;                       [y16 - количество выстрелов у командира]
!!VRy15&y16<1:S1;                     [если выстрелов не осталось, выставляем признак врагов рядом]
!!if&y15=1/y13=1;                     [если рядом есть враги и командир может стрелять]
  !!SN:W^algor_csf_stack^/y2 W^algor_csf_cmndr^/y12; [сохраняем значение навыка стрельбы]
  !!COy12:B1/4/0;                     [убираем навык стрельбы]
  !!BMy2:F?y14;                       [y14 - текущие флаги отряда]
  !!VRy14:|4 -4;                      [удаление флага стрельбы]
  !!BMy2:Fy14;                        [обновление флагов]
!!en; 

*** Фикс демонологии
*** исправляет баг с некорректным воскрешением босле боя поднятых в этом бою демонов при использовании опции "Первая помощь I"

!?BG0;                                [перед действием]
!!BG:A?y1 E?y2 N?y3 S?y4;             [y1 - тип действия, y2 - целевой отряд, y3 - активный отряд]
!!FU&y1<>10:E;                        [выход если не каст отряда]
!!BMy3:T?y4;                          [y4 - тип монстров колдующего отряда]
!!FU&y4<>51:E;                        [выход, если колдуют не адские отродья]
!!SN:W^algor_df^/1;                   [установка признака поднятия демонов]

!?BG1;                                [после действия]
!!SN:W^algor_df^/?y1;                 [y1 - признак поднятия демонов]
!!FU&y1<1:E;                          [выход, если демоны не поднимались]
!!DO7982/0/41/1:P;                    [корректировка начальной численности отрядов]
!!SN:W^algor_df^/0;                   [сброс признака поднятия демонов]

!?FU7982;                             [корректировка начальной численности отряда x16]
!!BMx16:B?y1 O?y2 I?y3;               [y1 - начальное. количество существ в отряде, y2 - номер отряда в армии героя, y3 - принадлежность (0/1)]
!!FU&y2=-1:E;                         [выход, если отряд не состоит в армии героя]
!!VRy4:Sy3 *7 +y2 +7040;              [y4 - индекс v-переменной, с начальным значением численности отряда]
!!VRvy4:Sy1;                          [установка начальной численности отряда для скрипта "Первая помощь I"]

*** Фикс оригинального бага SoD, когда летающий отряд теряет ход, если гекс,
*** на который летяющий отряд должен встать занят другим отрядом.
; Disable for now. The problem is likely gone. It should be fixed in a plugin anyway.

*?BG0;
*!BG:A?y1 S?y2;                         // y1/y2 - действие/клетка для атаки
*!FU(Fun_RedrawShadowAfterAction)&y1=6/y2=-1:P; // перерисовка тени после действия
*!BG&y1=6/y2=-1:A0;                     // отменить действие, если невозможно переместиться на позицию для рукопашной атаки

*?FU(Fun_RedrawShadowAfterAction);      // перерисовка тени перемещения после текущего действия в бою
; параметров нет
; Пример:
; !FU(Fun_RedrawShadowAfterAction):P; !BG:A0; // отменить текущее действие и перерисовать тень
*!SN&1000:W^fun.rsaa^/1;                // установка флага перерисовки для нетеоретической битвы

*?FU(OnAfterBattleAction);              // после действия в бою
*!SN:W^fun.rsaa^/?y1; *!FU&y1<>1:E;     // выход, если перерисовка тени не требуется
*!UN:C6919200/4/?y2; *!SN:E4797616/2/y2/0/1; *!BU:R; // перерисовка тени перемещения

*** Фикс бага генератора, когда при старте с несколькими городами
*** разных типов, стартовый герой помещается не в родной город
*** ТОЛЬКО ДЛЯ СЛУЧАЙНЫХ КАРТ

!#TM1:S1/999/1/255;                   [переопределение стандартного таймера]
!#SN:W^NativeTownFix^/0;

!?TM1;                                [...]
!!SN:W^NativeTownFix^/?y1 W^NativeTownFix^/1; [проверка на повторное выполнение]
!!UN:C6919480/4/?y10; !!VRy11:Sy10 +128980; !!UN:Cy11/1/?y11; [y11=114 для рандомной карты (каталог [r]andom_maps) thanks to feanor] 
!!DO7983/0/7/1&y1=0/y11=114:P;        [для каждого игрока]

!?FU7983;
!!OW:Ix16/?y1/?y2;                    [y1/y2 - ИИ статус/наличие игрока]
!!FU&y2=1:E;                          [выход, если игрока нет]
!!OW:Hx16/1/0;                        [v1 - количество героев]
!!FU&v1<>1:E;                         [выход, если героя нет или он не единственный]
!!OW:Hx16/1/1;                        [v1 - номер первого героя]
!!VRy1:Sv1;                           [y1 - номер первого героя]
!!HEy1:B2/?y2;                        [y2 - класс героя]
!!VRy2::2;                            [y2 - родной тип города]
!!OW:Wx16/?y3;                        [y3 - количество городов у игрока]
!!VRy3:-1;                            [номер последнего города]
!!DO7984/0/y3/1&y3>=0:Px16/y1/y2;     [поиск подходящего города]

!?FU7984;
!!OW:Wx1/x16/?y1;                     [y1 - номер города на карте]
!!CA0/y1&y1>-1:T?y4 P?y5/?y6/?y7;     [y4 - тип города, y5/y6/y7 - координаты]
!!FU|x3<>y4/y1<0:E;                   [выход, если город не родного типа]
!!HEx2:P?y15/?y16/?y17;               [y15-y17 - координаты героя]
!!HEx2|y5<>y15/y6<>y16/y7<>y17:Py5/y6/y7/0; [перемещение героя в город, если он не в нем]
!!VRx16:S100500;                      [прерываем цикл, если найден город родного типа]

*** Фикс бага связанного с большим количеством объектов
*** удаляет из WoG-массива настроек для объектов информацию о "подбираемых" объектах (ресурсы, костры и пр.) после их подбора

!$OB5;                                [Артефакт]
!!OB998:T?y1; !!FU&y1=5:E;            [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB6;                                [Ящик Пандоры]
!!OB998:T?y1; !!FU&y1=6:E;            [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB12;                               [Кострище]
!!OB998:T?y1; !!FU&y1=12:E;           [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB29;                               [Обломки]
!!OB998:T?y1; !!FU&y1=29:E;           [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB79;                               [Ресурс]
!!OB998:T?y1; !!FU&y1=79:E;           [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB81;                               [Ученый]
!!OB998:T?y1; !!FU&y1=81:E;           [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB82;                               [Морской сундук]
!!OB998:T?y1; !!FU&y1=82:E;           [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB86;                               [Потерпевший кораблекрушение]
!!OB998:T?y1; !!FU&y1=86:E;           [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB93;                               [Свиток с заклинанием]
!!OB998:T?y1; !!FU&y1=93:E;           [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

!$OB101;                              [Сундук с сокровищами]
!!OB998:T?y1; !!FU&y1=101:E;          [выход, если объект не подобран]
!!UN:Ov998/v999/v1000/1;              [удаление объекта]

*** При просмотре героя в таверне, открывается не схлопывающееся окно, в котором можно посмотреть параметры командира, заклинания в книге и т.п.

!?FU(OnStartOrLoad);
!!UN:C6127990/1/0; [спс igrik'у]

; Prevent moving artifacts from neutral heroes in taverns by Archer30
!?FU(OnHeroScreenMouseClick)&i^mouse_action^=(MOUSE_LMB_PRESSED);
!!HE(CURRENT_HERO):O?(owner:y);
!!FU&(owner)>(NO_OWNER):E;

!!VR(flag:y):S(FALSE);
!!VR(flag)&i^mouse_item^>=2/i^mouse_item^<=17:S(TRUE);
!!VR(flag)&i^mouse_item^=20:S(TRUE);
!!VR(flag)&i^mouse_item^>=40/i^mouse_item^<=44:S(TRUE);
!!FU&(flag)<>(TRUE):E;

!!CM:R0;

** корректировка доступных для найма существ в городах и внешних жилищах в соответствии с обновленными приростами
!?FU(OnEveryDay)&i^timerOnce^/i^timerDay^=1;
!!UN:U98/-1/?y99; !!VRy99:-1;            [y99 - максимальный номер города на карте]
!!DO7891/0/y99/1&y99>0:P;                [перебор всех городов]
!!UN:U98/-1/?y99; !!VRy99:-1;            [y99 - максимальный номер жилища на карте]
!!UN:U17/-1/?y1; !!VRv10:S-1;            [y1 - количество жилищ на карте (тип 17), инициализация v10 для быстрого поиска]
!!DO7886/1/y1/1&y1>0:P17;                [перебор жилищ]
!!UN:U20/-1/?y1; !!VRv10:S-1;            [y1 - количество жилищ на карте (тип 20), инициализация v10 для быстрого поиска]
!!DO7886/1/y1/1&y1>0:P20;                [перебор жилищ]

!?FU7886;
!!UN:Ux1/-1/-1/10;                       [v10..v12 - координаты жилища]

!!re i/0/3;
  !!DW10:Mi/?(mon:y)/d;

  !!if&(mon)>(NO_MON);
    !!MA:G(mon)/?(growth:y);
    !!DW10:Mi/d/(growth);
  !!en;
!!en;

!?FU7891;
!!CA0/x16:T?y89;                         [y89 - тип города]
!!VRy88:S100;                            [y88 - бонус прироста от строений города]
!!CA0/x16:B3/9; !!VRy88&1:S200;          [Замок - 200% прирост]
!!CA0/x16:B3/8; !!VRy88&1/y88<=100:S150; [Цитадель - 150% прирост]
!!CA0/x16:B3/26; !!VRy88&1:*3 :2;        [Грааль - +50% прирост]
!!DO7890/0/6/1:Px16/y88/y89;             [корректировка доступных существ для каждого уровня]

!?FU7890;
!!CA0/x1:M1/x16/?y95/?y94;               [y95/y94 - количество доступных для найма существ (down/up)]
!!UN:Tx3/x16/0/?y93 Tx3/x16/1/?y92;      [y93/y92 - типы существ (down/up)]
!!MA:Gy93/?y91 Gy92/?y90;                [y90/y91 - базовые приросты существ (down/up)]
!!VRy91:*x2 :100; !!VRy90:*x2 :100;      [y90/y91 - приросты существ с учетом строений (down/up)]
!!CA0/x1&y95>0:M1/x16/y91/d;             [обновление количества существ, доступных для найма]
!!CA0/x1&y94>0:M1/x16/d/y90;             [...]

** фикс некорректной z-координаты битвы
!?BA52;                                  [фикс некорректной z-координаты битвы]
!!BA:P?y1/?y2/?y3;                       [y1-y3 - координаты битвы]
!!HE-1:P?y4/?y5/?y6;                     [y4-y6 - координаты героя]
!!BA&y3<>y6:Py1/y2/y6;                   [обновление z-координаты битвы, если она не совпадает с z-координатой героя]

** фикс вылета игры при ходе юнитов со свойством "абсолютный стрелок" под Берсерком
!?BG1;                           [после действия]
!!BG:N?y1;                       [y1 - номер следующего ходящего отряда]
!!BMy1:G59/?y3/?y4;              [y2/y3 - боезапас/длительность берсерка]
!!VRy5:Sy1 +1 *-1;               [y5 - номер стека для EA]
!!EAy5:F102/115/?y6;             [y6 - номер бонусной линии "стрельбы вплотную" или первой пустой линии]
!!EAy5:F0/?y7;                   [y7 - номер первой пустой линии]
!!if&y3>0/y6<>y7;                [если отряд под берсерком и имеет бонусную линию "стрельба вплотную"]
  !!EAy5:By6/0/d/d/d/d/d/d/d/d/d/d/d/d/d; [отключаем бонусную линию "стрельба вплотную", если такая есть]
  !!SN:W^fix.sniper^/y6;         [сохраняем номер линии]
!!en; 

!?BG0;                           [перед действием]
!!BG:N?y1;                       [номер действующего отряда]
!!VRy5:Sy1 +1 *-1;               [y5 - номер стека для EA]
!!SN:W^fix.sniper^/?y2 W^fix.sniper^/0;      [получаем и обнуляем сохраненный номер бонусной линии]
!!EAy5&y2>0:By2/1/d/d/d/d/d/d/d/d/d/d/d/d/d; [включаем бонусную линию, если она была отключена]

; центрируем текст названия города по вертикали в окне города (id 149) (c)igrik
*#UN:C6052891/1/4; 5- вообще по центру по вертикали и горизонтали - DOES NOT WORK!
*** end

ZVSE2
** Author orig.  : Algor [Idea: Ivor]
** Rewritten by  : Archer30
** Name          : Hi-level monsters
** Name rus.     : Высокоуровневые монстры
** Options       : 745

** Отряды существ на карте могут заменяться аналогичными по силе и расе отрядами существ более высокого уровня.
** Отряды нейтральных существ не заменяются.
** Новая численность округляется вверх от 1/3.

; Note: Nonster replacements have to be executed on every day. Crashes happen if on after erm instructions.


!?FU(OnEveryDay)&i^timerDay^=1/i^timerOnce^;
!!UN:P745/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VR(x:y):S-1;                          [setting x-coordinate to -1 will force to start search from scratch]

!!re i;                                 [endless loop]
  !!UN:U(OBJ_MONSTER)/(ANY_OBJ)/-1/(x)/(y:y)/(z:y); [find next monster, (x) = -1 on failure]
  !!br&(x)<0;                           [exit loop if nothing found]

  !!OB(x)/(y)/(z):U?(objSubtype:y);
  !!FU(ES_745_UpgradeMonster):P(x)/(y)/(z)/(objSubtype);
!!en;

!!FU(H3Quests_RefreshData):P(FALSE);
!!SN:D;

!?FU(ES_745_UpgradeMonster);
!#VA(x:x) (y:x) (z:x) (mon:x);

!!VR(random:y):R0/1/100;                [Random number: 1-100]
!!FU&(random)>25:E;                     [Only a 25% chance of monster would be replaced]

!!FU(WOG_CheckIfMonIsEligibleToBeRepalced):P(x)/(y)/(z)/?(result:y);
!!FU&(result)<>(TRUE):E;

!!FU|(mon)>(MON_ENERGY_ELEMENTAL)/(mon)=(MON_GOLD_GOLEM)/(mon)=(MON_DIAMOND_GOLEM):E;   [exit if the creature is neutral]

!!MO(x)/(y)/(z):G?(num:y);              [number of creatures]
!!MA:L(mon)/?(lv:y);                    [creature level (0..6)]
!!VR(lv):*-1 +6;                        [creature level (6..0)]
!!VR(lv)&(lv)>3:S3;                     [Max. number of levels to increase (0..3)]
!!VR(typeOffset:y):S0 R(lv) *2;         [number of “numbers” for promotion (0/2/4/6)]
!!FU&(typeOffset)=0:E;                  [exit if the level of creatures does not increase]

!!VR(newMon:y):S(mon) +(typeOffset); [(newMon) - new type of creature]

!!if&(mon)>=(MON_GNOLL)/(mon)<=(MON_ENERGY_ELEMENTAL);  [нестандартные номера существ]
  !!VR(newMon)&(mon)=98/(newMon)=102:S106;   [...]
  !!VR(newMon)&(mon)=99/(newMon)=103:S107;   [...]
  !!VR(newMon)&(mon)=104/(newMon)=110:S102;  [...]
  !!VR(newMon)&(mon)=105/(newMon)=111:S103;  [...]
  !!VR(newMon)&(mon)=106/(newMon)=112:S102;  [...]
  !!VR(newMon)&(mon)=107/(newMon)=113:S103;  [...]
  !!VR(newMon)&(mon)=102/(newMon)=104:S108;  [...]
  !!VR(newMon)&(mon)=102/(newMon)=106:S110;  [...]
  !!VR(newMon)&(mon)=103/(newMon)=105:S109;  [...]
  !!VR(newMon)&(mon)=103/(newMon)=107:S111;  [...]
  !!VR(newMon)&(mon)=118/(newMon)=120:S112;  [...]
  !!VR(newMon)&(mon)=118/(newMon)=122:S115;  [...]
  !!VR(newMon)&(mon)=118/(newMon)=124:S114;  [...]
  !!VR(newMon)&(mon)=119/(newMon)=121:S127;  [...]
  !!VR(newMon)&(mon)=119/(newMon)=123:S123;  [...]
  !!VR(newMon)&(mon)=119/(newMon)=125:S129;  [...]
  !!VR(newMon)&(mon)=112/(newMon)=114:S115;  [...]
  !!VR(newMon)&(mon)=112/(newMon)=116:S114;  [...]
  !!VR(newMon)&(mon)=112/(newMon)=118:S113;  [...]
  !!VR(newMon)&(mon)=127/(newMon)=129:S123;  [...]
  !!VR(newMon)&(mon)=127/(newMon)=131:S129;  [...]
  !!VR(newMon)&(mon)=127/(newMon)=133:S125;  [...]
  !!VR(newMon)&(mon)=115/(newMon)=117:S114;  [...]
  !!VR(newMon)&(mon)=115/(newMon)=119:S113;  [...]
  !!VR(newMon)&(mon)=115/(newMon)=121:S120;  [...]
  !!VR(newMon)&(mon)=123/(newMon)=125:S129;  [...]
  !!VR(newMon)&(mon)=123/(newMon)=127:S125;  [...]
  !!VR(newMon)&(mon)=123/(newMon)=129:S121;  [...]
  !!VR(newMon)&(mon)=114/(newMon)=116:S113;  [...]
  !!VR(newMon)&(mon)=114/(newMon)=118:S120;  [...]
  !!VR(newMon)&(mon)=114/(newMon)=120:S130;  [...]
  !!VR(newMon)&(mon)=129/(newMon)=131:S125;  [...]
  !!VR(newMon)&(mon)=129/(newMon)=133:S121;  [...]
  !!VR(newMon)&(mon)=129/(newMon)=135:S131;  [...]
  !!VR(newMon)&(mon)=113/(newMon)=115:S120;  [...]
  !!VR(newMon)&(mon)=113/(newMon)=117:S130;  [...]
  !!VR(newMon)&(mon)=125/(newMon)=127:S121;  [...]
  !!VR(newMon)&(mon)=125/(newMon)=129:S131;  [...]
  !!VR(newMon)&(mon)=120/(newMon)=122:S130;  [...]
  !!VR(newMon)&(mon)=121/(newMon)=123:S131;  [...]
!!en;                                   [...]

; Get the quantity depending on Fv
!!MA:F(mon)/?(fv:y) F(newMon)/?(newFv:y); [FV старого и нового существа]
!!VR(newNum:y):S(num) *(fv) :(newFv);   [кол-во новых существ]
!!VR(totalFv:y):S(num) *(fv);           [FV старого отряда]
!!VR(newTotalFv:y):S(newNum) *(newFv);  [FV нового отряда]
!!VR(tripleFvGap:y):S(totalFv) -(newTotalFv) *3; [утроенное "потерянное" FV]
!!VR(newNum)&(tripleFvGap)>=(newFv):+1 F1/(INT_MAX); [кол-во новых существ, с округлением вверх от 1/3]

; Replace
!!UN:O(x)/(y)/(z)/0;                    [удаление старого существа]
!!UN:I(x)/(y)/(z)/(OBJ_MONSTER)/(newMon)/(OBJ_MONSTER)/(newMon)/-1/0; [установка нового существа]

; Set new quantity
!!MO(x)/(y)/(z):G(newNum);

**end

ZVSE
ERMS_ScriptDate=24.1(January).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : New mana regeneration system
** Name rus.     : Новая система восполнения маны
** Options       : 787
** Dialogs       : -
** Variables     : v939, w99
** Tmp variables : -
** Timers        : -
** Functions     : FU7920-FU7921, FU7994

!#TM2:S1/999/1/255;           [каждый день для каждого игрока, но после всех операций триггрера TM1]
!#VRv939:S-1;                 [v939=-1 при старте игры (нет предыдущего игрока)]
!#UN:A73/1/1000 A74/1/2000;   [цена Амулета маны = 1000, Талисмана маны = 2000]
!#UN:A75/1/3000 A75/3/4;      [цена Медали маны = 3000, класс = Minor]

!?FU(OnAfterErmInstructions); [перед началом игры]
!!UN:P787/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!UN:P35/0;                   [принудительно выключаем опцию 35 (Мистицизм 1)]
!!re y1/0/155:;                         // установка описания сепциализаций героям
  !!HEy1:X?y2/?y3/?y4/?y5/?y6/?y7/?y8;  // y2-y8 - параметры специализации героя
  !!FU(Fun_SetDescriptionFromERT)&y2=0/y3=8:P2/y1/2/179016; // установка описания специализации Мистицизм
!!en:;                                  // 
!!FU(Fun_SetDescriptionFromERT):P0/8/1/179017;  // установка описания баззового навыка Мистицизм
!!FU(Fun_SetDescriptionFromERT):P0/8/2/179018;  // установка описания продвинутого навыка Мистицизм
!!FU(Fun_SetDescriptionFromERT):P0/8/3/179019;  // установка описания экспертного навыка Мистицизм
!!FU(Fun_SetDescriptionFromERT):P3/73/0/179290; // установка названия Амулета маны
!!FU(Fun_SetDescriptionFromERT):P3/73/1/179020; // установка описания Амулета маны
!!FU(Fun_SetDescriptionFromERT):P3/74/0/179291; // установка названия Талисмана маны
!!FU(Fun_SetDescriptionFromERT):P3/74/1/179021; // установка описания Талисмана маны
!!FU(Fun_SetDescriptionFromERT):P3/75/0/179292; // установка названия Медали маны
!!FU(Fun_SetDescriptionFromERT):P3/75/1/179022; // установка описания Медали маны

!?FU(OnAfterLoadGame);        [после загрузки игры]
!!UN:P787/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!UN:P35/0;                   [принудительно выключаем опцию 35 (Мистицизм 1)]
!!re y1/0/155:;                         // установка описания сепциализаций героям
  !!HEy1:X?y2/?y3/?y4/?y5/?y6/?y7/?y8;  // y2-y8 - параметры специализации героя
  !!FU(Fun_SetDescriptionFromERT)&y2=0/y3=8:P2/y1/2/179016; // установка описания специализации Мистицизм
!!en:;                                  // 
!!FU(Fun_SetDescriptionFromERT):P0/8/1/179017;  // установка описания баззового навыка Мистицизм
!!FU(Fun_SetDescriptionFromERT):P0/8/2/179018;  // установка описания продвинутого навыка Мистицизм
!!FU(Fun_SetDescriptionFromERT):P0/8/3/179019;  // установка описания экспертного навыка Мистицизм
!!FU(Fun_SetDescriptionFromERT):P3/73/0/179290; // установка названия Амулета маны
!!FU(Fun_SetDescriptionFromERT):P3/73/1/179020; // установка описания Амулета маны
!!FU(Fun_SetDescriptionFromERT):P3/74/0/179291; // установка названия Талисмана маны
!!FU(Fun_SetDescriptionFromERT):P3/74/1/179021; // установка описания Талисмана маны
!!FU(Fun_SetDescriptionFromERT):P3/75/0/179292; // установка названия Медали маны
!!FU(Fun_SetDescriptionFromERT):P3/75/1/179022; // установка описания Медали маны

!?TM2;                        [каждый день для каждого игрока]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRy1:Sv939;                 [y1 - номер предыдущего игрока 0-7]
!!OW:C?v939;                  [v939 - номер текущего игрока сохраняем для использования как предыдущего при следующем срабатывании]
!!DO7920/0/155/1&y1>-1:Py1;   [Для каждого героя предыдущего игрока y1 сохраняем текущую ману в w99]
!!VRy2:Sc;                    [y2 - текущий день]
!!FU&y2=1:E;                  [выход в первый день (мана не восстанавливается)]
!!DO7921/0/155/1:P;           [Для каждого героя текущего игрока v939 восстанавливаем ману]

!?FU7920;                     [Сохранение текущей маны героя x16 в w99 для героев игрока x1]
!!HEx16:O?y1;                 [y1 - игрок-владелец героя x16 [0..7]]
!!FU&y1<>x1:E;                [выход, если владелец героя не предыдущий игрок]
!!IF:Wx16;                    [выбор героя для установки w99]
!!HEx16:I?w99;                [сохранение текущей маны героя в w99]

!?BA53;                       [в конце боя обновляем текущим количеством маны w99 сражавшихся героев]
!!BA:H0/?y1 H1/?y2;           [y1/y2 - номера бьющихся героев (-2 - нет защитника)]
!!if&y1>-1:;
  !!HEy1:I?y3;                [y3 - кол-во маны у героя]
  !!IF:Wy1;                   [выбор героя для использования w99]
  !!VRw99:Sy3;                [обновление w99]
!!en:;
!!if&y2>-1:;
  !!HEy2:I?y3;                [y3 - кол-во маны у героя]
  !!IF:Wy2;                   [выбор героя для использования w99]
  !!VRw99:Sy3;                [обновление w99]
!!en:;

!?FU7921;                     [ежедневное восстановление маны героя x16 для героев игрока v939]
!!HEx16:O?y1;                 [y1 - игрок-владелец героя x16 [0..7]]
!!FU&y1<>v939:E;              [выход, если владелец героя не текущий игрок]
!!HEx16:A2/138/d/?y1;         [y1>0, если на герое надет Колодец Волшебника(138)]
!!FU&y1>0:E;                  [выход, если на герое надет Колодец Волшебника]
!!FU7994:Px16/?y1/?y2/?y3;    [y1/y2/y3 - рейтинг/макс.мана/макс.восст.]
!!IF:Wx16;                    [выбор героя для использования w99]
!!VRy3:+w99;                  [y3 - мана после восстаногвления]
!!VRy3&y3>y2:Sy2;             [не более макс. возможного запаса]
!!VRy3&y3<w99:Sw99;           [и не менее того что было (экстра мана) ]
!!VRy3&y3<0:S0;               [не меньше нуля]
!!HEx16:Iy3/1;                [восстановление маны герою]

!?OB49;                       [посещение колодца маны (перед посещением)]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!HE-1:I?y1;                  [y1 - текущая мана героя]
!!IF:W-1;                     [выбор текущего героя для использования w99]
!!VRw99&w99>-1:Sy1;           [сохраняем текущую ману героя в w99, если герой сегодня еще не посещал колодец (w99>-1)]

!$OB49;                       [посещение колодца маны (после посещения)]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!IF:W-1;                     [выбор текущего героя для использования w99]
!!FU&w99<0:E;                 [выход если герой сегодня уже посещал колодец]
!!HE-1:Y61/d/?y1/2;           [y1 - длительность проклятия, запрещающего посещать Колодец]
!!FU&y1>1/y1<10000:E;         [выход, если герою запрещено богами посещать Колодец]
!!FU7994:P-1/?y1/?y2/?y3;     [y1/y2/y3 - рейтинг/макс.мана/макс.восст.]
!!VRy1:*20;                   [y1 - % восстановления маны в колодце (20% за ед. рейтинга), минимум восстановленной маны]
!!VRy3:Sy2 *y1 :100;          [y3 - макс. возможное восстановление маны в колодце]
!!VRy3&y3<y1:Sy1;             [...]
!!VRy3:+w99;                  [мана после посещения колодца]
!!VRy3&y3>y2:Sy2;             [не больше максимума]
!!VRy3&y3<w99:Sw99;           [и не меньше того, что было до посещения]
!!HE-1:Iy3/1;                 [восстановление маны герою]
!!VRw99:Sy3 *-1;              [сохраняем текущую ману героя с минусом в w99 (признак посещения колодца сегодня)]

!?FU7994;                     [для героя x1 возврат в x2 рейтинга восполнения маны, в x3 - макс. запаса маны героя x1, в x4 - макс. восст. маны]
** рейтинг восполенния маны
!!HEx1:S8/?y1;                [y1 - уровень навыка Мистицизм [0..3]]
!!HEx1:X?y2/?y3/d/d/d/d/d;    [для специалистов по Мистицизму y2=0,y3=8]
!!VRy1&y2=0/y3=8:+1;          [y1 - уровень навыка Мистицизм с учетом специализации [0..4]]
!!HEx1:A2/73/d/?y2;           [y2>0, если на герое надет Амулет Маны]
!!VRy2&y2>0:S1;               [y2=1, если на герое надет Амулет Маны]
!!HEx1:A2/74/d/?y3;           [y3>0, если на герое надет Талисман Маны]
!!VRy3&y3>0:S2;               [y3=2, если на герое надет Талисман Маны]
!!HEx1:A2/75/d/?y4;           [y4>0, если на герое надета Медаль Маны]
!!VRy4&y4>0:S3;               [y4=3, если на герое надета Медаль Маны]
!!VRy2:+y3 +y4;               [y2 - суммарный коэффициент для надетых артефактов из сборника Колодец Волшебника [0..6]]
!!VRy10:S1;                   [y10 - базовый рейтинг восполнения]
** бонус базового рейтинга для ИИ за уровень сложности
!!HEx1:O?y11;                 [y11 - игрок-владелец героя]
!!OW:Iy11/?y12;               [y12=1, для ИИ-игрока]
!!UN:J2/?y13;                 [y13 - выбранный уровень сложности 0..4]
!!VRy10&y12=1:+y13;           [ИИ получает +1 к базовому рейтингу восполнения за каждый уровень сложности выше легкого]
**
!!VRx2:Sy10 +y1 +y2;          [рейтинг восполнения = базовый рейтинг + бонус Мистизизма + бонус артефактов]
** макс. запас маны
!!HEx1:Fd/d/d/?y5;            [y5 - Знание героя]
!!VRy5:*10;                   [y5 базовый макс. запас маны героя]
!!HEx1:S24/?y2;               [y2 - уровень навыка Интеллект [0..3]]
!!HEx1:X?y3/?y4/d/d/d/d/d;    [для специалистов по Интеллекту y3=0,y4=24]
!!HEx1:Ed/?y6;                [y6 - уровень героя]
!!VRy7:S0;                    [------------------------------------------]
!!VRy7&y2=1:Sy5 :4;           [                                          ]
!!VRy7&y2=2:Sy5 :2;           [ y7 - бонус запаса маны от Интеллекта     ]
!!VRy7&y2=3:Sy5;              [     и специализации Интеллект            ]
!!VRe99&y3=0/y4=24:Sy6 :20 +1;[                                          ]
!!VRy7&y3=0/y4=24:*e99;       [------------------------------------------]
!!VRx3:Sy5 +y7;               [макс. запас маны = запас от Знания + бонус от Интеллекта]
** максимальное восстановление маны
!!UN:X?y6/?y3;                [y6 - размер карты]
!!VRy8:S98;                   [пространство за пределами карты считается городом]
!!VRy9:S98;                   [...]
!!HEx1:P?y3/?y4/?y5;          [y3,y4,y5 - координаты героя]
!!VRy10&y3>0:Sy3 -1;          [y10 - позиция слева от героя]
!!OBy10/y4/y5&y3>0:T?y8;      [y8 код объекта слева от героя]
!!VRy10&y3<y6:Sy3 +1;         [y3 - позиция справа от героя]
!!OBy10/y4/y5&y3<y6:T?y9;     [y9 код объекта справа от героя]
!!VRy6&y8=98/y9=98:S1;        [y6=1, если герой в городе (слева и справа объект город)]
!!VRy7:S0;                    [------------------------------------------]
!!CAy3/y4/y5&y6=1:B3/0;       [                                          ]
!!VRy7&1/y6=1:S1;             [                                          ]
!!CAy3/y4/y5&y6=1:B3/1;       [                                          ]
!!VRy7&1/y6=1:S2;             [                                          ]
!!CAy3/y4/y5&y6=1:B3/2;       [y7 - уровень гильдии магов в городе [0..5]]
!!VRy7&1/y6=1:S3;             [                                          ]
!!CAy3/y4/y5&y6=1:B3/3;       [                                          ]
!!VRy7&1/y6=1:S4;             [                                          ]
!!CAy3/y4/y5&y6=1:B3/4;       [                                          ]
!!VRy7&1/y6=1:S5;             [------------------------------------------]
** y9 - % восстановления маны, y8 - мин. кол-во восстановленной маны
!!VRy8:Sx2;                   [мин. 1 маны за единицу рейтинга восполнения на открытой местности/в городе без ГМ]
!!VRy8&y7>0:Sx2 + y7 -1 *20;  [мин. 20 маны за доп.единицу рейтинга и уровень ГМ в городе с ГМ]
!!VRy9:Sx2 * 3;               [3% за единицу рейтинга восполнения на открытой местности/в городе без ГМ]
!!VRy9&y7>0:Sx2 + y7 -1 * 20; [20% за доп.единицу рейтинга и уровень ГМ в городе с ГМ]
**
!!VRy2:Sx3 *y9 :100;          [y2 - макс. количество маны, которое может восстановить герой]
!!VRy2&y2<y8:Sy8;             [...]
!!HEx1:Y6/?y11/?y12/d;        [y11/y12 - сила/продолжительность проклятия потери маны]
!!HEx1:Y7/?y13/?y14/d;        [y13/y14 - сила/продолжительность благословения посполнения маны]
!!VRy10:S0;                   [y10 - суммарный эффект проклятий/благословений маны]
!!VRy10&y11>0/y12>0/y12<1000:-y11 -1; [...]
!!VRy10&y13>0/y14>0/y14<1000:+y13;    [...]
!!VRx4:Sy2 +y10;              [макс. восстановление = восстановление на базе рейтинга/ГМ + бонус/штраф благословений/проклятий]
!!VRx4&x4<0:S0;               [не ниже 0]

!?CM2;                        [клик в окне героя]
!!UN:P787/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!CM:I?y1 S?y2 F?y8;          [y1/y2 - место/флаги/тип клика]
!!FU&y1<>109/y1<>113/y1<>120:E; [выход если нажатие не на мане]
!!CM:H?y3/?y4;                [y3/y4 - левый/правый герои]
!!FU7994:Py3/?y4/?y5/?y6;     [y4/y5/y6 - рейтинг/макс.мана/макс.восст.]
!!HEy3:B0/?z1 I?y7/1;         [z1/y7 - имя/мана героя]
!!VRz1:Sz179284;              [z1 - сообщение для вывода]
!!IF&y2=13/y8=0:M^%Z1^;       [вывод сообщения ЛКМ (отпущена, без модификаторов)]
!!IF&y2=14:M0/4/^%Z1^;        [вывод сообщения ПКМ]
!!CM:R0;                      [отключаем стандартное действие по клику]

!?CM3;                        [клик в окне обмена между героями]
!!UN:P787/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!CM:I?y1 S?y2 F?y8;          [y1/y2 - место/флаги/тип клика]
!!FU&y1<>109/y1<>113/y1<>120:E; [выход если нажатие не на мане]
!!CM:H?y3/?y4;                [y3/y4 - левый/правый герои]
!!VRy3|y1=84/y1=114:Sy4;      [y3 - выбранный герой]
!!FU7994:Py3/?y4/?y5/?y6;     [y4/y5/y6 - рейтинг/макс.мана/макс.восст.]
!!HEy3:B0/?z1 I?y7/1;         [z1/y7 - имя/мана героя]
!!VRz1:Sz179284;              [z1 - сообщение для вывода]
!!IF&y2=13/y8=0:M^%Z1^;       [вывод сообщения ЛКМ (отпущена, без модификаторов)]
!!IF&y2=14:M0/4/^%Z1^;        [вывод сообщения ПКМ]
!!CM:R0;                      [отключаем стандартное действие по клику]

!?CM0;                        [клик на карте приключений]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!CM:I?y1;                    [y1 - место нажатия]
!!FU&y1<>2010:E;              [выход если нажатие не на мане]
!!OW:A-1/?y3;
!!FU7994:Py3/?y4/?y5/?y6;     [y4/y5/y6 - рейтинг/макс.мана/макс.восст.]
!!HEy3:B0/?z1 I?y7/1;         [z1/y7 - имя/мана героя]
!!VRz1:Sz179285;              [z1 - сообщение для вывода]
!!IF:M0/4/^%Z1^;              [вывод сообщения]
!!CM:R0;                      [отключаем стандартное действие по клику]

** end

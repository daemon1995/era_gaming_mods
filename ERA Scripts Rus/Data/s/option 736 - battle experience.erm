ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Battle experience
** Name rus.     : Боевой опыт
** Options       : 736
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7898,FU7899, FU7999
** PO-values     : -

** Отряды в битвах с участием игрока-человека получают опыт и новые ранги непосредственно в процессе боя.
** Опыт начисляется только за убийства вражеских существ и только убившему отряду.
** Отряд, который был непосредственной целью атаки получает опыт за все потери врага, связанные
** с данной атакой, в т.ч. из-за многоклеточных атак/огненного щита/огненной стены/рва/мин и пр.
** ИИ-отряды получают бонус опыта в зависимости от выбранного уровня сложности.
** Знамена полководца с бонусом опыта дают соответствующую прибавку.
** Призываемые существа опыта не получают. Убийство Клона опыта не дает.


; Turn off battle exp option at the start of the game if stack exp is off
!?FU(OnAfterErmInstructions);
!!UN:P736/?(battleExp:y) P(WOG_OPT_STACK_EXPERIENCE)/?(stackExp:y);

!!if&(battleExp)/(stackExp)=(FALSE);
  !!UN:P736/(FALSE);
!!en;

; Store stack exp
!?FU(OnBeforeBattleUniversal)&(ERM_FLAG_IS_HUMAN);
!!UN:P736/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VR(isAttackingTown:y):S(FALSE);
!!VR(x:y):Si^battle_x^ -1;
!!VR(y:y):Si^battle_y^;
!!VR(z:y):Si^battle_z^;
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/-1/(x)/(y)/(z);
!!VR(isAttackingTown)&(x)=i^battle_x^/(y)=i^battle_y^/(z)=i^battle_z^:S(TRUE);

!!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST); 
  !!SN:W^opt736.0.%i^/0;
  !!SN:W^opt736.1.%i^/0;                [обнуление массива опыта существ]
  
  ; Attacking hero
  !!HEi^battle_hero_0^:C0/i/d/?(num:y); [кол-во сущест в i-м отряде атакующего]
  !!EXi^battle_hero_0^/i&(num)>0:E?i^opt736.0.%i^ E?i^opt736.r0.%i^; [сохранение опыта отряда атакующего, если есть существа]

  ; Defending hero
  !!if&i^battle_hero_1^>=0;             [если есть герой-защитник]
    !!HEi^battle_hero_1^:C0/i/d/?(num); [кол-во сущест в i-м отряде защищающегося]
    !!EXi^battle_hero_1^/i&(num)>0:E?i^opt736.1.%i^ E?i^opt736.r1.%i^; [сохранение опыта отряда защищающегося, если есть существа]
  ; Defending garrison (in a town)
  !!el&i^battle_hero_1^<0/(isAttackingTown);[если осадная битва без героя-защитника]
    !!CAi^battle_x^/i^battle_y^/i^battle_z^:M2/i/d/?(num); [кол-во сущест в i-м отряде гарнизона]
    !!EXi^battle_x^/i^battle_y^/i^battle_z^/i&(num)>0:E?i^opt736.1.%i^ E?i^opt736.r1.%i^; [сохранение опыта отряда гарнизона, если есть существа]
  !!en;
!!en;

; Restore stack exp on battle replay
!?FU(OnBattleReplay);
!!UN:P736/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VR(isAttackingTown:y):S(FALSE);
!!VR(x:y):Si^battle_x^ -1;
!!VR(y:y):Si^battle_y^;
!!VR(z:y):Si^battle_z^;
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/-1/(x)/(y)/(z);
!!VR(isAttackingTown)&(x)=i^battle_x^/(y)=i^battle_y^/(z)=i^battle_z^:S(TRUE);

!!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST); 
  ; Attacking hero
  !!HEi^battle_hero_0^:C0/i/d/?(num:y); [кол-во сущест в i-м отряде атакующего]
  !!EXi^battle_hero_0^/i&(num)>0:Ei^opt736.r0.%i^; [восстановление опыта отряда для переигрываемой битвы]

  ; Defending hero
  !!if&i^battle_hero_1^>(NO_HERO);      [если есть герой-защитник]
    !!HEi^battle_hero_1^:C0/i/d/?(num); [кол-во сущест в i-м отряде защищающегося]
    !!EXi^battle_hero_1^/i&(num)>0:Ei^opt736.r1.%i^; [восстановление опыта отряда для переигрываемой битвы]
  ; Defending garrison (in siege)
  !!el&i^battle_hero_1^=(NO_HERO)/(isAttackingTown); [если осадная битва без героя-защитника]
    !!CAi^battle_x^/i^battle_y^/i^battle_z^:M2/i/d/?(num); [кол-во сущест в i-м отряде гарнизона]
    !!EXi^battle_x^/i^battle_y^/i^battle_z^/i&(num)>0:Ei^opt736.r1.%i^; [восстановление опыта отряда для переигрываемой битвы]
  !!en;
!!en;

!!re i/(BATTLE_STACK_FIRST)/(BATTLE_STACK_LAST); [восстановление опыта существам на поле боя при переигрывани битвы]
  !!BMi:O?(armySlot:y) N?(num:y);       [номер слота в армии / численность отряда]

  !!if&(num)>0/(armySlot)>=(ARMY_SLOT_FIRST);
    !!BMi:I?(side:y);

    !!if&i^opt736.r%(side).%(armySlot)^>0; [This check is required for neutral creatures (they have BM:O>0)]
      !!VR(eaStack:y):Si *-1 -1;        [номер отряда для EA]
      !!EA(eaStack):Ei^opt736.r%(side).%(armySlot)^/2/d/d; [восстановление опыта отряда]
    !!en;
  !!en;
!!en;

!?FU(OnBeforeBattleAction)&1000; [для нетеоретической битвы]
!!UN:P736/?y1 P900/?y2;          [проверяем включены ли опция 736 и 900 в y1 и y2 соотв.]
!!FU|y1=0/y2=0:E;                [выход если хоть одна опция не включена]

!!BG:A?y1;                       [y1 - тип действия в бою]
!!SN&y1=(BATTLE_ACTION_SURRENDER):W^opt736.surrend^/1;   [установка флага капитуляции при капитуляции героя]
!!SN&y1<>(BATTLE_ACTION_SURRENDER):W^opt736.surrend^/0;  [сброс флага капитуляции, если действие - не капитуляция]
!!FU&y1<>(BATTLE_ACTION_WALK_AND_ATTACK)/y1<>(BATTLE_ACTION_SHOOT)/y1<>(BATTLE_ACTION_MONSTER_CAST):E;       [выход, если не атака отряда]

!!BG:N?y1;
!!BMy1:O?y2;                     [y1/y2 - номер отряда/номер отряда в армии героя]
!!SN:W^opt736.bm.1^/-1;
!!SN:W^opt736.bm.2^/-1;          [...]

!!if&y2>=0;                      [если отряд состоит в армии (не призван)]
  !!VRv1:S0;                     [суммирование FV вражеских отрядов в v1]
  !!DO7999/0/20/1&y1>20:P;       [...]
  !!DO7999/21/41/1&y1<21:P;      [...]
  !!SN:W^opt736.fv.1^/v1;        [сохранение параметров перед действием]
  !!SN:W^opt736.bm.1^/y1;        [...]
  !!SN:W^opt736.sn.1^/y2;        [...]
!!en; 

!!BG:E?y1;
!!BMy1&y1>=0:O?y2;    [y1/y2 - номер атакуемого отряда/номер отряда в армии героя]

!!if&y1>=0/y2>=0;                [если атакуемый отряд существует и состоит в армии (не призван)]
  !!VRv1:S0;                     [суммирование FV вражеских отрядов в v1]
  !!DO7999/0/20/1&y1>20:P;       [...]
  !!DO7999/21/41/1&y1<21:P;      [...]
  !!SN:W^opt736.fv.2^/v1;        [сохранение параметров перед действием]
  !!SN:W^opt736.bm.2^/y1;        [...]
  !!SN:W^opt736.sn.2^/y2;        [...]
!!en; 

!?FU(OnBattleStackObtainsTurn)&1000;    [phase received move]
!!UN:P736/?y1 P900/?y2;                 [check if option 736 and 900 are enabled in y1 and y2 respectively]
!!FU|y1=0/y2=0:E;                       [exit if at least one option is not enabled]

!!FU7899:P1/1;                          [call the function to award experience to the attacker]
!!FU7899:P1/2;                          [call the function to award experience to the attacked]

!?FU(OnBattleRegeneratePhase)&1000;     [regeneration phase]
!!UN:P736/?y1 P900/?y2;                 [check if option 736 and 900 are enabled in y1 and y2 respectively]
!!FU|y1=0/y2=0:E;                       [exit if at least one option is not enabled]

!!FU7899:P1/1;                          [call function to award experience to the attacker]
!!FU7899:P1/2;                          [call function to award experience to the attacked]

!?FU(OnAfterBattleUniversal)&1000;      [end of non-theoretical battle]
!!UN:P736/?y1 P900/?y2;                 [check if option 736 and 900 are enabled in y1 and y2 respectively]
!!FU|y1=0/y2=0:E;                       [exit if at least one option is not enabled]

!!FU7899:P0/1;                          [call function to award experience to the attacker (without animation, text and sound)]
!!FU7899:P0/2;                          [call function to award experience to the attacked (without animation, text and sound)]
!!BA:H0/?y1 H1/?y2 S?y3 P?y7/?y8/?y9;   [y2, y8, y1/2 - heroes, y3>0 for siege battle, y7-y9 - battle coordinates]
!!SN&y1=5:W^opt736.surrend^/?y4;        [y4>0, if there was a capitulation]
!!HEy1:O?y5;
!!HEy2&y2>=0:O?y6;                      [y5/y6 - hero owners]

!!if|y5>=0/y4=1;                        [если нападающий жив или капитулировал]
  !!SN:W^opt736.0.0^/?y10 W^opt736.0.1^/?y11 W^opt736.0.2^/?y12 W^opt736.0.3^/?y13 W^opt736.0.4^/?y14 W^opt736.0.5^/?y15 W^opt736.0.6^/?y16; [опыты отрядов атакующего]
  !!HEy1:C0/0/d/?y20 C0/1/d/?y21 C0/2/d/?y22 C0/3/d/?y23 C0/4/d/?y24 C0/5/d/?y25 C0/6/d/?y26; [количества существ в отрядах]
  !!EXy1/0&y20>0/y10>0:Ey10;
  !!EXy1/1&y21>0/y11>0:Ey11;
  !!EXy1/2&y22>0/y12>0:Ey12;            [установка опытов отрядов атакующего]
  !!EXy1/3&y23>0/y13>0:Ey13;
  !!EXy1/4&y24>0/y14>0:Ey14;
  !!EXy1/5&y25>0/y15>0:Ey15;
  !!EXy1/6&y26>0/y16>0:Ey16;            [...]
  !_!IF:M^DEB OUT: %Y10(%Y20), %Y11(%Y21), %Y12(%Y22), %Y13(%Y23), %Y14(%Y24), %Y15(%Y25), %Y16(%Y26)^;
!!en; 

!!if&y2>=0;                             [если был защищающийся герой]
  !!if|y6>=0/y4=1;                      [и он жив или капитулировал]
    !!SN:W^opt736.1.0^/?y10 W^opt736.1.1^/?y11 W^opt736.1.2^/?y12 W^opt736.1.3^/?y13 W^opt736.1.4^/?y14 W^opt736.1.5^/?y15 W^opt736.1.6^/?y16; [опыты отрядов защищающегося]
    !!HEy2:C0/0/d/?y20 C0/1/d/?y21 C0/2/d/?y22 C0/3/d/?y23 C0/4/d/?y24 C0/5/d/?y25 C0/6/d/?y26; [количества существ в отрядах]
    !!EXy2/0&y20>0/y10>0:Ey10;
    !!EXy2/1&y21>0/y11>0:Ey11;
    !!EXy2/2&y22>0/y12>0:Ey12;          [установка опытов отрядов защищающегося]
    !!EXy2/3&y23>0/y13>0:Ey13;
    !!EXy2/4&y24>0/y14>0:Ey14;
    !!EXy2/5&y25>0/y15>0:Ey15;
    !!EXy2/6&y26>0/y16>0:Ey16;          [...]
  !!en; 
!!en; 

!!if&y2<0/y3>0/y5<0;                    [если была осадная битва без защищающегося героя и атакующий проиграл]
  !!SN:W^opt736.1.0^/?y10 W^opt736.1.1^/?y11 W^opt736.1.2^/?y12 W^opt736.1.3^/?y13 W^opt736.1.4^/?y14 W^opt736.1.5^/?y15 W^opt736.1.6^/?y16; [опыты отрядов защищающегося]
  !!CAy7/y8/y9:M2/0/d/?y20 M2/1/d/?y21 M2/2/d/?y22 M2/3/d/?y23 M2/4/d/?y24 M2/5/d/?y25 M2/6/d/?y26; [количества существ в отрядах]
  !!EXy7/y8/y9/0&y20>0/y10>0:Ey10;
  !!EXy7/y8/y9/0&y21>0/y11>0:Ey11;
  !!EXy7/y8/y9/0&y22>0/y12>0:Ey12;      [установка опытов отрядов защищающегося]
  !!EXy7/y8/y9/0&y23>0/y13>0:Ey13;
  !!EXy7/y8/y9/0&y24>0/y14>0:Ey14;
  !!EXy7/y8/y9/0&y25>0/y13>0:Ey15;
  !!EXy7/y8/y9/0&y26>0/y16>0:Ey16;      [...]
!!en; 

!?FU7999;                        [подсчет суммарного FV отрядов]
!!BMx16:T?y1 N?y2;               [y1/y2 - тип/кол-во существ в отряде]
!!FU|y1<=(NO_MON)/y2<=0:E;

!!BMx16:F?y4;                    [флаги отряда]
!!VRy4:&(MON_FLAG_CLONE);        y4>0 для Клонов]
!!FU&y4>0:E;                     [Отряды-Клоны не дают опыта]

!!MA:Fy1/?y3;                    [y3 - FV существа]
!!VRy2:*y3;                      [y2 - FV отряда]
!!VRv1:+y2;                      [суммирование FV в v1]

!?FU7899;
!!SN:W^opt736.bm.%X2^/?y1;              [y1, get saved parameters]
!!SN:W^opt736.sn.%X2^/?y2;              [y2, ...]
!!SN:W^opt736.fv.%X2^/?y3;              [y3, ...]
!!SN:W^opt736.bm.%X2^/-1;               [...]
!!FU&y1=-1:E;                           [exit if no experience is required]
!!FU&y2=-1:E;                           [Skip if the stack is not from the hero's army (like henchmen)]

!!BMy1:N?y4;                            [y4 - stack size]
!!FU&y4<1:E;                            [exit if stack is destroyed]

; The exp gained is calculated based on the level of creature and difficulty. 
; The number of creature in a stack affects the exp gained for each individual creature.
!!VRv1:S0;                              [set up enemy unit FV in v1]
!!DO7999/0/20/1&y1>20:P;                [...]
!!DO7999/21/41/1&y1<21:P;               [...]
!!VRy3:-v1 *20;                         [y3 - enemy army FV change *20]
!!VRy5:Sy1 *-1 -1;                      [y5 - unit number for EA]
!!EAy5:R?y13/?y14;                      [y13/y14 - unit has and selected Commander's Banner option]
!!VRy3&y13=(ART_WARLORDS_BANNER)/y14=5:*3 :2; [y3, +50% exp if unit has Banner with experience bonus]

!!BA:O?y13/?y14;                        [y13/y14 - numbers of fighting players]
!!OW&y1<21:Iy13/?y15;                   [y15 - AI unit indicator]
!!OW&y1>20/y14>=0:Iy14/?y15;            [y15, ...]
!!VRy15&y14<0/y1>20:S1;                 [y15, neutrals get experience bonus like AI]

; For human, applys the human exp multiplier
!!if&y15=0;
  !!EA0:H?y38;
  !!VRy3:*y38 :100;
; For AI (and neutrals), apply the AI exp multiplier
!!el;
  !!UN:J2/?y16;                         [y16 - difficulty level (0..4)]
  !!EA0:Ay16/?y38;
  !!VRy3:*y38 :100;
!!en;

!!BMy1:T?y17;                           [y17 - creature type]

; Apply the exp multiplier of the creature
!!EAy17:M?y37;
!!VRy3:*y37 :1000;

; Get monster level (1-8). Monster with fight value more than Blood Dragon would be considered as level 8
!!MA:F(MON_BLOOD_DRAGON)/?y35;
!!MA:Ly17/?y18 Fy17/?y36;               [y36, y18 - creature level (0..6)]
!!VRy18:+1;                             [y18 - creature level (1..7)]
!!VRy18&y36>=y35:S8;

!!VRy3::y18 :y4;                        [y3 - gained experience divided by level and number of creatures in the squad]
!!FU&y3<=0:E;                           [exit if the enemy army has not become weaker or the gained experience is too small]

!!EAy5:E?y6/2/d/d P?y10 L?y11;          [y6 / y10 / y11 - squad experience before gaining experience / experience above rank 10 / experience of rank 10]
!!VRy10:+y11;                           [y10 - maximum possible squad experience]
!!VRy11:Sy6 +y3;                        [y11, if the gained experience exceeds the maximum]
!!VRy3&y11>y10:Sy10 -y6;                [y3, correct it]
!!FU&y3<=0:E;                           [exit if the squad experience is already at maximum]

**
!!BA&y1<21:H0/?y30;
!!BA&y1>20:H1/?y30;
!!BA:S?y31 P?y32/?y33/?y34;      [y30 - герой, y31>0 для осадной битвы, y32-y34 - координаты битвы]
!!FU&y30<0/y31=0:E;              [выход, если отряд без героя и не в гарнизоне]

!!EAy5:Ey3/3/d/d;                [добавление опыта отряду на поле боя]
!!EAy5:E?y7/2/d/d;               [y7 - новый опыт отряда на поле боя]

!!if&y2>=0;
  !!EXy30/y2&y30>=0:Ey7;     [установка опыта отряду в армии героя]
  !!EXy32/y33/y34/y2&y1>20/y30<0/y31>0:Ey7; [установка опыта отряду в гарнизоне города без защитника]
  !!SN&y1<21:W^opt736.0.%Y2^/y7;
  !!SN&y1>20:W^opt736.1.%Y2^/y7; [сохранение опыта отряда]
!!en;
**
!!BMy1:T?y8;                     [y8 - тип существа]
!!VRy11:S0;
!!VRy12:S0;                      [инициализация]

!!FU(ES_GetMonRankWithExp):Py17/y6/?y11; [y11 - ранг существа до получения опыта]
!!FU(ES_GetMonRankWithExp):Py17/y7/?y12; [y12 - ранг существа после получения опыта]

; Show battle log, play sound and animation if ranked up
!!if&y12>y11/i^battle_isVisible^/x1=1;  [если отряд получил новый ранг, битва не быстрая и бой еще не окончен]
  !!SN:T^es.736.battleLog^/?z1;
  !!MM:Sz1;                             [вывод текста в лог битвы]

  !!SN:P^GOODLUCK^;                     [вывод звука удачи]

  !!FU(ES_PlayCustomAnimationOnStack):Py1/^Chevron.def^/0;
!!en;

** end

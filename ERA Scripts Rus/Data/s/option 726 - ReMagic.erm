ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : ReMagic
** Name rus.     : ReMagic
** Options       : -
** Dialogs       : -
** Variables     : -
** Tmp variables : z1-z3
** Timers        : -
** Functions     : -
** PO-values     : -

** Балансировка Знания/Силы героев под новые цены заклинаний
** Палач дает +8 к атаке против всех существ и позволяет добивать существо врага, у которого после атаки останется не более 20/30/40/50% здоровья.
** Специалист по Палачу повышает порог здоровья вражеского существа, которое можно добить дополнительно на 10%.
** Дополнительные участки Зыбучих песков
** Установка флага "заклинание с уроном" Огненной стене и Миному полю для отображения урона в описании
** Заклинания призыва:
** - дают посланников вместо элементалей.
** - имеют "стандартную" расчетную формулу (SP*a+b)
** - получают 50% бонус при наличии у героя сферы соответствующей стихии.
** - не имеют ограничение на призыв только одного типа существ в бою.
** Отключение заклинаний призыва у существ
** Городской Портал кроме маны расходует еще и очки передвижения (только для игрока-человека)
** Из подписей к заклинаниям в книге магии убраны уровни заклинания и школы (автор: feanor)
** Клоны при атаке игнорируют защиту противника
** Силовое поле имеет длительность A+SP/B
** Заклинания, колдуемых компьютерным игроком могут отражаться "Волшебным зеркалом"
** Волна Смерти наносит урон только "живым"" существам


** Балансировка Знания/Силы героев под новые цены заклинаний
!?FU(ES_726_Initialization);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;   [выход, если опция не включена]

!!HE11:F0/0/2/3 I30;    [Аделаида (Кольцо льда) +1 Знания, -1 Атака]
!!HE14:F0/0/1/4 I40;    [Лоинс (Молитва) +2 Знания, -1 Атака, -1 Сила]
!!HE24:F0/0/2/3 I30;    [Корониус (Палач) +1 Сила +1 Знания, -2 Защита]
!!HE34:F0/1/2/3 I30;    [Жозефина (Ускорение) +1 Знания, -1 Атака]
!!HE37:F0/1/2/3 I30;    [Фафнер (Ускорение) +1 Знания, -1 Атака]
!!HE40:F0/0/1/4 I40;    [Астрал (Гипноз) +1 Знания, -1 Сила]
!!HE45:F0/0/1/4 I40;    [Солмир (Цепь молний) +1 Знания, -1 Сила]
!!HE57:F0/0/1/4 I40;    [Ксирон (Инферно) +3 Знания, -1 Атака, -1 Защита, -1 Сила]
!!HE59:F1/0/2/2 I20;    [Олема (Слабость) +1 Знания, -1 Защита]
!!HE60:F0/0/2/3 I30;    [Калид (Ускорение) +1 Знания, -1 Атака, -1 Защита]
!!HE62:F0/1/2/2 I20;    [Зидар (Каменная кожа) +1 Знания, -1 Атака]
!!HE63:F0/0/2/3 I30;    [Ксарфакс (Огненный шар) +2 Знания, -1 Атака, -1 Защита]
!!HE64:F1/0/2/3 I30;    [Стракер (Ускорение) +2 Знания, -2 Защита]
!!HE65:F1/1/2/2 I20;    [Вокиал (Каменная кожа) +1 Знания, -1 Защита]
!!HE66:F1/0/2/3 I30;    [Моандор (Медлительность) +2 Знания, -2 Защита]
!!HE72:F0/0/2/3 I30;    [Септиенна (Волна смерти) +1 Знания, -1 Атака]
!!HE73:F0/0/1/4 I40;    [Аислин (Шквал метеоритов) +2 Знания, -1 Атака, -1 Сила]
!!HE74:F0/0/2/3 I30;    [Сандро (Медлительность) +1 Знания, -1 Атака]
!!HE76:F0/0/1/4 I40;    [Тант (Оживление мертвецов) +2 Знания, -1 Атака, -1 Сила]
!!HE88:F0/0/1/4 I40;    [Аламар (Воскрешение) +2 Знания, -2 Сила]
!!HE91:F0/0/1/4 I40;    [Жеддит (Воскрешение) +2 Знания, -2 Сила]
!!HE92:F0/0/2/3 I30;    [Геон (Медлительность) +1 Знания, -1 Сила]
!!HE93:F0/0/1/4 I40;    [Деемер (Шквал Метеоритов) +2 Знания, -2 Сила]
!!HE106:F0/0/2/3 I30;   [Десса (Каменная кожа) +2 Знания, +1 Сила, -2 Атака, -1 Защита]
!!HE107:F1/0/1/3 I30;   [Терек (Ускорение) +2 Знания, -1 Атака, -1 Защита]
!!HE109:F1/0/1/3 I30;   [Гундула (Медлительность) +2 Знания, -1 Атака, -1 Защита]
!!HE122:F0/0/2/3 I30;   [Вой (Медлительность) +1 Знания, -1 Защита]
!!HE155:F0/0/2/3 I30;   [Лорд Хаарт (Медлительность) +2 Знания, -1 Защита]

!#FU(ES_726_Initialization):P;

** Подмена файла с параметрами заклинаний
; Note: SN:R can't be used here
!?FU(OnAfterLoadGame);
!!FU(ES_726_ReplaceSpellTable):P;

!?FU(ES_726_ReplaceSpellTable);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход, если опция не включена]

; ~~Exit if file does not exist (not possible to check txt inside pac)~~ daemon_n found the soulution
!!VRz1:S^opt726.txt^;                   [полное название тектовика]
!!SN:E5619824/(CALLCONV_THISCALL)/z1;   [TRY TO LOAD TXT FILE _DWORD *__thiscall LoadTXTFile2(const char *name)]

!!if&v1>0;
  !!UN:C5890966/(UNC_INT)/9597928;      [подмена ссылки на адрес переменной z1 "opt726.txt"]
  !!SN:E5890960/1;                      [загрузка "opt726.txt" в функции загрузки "sptraits.txt"]
  !!UN:C5890966/(UNC_INT)/6849372;      [возврат оригинальной ссылки на файл "sptraits.txt"]
!!el;
  !!IF:M^Enable ERA Scripts mod or restore it^;
!!en;

!#FU(ES_726_ReplaceSpellTable):P;

** Палач дает +8 к атаке против всех существ.

!?FU(ES_BattleStack_InitParams);[Установка флага Король1 всем отрядам на поле]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]
!!BMx1:Fd|(MON_FLAG_KING_1) Fd~(MON_FLAG_KING_2) Fd~(MON_FLAG_KING_3);[добавление флага Король1, удаление Король2, Король3]

** Палач позволяет существам добивать существо врага, у которого после атаки останется не более 20/30/40/50% здоровья.
!?FU(OnMonsterPhysicalDamage);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Exit if not melee attacking or shooting
!!UN:C42149568/(UNC_INT)/?(dmgType:y);
!!FU&(dmgType)<>4462398/(dmgType)<>4455011:E;

; Get the stack attacking
!!MF:N?(defStack:y);
!!BG:E?(targetStack:y);

!!if&(defStack)=(targetStack);
  !!VR(atkStack:y):Si^battle_acting_stack^;
!!el;
  !!VR(atkStack):S(targetStack);
!!en;

!!FU|(atkStack)<=(NO_STACK)/(defStack)<=(NO_STACK):E;

; Check if the attacking stack has Slayer
!!BM(atkStack):G(SPELL_SLAYER)/?(duration:y)/?(level:y);

!!if&(duration)>0;
  ; Get the spell effect from the level of Slayer
  !!SS(SPELL_SLAYER):E(level)/?(finishHpPercent:y);
  !!BM(atkStack):I?(side:y);

  ; Check if the owner of the stack is a Slayer specialist
  !!if&i^battle_hero_%(side)^>(NO_HERO);
    !!HEi^battle_hero_%(side)^:X?(specType:y)/?(specSubtype:y);
    !!VR(finishHpPercent)&(specType)=3/(specSubtype)=(SPELL_SLAYER):+10;
  !!en;

  ; Check if the damage exceeds the total HP of the defending stack
  !!MF:F?(damage:y);
  !!BM(defStack):H?(hp:y) L?(lostHp:y) N?(defNum:y);
  !!VR(totalHp:y):S(hp) *(defNum) -(lostHp);

  !!if&(damage)<(totalHp);
    ; Get the hp left after attacked
    !!VR(hpAfterDamage:y):S(totalHp) -(damage) %(hp);

    !!if&(hpAfterDamage)>0;
      ; Get the percentage of HP left after attacked
      !!VR(hpPercentAfterDamage:y):S(hpAfterDamage) *100 :(hp);

      ; If the percentage of HP equals to or is lower than the threshold, increase damage to finish off the creature
      !!if&(hpPercentAfterDamage)<=(finishHpPercent); [если эффективности "Палача" хватает для добивания существа]
        !!MF:Fd(hpAfterDamage);             [увеличение наносимого урона]
        
        !!if&i^battle_isVisible^;
          !!BM(atkStack):N?(atkNum:y) T?(type:y); [численность/тип атакующих существ]
          !!VR(isPlural:y):S(atkNum) -1 B;
          !!SN:H^monname^/(type)/(isPlural)/?z2;
          !!SN:T^es.726.slayer%(isPlural)^/?z1/^mon^/z2;
          !!MM:Sz1;                         [вывод текста в лог битвы]
          !!SN:P^SLAYER^;                   [вывод звука Палача]
          !!BM(defStack):V28;               [анимация на целевом отряде]
        !!en;
      !!en;
    !!en;
  !!en;
!!en;

** Установка флага "заклинание с уроном" Огненной стене и Миному полю для отображения урона в описании
!?FU(OnAfterErmInstructions); [перед загрузкой карты]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!SS11:Fd|512;                [y1/y2 - флаги минного поля и огненной стены]
!!SS13:Fd|512;                [добавляем флаг "заклинание с уроном"]

** установка нового описания специалисту "Палача"
!?FU(OnAfterErmInstructions);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Set the description of Slayer specialist
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);

  !!if&(spec)=3/(spec2)=(SPELL_SLAYER);
    !!SN:H^spec^/i/2/^%T(es.726.slayerSpec)^;
  !!en;
!!en;

** Дополнительные участки Зыбучих песков
!?FU(OnBeforeBattleAction);   [Зыбучие пески 8/10/12/14 участков]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1;                    [y1 - тип действия]
!!FU&y1<>1:E;                 [выход, если не колдовство героя]

!!BG:S?y1 Q?y2;               [y1 - номер колдующегося заклинания, y2 - текущая сторона]
!!FU&y1<>10:E;                [выход, если не каст Зыбучих песков]

!!SS10:C0/?y3 C2/?y6 C0/0 C2/0;[y3/y6 - стоимость базовых/продвинутых Зыбучих песков, обнуление стоимостей]
!!BA:Hy2/?y4;                 [y4 - номер колдующего героя]
!!HEy4:S17/?y5;               [y5 - уровень магии Земли]
!!HEy4&y5>0:S17/2;            [уровень магии Земли продвинутый, если школа есть(+6 участков)]
!!BHy2:C10/0/0/0;             [доп. каст Зыбучих песков]
!!HEy4:S17/y5;                [восстанавливаем уровень магии Земли]
!!SS10:C0/y3 C2/y6;           [восстанавливаем стоимости базовых/продвинутых Зыбучих песков]
!!HEy4:Id1/1;                 [возвращаем 1 маны, которая все равно тратится при нулевой стоимости заклианния]

** Summoning Spells:
** - give messengers instead of elementals.
** - have a “standard” calculation formula (SP*a+b)
** - receive a 50% bonus if the hero has a sphere of the corresponding element.
** - do not have a restriction on summoning only one type of creature in battle.
!?FU(OnStartOrLoad);                  [после загрузки]
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;           [выход, если опция не включена]

!!UN:C5896327/(UNC_INT8)/?i^es_726_patch1^;
!!UN:C5936848/(UNC_INT8)/?i^es_726_patch2^;

!!UN:C5896327/(UNC_INT8)/235;                  [Отключение проверки типа призываемых элементалей]
!!UN:C5936848/(UNC_INT8)/235;                  [большое спасибо Sav'у за пример...]


!?FU(OnGameLeave);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;           [выход, если опция не включена]

!!UN:C5896327/(UNC_INT8)/i^es_726_patch1^;
!!UN:C5936848/(UNC_INT8)/i^es_726_patch2^;

!?FU(ES_CreateERMHook);
!#VA(address:x);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;           [выход, если опция не включена]
!!SN:E(address)/1/5928230/(ES_726_OnCombatElementalSummonCount);

!?FU(ES_726_OnCombatElementalSummonCount);
!#VA(hook:x);

*!BG:A?(action:y);
*!FU&(action)<>(BATTLE_ACTION_HERO_CAST):E; [This hook works with any summoning cast]

!!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/8/4/?(spellId:y);

!!VR(newMonId:y):S(NO_MON);
!!UN:C(ebp)/12/4/?(monId:y);
!!VR(artId:y):S(NO_ART);

!!if&(spellId)=(SPELL_FIRE_ELEMENTAL)/(monId)=(MON_FIRE_ELEMENTAL);
  !!VR(newMonId):S(MON_FIRE_MESSENGER);
  !!VR(artId:y):S(ART_ORB_OF_TEMPESTUOUS_FIRE);
!!el&(spellId)=(SPELL_EARTH_ELEMENTAL)/(monId)=(MON_EARTH_ELEMENTAL);
  !!VR(newMonId):S(MON_EARTH_MESSENGER);
  !!VR(artId:y):S(ART_ORB_OF_SILT);
!!el&(spellId)=(SPELL_WATER_ELEMENTAL)/(monId)=(MON_WATER_ELEMENTAL);
  !!VR(newMonId):S(MON_WATER_MESSENGER);
  !!VR(artId:y):S(ART_ORB_OF_DRIVING_RAIN);
!!el&(spellId)=(SPELL_AIR_ELEMENTAL)/(monId)=(MON_AIR_ELEMENTAL);
  !!VR(newMonId):S(MON_AIR_MESSENGER);
  !!VR(artId:y):S(ART_ORB_OF_THE_FIRMAMENT);
!!en;

!!if&(newMonId)<>(NO_MON);
  !!UN:C(ebp)/16/4/?(spellPwr:y) C(ebp)/20/4/?(skillLevel:y);
  !!SS(spellId):E(skillLevel)/?(skillLevelEffect:y) P?(spellEffect:y);
  !!VR(totalHp:y):S(spellPwr) *(spellEffect) +(skillLevelEffect);   [y13 - HP призванных существ (SP*P+E)]

  !!VR(heroId:y):Si^battle_hero_%i(battle_current_side)^;
  !!if&(heroId:y)<>(NO_HERO);
    !!HE(heroId:y):A2/(artId)/?i/?n;
    !!VR(totalHp:y)&n>0:*3 :2;                                      [+50% к HP призываемых посланников, если на герое надета Сфера соотв. стихии]
  !!en;
  !!MA:P(newMonId)/?(baseHp:y);[y20 - базовое здоровье призываемого существа]
  !!VR(totalSummonCount:y):S(totalHp:y) :(baseHp);F1/(INT_MAX);
  !!if&(totalSummonCount)>0;
    !!VR(lostHp:y):S(totalHp:y) %(baseHp);
    !!VR(totalSummonCount)&(lostHp): +1;
  !!en;
  !!VR(totalSummonCount):F1/(INT_MAX);
  !!VR(monInfoOffset:y):S(newMonId) *116;
  !!UN:C(hook)/(STRUCT_HOOK_CONTEXT_ESI)/4/(totalSummonCount:y) C(ebp)/12/4/(newMonId:y) C(ebp)/-28/4/(monInfoOffset:y); [set new mon count and id to summon and text]
!!en;


// Adapt Summon Elementals (wog option 74)
// The following lines must be executed later than the functions with the same name in 4 wog - summon elementals.erm
!?FU(OnAfterErmInstructions)&i^wog_74_enabled^;
!!UN:P726/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;

!!VRi^wog_74_mon_%(SPELL_AIR_ELEMENTAL)^:S(MON_AIR_MESSENGER);
!!VRi^wog_74_mon_%(SPELL_EARTH_ELEMENTAL)^:S(MON_EARTH_MESSENGER);
!!VRi^wog_74_mon_%(SPELL_WATER_ELEMENTAL)^:S(MON_WATER_MESSENGER);
!!VRi^wog_74_mon_%(SPELL_FIRE_ELEMENTAL)^:S(MON_FIRE_MESSENGER);

!?FU(WOG_74_GetSummonQty);
!#VA(hero:x) (spell:x) (level:x) (qty:x);

!!UN:P726/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;

!!SS(spell):E(level)/?(effect:y) P?(spellPwr:y);
!!HE(hero):Fd/d/?(pwr:y)/d;
!!VR(totalHp:y):S(pwr) *(spellPwr) +(effect);

; Calculate bonus from Artifacts
!!HE(hero):A2/(ART_ORB_OF_THE_FIRMAMENT)/?i/?(hasAirOrb:y) A2/(ART_ORB_OF_DRIVING_RAIN)/?i/?(hasWaterOrb:y) A2/(ART_ORB_OF_TEMPESTUOUS_FIRE)/?i/?(hasFireOrb:y) A2/(ART_ORB_OF_SILT)/?i/?(hasEarthOrb:y);
!!VR(totalHp)&(spell)=(SPELL_FIRE_ELEMENTAL)/(hasAirOrb)>0:*3 :2;
!!VR(totalHp)&(spell)=(SPELL_EARTH_ELEMENTAL)/(hasEarthOrb)>0:*3 :2;
!!VR(totalHp)&(spell)=(SPELL_WATER_ELEMENTAL)/(hasWaterOrb)>0:*3 :2;
!!VR(totalHp)&(spell)=(SPELL_AIR_ELEMENTAL)/(hasAirOrb)>0:*3 :2;

!!MA:Pi^wog_74_mon_%(spell)^/?(hp:y);
!!VR(qty):S(totalHp) :(hp) +1;

; Fix quantity in particular cases
!!VR(lostHp:y):S(totalHp) %(hp) -(hp) *-1;
!!VR(qty)|(hp)=(lostHp)/(lostHp)=0:-1;

** Отключение заклинаний призыва у существ
*?FU(OnAfterErmInstructions);         [опыт существ: удаление заклинаний призыва у существ]
*!UN:P726/?(wogOption:y);
*!FU&(wogOption)<>(TRUE):E;           [выход, если опция не включена]

*!FU(GetMaxMonsterId):P?(maxMonId:y);

*!re (monId:y)/0/(maxMonId);
  *!re l/0/19;
    *!EA(monId:y):Bl/?i/?(skill:y)/?m/d/d/d/d/d/d/d/d/d/d/d; [получение параметров линии опыта]
    *!if|(skill:y)=74/(skill:y)=97/(skill:y)=99/(skill:y)=106/(skill:y)=112/(skill:y)=115;   [если линия связана с сотворением заклинания призыва]
      *!EA(monId:y)&m>=(SPELL_FIRE_ELEMENTAL)/m<=(SPELL_AIR_ELEMENTAL):Bl/0/d/d/0/0/0/0/0/0/0/0/0/0/0; [отключение линии опыта]
    *!en;
  *!en;
*!en;


** Городской Портал кроме маны расходует еще и очки передвижения (только для игрока-человека)
!?FU(OnBeforeAdventureMagic);
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!OW:A-1/?h;                          [h - номер выбранного героя]
!!HEh:P?y1/?y2/?y3 W?y4 Z?h;              [сохраняем координаты и ОД героя при открытии книги заклинаний]

!!SN:E5132816/(CALLCONV_THISCALL)/h; Get Land Type
!!SN:E5133040/(CALLCONV_THISCALL)/h/(SPELL_TOWN_PORTAL)/v1;
[v1 - уровень Школы для заклианния]
!!SN:W^ReMagicTPx^/y1 W^ReMagicTPy^/y2 W^ReMagicTPz^/y3 W^ReMagicTPw^/y4 W^ReMagicTPs^/v1; [...]

!?FU(OnAfterAdventureMagic);
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]
!!FU&v997<>9:E;                       [выход, если кастован не городской портал]

!!OW:A-1/?h;                          [h - номер выбранного героя]
!!HEh:P?y1/?y2/?y3 G?y11;             [проверяем, изменились ли координаты героя]
!!SN:W^ReMagicTPx^/?y4 W^ReMagicTPy^/?y5 W^ReMagicTPz^/?y6 W^ReMagicTPw^/?y12 W^ReMagicTPs^/?y10; [...]
!!FU&y1=y4/y2=y5/y3=y6:E;             [выход, если герой не телепортировался]

!!SS9:Ey10/?y13;                      [y13 - штраф передвижения (%), y11 - начальные ОД, y12 -  текущие ОД]
!!VRy11:*y13 :100;                    [y11 - штраф ОД]
!!VRy12:-y11;                         [уменьшаем текущие ОД героя]
!!VRy12&y12<0:S0;                     [...]
!!HEh:Wy12;                           [...]

** Клоны при атаке игнорируют защиту противника
!?FU(OnBattleStackObtainsTurn);       [при получении хода отрядом]
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!SN:X?y1/?y2;                        [y1/y2 - сторона и номер отряда]
!!VRy3:Sy1 *20 +y2;                   [y3 - номер отряда на поле боя]
!!BMy3:F?y4 T?i^es_726_clone_mon_id^;                          [y4 - флаги атакующего отряда]
!!VRy4:&8388608;                      [y4>0 для атакующего "клона"]
!!FU&y4=0:E;                          [выход, если атакует не клон]
!!UN:C7722324/(UNC_INT)/i^es_726_clone_mon_id^;                [156 Ghost Behemoth 75d554 7722324 4 156 (9Ch)]
!!VRi^es_726_clone_mon_id^: +1;

!?FU(ES_OnAfterAttack)&i^es_726_clone_mon_id^;
  !!UN:C7722324/(UNC_INT)/(MON_GHOST_BEHEMOTH);
  !!VRi^es_726_clone_mon_id^:S0;

*** Силовое поле имеет длительность A+SP/B
!?FU(OnBeforeBattleAction);                        [Перед действием в бою]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1 S?y2 D?y3 Q?y4;     [y1/y2/y3/y4 - действие/заклинание/место/сторона]
!!VRy5:Sy4 +1 *-10;           [y5 - герой]
!!FU|y1<>1/y2<>12:E;          [выход, если не колдовство "Силового поля"]


!!HEy5:Fd/d/?y7/d Z?h;            [y7 - Сила героя]
!!UN:C(COMBAT_MANAGER)/4/?(cmbMgr:y) C(cmbMgr)/21440/4/?(ground:y);
!!SN:E5133040/(CALLCONV_THISCALL)/h/(SPELL_FORCE_FIELD)/(ground:y);
!!SS(SPELL_FORCE_FIELD):P?y8 Ev1/?y9;          [y8/y9 - Сила/Эффект заклинания]
!!VRy10:Sy7 :y9 +y8;          [y10 - длительность Силового поля]
!!UN:C5901022/4/y10;          [устанавливаем длительность (спс. gamecreator'у)]

*** Заклинания, колдуемых компьютерным игроком могут отражаться "Волшебным зеркалом"
!?FU(OnBeforeBattleAction);                        [триггер: действия в битве]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1 S?y2 H?y3 E?y4 Q?y5;[y1-y5 - параметры действия]
!!FU&y1<>1|y4<0:E;            [выход, если не каст героем или не целенаправленное заклинание]

!!HEy3:O?y31;                 [y31 - получить цвет кастующего героя]
!!OW:Iy31/?y32;               [y32 - ИИ или человек]
!!BMy4:G36/?y41/?y42;         [y41/y42 - продолжительность/сила волшебного зеркала]
!!FU|y32<>1/y41<1/y42<0:E;    [выход, если не ИИ, или нет зеркала на цель-стеке]

!!VRy7:S1 R99;                [y7 - случайное число 1..100]
!!SS36:Ey42/?y43;             [y43 - шанс отражения заклинания]
!!FU&y7>y43:E;                [выход, если заклинание не отражено]

!!VRy1:Sy5 *21 R20;           [генерация случайного стека своей стороны (SN:G сюда)]
!!BMy1:N?y2 P?y3 T?y6;        [узнать кол-во существ в этом стеке и его позицию, и тип монстра]
!!SN&y2<1|y6=149:G9;          [если монстров нет или убиты, или это башня, вызваем генерацию заново]
!!UN:C6919200/4/?y1;          [y1 - комбат мэнеджер]
!!VRy1:+72;                   [переход к номеру позиции цели при отражении зеркалом]
!!UN:Cy1/4/y3;                [установить позицию цели]

** Волна Смерти наносит урон только "живым"" существам
!?FU(OnDwarfMagicResistance);           // проверка сопротивлений
!!UN:P726/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!MR:S?y1;
!!FU&y1<>(SPELL_DEATH_RIPPLE):E;                          // выход, если не Волна смерти

!!MR:N?y1;
!!BMy1:F?y2;
!!VRy2:&16;                             // y2=0, если у отряда нет флага "живой"
!!MR&y2=0:F100;                         // иммунитет к Волне смерти для существ без флага "живой"

** end

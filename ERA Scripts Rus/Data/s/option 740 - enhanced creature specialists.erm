ZVSE2
** Author        : igrik, Algor (sponsor: swallowmee)
** Updated by    : Archer30
** Name          : Enhanced creature specialists
** Name rus.     : Измененные специалисты по существам
** Options       : 740

** Существа под командованием соответствующего специалиста получают единовременный бонус +1 к скорости,
** а также +1 к атаке за каждый нечётный уровень героя и +1 к защите за каждый чётный.

; This script disables the monster bonuses from option 39 Hero Specialty Boost.


// Disable Monster Specialty bonuses from Hero Specialty Boost
!#VRi^wog_39_monSpecEnabled^:S(FALSE);

!?FU(OnAfterErmInstructions);
!!UN:P740/?(wogOption:y);               [проверяем включена ли опция 740]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Loop though all the non-special heroes
!!re i/(HERO_FIRST)/(HERO_GRINDAN);
  ; Standarise all the creature specialists to type 4
  !!HEi:X?(spec:y)/?(spec2:y)/d/d/d/d/d;

  !!HEi&(spec)=4:X1/(spec2)/d/d/d/d/d;

  !!SN&(spec)=1/(spec2)<>(MON_BALLISTA):H^spec^/i/2/^%T(es.740.spec)^;
!!en;

!?FU(OnStartOrLoad);
!!UN:P740/?(wogOption:y);               [проверяем включена ли опция 740]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!SN:L^EraPlugins\erm_hooker.era^/?(hooker:y) A(hooker:y)/^SetHook^/?(address:y);
!!SN:E(address)/1/5137688/(ES_740_Hook);[0x004E6518]

!?FU(ES_740_Hook);
!!SN:X?y1/0;
; [получаем параметры героя]
!!VRy2:Sy1 +28;                           [EAX]
!!UN:Cy2/4/?y3;                           [структура героя]
!!VRy3:+85;                               [EAX+0x55]
!!UN:Cy3/2/?y4;                           [узнать уровень героя]
; [получаем параметры монстра]
!!VRy5:Sy1 +4;                            [ESI]
!!UN:Cy5/4/?y6;                           [структура монстра]
!!VRy7:Sy6 +4;                            [ESI+4]
!!UN:Cy7/4/?y7;                           [уровень монстра]
!!VRy7:+11;                               [y7 - индекс в таблице уровней героя, с которых начинают даваться бонусы]
!!VRv11:C2/3/4/5/6/7/8;                   [таблица уровней героя, с которых начинают даваться бонусы существам - оригинал]
!!VRv11:C1/1/1/1/1/1/1;                   [таблица уровней героя, с которых начинают даваться бонусы существам - тест]
; [бонус от специальности]
!!VRy10:Sy4 -vy7 +1;                      [y10 - текущее количество бонусных уровней]
!!VRy10&y10<0:S0;                         [проверка на отрицательное значение]
!!VRy13:Sy10 :2;                          [y13 - бонус атаки (+1 за каждый четный уровень)]
!!VRy14:Sy10 +1 :2;                       [y14 - бонус защиты (+1 за каждый нечетный уровень)]
; [устанавливаем бонусы]
!!VRy11:Sy6 +84;
!!UN:Cy11/4/dy13;                         [бонус к атаке]
!!VRy12:Sy6 +88;
!!UN:Cy12/4/dy14;                         [бонус к защите]
!!VRy99:Sy1 +32;
!!UN:Cy99/4/5137828;                      [возврат в 0x004E65A4]

** end

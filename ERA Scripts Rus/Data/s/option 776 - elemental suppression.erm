ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Updated by    : daemon_n - no more w-vars, changed trigger to execute after neutral hero

** Name          : Elemental suppression
** Name rus.     : Подавление стихий
** Options       : 776


!?FU(OnAfterErmInstructions);           [пост-инструкция: установка описаний]
!!UN:P776/?(elemSupp:y);                [проверяем включена ли опция 776]
!!FU&(elemSupp)<>(TRUE):E;              [выход если опция не включена]

; Disable the feature to remove Eagle Eye skill when the hero has learned all the spells of Eagle Eye I
!!VRi^wog_103_removeEagleEye^:S(FALSE);

; Set up new skill name and description for Elemental Suppression 
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/0/^%T(es.776.name)^;

!!SN:T^es.776.desc^/?(desc1:z)/^lv^/1;
!!SN:T^es.776.desc^/?(desc2:z)/^lv^/2;
!!SN:T^es.776.desc^/?(desc3:z)/^lv^/3;

!!VR(basicDesc:z):S^{%T(es.basic)%T(es.776.name)}%(desc1)%T(es.776.neutralized)^;
!!VR(advancedDesc:z):S^{%T(es.advanced)%T(es.776.name)}%(desc2)%T(es.776.neutralized)^;
!!VR(expertDesc:z):S^{%T(es.expert)%T(es.776.name)}%(desc3)%T(es.776.neutralized)^;
!!VR(newLine:z):S^
^;

; Add in text of Eagle Eye I if the option is enabled
!!UN:P103/?(eagleEyeI:y);

!!if&(eagleEyeI);
  !!VR(basicDesc):+z175167;
  !!VR(advancedDesc):+z175168;
  !!VR(expertDesc):+z175169;
!!en;

; Add in text of Eagle Eye II if the option is enabled
!!UN:P202/?(eagleEyeII:y);

!!if&(eagleEyeII);
  !!VR(basicDesc):+z175170;
  !!VR(advancedDesc):+z175171;
  !!VR(expertDesc):+z175172;
!!en;

; Change the skill descriptions
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/(SKILL_BASIC)/(basicDesc);
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/(SKILL_ADVANCED)/(advancedDesc);
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/(SKILL_EXPERT)/(expertDesc);

; Set up spec text for Eagle Eye specialists
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);
  !!SN&(spec)=0/(spec2)=(SKILL_EAGLE_EYE):H^spec^/i/2/^%T(es.776.spec)^;
!!en;

; Set up text for Suppression Artifacts
; Must be in Erm Instruction to be cmopatible with Enhanced Artifacts I (WoG Scripts)
!?FU(ES_776_SetArtDescriptions);
!!UN:P776/?(elemSupp:y);                [проверяем включена ли опция 776]
!!FU&(elemSupp)<>(TRUE):E;              [выход если опция не включена]

!!SN:T^es.776.desc^/?(desc1:z)/^lv^/1;
!!SN:T^es.776.desc^/?(desc2:z)/^lv^/2;

!!SN:H^art^/(ART_BIRD_OF_PERCEPTION)/0/?(artName:z);
!!SN:H^art^/(ART_BIRD_OF_PERCEPTION)/1/^{%(artName)}%(desc1)%T(es.776.neutralized)%T(es.period)^;

!!SN:H^art^/(ART_STOIC_WATCHMAN)/0/?(artName);
!!SN:H^art^/(ART_STOIC_WATCHMAN)/1/^{%(artName)}%(desc1)%T(es.776.neutralized)%T(es.period)^;

!!SN:H^art^/(ART_EMBLEM_OF_COGNIZANCE)/0/?(artName);
!!SN:H^art^/(ART_EMBLEM_OF_COGNIZANCE)/1/^{%(artName)}%(desc2)%T(es.776.neutralized)%T(es.period)^;

!#FU(ES_776_SetArtDescriptions):P;

; Add the Suppression artifact text at the end of description
; Must be OnAfterErmInited to be compatible with Enhanced Artifacts I (WoG Scripts)
!?FU(OnAfterErmInited);
!!UN:P776/?(elemSupp:y);                [проверяем включена ли опция 776]
!!FU&(elemSupp)<>(TRUE):E;              [выход если опция не включена]

!!re i/(ART_BIRD_OF_PERCEPTION)/(ART_EMBLEM_OF_COGNIZANCE);
  !!SN:H^art^/i/1/?(artDesc:z);
  !!SN:H^art^/i/1/^%(artDesc)%T(es.776.suppArt)^;
!!en;

// Disable the standard effect of Eagle Eye and related artifacts
!?FU(OnStartOrLoad);
!!UN:P776/?(wogOption:y);     [проверяем включена ли опция 776]
!!FU&(wogOption)<>(TRUE):E;   [выход если опция не включена]

!!UN:C5129872/(UNC_INT8)/?i^es_776_patch1^;
!!UN:C5129873/(UNC_INT)/?i^es_776_patch2^;
!!UN:C5129872/(UNC_INT8)/233; [отключаем стандартное действие навыка Зоркость (спс. feanor'у)]
!!UN:C5129873/(UNC_INT)/340;  [отключаем стандартное действие артефактов Зоркости (спс. feanor'у)]

!?FU(OnGameLeave);
!!UN:P776/?(wogOption:y);     [проверяем включена ли опция 776]
!!FU&(wogOption)<>(TRUE):E;   [выход если опция не включена]

!!UN:C5129872/(UNC_INT8)/i^es_776_patch1^;
!!UN:C5129873/(UNC_INT)/i^es_776_patch2^;

!?FU(ES_776_StoreAndChangeMagicSchoolLvl);
!#VA(heroId:x) (value:x);
!#VA(skillLvl[4]:y);

!!re (ss:y)/(SKILL_FIRE_MAGIC)/(SKILL_EARTH_MAGIC);
  !!VR(index:y):S(ss) -(SKILL_FIRE_MAGIC);
  !!HE(heroId):S(ss)/?(skillLvl[index]);
  !!VR(newLevel:y):S(skillLvl) -(value) F(SKILL_NOT_LEARNED)/(SKILL_EXPERT);
  !!HE(heroId):S(ss)/(newLevel:y);
!!en;

!!FU(PackUnion):P(heroId)/8/(skillLvl[0])/6/(skillLvl[1])/6/(skillLvl[2])/6/(skillLvl[3])/6/?i^es_776_heroIdAndSSLevel^;

!?FU(ES_776_RestoreMagicSchoolLvl);
!#VA(skillLvl[4]:y);

!!FU(UnpackUnion):Pi^es_776_heroIdAndSSLevel^/?(heroId:y)/8/?(skillLvl[0])/6/?(skillLvl[1])/6/?(skillLvl[2])/6/?(skillLvl[3])/6;

!!re (ss:y)/(SKILL_FIRE_MAGIC)/(SKILL_EARTH_MAGIC);
  !!VR(index:y):S(ss) -(SKILL_FIRE_MAGIC);
  !!HE(heroId):S(ss)/(skillLvl[index]);
!!en;

!?FU(ES_776_Hero_GetSuppressionPower);
!#VA(heroId:x) (result:x);

!!VR(result):S(NULL);
!!HE(heroId):X?(spec:y)/?(specSkill:y) S(SKILL_EAGLE_EYE)/?(result);
!!VR(result)&(spec)=0/(specSkill)=(SKILL_EAGLE_EYE):+1;

!!re i/(ART_BIRD_OF_PERCEPTION)/(ART_EMBLEM_OF_COGNIZANCE);
  !!HE(heroId):A2/i/?(has:y)/?(isEquipped:y);

  !!if&(isEquipped);
    !!VR(result):+1;
    !!VR(result)&i=(ART_EMBLEM_OF_COGNIZANCE):+1;
  !!en;
!!en;

!?FU(OnSetupBattlefield)&i^battle_hero_vs_hero^;
!!UN:P776/?(isEnabled:y);
!!FU&(isEnabled:y)=0:E;

!#VA(suppLvls[2]:y);

!!re i/(BATTLE_LEFT)/(BATTLE_RIGHT);
  !!FU(ES_776_Hero_GetSuppressionPower):Pi^battle_hero_%i^/?(suppLvls[i]);
!!en;

!!if&(suppLvls[0])<>(suppLvls[1]);
  !!VR(valueToDecrease:y):S(suppLvls[0]) -(suppLvls[1]);

  !!if&(valueToDecrease:y)>0;
    !!VR(side:y):S(BATTLE_RIGHT);
  !!el;
    !!VR(valueToDecrease):*-1;
    !!VR(side):S(BATTLE_LEFT);
  !!en;

  !!FU(ES_776_StoreAndChangeMagicSchoolLvl):Pi^battle_hero_%(side)^/(valueToDecrease:y);
!!en;

!?FU(OnAfterBattleAction)&i^es_776_heroIdAndSSLevel^;
!!BU:C?(battleIsEnded:y);               [если бой закончен]
!!FU&(battleIsEnded)<>(TRUE):E;         [выход, если бой не закончен]

!!FU(ES_776_RestoreMagicSchoolLvl):P;
!!VRi^es_776_heroIdAndSSLevel^:S0;

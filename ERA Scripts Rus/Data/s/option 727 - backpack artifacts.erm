ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Backpack artifacts
** Name rus.     : Артефакты рюкзака
** Options       : 727

** Артефакты, производящие ресурсы или повышающие запас хода теперь дают бонусы даже находясь в рюкзаке героя.


!?FU(OnAfterErmInstructions);
!!UN:P727/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!re i/(ART_EVERFLOWING_CRYSTAL_CLOAK)/(ART_ENDLESS_PURSE_OF_GOLD);
  !!FU(ES_727_SetNewArtifactDescriptions):Pi;
!!en;

!!FU(ES_727_SetNewArtifactDescriptions):P(ART_CORNUCOPIA) P(ART_EQUESTRIANS_GLOVES) P(ART_NECKLACE_OF_OCEAN_GUIDANCE) P(ART_BOOTS_OF_SPEED);

; Don't allow these artifacts to be equipped
*!UN:A(ART_INEXHAUSTIBLE_CART_OF_ORE)/2/0;
*!UN:A(ART_INEXHAUSTIBLE_CART_OF_LUMBER)/2/0;
*!UN:A(ART_ENDLESS_SACK_OF_GOLD)/2/0;
*!UN:A(ART_ENDLESS_BAG_OF_GOLD)/2/0;
*!UN:A(ART_ENDLESS_PURSE_OF_GOLD)/2/0;
*!UN:A(ART_BOOTS_OF_SPEED)/2/0;

!?FU(ES_727_SetNewArtifactDescriptions);
!#VA(art:x);

!!SN:H^art^/(art)/1/?(desc:z);
!!SN:H^art^/(art)/1/^%(desc)%T(es.727.desc)^;

; Add in extra movement points when movement points is updated
!?FU(ES_OnSetHeroMovementPoints);
!#VA(hook:x);

!!UN:P727/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EBX)/(UNC_INT)/?(heroStruct:y);
!!UN:C(heroStruct)/26/(UNC_INT)/?(heroId:y);

!!FU(ES_727_GetExtraMovementPointsFromBackpackArts):P(heroId)/?(extraMp:y);

; Compatibility with Double Movement Points (WoG Scripts - Map rule)
!!VR(extraMp)&i^wog_101_map_rule_10_isEnabled^:*2;

!!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EAX)/(UNC_INT)/d(extraMp);

!?FU(ES_727_GetExtraMovementPointsFromBackpackArts);
!#VA(hero:x) (extraMp:x);

!!VR(extraMp):S0;

; Check the terrain type of the hero standing
!!HE(hero):P?(x:y)/?(y:y)/?(z:y);
!!TR(x)/(y)/(z):T?(terrain:y)/?i/?i/?i/?i/?i/?i/?i; // j - тип почвы под героем (8 - вода)

; If on land
!!if&(terrain)<>8;
  !!HE(hero):A2/(ART_EQUESTRIANS_GLOVES)/?i/?j;
  !!VR(extraMp)&j=0/i>0:+300;  // Перчатки Всадника +300 MP (суща)
  !!HE(hero):A2/(ART_BOOTS_OF_SPEED)/?i/?j;
  !!VR(extraMp)&j=0/i>0:+600;  // Сапоги-Скороходы +600 MP (суша)
; If on water
!!el;
  !!HE(hero):A2/(ART_ADMIRALS_HAT)/?i/?j;

  !!if&j=0/i>0;
    !!VR(extraMp):+1500;
  !!el;
    !!HE(hero):A2/(ART_SEA_CAPTAINS_HAT)/?i/?j;
    !!VR(extraMp)&j=0/i>0:+500;
    !!HE(hero):A2/(ART_NECKLACE_OF_OCEAN_GUIDANCE)/?i/?j;
    !!VR(extraMp)&j=0/i>0:+1000;
  !!en;
!!en;

!?FU(ES_727_CalcBonus);                        [подсчет в v4..v10 бонуса от артефактов-ресурсников в рюкзаках героев (x16) игрока x1]
!!OW:Hx1/1/x16;
!!VRy21:Sv1;     [y21 - номер героя]
!!HEy21:A2/109/?y30/?y40 A2/110/?y31/?y41 A2/111/?y32/?y42 A2/112/?y33/?y43 A2/113/?y34/?y44; [y30..39 - общее кол-во артов]
!!HEy21:A2/114/?y35/?y45 A2/115/?y36/?y46 A2/116/?y37/?y47 A2/117/?y38/?y48 A2/(ART_CORNUCOPIA)/?y39/?y49; [y40..49 - кол-во надетых артов]
!!VRy30:-y40;
!!VRy30&y30<0:S0; 
!!VRy31:-y41;
!!VRy31&y31<0:S0; [y30..39 - кол-во артов в рюкзаке]
!!VRy32:-y42;
!!VRy32&y32<0:S0;
!!VRy33:-y43;
!!VRy33&y33<0:S0; [...]
!!VRy34:-y44;
!!VRy34&y34<0:S0;
!!VRy35:-y45;
!!VRy35&y35<0:S0; [...]
!!VRy36:-y46;
!!VRy36&y36<0:S0;
!!VRy37:-y47;
!!VRy37&y37<0:S0; [...]
!!VRy38:-y48;
!!VRy38&y38<0:S0;
!!VRy39:-y49;
!!VRy39&y39<0:S0; [...]
!!VRy36:*1000;
!!VRy37:*750;
!!VRy38:*500;
!!VRy39:*5;           [y30..39 - бонус ресурса от артефактов в рюкзаке]
!!VRv4:+y35;
!!VRv6:+y33;
!!VRv10:+y36 +y37 +y38;                [дерево, руда, золото]
!!VRv5:+y32 +y39;
!!VRv7:+y34 +y39;
!!VRv8:+y30 +y39;
!!VRv9:+y31 +y39;        [ртуть, сера, кристаллы, драг.камни]

!?FU(ES_ArtGoldIncomeCalculation); // расчет дохода за артефакты героев игрока (золото)
!!UN:P727/?y1;
!!FU&y1=0:E;      [выход, если опция не включена]

!!SN:X?y1/0;
!!VRy2:Sy1 +0;
!!UN:Cy2/4/?y3;   [EDI]
!!VRy4:Sy1 +24;
!!UN:Cy4/4/?y5;   [ECX]
!!VRy6:Sy1 +8;
!!UN:Cy6/4/?y7;   [EBP]
!!VRy8:Sy7 -4;
*!UN:Cy8/4/?y9;   [EBP+Gold]
!!VRy10:Sy7 +8;
!!UN:Cy10/4/?y11; [y11 - цвет игрока]
!!OW:Hy11/1/0;
!!VRy12:Sv1;      [y12 - кол-во героев текущего игрока]
!!VRv4:C0/0/0/0/0/0/0;           [инициализация v4-v10 для подсчета бонусов за ресурсогенерирующие артефакты в рюкзаках героев]
!!DO(ES_727_CalcBonus)/1/y12/1&y12>0:Py11;     [перебираем героев игрока и подсчитываем бонусы в v4-v10]
!!VRy5:+v10;                     [добавляем бонус золота]
!!UN:Cy2/4/0;                    [xor edi, edi]
!!UN:Cy8/4/y5;                   [mov [ebp+gold], ecx]
!!VRy9:Sy1 +32;
!!UN:Cy9/4/5011269; 4C7745

!?FU(ES_ArtResIncomeCalculation); // расчет дохода за артефакты героев игрока (ресурсы)
!!UN:P727/?y1;
!!FU&y1=0:E;      [выход, если опция не включена]

!!SN:X?y1;
!!VRy2:Sy1 +4;
!!UN:Cy2/4/?y3; [ESI] - структура игрока - ресурсы (ртуть)
!!VRy2:Sy3 -268;
*!UN:Cy2/4/?y4; [EBX] - что-то в структуре игрока
!!VRy6:Sy1 +8;
!!UN:Cy6/4/?y7; [EBP]
!!VRy7:-4;
!!UN:Cy7/4/?y8; [y8 - цвет игрока в цикле в коде игры в проверке]
!!OW:Hy8/1/0;
!!VRy9:Sv1;        [y9 - кол-во героев текущего игрока]
!!VRv4:C0/0/0/0/0/0/0;           [инициализация v4-v10 для подсчета бонусов за ресурсогенерирующие артефакты в рюкзаках героев]
!!DO(ES_727_CalcBonus)/1/y9/1&y9>0:Py8;        [перебираем героев игрока и подсчитываем бонусы в v4-v10]
!!VRy5:Sy3 -4;
!!UN:Cy5/4/dv4; [дерево]
!!VRy5:Sy3;
!!UN:Cy5/4/dv5; [ртуть]
!!VRy5:Sy3 +4;
!!UN:Cy5/4/dv6; [руда]
!!VRy5:Sy3 +8;
!!UN:Cy5/4/dv7; [сера]
!!VRy5:Sy3 +12;
!!UN:Cy5/4/dv8; [кристаллы]
!!VRy5:Sy3 +16;
!!UN:Cy5/4/dv9; [драг.камни]

; Compatibility with Enhanced Artifact I (Emblem of Cognizance)
!?FU(WOG_102_GetEmblemOfCognizanceQty);
!#VA(hero:x) (result:x);

!!UN:P727/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!HE(hero):A2/(ART_EMBLEM_OF_COGNIZANCE)/?(result)/?(equipped:y);

** end

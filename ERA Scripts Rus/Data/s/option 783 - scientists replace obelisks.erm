ZVSE
ERMS_ScriptDate=21.1(January).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Scientists replace obelisks
** Name rus.     : Ученые вместо обелисков
** Options       : 783
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v3
** Timers        : TM19(standard)
** Functions     : FU7917
** PO-values     : -

!#TM19:S1/1/1/255;            [первый день для всех игроков]

!?TM19;                       [в первый день: замена обелисков камнями знаний]
!!UN:P783/?y1;                [проверяем включена ли опция 783 в y1]
!!FU&y1<>1:E;                 [выход если опция не включена]
!!UN:U57/-1/?y1;              [y1 - количество обелисков]
!!DO7917/1/y1/1:P;            [заменяем обелиски учеными]
!!UN:P883/2;                  [отключаем опцию 883]

!?FU7917;                     [замена обелиска ученым]
!!UN:U57/-1/1/1;              [v1..v3 - координаты обелиска]
!!UN:Ov1/v2/v3/1;             [удаляем обелиск]
!!UN:Iv1/v2/v3/81/0/0;        [помещаем ученого]
!!VRy1:S0 R31;                [выбираем первичный навык для обучения (0..3 - первичный, 4-31 - вторичный)]
!!if&y1>3:;
  !!VRy1:-4;                  [y1 - вторичный навык для изучения (0..27)]
  !!UN:P773/?y2;              [для безводной карты со включенной опцией "Сухопутная навигация"...]
  !!VRy1&y1=5/y2=2:S2;        [...меняем "Навигацию" на "Логистику"]
  !!SCv1/v2/v3:T1 Sy1;        [устанавливаем вторичный навык для обучения]
!!el:;
  !!SCv1/v2/v3:T0 Py1;        [устанавливаем первичный навык для обучения (0..3)]
!!en:;

!?OB81&999;                   [посещение ученого игроком-человеком]
!!UN:P783/?y1;                [проверяем включена ли опция 783 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!HE-1:Y57/d/?y1/2;           [y1 - длительность проклятия, запрещающего посещать Ученого]
!!FU&y1>1/y1<1000:E;          [выход, если герою запрещено богами посещать ученого]
!!HE-1:O?y2;                  [y2 - игрок владедец героя]
!!SN:W^o783_%Y2_%V998_%V999_%V1000^/1; [выставление признака посещения ученого]
!!UN:P786/?y2;                [проверяем включена ли опция 786 в y2]
!!VRy12:S0; !!VRy13:S0; !!VRy14:S0; !!VRy15:S10; [инициализация переменных]
!!HE-1&y2<>0:S4/?y15 A2/66/d/?y12 A2/67/d/?y13 A2/68/d/?y14; [y15 - уровень дипломатии героя, если включена опция 786 "Дипломатические торги"]
!!VRy13&y2<>0/y13>0:S1;       [учитываем только одно кольцо]
!!VRy15&y2<>0:+y12 +y13 +y14 -10 *-1; [y15 - скидка с учетом навыка Дипломатии и артефактов дипломата]
!!SC998:T?y1;                 [y1 - тип бонуса (0-первичный, 1-вторичный, 2-заклинание)]
!!OW:C?y5;                    [y5 - текущий игрок]
!!OB998:Dy5;                  [запрет посещения объекта текущему игроку и отключение стандартного сообщения объекта]
!!OW:R-1/6/?y3;               [количество золота игрока]
!!VRy11:S6;                   [y11 - тип 2й картинки (золото)]
!!if&y1=0:;                   [первичный навык]
  !!SC998:P?y2;               [y2 - значение бонуса]
  !!VRz9&y2=0:Sz179168;       [z9 - название первичного навыка]
  !!VRz9&y2=1:Sz179169;       [...]
  !!VRz9&y2=2:Sz179170;       [...]
  !!VRz9&y2=3:Sz179171;       [...]
  !!VRy9:S250 *y15 :10;       [стоимость обучения - 250 золотых минус скидка Дипломатии]
  !!if&y3>=y9:;               [если обучение возможно]
    !!VRz8:Sz179173;          [z8 - текст обучения]
    !!VRy8:S1;                [y8=1, если обучение возможно]
  !!el:;
    !!VRz8:Sz179174;          [z8 - текст обучения]
    !!VRy8:S0;                [y8=0, если обучение невозможно]
  !!en:;
  !!VRz1:Sz179172;            [z1 - текст сообщения]
  !!VRy6:Sy2 +31;             [y6 - тип 1й картинки]
  !!VRy7:S1;                  [y7 - подтип 1й картинки]
!!en:;
!!if&y1=1:;                   [втор. навык]
  !!SC998:S?y2;               [y2 - значение бонуса]
  !!UN:N4/9/y2;               [z9 - название втор. навыка]
  !!HE-1:Sy2/?y4;             [y4 - уровень навыка у героя]
  !!VRy9:Sy4 +1 *500 *y15 :10;[y9 - стоимость обучения 500/1000/1500 за Б/П/Э уровень навыка минус скидка Дипломатии]
  !!VRv1:S0;                  [инициализация v1]
  !!DO7942/0/27/1:P;          [v1 - количество известных герою навыков]
  !!UN:C4881872/1/?y21;       [y21 - установленный лимит вториных навыков]
  !!VRy4&y4=0/v1>=y21:S3;     [y4=3, если нет места под навык]
  !!VRy8:S0;                  [y8=0, если обучение невозможно]
  !!VRz8&y3<y9:Sz179174;      [z8 - текст обучения, если недостаточно золота]
  !!VRz8&y4=3:Sz179175;       [z8 - текст обучения, если обучение невозможно]
  !!if&y3>=y9/y4<3:;          [если навык может быть выучен]
    !!VRy8:S1;                [y8=1, если обучение возможно]
    !!VRz8:Sz179173;          [z8 - текст обучения]
  !!en:;
  !!VRz1:Sz179172;            [z1 - текст сообщения]
  !!VRy6:S20;                 [y6 - тип 1й картинки]
  !!VRy11&y4=3:S-1;           [отключаем картинку золота, если навык уже на эксперте]
  !!VRy4&y4=3:S2;             [картинка текущего навыка, если навык уже на эксперте]
  !!VRy7:Sy2 +1 *3 +y4;       [y7 - подтип 1й картинки]
!!en:;
!!if&y1=2:;                   [заклинание]
  !!SC998:L?y2;               [y2 - значение бонуса]
  !!VRy2&y2>=1024:-1024;      [y2 - номер заклианния]
  !!UN:N1/9/y2;               [z9 - название заклинания]
  !!SSy2:L?y4;                [y4 - уровень заклинания]
  !!VRy5:Sy4 -2;              [y5 - требуемый уровень Мудрости]
  !!VRy9:Sy4 *y4 *200 *y15 :10; [y9 - стоимость обучения 200/800/1800/3200/5000 золотых минус скидка Дипломатии]
  !!HE-1:S7/?y6 A2/0/?y7/?y12 My2/?y7; [y6 - уровень Мудрости героя, y7 - есть/нет заклинание у героя, y12 - есть/нет книжка магии]
  !!VRy8:S0;                  [y8=0, если обучение невозможно]
  !!VRz8&y3<y4=1:Sz179174;    [z8 - текст обучения, если не хватает золотых]
  !!VRz8&y12=0:Sz179176;      [z8 - текст обучения, если нет книжки]
  !!VRz8&y5=1/y6<y5:Sz179177; [z8 - текст обучения, если нет базовой мудрости]
  !!VRz8&y5=2/y6<y5:Sz179178; [z8 - текст обучения, если нет продвинутой мудрости]
  !!VRz8&y5=3/y6<y5:Sz179179; [z8 - текст обучения, если нет экспертной мудрости]
  !!VRz8&y7=1:Sz179175;       [z8 - текст обучения, если заклинание уже известно]
  !!VRy11&y7=1:S-1;           [отключаем картинку золота, если заклинание уже известно]
  !!if&y6>=y5/y12>0/y7=0/y3>=y9:; [если заклинание может быть выучено]
    !!VRy8:S1;                [y8=1, если обучение возможно]
    !!VRz8:Sz179173;          [z8 - текст обучения]
  !!en:;
  !!VRz1:Sz179172;            [z1 - текст сообщения]
  !!VRy6:S9;                  [y6 - тип 1й картинки]
  !!VRy7:Sy2;                 [y7 - подтип 1й картинки]
!!en:;
!!VRy10:Sy9 *-1 -100000;      [y10 - отрицательное количество золота для отображения в диалоге]
!!if&y8=1:;                   [если обучение возможно]
  !!IF:Q1/y6/y7/y11/y10/2/1;  [вопрос об обучении]
  !!if&1:;                    [при положительном ответе]
    !!OB998:R M-1/1/0;        [разрешение посещения объекта и отключение стандартного сообщения]
    !!VRy9:*-1;               [отрицательное значение золота]
    !!OW:R-1/6/dy9;           [уменьшение золота]
  !!en:;
!!el:;
  !!IF:Q1/y6/y7/y11/y10/1/1;  [сообщение о невозможности обучения]
!!en:;

!?FU7942;                     [подсчет втор.навыков героя в v1]
!!HE-1:Sx16/?y1;              [y1>0, если навык известен]
!!VRv1&y1>0:+1;               [увеличение v1 если навык x16 известен герою]

!?CM0;                        [клик на карте приключений]
!!UN:P783/?y1;                [проверяем включена ли опция 783 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!CM:P?y1/?y2/?y3 S?y4;       [y1-y3 - координаты клика, y4 - тип клика]
!!FU&y4<>14:E;                [выход, если не ПКМ]
!!OBy1/y2/y3:T?y4;            [y4 - тип объекта под кликом]
!!FU&y4<>81:E;                [выход, если под кликом не ученый]
!!OW:C?y5;                    [y5 - текущий игрок]
!!SN:W^o783_%Y5_%Y1_%Y2_%Y3^/?y4; [получение признака посещения ученого]
!!FU&y4<>1:E;                 [выход, если ученый ранее не посещался героями игрока]
!!CM:R0;                      [отключение стандартной реакции на клик]
!!SCy1/?y2/?y3:T?y6;          [y6 - тип бонуса ученого]
!!VRz1:Sz179205;              [z1 - текст сообщения]
!!if&y6=0:;                   [первичный навык]
  !!SCy1/y2/y3:P?y7;          [y7 - подтип бонуса]
  !!VRy7:+31;                 [...]
  !!IF:Q1/y7/1/4/1;           [вывод сообщения]
!!en:;
!!if&y6=1:;                   [вторичный навык]
  !!SCy1/y2/y3:S?y7;          [y7 - подтип бонуса]
  !!VRy7:*3 +5;               [...]
  !!IF:Q1/20/y7/4/1;          [вывод сообщения]
!!en:;
!!if&y6=2:;                   [заклинание]
  !!SCy1/y2/y3:L?y7;          [y7 - подтип бонуса]
  !!VRy7&y7>=1024:-1024;      [y7 - номер заклианния]
  !!IF:Q1/9/y7/4/1;           [вывод сообщения]
!!en:;

** end

ZVSE2
** Author        : Algor (Idea from Phoenix)
** Updated by    : Archer30
** Name          : Limited mines
** Name rus.     : Истощающиеся шахты
** Options       : 755


!?FU(OnAfterErmInstructions); [перед началом игры: установка запаса ресурсов]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!UN:X?y1/?y2;                [y1/y2 - размер/этажность карты]
!!VRy1::36 +y2;               [y1 - коэффициент от размера карты: S=1,L=2...G=7, +1 за подземелье]
!!UN:U53/-1/?y2;              [y2 - количество шахт]
!!VRv2:S-1;                   [инициализация v1 для быстрого поиска]
!!DO(ES_755_SetMineStorage)/1/y2/1:Py1; [для каждой шахты устанавливаем запас]

!?FU(ES_755_SetMineStorage);  [x1 - коэффициент от размера карты]
!!UN:U(OBJ_MINE)/-1/-1/2;             [получаем координаты шахты в v1-v3]
!!VRy1:Sx1 *10 Ry1;           [y1 - запас шахты S:10-20, L:20-40 ... G:70-140, +(10-20) за подземелье]
!!SN:W^opt755_%V2_%V3_%V4^/y1;[установка запаса]

!?FU(OnEveryDay)&i^timerDay^>1; [каждый день: истощение работающих шахт]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!UN:U(OBJ_MINE)/-1/?y2;              [y2 - количество шахт]
!!VRv2:S-1;                   [инициализация v1 для быстрого поиска]
!!DO(ES_755_ReduceMineStorage)/1/y2/1:P; [для каждой работающей шахты уменьшаем запас]

!?FU(ES_755_ReduceMineStorage); [x1 - текущий игрок]
!!UN:U(OBJ_MINE)/-1/-1/2;             [получаем координаты шахты в v1-v3]
!!MN2:O?y1/1;          [y1 - владелец шахты]
!!FU&y1<>i^timerOwner^:E;                [выход, если шахта не принадлежит текущему игроку]

!!VRi^opt755_%V2_%V3_%V4^:-1;[обновление запаса]

!!if&i^opt755_%V2_%V3_%V4^=0;[выход, если шахта не истощена]
  !!MN2:O(NO_OWNER) R?y30;             [убираем владельца]
  !!TR2:T?y2;[y2 - тип земли]
  !!UN:Iv2/v3/v4/(OBJ_BLANK_1)/0/(OBJ_SIGN)/0/y2/1; [вешаем "знак" <Путо> на вход]
  *!FU(ES_GetStringByFilename):P^minename^/y30/?(mineName:z);
  *!SG2:M^%(mineName)^;
  !!TR2:E1 P0;           [закрываем вход]
!!en;

!?FU(OnAdventureMapRightMouseClick)&i^mouse_action^=(MOUSE_RMB_PRESSED); [клик на карте приключений]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

; Check if clicked on a mine
!!CM:P?y1/?y2/?y3;                                         [координаты и параметры клика]
!!SN:O?y1/?y2/?y3;
!!OBy1/y2/y3:T?y5;                      [y5 - тип объекта]

; Get the basic info of the mine
!!if&y5=(OBJ_MINE);
  !!CM:R0;                                                   [отключение стандартного действия]
  !!MNy1/y2/y3:R?y6/1 O?y4; [y6 - ресурс]

  ; Calculate the rest of the resources
  !!SN:W^opt755_%Y1_%Y2_%Y3^/?y7;                            [y7 - запас]
  !!VRy7|y6=(RES_WOOD)/y6=(RES_ORE):*2;                                       [удвоенный запас для дерева/руды]
  !!VRy7&y6=(RES_GOLD):*1000;                                         [*1000 для золота]

  ; Set up and display the remainning res
  !!FU(ES_GetStringByFilename):P^minename^/y6/?(mineName:z);
  !!FU(ES_GameMgr_GetObjOwnerStr):Py4/?(ownerName:z);
  !!IF:Q1/y6/y7/4/^%(mineName)
%(ownerName)^;            [вывод сообщения]
!!en;

**end

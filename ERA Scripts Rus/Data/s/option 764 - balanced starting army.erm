ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : balanced starting army
** Name rus.     : Сбалансированные стартовые армии
** Options       : 764

; Note: This option will only be enabled in random maps (managed in 1000 es stdlib). Enabling this option would disable Rebalanced Starting Armies option (WoG)


!?FU(OnAfterErmInstructions);           // перед показом карты
!!UN:P764/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;                            // выход, если опция не включена

!!FU(ES_CheckRandomMap):P?(isRandMap:y);

!!DO(ES_764_Bsa)/(HERO_FIRST)/(HERO_LAST_WOG)/1:P0/0/(isRandMap); // если балансировка еще не выполнялась, выполняем

!?FU(OnEveryDay)&i^timerWeekday^=1;                       // ежедневно
!!UN:P764/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;                               // выход, если опция не включена

!!OW:Vi^timerOwner^/?(leftHero:y)/?(rightHero:y);         // герои в таверне текущего игрока
!!FU(ES_764_Bsa)&(leftHero)>(NO_HERO):P1/(leftHero)/(FALSE);    // балансировка стартовых армий героев таверны
!!FU(ES_764_Bsa)&(rightHero)>(NO_HERO):P1/(rightHero)/(FALSE);  // ...

!?FU(ES_764_Bsa);                       [Balancing the hero's starting army]
; Set up var
!!VRx16&x1=1:Sx2;                       [checks hero from x2 for heroes in taverns]
!!HEx16:O?y1;
; Exit if day 1 and the hero is not owned
!!FU&x1=0/y1=(NO_PLAYER):E;
; Exit if not day 1 and the hero is owned
!!FU&x1=1/y1>(NO_PLAYER):E;

; If the hero belongs to no one, check if in the prison. Change their army only on random maps if in the prison on day 1
!!if&y1=(NO_PLAYER);
  !!HEx16:P?y2/?y3/?y4;

  !!FU&y2>0/x3=0:E;
!!en;

!!FU&x1=1/i^opt764.H%X16.BDay^>i^timerDay^:E; [exit if the hero ran away / paid off yesterday]

!!VRy10:S4000;               [y10 - макс. FV армии героя]
!!HEx16:B2/?y1 X?y2/?y3/d/d/d/d/?i; [y1 - класс, y2/y3 - специальность]
!!VRy2&y2=4:S1;              [Учет специальности "Экстраспец по существам"]
!!VRy3&y2=6:Si;              [Учет специальности "Спец по улучшениям"]
!!VRy2&y2=6:S1;              [...]
!!VRy1::2;                   [y1 - тип родного города]
!!UN:Ty1/0/0/?y11 Ty1/1/0/?y12 Ty1/2/0/?y13; [y11-y13 типы существ по умолчанию для 1-3 слота]

!!if&y2=1/y3<>145/y3<>146/y3<>147/y3<>148/y3<>149;  [если герой спец по существам (кроме БМ), переопределяем типы существ в слотах]
  !!MA:Ly3/?y4;              [y4 - уровень спец.существа (0..6)]
  !!VRy3&y4>2/y4<6/y3<112:Sy3 :2 *2 +1; // используем улучшенных спец.существ для 4-6 уровней
  !!VRy3&y3=114:S129;        [... энергетический эл.]
  !!VRy3&y3=113:S125;        [... эл. магмы]
  !!VRy3&y3=120:S121;        [... эл. магии]

  !!UN:P67/?k;               [Совместимость с WoG-опцией "Нейтральный город"]

  !!if&k>0;                  [если опция включена, переопределяем апгрейды существ]
    !!VRy3&y3=138:S143;      [Halfling-Rogue]
    !!VRy3&y3=140:S142;      [Boar-Nomad]
    !!VRy3&y3=164:S166;      [Fire messenger-Air messenger]
    !!VRy3&y3=194:S144;      [Werewolf-Troll]
    !!VRy3&y3=193:S136;      [Sorceress-Enchanter]
    !!VRy3&y3=195:S172;      [Hell Steed-Nightmare]
    !!VRy3&y3=168:S135;      [Gorynych-Rust Dragon]
  !!en;

  !!VRy12&y4<3:Sy3;          [спецы по существам 1-3 уровней имеют их во 2 слоте]
  !!VRy12&y4>2/y4<6:Sy11;    [спецы по существам 4-6 уровней имеют во 2 слоте существ 1го уровня]
  !!VRy13&y4<6:Sy3;          [спецы по существам 1-6 уровней имеют их в 3 слоте]
!!en; 

** Герои с машинным навыком получают соотв. машину
!!HEx16:S10/?y51 S20/?y52 S27/?y53; [y51/y52/y53 - навыки Баллистики/Артиллерии/ПП у героя]
!!VRy51&y2=1/y3=148:S1;      [Тележка положена спецам по тележкам (а вдруг!)]
!!VRy51&y2=0/y3=10:S1;       [Тележка положена спецам по Баллистике/Осадному делу]
!!VRy52&y2=1/y3=146:S1;      [Баллиста положена спецам по баллистам]
!!VRy52&y2=0/y3=20:S1;       [Баллиста положена спецам по Артиллерии]
!!VRy53&y2=1/y3=147:S1;      [Палатка положена спецам по Палаткам]
!!VRy53&y2=0/y3=27:S1;       [Палатка положена спецам по Первой помощи/Медицине]
!!HEx16&y51>0:A1/5/14;       [даем Тележку]
!!VRy10&y51>0:-500;          [уменьшаем макс. FV армии на FV боевой машины]
!!HEx16&y52>0:A1/4/13;       [даем Баллисту]
!!VRy10&y52>0:-1500;         [уменьшаем макс. FV армии на FV боевой машины]
!!HEx16&y53>0:A1/6/15;       [даем Палатку]
!!VRy10&y53>0:-750;          [уменьшаем макс. FV армии на FV боевой машины]
!!FU(WOG_73_Func_CorrectHeroVariablesW)&i^WOG_73_enabled^:Px16; совместимость с боевыми машинами III
** Распределение сил по слотам:
** 3й слот - 30-60% FV
** 2й слот - 40-80% оставшегося FV
** 1й слот - оставшееся FV
!!MA:Fy11/?y31 Fy12/?y32 Fy13/?y33; [y31-y33 -  FV существ в 1-3 слотах]
!!VRy30:Sy10 *3 :5 :y33;     [y30 - макс. кол-во существ в 3 слоте]
!!VRy40:Sy30 :2;
!!VRy40&y40<1:S1;            [y40 - мин кол-во существ (50% от максимума, но не менее 1)]
!!VRy30:-y40;
!!VRy30&y30<0:S0;            [y30 - доп. кол-во существ (не меньше 0)]
!!VRy43:Sy40 Ry30;           [y43 - кол-во существ в 3 слоте]
!!VRy30:Sy43 *y33;           [y30 - FV 3го слота]
!!VRy10:-y30;                [y10 - оставшееся FV]
!!VRy10&y10<0:S1;            [...но не меньше 1]
!!VRy30:Sy10 *4 :5 :y32 -1;  [y30 - макс. кол-во существ во 2 слоте -1]
!!VRy40:Sy30 :2;
!!VRy40&y40<1:S1;            [y40 - мин кол-во существ (50% от максимума, но не менее 1)]
!!VRy30:-y40;
!!VRy30&y30<0:S0;            [y30 - доп. кол-во существ (не меньше 0)]
!!VRy42:Sy40 Ry30;           [y43 - кол-во существ во 2 слоте]
!!VRy30:Sy42 *y32;           [y30 - FV 2го слота]
!!VRy10:-y30;                [y10 - оставшееся FV]
!!VRy10&y10<0:S1;            [...но не меньше 1]
!!VRy41:Sy10 :y31;           [y41 - кол-во существ в 1 слоте]
!!VRy41&y41<1:S1;            [...но не меньше 1]
!!HEx16:C0/0/y11/y41 C0/1/y12/y42 C0/2/y13/y43; [установка текущей армии героя]

!?FU(OnBeforeBattleAction);             [перед действием в бою]
!!UN:P764/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [Выход, если опция не включена]

!!BG:A?(action:y);
!!FU|(action)<(BATTLE_ACTION_RETREAT)/(action)>(BATTLE_ACTION_SURRENDER):E;            [выход, если не сдача/откуп]

!!BG:Q?(side:y);
!!BA:H(side)/?(hero:y);                 [сбегающий/откупающийся герой]
!!VRi^opt764.H%(hero).BDay^:Si^timerDay^ +2; [установка минимального дня для балансировки стартовой армии героя]

** end

ZVSE2
** Author orig.  : Неизвестен
** Rewritten by  : Archer30
** Name          : Block snipers and wraiths
** Name rus.     : Блок Снайперов и Призраков
** Changes rus.  : [Algor] вынос опции в отдельный erm-файл для мода ERA
** Options       : 822


!?FU(OnAfterErmInited);
!!UN:P822/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Set new monster descriptions
!!SN:H^monname^/(MON_WIGHT)/2/?(desc:z);
!!SN:H^monname^/(MON_WIGHT)/2/^%(desc)%T(es.822.wraithDesc)^;

!!SN:H^monname^/(MON_WRAITH)/2/?(desc:z);
!!SN:H^monname^/(MON_WRAITH)/2/^%(desc)%T(es.822.wraithDesc)^;

!!SN:H^monname^/(MON_ARCTIC_SHARPSHOOTER)/2/?(desc);
!!SN:T^es.822.shooterDesc^/?(newDesc:z)/^percent^/15;
!!SN:H^monname^/(MON_ARCTIC_SHARPSHOOTER)/2/^%(desc)%(newDesc)^;

!!SN:H^monname^/(MON_LAVA_SHARPSHOOTER)/2/?(desc);
!!SN:T^es.822.shooterDesc^/?(newDesc:z)/^percent^/20;
!!SN:H^monname^/(MON_LAVA_SHARPSHOOTER)/2/^%(desc)%(newDesc)^;

; Reduce the HP of Wights and Wraiths (for balance purpose)
!!MA:P(MON_WIGHT)/?(hp:y);
!!VR(newHp:y):S(hp) :3 *2;
!!MA:P(MON_WIGHT)/(newHp);

!!MA:P(MON_WRAITH)/?(hp:y);
!!VR(newHp:y):S(hp) :6 *5;
!!MA:P(MON_WRAITH)/(newHp);

!?FU(OnStackToStackDamage);
!#VA(atkStack:x) (defStack:x) (finalDmgConst:x) (finalDmg:x) (basicDmg:x) (dmgBonus:x) (isDistant:x) (distanceArg:x) (isTheoretical:x);

!!UN:P822/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BM(defStack):T?(mon:y);

; Artic Sharpshoooter and Lava Sharpshooter blocking (15% and 20% damage reduction)
!!FU(ES_822_CheckMonDamageReduction):P(mon)/?(dmgReduction:y);

!!if&(dmgReduction)>0;
  !!VR(finalDmg):S100 -(dmgReduction) *(finalDmgConst) :100; [Calculate the Final Damage with Final Damage Constant only (since OnStackToStackDamage triggers multiple times)!]
  !!FU:E;
!!en;

; Wight and Wraith blocking (30% chance to evade an attack)
; Skip theoretical (mouse hover)
!!FU&(isTheoretical)=(TRUE):E;

!!FU(ES_822_CheckIfMonHasBlock):P(mon)/?(result:y);

!!if&(result);
  !!VR(random:y):R0/0/99;

  !!if&(random)<30;                     [30% chance]
    !!VR(finalDmg):S0;

    !!if&i^battle_isVisible^;
      ; Show animation and battle log
      !!BM(defStack):N?(num:y);
      !!VR(isPlural:y):S(num) -1 B;
      !!SN:H^monname^/(mon)/(isPlural)/?(wraithName:z);
      !!SN:T^es.822.battleLog^/?(battleLog:z)/^mon^/(wraithName);
      !!MM:S(battleLog);

      // Set up defence animation (by setting defence flag)
      !!BM(defStack):V84 F?(flags:y);
      !!VR(isDefending:y):S(flags) &(MON_FLAG_DEFENDING);

      !!if&(isDefending)=(FALSE);
        !!VRi^es_822_blockingStack^:S(defStack) +1;
        !!BM(defStack):Fd|(MON_FLAG_DEFENDING);
      !!en;
    !!en;
  !!en;
!!en;

// Restore defence flag if set for animation
!?FU(ES_OnAfterAttack)&i^battle_isVisible^/i^es_822_blockingStack^>0;
!!VR(stack:y):Si^es_822_blockingStack^ -1;
!!BM(stack):Fd~(MON_FLAG_DEFENDING);
!!VRi^es_822_blockingStack^:S0;

// Funciton to check if the targeted monster is eligible for damage reduction
!?FU(ES_822_CheckMonDamageReduction);
!#VA(mon:x) (dmgReduction:x);

!!VR(dmgReduction):S0;
!!VR(dmgReduction)&(mon)=(MON_ARCTIC_SHARPSHOOTER):S15; [15% damage reduction]
!!VR(dmgReduction)&(mon)=(MON_LAVA_SHARPSHOOTER):S20; [20% damage reduction]

// Funciton to check if the targeted monster is eligible for block (not working for monster with sharpshooter's damage reduction)
!?FU(ES_822_CheckIfMonHasBlock);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)|(mon)=(MON_WIGHT)/(mon)=(MON_WRAITH):S(TRUE);

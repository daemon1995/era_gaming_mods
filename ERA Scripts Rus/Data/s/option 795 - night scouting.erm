ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30 and daemon_n
** Name          : Night scouting
** Name rus.     : Ночная разведка
** Options       : 795
** Dialogs       : 77
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7916, FU7997-FU7998,FU8001-FU8009
** PO-values     : -

** Перед началом и в процессе игры на некоторых свободных участках карты размещаются случайные события
** найти и активировать которые может любой герой заночевавший на данном или соседнем участке.
** Герои с навыком Разведка и/или артефактами Телескоп и Подзорная труба во время ночлега
** могут искать события на большем количестве соседних участков.
** За одну ночевку активируется не более одного события (даже если в "радиусе поиска" их несколько).
** После активации событие исчезает не зависимо от того, восползовался им герой или нет.
** ИИ использует события, только если они ему выгодны.


** Сдвиг/скрытие кнопки "Ассасины" (спс igrik)
!?FU(ES_OnUpdateHeroScreen);         // перед обновлением экрана героя
!!UN:P795/?y1;                          // проверка опции
!!UN:C6916808/4/?y2;                    // адрес структуры диалога героя
!!SN:F^GetButtonID^/^assasins^;         // получаем ID кнопки assasins
!!FU&v1<1:E;

!!VRy3:Sv1;                             // перезаписываем ID кнопки
!!SN:E6288816/2/y2/y3;                  // получаем структуру кнопки
!!FU&v1=0:E;

!!VRy4:Sv1;                             // перезаписываем структуру кнопки
!!SN&y1=0:E6286720/2/y4/6/6;            // скрываем кнопку (если опция отключена)
!!SN:L^HD_WOG.dll^/?y5;                 // HD-мод: 0 - значит НЕ включен
!!FU&y5<>0|y1=0:E;                      // выход, если HD включен или опция выключена

!!UN&y4<>0:Cy4/24/2/d40;                // сдвигаем кнопку, если HD нет и опция ВКЛ
!!SN:E6288816/2/y2/111;                 // получаем структуру текста "уволить героя" id=111
!!SN&v1<>0:E6286720/2/v1/6/6;           // прячем текст "уволить героя"

!?FU(OnAfterErmInstructions); [перед началом новой игры]
!!UN:P795/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;   [выход если опция не включена]

** Установка описаний навыка Разведка

!!re i/(SKILL_EXPERT)/(SKILL_BASIC)/-1; from exp to bas
  !!VR(range:y):Si*4;
  !!SN:H^secskill^/(SKILL_SCOUTING)/i/?(desc:z) T^es.795.desc^/?(desc1:z)/^range^/(range:y);
  !!SN:H^secskill^/(SKILL_SCOUTING)/i/^%(desc)%(desc1)^;
!!en;


** Установка описаний артефактов
!!SN:H^art^/(ART_SPECULUM)/1/?(desc);
!!SN:H^art^/(ART_SPECULUM)/1/^%(desc)%(desc1)^;

!!SN:H^art^/(ART_SPYGLASS)/1/?(desc);
!!SN:H^art^/(ART_SPYGLASS)/1/^%(desc)%(desc1)^;

** Генерация событий
!!UN:X?(size:y)/?(level:y);                          [размер/этажность карты]
!!VR(maxEvents:y):S(level) +1 *(size) *(size) :36;   [макс. количество генерируемых событий (одно на 36 квадратов карты)]
!!VR(maxCoord:y):S(size) -1;                         [макс. координата]
!!DO7916/1/(maxEvents)/1:P(size)/(level);            [генерация события]

!?FU(OnEveryDay)&i^timerDay^>1;         [в начале дня начиная со второго]
!!UN:P795/?y1;
!!FU&y1=0:E;   [выход если опция не включена]

** event generation (up to 1 event for each player on each turn)
!!UN:X?(mapSize:y)/?(twoLvlMap:y);                [y1/y2 - размер/этажность карты]
!!VR(mapSize):-1;                    [y1 - макс. координата]
!!FU7916:P(mapSize)/(twoLvlMap);              [генерация события]

** detect events for each hero of the current player
!#VA(hero[8]:y);
!!OW:Oi^timerOwner^/?(hrOnMap:y)/?(hero[0])/?(hero[1])/?(hero[2])/?(hero[3])/?(hero[4])/?(hero[5])/?(hero[6])/?(hero[7]);

!!if&(hrOnMap);
  !!re i/0/(hrOnMap)/1/-1;

    !!if&(hero[i])<>(NO_HERO);
      !!HE(hero[i]):P?(x:y)/?(y:y)/?(z:y) O?(checkOwner:y);

      !!if&(x)>-1/(y)>-1/(checkOwner)=i^timerOwner^;
        !!HE(hero[i]):S(SKILL_SCOUTING)/?(radius:y);
        !!FU(ES_795_GetArtNightScoutingRadius):P(hero[i])/?(artRadius:y); [Here we use a function for mod compatibility]
        !!VR(radius):+(artRadius) *4 +5;
        
        !!DO(ES_795_SearchEventForPlayerHero)/1/(radius)/1:Pi^timerOwner^/i^timerIsAi^/(mapSize)/(hero[i])/(x)/(y)/(z);
      !!en;
    !!en;
  !!en;
!!en;

; Function for mod compatibility
!?FU(ES_795_GetArtNightScoutingRadius);
!#VA(hero:x) (result:x);

!!VR(result):S0;
!!HE(hero):A2/(ART_SPECULUM)/d/?(hasSpec:y) A2/(ART_SPYGLASS)/d/?(hasSpy:y);
!!VR(result)&(hasSpec):+1;
!!VR(result)&(hasSpy):+1;

!?FU(ES_795_SearchEventForPlayerHero);
!#VA(player:x) (isAi:y) (size:x) (heroId:y) (x:x) (y:x) (z:x);

** x1 - игрок, x2 - ИИ, x3 - макс координата, x4 - герой, x5/x6/x7 - координаты героя, x16 - исследуемый квадрат
!!VRy1:Sx5;
!!VRy2:Sx6;
!!VRy3:Sx7;                         [y1/y2/y3 - координаты исследуемого участка]
!!VRy2&x16=2:-1;                    [...]
!!VRy1&x16=3:+1;                    [...]
!!VRy2&x16=4:+1;                    [...]
!!VRy1&x16=5:-1;                    [...]
!!VRy1&x16=6:-1;
!!VRy2&x16=6:-1;                    [...]
!!VRy1&x16=7:+1;
!!VRy2&x16=7:-1;                    [...]
!!VRy1&x16=8:+1;
!!VRy2&x16=8:+1;                    [...]
!!VRy1&x16=9:-1;
!!VRy2&x16=9:+1;                    [...]
!!VRy2&x16=10:-2;                   [...]
!!VRy1&x16=11:+2;                   [...]
!!VRy2&x16=12:+2;                   [...]
!!VRy1&x16=13:-2;                   [...]
!!VRy1&x16=14:+1;
!!VRy2&x16=14:-2;                   [...]
!!VRy1&x16=15:+2;
!!VRy2&x16=15:+1;                   [...]
!!VRy1&x16=16:-1;
!!VRy2&x16=16:+2;                   [...]
!!VRy1&x16=17:-2;
!!VRy2&x16=17:-1;                   [...]
!!VRy1&x16=18:-1;
!!VRy2&x16=18:-2;                   [...]
!!VRy1&x16=19:+2;
!!VRy2&x16=19:-1;                   [...]
!!VRy1&x16=20:+1;
!!VRy2&x16=20:+2;                   [...]
!!VRy1&x16=21:-2;
!!VRy2&x16=21:+1;                   [...]
!!VRy1&x16=22:-2;
!!VRy2&x16=22:-2;                   [...]
!!VRy1&x16=23:+2;
!!VRy2&x16=23:-2;                   [...]
!!VRy1&x16=24:+2;
!!VRy2&x16=24:+2;                   [...]
!!VRy1&x16=25:-2;
!!VRy2&x16=25:+2;                   [...]
!!FU|y1<0/y2<0/y1>x3/y2>x3:E;       [выход, если координаты за пределами карты]

!!SN:W^o795.%Y1.%Y2.%Y3.T^/?y4;     [y4 - тип события]

!!if&y4>0; 
  !!SN:W^o795.%Y1.%Y2.%Y3.P1^/?y5;  [y5-y9 - параметры события]
  !!SN:W^o795.%Y1.%Y2.%Y3.P2^/?y6;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P3^/?y7;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P4^/?y8;  [...]
  !!SN:W^o795.%Y1.%Y2.%Y3.P5^/?y9;  [...]

  !!VRv1:Sy4 +8000;                 [v1 - номер функции обработчика события]
  !!FUv1:Px1/x4/x2/y1/y2/y3/y5/y6/y7/y8/y9; [Handle function call - FU8000, FU8001, FU8002, FU8003, FU8004, FU8005, FU8006, FU8007, FU8008, FU8009]

  !!SN:W^o795.%Y1.%Y2.%Y3.T^/?y10;  [y10 - тип события]
  !!VRx16&y10=0:S100500;            [прерывание поиска событий, если событие активировано]
!!en; 

!?FU8097;                           [вызов битвы]
!!HEx2:Tx4/x5/x6/0/1;               [вызов битвы]

!?FU8098;                           [возврат в v1 номера отряда попадающего под критерии отбора]
** x1-номер героя, x2-x4-критерии отбора
** x2=0/1/2/3/4/5/6 - любой/пеший/стрелок/летун/живой/нежить/элементаль
** x3=0/1/2/3/4 - проверяемый параметр Уровень/FV/Численность/Сила(FV*кол-во)/Опыт
** x4=0/1 - максимум/минимум
!!if&x16=0;                         [инициализация переменных при первом запуске]
  !!VRv1:S-1;                       [v1 - найденный отряд]
  !!VRv2&x4=0:S-1;
  !!VRv2&x4=1:S2000000000;          [v2 - оцениваемый параметр]
!!en; 

!!HEx1:C0/x16/?y1/?y2/?y3/2;        [y1/y2/y3 - тип/кол-во/опыт существ]
!!FU|y1<0/y2<1:E;                   [выход, если отряда нет]

!!MA:Xy1/?y4;                       [y4 - флаги существа]
!!VRy5:Sy4 &2;                      [y5 - летун]
!!VRy6:Sy4 &16;                     [y6 - живой]
!!VRy7:Sy4 &262144;                 [y7 - нежить]
!!VRy4:&4;                          [y4 - стрелок]
!!FU&x2=1/y4>0:E;
!!FU&x2=1/y5>0:E;                   [выход если тип отряда не совпадает с требуемым]
!!FU&x2=2/y4=0:E;
!!FU&x2=3/y5=0:E;                   [...]
!!FU&x2=4/y6=0:E;
!!FU&x2=5/y7=0:E;                   [...]
!!FU&x2=6/y6>0:E;
!!FU&x2=6/y7>0:E;                   [...]

!!MA&x3=0:Ly1/?y3;                  [y3 - проверяемый параметр]
!!MA|x3=1/x3=3:Fy1/?y3;             [...]
!!VRy3&x3=2:Sy2;                    [...]
!!VRy3&x3=3:*y2;                    [...]
!!VRv1&y3>v2/x4=0:Sx16;             [обновление максимума/минимума/номера отряда]
!!VRv2&y3>v2/x4=0:Sy3;              [...]
!!VRv1&y3<v2/x4=1:Sx16;             [...]
!!VRv2&y3<v2/x4=1:Sy3;              [...]

!?FU(ES_795_Num2Def);            [возврат в zx2 названия def-файла № x1 для диалогов "Ночной разведки"]
!!VRzx2&x1=0:S^not used^;           [...]
!!VRzx2&x1=1:S^twcrport.def^;       [...]
!!VRzx2&x1=2:S^SpellBon.def^;       [...]
!!VRzx2&x1=3:S^BoRes.def^;          [...]
!!VRzx2&x1=4:S^opt795ps.def^;       [...]
!!VRzx2&x1=5:S^sskilbon.def^;       [...]
!!VRzx2&x1=6:S^artifact.def^;       [...]
!!VRzx2&x1=7:S^opt795he.def^;       [...]

!?FU(ES_795_ShowDialog);         [формирование и вызов диалога]
** x1-герой, x2/x3-тип/номер НПЦ, x4-кол-во картинок (0..3), x5-переменная для возврата результата выбора (1-да, 2-нет),
** x6/x7-тип/номер левой картинки, x8/x9-тип/номер центральной картинки, x10/x11-тип/номер правой картинки, 
** x12 - запрет открытия окна героя, x13 - номер события
** Типы: 0-герой, 1-монстр, 2-заклинание, 3-ресурс, 4-перв.параметр, 5-втор.навык/ранг, 6-артефакт
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
!!UN:C6885977/1/?y1;                                       [if gosolo mode is active]
!!if&y1;
  !!FU:E;
!!en;
!!VRx5:S0;                    [инициализация]
!!DL77:N^opt795_%X4.txt^;     [подгрузка 77 диалога]

!!if&-1;                      [если диалог не найден]
  !!IF:M0/4/^ERROR: Dialog not found!^; [выводим сообщение об ошибке]
  !!FU:E;                     [и выходим]
!!en;

!!VRx3&x2=1:+2;
!!VRx7&x6=1:+2;
!!VRx9&x8=1:+2;
!!VRx11&x10=1:+2;             [Смещение кадров в стндартном def'е существ]
!!FU(Fun_GetHeroPortrait):Px1/1/30;     // z30 - имя файла с портретом героя
!!DL77:A10/11/z30/1;
!!VRv101:Cx1/x2/x3/x4/x5/x6/x7/x8/x9/x10/x11; [установка номера и портрета героя]
!!FU(ES_795_Num2Def):Px2/12;
!!DL77:A12/9/z12/1 A12/4/x3/1; [установка портрета НПЦ]
!!DL77:A7/3/z7/1;
!!DL77:A8/3/z8/1;             [установка текстов]
!!VRy1:Sx13 -1;
!!DL77:A30/4/y1/1;            [установка шапки]

!!if|x4=2/x4=3;               [отображение левой картинки (для случая 2 или 3 картинок)]
  !!FU(ES_795_Num2Def):Px6/13;
  !!DL77:A14/9/z13/1 A14/4/x7/1; [установка картинки]
  !!DL77:A15/3/z9/1;          [установка подписи]
!!en; 

!!if|x4=1/x4=3;               [отображение средней картинки (для случая 1 или 3х картинок)]
  !!FU(ES_795_Num2Def):Px8/14;
  !!DL77:A17/9/z14/1 A17/4/x9/1; [установка картинки]
  !!DL77:A18/3/z10/1;         [установка подписи]
!!en; 

!!if|x4=2/x4=3;               [отображение левой картинки (для случая 2 или 3 картинок)]
  !!FU(ES_795_Num2Def):Px10/15;
  !!DL77:A20/9/z15/1 A20/4/x11/1; [установка картинки]
  !!DL77:A21/3/z11/1;         [установка подписи]
!!en; 

!!VRv7:Sx12;                  [передача запрета открытия окна героя]
!!VRz31:Sz179096;
!!VRz32:Sz179097;
!!VRz33:Sz179098;             // получение подсказок из ERT
!!DL77:H1/z31 H2/z32 H9/z33;            // установка подсказок в диалог
!!DL77:S?x5;                  [вызов диалога и получение результата выбора (1-да, 2-нет)]

!?FU(OnCustomDialogEvent)&i^dlg_id^=77/i^mouse_action^=(MOUSE_LMB_RELEASED); [обработка диалога 77 (отпущена ЛКМ)]
!!DL77|i^mouse_item^=1/i^mouse_item^=2:C1;                [кнопки "ОК"/"Отмена"]

!!if&i^mouse_item^=9/v7=0;                       [портрет героя, если не запрещено]
  !!UN:C5119174/1/184 C5119175/4/1;     [патч: деактивация кнопки "Уволить"]
  !!SN:E5118576/1/v101/0;               [открыть окно героя ЛКМ]
  !!UN:C5119174/1/161 C5119175/4/6916832; [патч: вернуть исходный код кнопки "Уволить"]
!!en; 

!!if&i^mouse_item^=14/v106=6;                    [клик по левой картинке-артефакту]
  !!UN:C6687592/4/?y12;                 [y12 - ссылка на таблицу артефактов]
  !!VRy13:Sv107 *32 +y12 +16;           [y13 - адрес указателя с описанием артефакта]
  !!UN:Cy13/4/?y14;                     [y14 - получить указатель на описание артефакта]
  !!SN:X?y9 Xy14 X?z3 Xy9;              [z3 - хранит описание артефакта]
  !!IF:Q1/8/v107/4^%Z3^;                [вывод окна с картинкой и названием артефакта]
!!en; 

** Генерация событий

!?FU7916;                     [генерация события, x1/x2 - размер/этажность карты]
!!VRy1:S0 Rx1;
!!VRy2:S0 Rx1;
!!VRy3:S0 Rx2;                [y1/y2/y3 - координаты события]
!!OBy1/y2/y3:T?y4;            [y4 - тип объекта в позиции]
!!TRy1/y2/y3:E?y5 P?y6;       [y5/y6 - "желтый"/"красный" квадрат]
!!TRy1/y2/y3:T?y7/d/d/d/d/d/d/d; [y7 - тип почвы]
!!FU|y4>0/y5=0/y6=0/y7>7:E;   [выход, если квадрат не свободен или морской]

** генерация события
!!VRy10:S0;
!!VRy11:S0;
!!VRy12:S0;
!!VRy13:S0;
!!VRy14:S0;
!!VRy15:S0;                   [инициализация параметров события]
!!VRy10:S1 R8;                [y10 - тип события (1..9)]

** y11-y15 - параметры события
!!if&y10=1;                   [Тайник: P1 = тип ресурса, P2 = Кол-во ресурса, P3 = тип охраны, P4 = кол-во охранников]
  !!VRy11:S0 R6;              [тип ресурса]
  !!VRy12:S5 R8;              [кол-во ресурса 5-13 шт]
  !!VRy13:S0 R7 *14 +y12 -5;  [тип охраны]
  !!VRy14:S15 R10;            [кол-во охранников]
  !!VRy12|y11=0/y11=2:*2;     [дерево и руда х2]
  !!VRy12&y11=6:*200;         [золото х200]
!!el&y10=2;                   [Продавец артефакта: P1 = тип артефакта, P2 = тип ресурса, P3 = Кол-во ресурса]
  !!UN:J6/6/?y11;             [артефакт класса сокровище..ценный]
  !!VRy12:S0 R6;              [тип ресурса]
  !!UN:Ay11/3/?y13;           [класс артефакта (2/4)]
  !!VRy13:*1000;              [стоимость в золоте 2000/4000]
  !!VRy13|y12=0/y12=2::250;   [:250 для стоимости в дереве/руде]
  !!VRy13|y12=1/y12=3/y12=4/y12=5::500; [:500 для стоимости в драг.ресурсах]
!!el&y10=3;                   [Инструктор: P1-P3 = критерии выбора отряда]
  !!VRy11:S0 R3;              [любой/пеший/стрелок/летун]
  !!VRy12:S0 R4;              [Уровень/FV/Численность/Сила(FV*кол-во)/Опыт]
  !!VRy13:S0 R1;              [максимум/минимум]
!!el&y10=4;                   [Наставник: P1 = параметр, P2 = увеличение параметра, P3 = тип ресурса, P4 = кол-во ресурса]
  !!VRy11:S0 R3;              [Параметр 0..3]
  !!VRy12:S1 R2;              [увеличение параметра 1..3]
  !!VRy13:S0 R6;              [тип ресурса]
  !!VRy14:Sy12 *5;            [стоимость 5 ресурса за 1 ед параметра]
  !!VRy14&y13=6: *200;        [х200 для Золота]
  !!VRy14|y13=0/y13=2: *2;    [х2 для Дерева/Руды]
!!el&y10=5;                   [Исчезающий призрак: P1 = требуемое кол-во маны, P2 = тип ресурса, P3 = кол-во ресурса]
  !!VRy11:S1 R4;              [ценность схрона]
!!el&y10=6;                   [Некромант: P1 = повышение ранга]
  !!VRy11:S0 R3;              [критерий выбора отряда 0..3]
  !!VRy12:S1 R2;              [макс.бонус уровня 1..3]
  !!VRy13:S0 R2;              [грейд 0..2]
!!el&y10=7;                   [Барахольщик: P1 множитель цены]
  !!VRy11:S75 R50;            [множитель цены - 75-125%]
!!el&y10=8;                   [Конфликт]
  !!VRy16:S0 R8;              [y16 - город защищающихся]
  !!VRy17:Sy16 +3 R3 %9;      [y17 - город атакующих (другое мировоззрение)]
  !!VRy18:S0 R6;              [y18 - уровень защищающихся]
  !!VRy19:S0 R1;              [y19 - грейд защищающихся]
  !!VRy20:Sy18 +1 R2;         [y20 - уровень атакующих (на 1..3 уровня больше, чем защищающихся)]
  !!VRy20&y20>6:S6;           [... максимум 7й уровень]
  !!VRy21:S0 R1;              [y19 - грейд атакующих]
  !!UN:Ty16/y18/y19/?y11;     [y11 - защищающиеся существа]
  !!UN:Ty17/y20/y21/?y12;     [y12 - атакующие существа существа]
  !!VRy13:S4 R8;              [y13 - 3 х коэфф.превосходства (4..12)]
!!el&y10=9;                   [Ассасины: P1 = размер группы, P2 = стоимость найма]
  !!VRy11:Sc +10 R20;         [размер группы = день размещения события +10..30 юнитов]
  !!VRy12:S50 R50 *y11 :500 *500; [стоимость найма = 50..100 золотых за ассасина, с округлением общей суммы до 500 золотых вниз]
!!en; 
**
!!SN:W^o795.%Y1.%Y2.%Y3.T^/y10;  [установка параметров события]
!!SN:W^o795.%Y1.%Y2.%Y3.P1^/y11; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P2^/y12; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P3^/y13; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P4^/y14; [...]
!!SN:W^o795.%Y1.%Y2.%Y3.P5^/y15; [...]

** Обработчики событий - FU80##
** x1 - игрок, x2 - герой, x3 - 0/1=Человек/ИИ, x4-x6 - координаты события, x7-x11 - параметры события

; Hidden Stash
!?FU8001;                     [Тайник]
** Обнаружение тайника с ресурсами
** P1 = тип ресурса, P2 = Кол-во ресурса, P3 = тип охраны, P4 = кол-во охранников
** проверка условия срабатывания
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!OW:Rx1/x7/dx8;            [ИИ получает ресурсы без боя]
!!el;                         [обработчик игрока-человека]
  !!VRy7:S0;
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
  !!VRz7:Sz179307;
  !!VRz8:Sz179308;            [получение описаний события]
  !!VRy1:Sx8 -3 R7;
  !!VRy1&x7=6:Sx8 -600 R1200 :100 *100;
  !!VRy2:Sx10 -5 R11;         [примерные количества ресурса и охраны]
  !!VRz9:S^~ %Y1^;
  !!VRz11:S^~ %Y2^;           [формирование подсказок]
  !!FU(ES_795_ShowDialog):Px2/1/x9/2/?y7/3/x7/0/0/1/x9/0/1; [вызов диалога и получение ответа в y7 (1-да,2-нет)]
  !!FU&y7<>1:E;               [выход, если человек отказался от активации события]

  !!HEx2:Tx4/x5/x6/x9/x10;    [вызов битвы]
  !!HEx2:O?y1;                [y1 - владелец героя]

  !!if&y1<>-1;                [если герой жив]
    !!OW:Rx1/x7/dx8;
    !!UN:R2;                  [игрок получает ресурсы]
    !!VRz1:Sz179309;          [z1 - текст сообщения]
    !!IF:Q1/x7/x8/1/1;        [вывод сообщения]
  !!en; 
!!en; 

!?FU8002;                     [Продавец артефакта]
** Artifact seller for resources
** p1 = Artifact Type, p2 = Resource Type, p3 = Resource Quantity
** checking the trigger condition
!!OW:Rx1/x8/?y5;              [y5 - кол-во требуемого ресурса у игрока]
!!FU&y5<x9:E;                 [событие не случается, если ресурса недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [AI player handler]
  !!HEx2:A2/x7/?y1/?y2;       [y1/y2 - кол-во артефактов предлагаемого типа у героя]
  !!UN:Ax7/2/?y3;             [y3 - позиция предлагаемого артефакта]
  !!HEx2:A1/?y10/0 A1/?y11/1 A1/?y12/2 A1/?y13/3 A1/?y14/4 A1/?y15/5 A1/?y16/6 A1/?y17/7 A1/?y18/8; [Artifacts on the hero]
  !!HEx2:A1/?y19/9 A1/?y20/10 A1/?y21/11 A1/?y22/12 A1/?y23/18;                                     [...]
  !!VRy1&y3=1/y10>=0:S1;      [проверка занятости необходимого слота]
  !!VRy1&y3=2/y11>=0:S1;      [...]
  !!VRy1&y3=3/y12>=0:S1;      [...]
  !!VRy1&y3=4/y13>=0:S1;      [...]
  !!VRy1&y3=5/y14>=0:S1;      [...]
  !!VRy1&y3=6/y15>=0:S1;      [...]
  !!VRy1&y3=7/y16>=0/y17>=0:S1; [...]
  !!VRy1&y3=8/y18>=0:S1;      [...]
  !!VRy1&y3=9/y19>=0/y20>=0/y21>=0/y22>=0/y23>=0:S1; [...]
  !!FU|y1>0/y2>0:E;           [Exit if the hero already has the artifact or all slots available to him are occupied]

  !!VRy5:-x9;                 [вычитание стоимости артефакта]
  !!OW:Rx1/x8/y5;             [обновление ресурсов игрока]
  !!HEx2:A4/x7;               [передача артефакта герою]
!!el;                         [Human player handler]
  !!VRy1:Sx8 +179311;         [y1 - индекс переменной с описанием события]
  !!VRz7:Szy1;
  !!VRz8:Sz179318;            [получение описаний события]
  !!VRy1:S3 +x8;              [тип НПЦ в зависимости от ресурса]
  !!VRz9:S^+1^;
  !!VRz11:S^- %X9^;          [формирование подсказок]
  !!FU(ES_795_ShowDialog):Px2/7/y1/2/?y7/6/x7/0/0/3/x8/0/2; [вызов диалога и получение ответа в y7 (1-да,2-нет)]
  !!FU&y7<>1:E;               [выход, если человек отказался от активации события]

  !!VRy5:-x9;                 [вычитание стоимости артефакта]
  !!OW:Rx1/x8/y5;
  !!UN:R2;                    [обновление ресурсов игрока]
  !!HEx2:A4/x7;               [передача артефакта герою]
!!en; 

!?FU8003;                     [Инструктор]
** Максимизация опыта отряда за деньги и время
** P1-P3 = критерии выбора отряда
!!UN:P900/?y1;
!!FU&y1=0:E;                  [выход, если опыт отрядов не включен]

!!DO8098/0/6/1:Px2/x7/x8/x9;  [возврат в v1 номера отряда попадающего под критерии отбора]
!!VRy1:Sv1;
!!FU&y1<0:E;                  [y1 - найденный отряд. Выход, если подходящего отряда нет]

!!HEx2:C0/y1/?y2/?y3/?y4/2;   [y2/y3/y4 - тип/кол-во/опыт отряда]
!!EAy2:L?y5;                  [y5 - опыт 10го ранга]
!!FU&y4>=y5:E;                [выход, если отряд уже имеет 10 ранг опыта]

!!VRy6:Sy5 -y4;               [y6 - необходимое количество опыта]
!!VRy7:Sy4 *100 :y5 -100 *-1; [y7 - % опыта, который надо получить отряду до 10 ранга]
!!MA:Fy2/?y8;                 [y8 - FV существ в отряде]
!!VRy8:*y3;                   [y8 - FV отряда]
!!VRy8:*y7 :500;              [y8 - стоимость тренировки FV/500 за 1% опыта]
!!VRy8&y8<100:S100;           [не менее 100 золотых]
!!OW:Rx1/6/?y9;               [y9 - золото текущего игрока]
!!FU&y9<y8:E;                 [выход, если средств не достаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!VRy10:Sy9 :2;             [y10 - половина золотого запаса ИИ]
  !!FU&y8>y10:E;              [выход, если цена превышает половину имеющегося золота]

  !!MA:Ly2/?y10;              [y10 - уровень существ в отряде (0..6)]
  !!VRy10:*-1 +7 *y10 *4;     [y10 - мин. кол-во сущест для тренировки (98/72/50/32/18/8/2 существ соотв. 1..7 уровня]
  !!HEx2:C0/y1/d/?y11;        [y11 - кол-во существ в тренируемом отряде]
  !!FU&y11<y10:E;             [выход, если существ недостаточно для тренировки]

  !!VRy8:*-1;                 [y8 - золото для списания]
  !!OW:Rx1/6/dy8;             [списание средств]
  !!HEx2:C0/y1/d/d/y5/2 W0/1; [максимизация опыта отряда и трата ОД]
!!el;                         [обработчик игрока-человека]
  !!VRy10:Sx7 +179320;        [y10 - индекс переменной с описанием события]
  !!VRz7:Szy10;
  !!VRz8:Sz179324;            [получение описаний события]
  !!VRz9:S^%Y3^;
  !!VRz10:S^+ %Y6^;
  !!VRz11:S^- %Y8^;           [подписи к картинкам]
  !!VRy11:S10 +x7;            [y11 - Существо-инструктор]
  !!FU(ES_795_ShowDialog):Px2/7/y11/3/?y12/1/y2/4/4/3/6/0/3; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!VRy8:*-1;                 [y8 - золото для списания]
  !!OW:Rx1/6/dy8;
  !!UN:R2;                    [списание средств]
  !!HEx2:C0/y1/d/d/y5/2 W0/1; [максимизация опыта отряда и трата ОД]
!!en; 

!?FU8004;                     [Наставник]
** Тренировка первичного параметра героя за ресурсы
** P1 = параметр, P2 = увеличение параметра, P3 = тип ресурса, P4 = кол-во ресурса
!!OW:Rx1/x9/?y1;              [y1 - кол-во необходимого ресурса у игрока]
!!FU&x10>y1:E;                [выход, если ресурса недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!VRy2:Sy1 :2;              [y2 - половина запаса ресурса у ИИ]
  !!FU&x10>y2:E;              [выход, если цена превышает половину имеющегося ресурса]

  !!VRy8:Sx10 *-1;            [y8 - средства к списанию]
  !!OW:Rx1/x9/dy8;            [списание средств]
  !!HEx2&x7=0:Fdx8/d/d/d;     [повышение первичного параметра героя]
  !!HEx2&x7=1:Fd/dx8/d/d;     [...]
  !!HEx2&x7=2:Fd/d/dx8/d;     [...]
  !!HEx2&x7=3:Fd/d/d/dx8;     [...]
  !!VRy2:Sx8 *-300;           [y2 - стоимость ОД (300 за 1 одно параметра)]
  !!HEx2:Wdy2/1;              [...]
!!el;                         [обработчик игрока-человека]
  !!VRy10:Sx7 +179326;        [y10 - индекс переменной с описанием события]
  !!VRz7:Szy10;
  !!VRz8:Sz179330;            [получение описаний события]
  !!VRz9:S^+ %X8^;
  !!VRz10:S^^;
  !!VRz11:S^- %X10^;          [подписи к картинкам]
  !!VRy11:S14 +x7;            [y11 - Существо-Тренер]
  !!FU(ES_795_ShowDialog):Px2/7/y11/2/?y12/4/x7/0/0/3/x9/0/4; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!VRy8:Sx10 *-1;            [y8 - средства к списанию]
  !!OW:Rx1/x9/dy8;
  !!UN:R2;                    [списание средств]
  !!HEx2&x7=0:Fdx8/d/d/d;     [повышение первичного параметра героя]
  !!HEx2&x7=1:Fd/dx8/d/d;     [...]
  !!HEx2&x7=2:Fd/d/dx8/d;     [...]
  !!HEx2&x7=3:Fd/d/d/dx8;     [...]
  !!VRy2:Sx8 *-300;           [y2 - стоимость ОД (300 за 1 одно параметра)]
  !!HEx2:Wdy2/1;              [...]
!!en; 

!?FU8005;                     [Исчезающий призрак]
** Покупатель маны/пп героя за ресурсы
** P1 = ценность схрона
!!HEx2:I?y1/1;                [y1 - кол-во маны у героя]
!!VRy2:Sx7 *20;               [y2 - требуемое кол-во маны 20*ценность схрона]
!!FU&y2>y1:E;                 [выход, если маны недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]
!!VRy3:Sy2 *-1;               [y3 - отрицательное значение маны]
!!VRy10:S1 Rx7;               [установка случайного кол-ва ресурсов]
!!VRy11:S1 Rx7;               []
!!VRy12:S1 Rx7;               []
!!VRy13:S1 Rx7;               []
!!VRy14:S1 Rx7;               []
!!VRy15:S1 Rx7;               []
!!VRy16:S1 Rx7 *100;          []

; Check if mithril is enabled
!!UN:P36/?y17;                [y17 - статус опции "Мифрил"]
!!VRy17&y17>0:S1 Rx7;         [кол-во мифрила, если включен]

!!if&x3=1;                    [обработчик игрока ИИ]
** ИИ жертвует ману, только если это не больше половины от имеющейся у него в даный момент
  !!VRy3:Sy1 :2;              [y3 - половина маны героя]
  !!FU&y2>y1:E;               [выход, если требуется более половины имеющейся маны]

  !!HEx2:Idy3/1;              [уменьшение маны героя]
  !!OW:Rx1/0/dy10 Rx1/1/dy11 Rx1/2/dy12 Rx1/3/dy13 Rx1/4/dy14 Rx1/5/dy15 Rx1/6/dy16 Rx1/7/dy17; [добавление ресурсов]
!!el;                         [обработчик игрока-человека]
  !!VRz7:Sz179338;
  !!VRz8:Sz179339;            [получение описаний события]
  !!VRz9:S^- %Y2^;
  !!VRz10:S^^;
  !!VRz11:S^???^;             [подписи к картинкам]
  !!FU(ES_795_ShowDialog):Px2/7/18/2/?y12/4/5/0/0/3/8/0/5; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!HEx2:Idy3/1;              [уменьшение маны героя]
  !!OW:Rx1/0/dy10 Rx1/1/dy11 Rx1/2/dy12 Rx1/3/dy13 Rx1/4/dy14 Rx1/5/dy15 Rx1/6/dy16 Rx1/7/dy17; [добавление ресурсов]
  !!UN:R2;                    [обновление строки ресурсов]
  !!VRz1:Sz179340;            [z1 - текст посещения схрона]
  !!IF&y17>0:N1/y11/3/y13/4/y14/5/y15/0/y10/2/y12/6/y16/7/y17; [схрон с мифрилом]
  !!IF&y17=0:N1/y11/3/y13/4/y14/5/y15/0/y10/6/y16/2/y12;       [схрон без мифрила]
  !!IF:N1/1;                  [показ диалога получения ресурсов]
!!en; 

; Necromancer
!?FU8006;                     [Некромант]
** Преобразование самого многочисленного живого отряда в нежить
** P1 = критерий выбора отряда, P2 = макс.бонус уровня, P3 = грейд
!!DO8098/0/6/1&x7=0:Px2/4/0/1;[возврат в v1 номера самого низкоуровневого живого отряда]
!!DO8098/0/6/1&x7=1:Px2/4/2/0;[возврат в v1 номера самого многочисленного живого отряда]
!!DO8098/0/6/1&x7=2:Px2/4/3/1;[возврат в v1 номера самого слабого живого отряда]
!!DO8098/0/6/1&x7=3:Px2/4/4/1;[возврат в v1 номера самого неопытного живого отряда]
!!VRy1:Sv1;
!!FU&y1<0:E;                  [выход, если подходящего отряда не найдено]

!!HEx2:C0/y1/?y2/?y3;         [y1/y2/y3 - отряд/тип/кол-во требуемых существ]
!!MA:Ly2/?y4;                 [y4 - уровень требуемых существ.]
!!VRy4:+x8;
!!VRy4&y4>7:S7;               [y4 - уровень преобразованных существ (не более 7. 7 - Кровавые драконы/Драколичи)]
** y5 - вид новых существ (уровень/грейд)
!!VRy5&y4=1/x9=0:S58;
!!VRy5&y4=1/x9=1:S59;
!!VRy5&y4=1/x9=2:S141;        [2й уровень: Мертвецы/Зомби/Мумии]
!!VRy5&y4=2/x9=0:S60;
!!VRy5&y4=2/x9=1:S61;
!!VRy5&y4=2/x9=2:S159;        [3й уровень: Стражи/Призраки/Привидения]
!!VRy5&y4=3/x9=0:S62;
!!VRy5&y4=3/x9=1:S63;
!!VRy5&y4=3/x9=2:S63;         [4й уровень: Вампиры/Л.Вампиры/Л.Вампиры]
!!VRy5&y4=4/x9=0:S64;
!!VRy5&y4=4/x9=1:S65;
!!VRy5&y4=4/x9=2:S65;         [5й уровень: Личи/М.Личи/М.Личи]
!!VRy5&y4=5/x9=0:S66;
!!VRy5&y4=5/x9=1:S67;
!!VRy5&y4=5/x9=2:S172;        [6й уровень: Черн.рыцарь/Р.Смерти/Кошмары]
!!VRy5&y4=6/x9=0:S68;
!!VRy5&y4=6/x9=1:S69;
!!VRy5&y4=6/x9=2:S69;         [7й уровень: Кост.драконы/Призр.драконы/Призр.драконы]
!!VRy5&y4=7/x9=0:S154;
!!VRy5&y4=7/x9=1:S154;
!!VRy5&y4=7/x9=2:S196;        [8й уровень: Кров.Драконы/Драколичи/Драколичи]

!!if&y5=196;                  [доп. требования по здоровью для поднятия Драколича]
  !!MA:Py2/?i P196/?j;
  !!VRi:*y3;                  [i/j - суммарное здоровье жертв/здоровье Драколича]
  !!VRy5&i<j:S154;            [если суммарное здоровье жертв ниже здоровья Драколича, поднимаются Кровавые драконы]
!!en; 

!!MA:Fy2/?y6 Fy5/?y7;         [y6/y7 - FV старого/нового существа]
!!VRy8:Sy3 *y6 :y7 +1;
!!VRy8&y8>y3:Sy3;             [y8 - кол-во новых существ (не менее 1го и не более старого кол-ва)]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!FU&y4<3:E;                [ИИ не соглашается на преобразование ниже Вампиров]

  !!HEx2:C0/y1/y5/y8;         [трансформация существ]
!!el;                         [обработчик игрока-человека]
  !!VRz7:Sz179342;
  !!VRz8:Sz179343;            [получение описаний события]
  !!VRz9:S^- %Y3^;
  !!VRz10:S^^;
  !!VRz11:S^+ %Y8^;           [подписи к картинкам]
  !!FU(ES_795_ShowDialog):Px2/7/2/2/?y12/1/y2/0/0/1/y5/0/6; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]
  !!HEx2:C0/y1/y5/y8;         [трансформация существ]
  !!UN:R1;                    [обновление экрана]
  !!VRz1:Sz179344;            [z1 - текст сообщения]
  !!IF:M1/1;                  [вывод сообщения]
!!en; 

!?FU8007;                               // hoarder - offers the most necessary resources for a random artifact in the hero’s backpack at a randomly set rate
** x1 - игрок, x2 - герой, x3 - 0/1=Человек/ИИ, x4-x6 - координаты события, x7-x11 - параметры события
** P1 = размер группы, P2 = стоимость групы
** проверка условия срабатывания
!!VRk:S-1;                              // k - количество различных артефактов в рюкзаке героя

!!re i/7/108;                           // перебор интересных Барахольщику артефактов
  !!HEx2:A2/i/?j/?f;
  !!VRj:-f;                             // j - количество артефактов типа i в рюкзаке героя
  !!VRk&j>0:+1;                         // увеличиваем счетчик разных артефактов
!!en; 

!!FU&k<0:E;                             // выход, если в рюкзаке героя нет нужных артефактов

!!SN:W^o795.%X4.%X5.%X6.T^/0;           // удаление события
!!VRl:S0 Rk;
!!VRk:S-1;                              // l - случайный артефакт

!!re i/7/108;                           // перебор интересных Барахольщику артефактов
  !!HEx2:A2/i/?j/?f;
  !!VRj:-f;                             // j - количество артефактов типа i в рюкзаке героя
  !!VRk&j>0:+1;

  !!br&l=k;                             // прерываем поиск, если искомый артефакт найден
!!en; 

!!VRy1:Si;
!!UN:Ay1/1/?y2 Ay1/3/?y3;               // y1/y2/y3 - номер/цена/класс артефакта для покупки
!!VRy4:S0;
!!OW:Rx1/0/?k;
!!VRk::2;                               // y4 - тип ресурса, вкотором игрок максимально нуждается

!!re i/1/6;                             // y10-y16 - количество дерева ... золота у игрока
  !!OW:Rx1/i/?j;                        // ...
  !!VRj|i=0/i=2::2;
  !!VRj&i=6::1000;                      // j  - приведенное количество ресурса
  !!VRy4&k>j:Si;
  !!VRk&k>j:Sj;
!!en;  

!!VRy2:*x7 :100;                        // y2 - цена в золоте с учетом заданного в событии курса, x 1.0 для treasure-артефактов
!!VRy2&y3=4:*5 :4;                      // х 1.25 для minor-артефактов
!!VRy2&y3=8:*6 :4;                      // х 1.5 для major-артефактов
!!VRy2&y3=16:*7 :4;                     // х 1.75 для relict-артефактов
!!VRy2&y4=6::500 *500;                  // y2 - цена для выбранного ресурса, с округлением до 1 рес / 500 злт
!!VRy2|y4=0/y4=2::400;                  // ...
!!VRy2|y4=1/y4=3/y4=4/y4=5::800;        // ...

!!if&x3=1;                              // обработчик игрока ИИ
  !!if&x7>=100;                         // ИИ соглашается на обмен при цене не менее 100% от базовой
    !!HEx2:A3/y1/1/0;                   // удаление артефакта из рюкзака
    !!OW:Rx1/y4/dy2;                    // добавление ресурсов
  !!en;                                 
!!el;                                   // обработчик игрока-человека
  !!VRz7:Sz179414;
  !!VRz8:Sz179415;                      // получение описаний события
  !!VRz9:S^-1^;
  !!VRz10:S^^;
  !!VRz11:S^%Y2^;                       // формирование подсказок
  !!FU(ES_795_ShowDialog):Px2/7/1/2/?y12/6/y1/0/0/3/y4/1/7; // вызов диалога и получение ответа в y12 (1-да,2-нет)

  !!if&y12<>1;                          // при отказе
    !!VRz1:Sz179416;                    // z1 - текст финального сообщения
    !!IF:M1/1;                          // вывод сообщения
  !!el;                                 // при согласии
    !!HEx2:A3/y1/1/0;                   // удаление артефакта из рюкзака
    !!OW:Rx1/y4/dy2;
    !!UN:R2;                            // добавление ресурсов
    !!VRz1:Sz179417;                    // z1 - текст финального сообщения при согласии
    !!IF:M1/1;                          // вывод сообщения
  !!en;     
!!en; 

; Creature conflict
!?FU8008;                     [Конфликт]
** Схватка 2х неравных по силе сторон с возможностью помочь слабой стороне за вознаграждение
** P1 = Защищающиеся Существа, P2 = Атакующие существа, P3 = утроенный коэффициент превосходства атакующих (4..12),
!!MA:Lx7/?y1 Fx7/?y2;         [y1/y2 - уровень/FV защищающихся]
!!VRy4:Sy1 +1 *y4;            [y4 - делитель = уровень^2]
!!VRy3:Sc *3 :y4 +1;          [y3 - кол-во защищающихся = (день*3)/(уровень^2)+1]
!!VRy4:Sy3 *y2 *x9 :3;        [y4 - общее FV атакующих]
!!MA:Fx8/?y5;                 [y5 - FV атакующего существа]
!!VRy4::y5 +1;                [y4 - кол-во атакующих]
!!VRy5:*y4;                   [y5 - общее FV атакующих]
!!MA:Cx7/6/?y6;
!!VRy6::2;                    [y6 - вознаграждение за каждого спасенного: 1/2 золотой части стоимости]
!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!HEx2:C0/0/?y11/?y21 C0/1/?y12/?y22 C0/2/?y13/?y23 C0/3/?y14/?y24 C0/4/?y15/?y25 C0/5/?y16/?y26 C0/6/?y17/?y27; [Армия ИИ]
  !!MA&y11>=0:Fy11/?y31;
  !!VRy31:*y21;               [ценности слотов армии ИИ]
  !!MA&y12>=0:Fy12/?y32;
  !!VRy32:*y22;               [...]
  !!MA&y13>=0:Fy13/?y33;
  !!VRy33:*y23;               [...]
  !!MA&y14>=0:Fy14/?y34;
  !!VRy34:*y24;               [...]
  !!MA&y15>=0:Fy15/?y35;
  !!VRy35:*y25;               [...]
  !!MA&y16>=0:Fy16/?y36;
  !!VRy36:*y26;               [...]
  !!MA&y17>=0:Fy17/?y37;
  !!VRy37:*y27;               [...]
  !!VRy31:+y32 +y33 +y34 +y35 +y36 +y37; [y31 - FV армии ИИ]
  !!VRy32:Sy31 :2;            [y32 - 1/2 силы армии ИИ]
  !!VRy33:Sy31 :5;            [y33 - 1/5 силы армии ИИ]
  !!FU&y32<y5:E;              [выход, если армия ИИ не более чем в 2 раза сильнее атакующих]

  !!if&y33<y5;                [если армия ИИ сильнее атакующих от 2 до 5 раз, ИИ получает ресурсы за половину "спасенных"]
    !!VRy34:Sy3 :2 *y6;       [y34 - вознаграждение за половину "спасенных"]
    !!OW:Rx1/6/dy34;          [вознаграждение ИИ]
  !!el;                       [если армия ИИ сильнее атакующих более чем в 5 раз, ИИ получает ресурсы/наемников за 100% "спасенных"]
    !!VRy34:S-1;
    !!HEx2&y34<0/y11<0:C0/0/x7/y3;
    !!FU&y34<0/y11<0:E;       [добавление "спасенных" в первый сводобный слот армии ИИ]
    !!HEx2&y34<0/y12<0:C0/1/x7/y3;
    !!FU&y34<0/y12<0:E;       [...]
    !!HEx2&y34<0/y13<0:C0/2/x7/y3;
    !!FU&y34<0/y13<0:E;       [...]
    !!HEx2&y34<0/y14<0:C0/3/x7/y3;
    !!FU&y34<0/y14<0:E;       [...]
    !!HEx2&y34<0/y15<0:C0/4/x7/y3;
    !!FU&y34<0/y15<0:E;       [...]
    !!HEx2&y34<0/y16<0:C0/5/x7/y3;
    !!FU&y34<0/y16<0:E;       [...]
    !!HEx2&y34<0/y17<0:C0/6/x7/y3;
    !!FU&y34<0/y17<0:E;       [...]
    !!DO8098/0/6/1:Px2/0/3/1; [возврат в v1 номера самого слабого отряда ИИ]
    !!VRy37:Sv1;              [y37 - номер самого слабого отряда ИИ]
    !!VRy34:Sv1 +11;
    !!VRy35:Sy34 +10;         [y34/y35 - индексы y-переменных с типом/кол-вом существ в отряде]
    !!MA:Fyy34/?y36;          [y36 - FV существа в отряде]
    !!VRy36:*yy35 *3;         [y36 - утроенное FV самого слабого отряда]
    !!VRy38:Sy3 *y2;          [y38 - общее FV "спасенных"]
    !!HEx2&y36<y38:C0/y37/x7/y3; [если FV спасенных минимум втрое превышает FV самого слабого отряда, ИИ заменяет ими самый слабый отряд]
    !!FU&y36<y38:E;           [выход, если существа присоединены]
    !!VRy34:Sy3 *y6;          [y34 - вознаграждение за 100% "спасенных"]
    !!OW:Rx1/6/dy34;          [вознаграждение ИИ]
  !!en;

!!el;                         [обработчик игрока-человека]
  !!UN:N3/1/x7/1;
  !!UN:N3/2/x8/1;             [z1/z2 - наименования существ (мн.ч.)]
  !!VRz7:Sz179346;
  !!VRz8:Sz179347;            [получение описаний события]
  !!VRy31:Sy3 -1 R2;
  !!VRy31&y31<1:S1;           [y31 - примерное число защищающихся (не менее 2)]
  !!VRy32:Sy4 -1 R2;
  !!VRy32&y32<1:S1;           [y32 - примерное число атакующих (не менее 2)]
  !!VRz9:S^~ %Y31^;
  !!VRz10:S^^;
  !!VRz11:S^~ %Y32^;          [подписи к картинкам]
  !!FU(ES_795_ShowDialog):Px2/4/0/3/?y12/1/x7/4/0/1/x8/0/8; [вызов диалога и получение ответа в y12 (1-да,2-нет)]
  !!FU&y12<>1:E;              [выход, если человек отказался от активации события]

  !!SN:W^opt795.10.dt^/x7;    [установка параместров конфликта]
  !!SN:W^opt795.10.dc^/y3;    [...]
  !!SN:W^opt795.10.at^/x8;    [...]
  !!SN:W^opt795.10.ac^/y4;    [...]
  !!HEx2:R4/?y35 R4/0;        [y35 - статус Тактики героя, запрет тактической расстановки]

  !!FU8097:Px1/x2/x3/x4/x5/x6;[вызов боя с сохранением значений переменных]

  !!HEx2:R4/y35;              [восстановление статуса Тактики героя]
  !!SN:W^opt795.10.dc^/?y31 W^opt795.10.dc^/0;     [y31 - кол-во защищающихся существ / сброс кол-ва]
  !!HEx2:O?y30;               [y30 - принадлежность героя (-1 - проиграл бой)]
  !!FU|y30<0/y31<1:E;         [выход, если герой не победил в стражении или никого не удалось спасти]

  !!VRy32:Sy31 *y6;           [y32 - вознаграждение]
  !!UN:N3/1/x7/0;             [z1 - название существа (ед.ч.)]

  !!if&y31<>y3;               [если удалось спасти не всех]
    !!VRz2:Sz179348;          [z2 - текст сообщения]
    !!IF:Q1/6/y32/1/2;        [показ сообщения]
    !!OW:Rx1/6/dy6;           [добавление золота]
    !!UN:R2;                  [обновление строки ресурсов]
  !!el;                       [если удалось спасти всех]
    !!VRz2:Sz179349;          [z2 - текст сообщения]
    !!VRy33:Sy3 *65536 +x7;   [y33 - кол-во существ в подписи]
    !!IF:Q1/21/y33/6/y32/7/2; [показ сообщения]

    !!if&1;                   [если выбраны существа]
      !!HEx2:C2/x7/y3/1;      [добавление существ герою]
      !!UN:R1;                [обновление экрана]
    !!el;                     [если выбрано золото]
      !!OW:Rx1/6/dy6;         [добавление золота]
      !!UN:R2;                [обновление строки ресурсов]
    !!en; 
  !!en; 
!!en; 

!?FU(OnBeforeBattleUniversal)&i^opt795.10.dc^; [кол-во защищающихся существ в конфликте]
!!SN:W^opt795.10.at^/?y4 W^opt795.10.ac^/?y5; [y4/y5 - тип/кол-во атакующих]
!!VRy12:Sy5 :2 *2;
!!VRy13:Sy5 :3 *3;            [y9 - количество атакующих отрядов (1..3)]
!!VRy9:S1;
!!VRy9&y12=y5:S2;
!!VRy9&y13=y5:S3;             [...]
!!VRy10:Sy5 :y9;              [y10 - кол-во существ в атакующих отрядах]
!!BA:M1/0/y4/y10;             [установка атакующих существ]
!!BA&y9>1:M1/1/y4/y10;        [...]
!!BA&y9>2:M1/2/y4/y10;        [...]

; Save a backup for battlereplay
!!VRi^opt795.10.dc1^:Si^opt795.10.dc^;

!?FU(OnSetupBattlefield);
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2; [y1/y2 - тип/кол-во атакующих]
!!FU&y2=0:E;                  [выход, если не конфликт]

!!BF:C;                       [удаление преград]
!!BU:Sy1/y2/94/0/-1/1 E94/?y3;[призыв защищающихся существ y3 - номер отряда]
!!SN:W^opt795.10.ds^/y3;      [сохранение номера защищающегося отряда]

!?FU(OnAfterBattleAction);
!!SN:W^opt795.10.dt^/?y1 W^opt795.10.dc^/?y2 W^opt795.10.ds^/?y3;
!!FU&y2=0:E;                  [выход, если не конфликт]

!!BU:C?y6;                    [y6=1, если бой завершен]
!!FU&y6=0:E;                  [выход, если бой не завершен]

!!BMy3:N?y4 T?y5 G-81/?y6/d;  [y4/y5/y6 - кол-во/тип/безвозвратные потери]
!!VRy4:-y6;
!!VRy4&y4<0:S0;               [y4 - реальное кол-во защищавшихся]
!!VRy4&y5<>y1:S0;             [обнуление кол-ва защищавшихся, если отряд тругого типа]
!!SN:W^opt795.10.dc^/y4;      [обновление кол-ва защищавшихся]

; Restore variables on battle replay
!?FU(OnBattleReplay)&i^opt795.10.dc1^;
!!VRi^opt795.10.dc^:Si^opt795.10.dc1^;

; Release variables
!?FU(OnAfterBattleUniversal);
!!VRi^opt795.10.dc1^:S0;

!?FU(OnAfterLoadGame);
!!VRi^opt795.10.dc^:S0;
!!VRi^opt795.10.dc1^:S0;

; Restore the henchman in battle replay
!?FU(OnBeforeBattleUniversal)&i^hench_enabled^;
!!SN:W^henchmen_side_0^/(FALSE)  W^henchmen_side_1^/(FALSE);
!!BA:H0/?(atkHero:y) H1/?(defHero:y);

!!if&(atkHero)>(NO_HERO);
  !!IF:W(atkHero);
  !!SN:W^henchmen_side_0^/w119;
!!en;

!!if&(defHero)>(NO_HERO);
  !!IF:W(defHero);
  !!SN:W^henchmen_side_1^/w119;
!!en;

!?FU(OnBattleReplay)&i^hench_enabled^;
!!BA:H0/?(atkHero:y) H1/?(defHero:y);

!!if&(atkHero)>(NO_HERO);
  !!IF:W(atkHero);
  !!VRw119&i^henchmen_side_0^:S(TRUE);
!!en;

!!if&(defHero)>(NO_HERO);
  !!IF:W(defHero);
  !!VRw119&i^henchmen_side_1^:S(TRUE);
!!en;

; Assassins
!?FU8009;                     [Ассасины]
** Приобретение ассасинов, принимающих участие в боях героя
** x1 - игрок, x2 - герой, x3 - 0/1=Человек/ИИ, x4-x6 - координаты события, x7-x11 - параметры события
** P1 = размер группы, P2 = стоимость групы
** проверка условия срабатывания
!!OW:Rx1/6/?y1;               [y1 - золото игрока]
!!FU&y1<x8:E;                 [выход, если золота недостаточно]

!!SN:W^o795.%X4.%X5.%X6.T^/0; [удаление события]

!!if&x3=1;                    [обработчик игрока ИИ]
  !!VRx8:*-1 :2;              [x8 - отрицательная сумма для списания (половина стоимости для ИИ)]
  !!OW:Rx1/6/dx8;             [списание средств]
  !!SN:W^opt795.11.%X2^/dx7;  [ИИ всегда берет ассасинов]
!!el;                         [обработчик игрока-человека]
** x1-герой, x2/x3-тип/номер НПЦ, x4-кол-во картинок (0..3), x5-переменная для возврата результата выбора (1-да, 2-нет)
** x6/x7-тип/номер левой картинки, x8/x9-тип/номер центральной картинки, x10/x11-тип/номер правой картинки
** Типы: 0-герой, 1-монстр, 2-заклинание, 3-ресурс, 4-перв.параметр, 5-втор.навык/ранг, 6-артефакт
** z6-событие, z7-описание, z8-вопрос, z9-подсказка 1, z10-подсказка 2, z11-подсказка 3
  !!VRz7:Sz179403;
  !!VRz8:Sz179404;            [получение описаний события]
  !!VRz9:S^%X7^;
  !!VRz10:S^^;
  !!VRz11:S^- %X8^;           [формирование подсказок]

  !!FU(ES_795_ShowDialog):Px2/7/0/2/?y12/1/143/0/0/3/6/0/9; [вызов диалога и получение ответа в y12 (1-да,2-нет)]

  !!if&y12<>1;                [при отказе]
    !!VRz1:Sz179405;          [z1 - текст финального сообщения]
    !!IF:M1/1;                [вывод сообщения]
  !!el;                       [при согласии]
    !!VRx8:*-1;               [x8 - отрицательная сумма для списания]
    !!OW:Rx1/6/dx8;           [списание средств]
    !!SN:W^opt795.11.%X2^/dx7;[добавление ассасинов герою]
    !!VRz1:Sz179406;          [z1 - текст финального сообщения при согласии]
    !!IF:M1/1;                [вывод сообщения]  
  !!en;     
!!en; 

; Assassin
!?FU(OnBattleRound)&i^battle_round^>=0;
!!re i/0/1;
  !!VRi^es_795_assassins_acted_%i^:S(FALSE);
!!en;

!?FU(OnBattleRegeneratePhase)&i^battle_round^>=0;
!!UN:P795/?(wogOption:y);
!!FU&(wogOption)=(FALSE):E;

!!VR(actSide:y):Sx1 :21;
!!VR(actHero:y):Si^battle_hero_%(actSide)^;

!!if&(actHero)<>(NO_HERO)/i^opt795.11.%(actHero:y)^>0/i^opt795.11.f%(actHero:y)^=(FALSE)/i^es_795_assassins_acted_%(actSide:y)^=(FALSE);
  !!VRr:R0/0/3;
  !!FU&r:E;                               [For testing]
  !!VR(oppSide:y):S(actSide) X1;

  !!FU(NewIntArray):P?(targetStackPtrs:y);
  !!VR(start:y):S(oppSide) *(BATTLE_STACKS_PER_SIDE);
  !!VR(end:y):S(start) +(BATTLE_STACKS_PER_SIDE) -1;

  !!re i/(start:y)/(end:y);(end_value);
    !!BMi:N?n T?t S?s;

    !!if&n>0/t<>(NO_MON)/s>0/t<>(MON_FIRST_AID_TENT);
      !!BMi:Z?(stackStruct:y);
      !!FU(ES_996_CheckStackCanAct):P(stackStruct:y)/?(canAct:y);

      !!if&(canAct);
        !!FU(Array_Push):P(targetStackPtrs)/(stackStruct);
      !!en;
    !!en;
  !!en;

  !!SN:M(targetStackPtrs)/?(size:y);

  !!if&(size);
    !!FU(Array_Shuffle):P(targetStackPtrs);
    !!VR(success:y):S(FALSE);
    !!UN:C(COMBAT_MANAGER)/4/?(cmbMgr:y);

    !!re (stackIdFromAray:y)/0/(size)/1/-1;
      !!SN:M(targetStackPtrs)/(stackIdFromAray)/?(targetStackPtr:y);
      !!FU(NewIntArray):P?(availableHexes:y);                                     [prepare array of the hexes]
      !!UN:C(targetStackPtr)/132/4/?f C(targetStackPtr)/132/4/d~(MON_FLAG_FLYER); [remove flying]
      !!UN:C(targetStackPtr)/696/4/?(bindDuration:y) C(targetStackPtr)/696/4/0;   [and bind spell]

      ; Archer's implementation: Search only the adjacenet hexes of the target to summon
      ; This implementation might be better than daemon's as it reduces the animation time, has less calculation and is less likely to crash
      !!UN:C(targetStackPtr)/248/4/?(targetStackIdOfSide:y);
      !!VR(targetStackId:y):S(BATTLE_STACKS_PER_SIDE) *(oppSide) +(targetStackIdOfSide);
      !!FU(ES_GetAvailAdjacentHexesOfStack):P(targetStackId)/?(adjacentHexesList:y);

      !!SN:M(adjacentHexesList)/?(adjacentListSize:y);

      !!if&(adjacentListSize)>0;
        !!FU(Array_Shuffle):P(adjacentHexesList);
        !!SN:M(adjacentHexesList)/0/?(randPos:y);
        !!VR(success):S(TRUE);
      !!en;

      ; daemon's implementation: Search for the whole battlefield to summon'
      ; This implementation has been reported buggy in siege battle. The game can crash when assassins' are crossing the moat
      *!re i/(BATTLE_HEX_FIRST)/(BATTLE_HEX_LAST);                                [loop hexes]
        *!SN:E5390176/(CALLCONV_THISCALL)/(targetStackPtr)/i/0;                   [can stack reach that hex]

        *!if&v1;
          *!FU(Array_Push):P(availableHexes)/i;                                   [add into array]
        *!en;
      *!en;

      !!UN:C(targetStackPtr)/132/4/f C(targetStackPtr)/696/4/(bindDuration:y);    [restore flying and bind duration]

      ; daemon's implementation: continue
      *!SN:M(availableHexes)/?(numOfHexes:y);                                     [get array size]

      *!if&(numOfHexes);                                                          [if not empty]
        *!VR(numOfHexes):-1;
        *!VR(randHexIndex:y):R0/0/(numOfHexes);                                   [get random hex]
        *!SN:M(availableHexes)/(randHexIndex:y)/?(randPos:y);                     [get position]
        *!VR(success:y):S(TRUE);

        *!br;
      *!en;
    !!en;

    !!FU&(success:y)<>(TRUE):E;         [end if no targets]

    !!VRi^es_795_summoned_stack_id^:S(NO_STACK); [assume summon is failed]
    !!UN:P758/?(cutThroats:y) P758/(FALSE); [Disable Cutthroat]

    !!FU(ES_795_GetAssassinMonType):P?(monType:y);
    !!SN:E4692528/(CALLCONV_THISCALL)/(cmbMgr:y)/(actSide:y)/(monType)/i^opt795.11.%(actHero:y)^/(randPos:y)/4194304/1; [summmon stack, basically BU:S?]

    !!VR(assassinsStackId:y):Si^es_795_summoned_stack_id^;
    !!VRi^es_795_summoned_stack_id^:S(NULL);

    !!if&(assassinsStackId:y)<>(NO_STACK); [if not failed]
      !!SN:E4628608/(CALLCONV_THISCALL)/(cmbMgr);
      !!VR(showIt:y):Sv1 X1;

      !!if&(showIt:y);
        !!VRz1:S^%T(es.795.assassins.appear)^;
        !!MM:Sz1;                       [^%T(es.795.singName)^]
        !!SN:P^CLONE^;                  [play sound]
      !!en;

      !!BM(assassinsStackId:y):Z?(assassinsStack:y) G(BMG_FIELD_LUCK)/0/d;

      ; daemon's implementation: continue
      *!UN:C(targetStackPtr)/248/4/?(id:y);
      *!VR(targetStackId:y):S21 *(oppSide) +(id);
      *!BM(targetStackId):P?p F?f;
      *!VRf:&(MON_FLAG_WIDE) B;         [f, check mon is wide]

      *#VA(positions[2]:y);

      *!VR(positions[0]):Sp;

      *!if&(oppSide);
        *!VR(positions[1]):Sp-1;
      *!el;
        *!VR(positions[1]):Sp+1;
      *!en;

      *!FU(NewIntArray):P?(nearHeaxesArr:y);

      *!re i/0/f;                                 [if target is wide]
        *!VR(offset:y):S(positions[i]) *12 +78952;
        *!VRo:S1 Xf;

        *!re j/0/5;
          *!UN:C(cmbMgr)/(offset)/2/?(hexId:y);
          *!SN:E5390176/(CALLCONV_THISCALL)/(assassinsStack)/(hexId:y)/0; can stack reach hex

          *!if&v1;/(hexId)>(BATTLE_HEX_FIRST)/(hexId)<>(positions[o]);
            *!FU(ES_IfHexIsFree):P(hexId)/?(isFree:y);

            *!if&(isFree)/(hexId)<>(positions[o]);
              *!FU(Array_Push):P(nearHeaxesArr)/(hexId:y);
            *!en;
          *!en;

          *!VR(offset):+2;
        *!en;
      *!en;

      *!FU(Array_SortedUnique):P(nearHeaxesArr);
      *!SN:M(nearHeaxesArr)/?(size:y);

      *!if&(size);
        *!VR(size):-1;
        *!VR(randomInd:y):R0/0/(size);
        *!SN:M(nearHeaxesArr)/(randomInd)/?(positionToMove:y);
        *!UN:C(cmbMgr)/78544/4/(positionToMove);
        *!SN:E4479536/(CALLCONV_THISCALL)/(assassinsStack)/(positionToMove:y)/0; [move to position;]
      *!en;

      !!UN:C(assassinsStack)/136/4/?(singlDescPtr:y) C(assassinsStack)/140/4/?(multDescPtr:y);
      !!SN:B(singlDescPtr)/d/?(storeS:z) B(multDescPtr)/d/?(storeP:z);
      !!SN:B(singlDescPtr)/d/^%T(es.795.assassins.singName)^ B(multDescPtr)/d/^%T(es.795.assassins.plurName)^;

      ; Archer's implementation: continue
      ; Get BM:P value as the position to attack
      ; There is no point getting another hex for wide unit as Assassins don't have Breath/Strike all Around
      !!BM(targetStackId):P?(attackPos:y);
      !!SN:E4478848/(CALLCONV_THISCALL)/(assassinsStack)/(attackPos)/0; [make attack to position]

      ; Daemon's implementatoin: continue
      ; (positions[0]) might be incorrect as it can be (positions[1]) too?
      *!SN:E4478848/(CALLCONV_THISCALL)/(assassinsStack)/(positions[0])/0;     [make attack to position]

      !!SN:B(singlDescPtr)/d/(storeS:z) B(multDescPtr)/d/(storeP:z);

      !!BM(assassinsStackId:y):P?p;
      !!BM(assassinsStackId:y):Fd|268435456 Fd|1073741824;
      !!UN:C(assassinsStack:y)/234/1/1;                                        [указываем, что отряд умер]
      !!VRi^es_795_assassins_acted_%(actSide:y)^:S(TRUE);

      !!if&(showIt);
        !!UN:C(COMBAT_MANAGER)/4/?(cmbMgr:y);
        !!SN:E4810128/2/(cmbMgr:y)/50/p/40/0;                                  [animate]
        !!VRz1:S^%T(es.795.assassins.disappear)^;
        !!MM:Sz1;
      !!en;

      !!BM(assassinsStackId:y):B0 N0;

      !!SN:E4621680/(CALLCONV_THISCALL)/(cmbMgr:y)/51/1;                       [remove body]
    !!en;

    !!UN:P758/(cutThroats);
  !!en;
!!en;

!?FU(ES_BattleStack_InitParams)&i^es_795_summoned_stack_id^=(NO_STACK);
!!VRi^es_795_summoned_stack_id^:Sx1;
!!VR(monFlags:y):S(MON_FLAG_NO_MORALE) |(MON_FLAG_NO_RETALIATION) |(MON_FLAG_FIRE_IMMUNITY); [Assassins have only 3 flags to reduce complexity (no breath/strike all around from Rogues)]
!!BMx1:Sd100 F(monFlags) A10 D10 H20 L0 U1/5 U2/7 G(SPELL_ANTI_MAGIC)/10/(SKILL_EXPERT); [параметры ассасинов 10ат/10зщ/20зд/10ск/5-7ур]
!!UN:C(COMBAT_MANAGER)/4/?(cmbMgr:y);
!!SN:E4628608/(CALLCONV_THISCALL)/(cmbMgr);

!!if&v1=0;
  !!BMx1:P?p;
  !!SN:E4810128/2/(cmbMgr:y)/50/p/40/0;    [вывод анимации]
!!en;

// Function to get the creature type of the Assassin
!?FU(ES_795_GetAssassinMonType);
!#VA(monType:x);

!!VR(monType):S(MON_ROGUE);


!?FU(OnHeroScreenMouseClick)&i^mouse_action^=(MOUSE_LMB_RELEASED)/i^mouse_flags^=(NO_MOUSE_FLAGS);           [Клик в окне героя]
!!UN:P795/?y1;
!!FU&y1=0:E;                            [выход если опция 795 не включена]

!!SN:F^GetButtonID^/^assasins^;
!!if&v1=i^mouse_item^;
  
  !!HE(CURRENT_HERO):N?h;
  !!VR(assassinsNum:y):Si^opt795.11.%h^;
  !!if&(assassinsNum:y)<1;                              [вывод информации об ассасинах героя и запрос роспуска, если ассасины есть]
    !!IF:M1/^%T(es.795.assassins.noUnits)^;                           // 
  !!el;                                   // 
    !!SN:T^es.795.assassins.header^/?(msg:z)/^num^/(assassinsNum:y);

    !!if&i^opt795.11.f%h^=0;
      !!VR(add:z):S^%T(es.795.assassins.allow)^;
    !!el;
      !!VR(add:z):S^%T(es.795.assassins.restrict)^;
    !!en;

    !!VR(msg):+^%T(es.endl)%T(es.endl)^ +(add);
    !!IF:Q1/(msg);текст сообщения
    !!VRi^opt795.11.f%h^&1:X(TRUE);
  !!en;

!!en;



!?FU(OnAfterBattleUniversal);
!!UN:P795/?y1;
!!FU&y1=0:E;                            [выход если опция 795 не включена]

!!BA:H0/?y1 H1/?y2;
!!FU&y2<1:E;                            // y1/y2 - атакующий/защищающийся герои, выход, если не битва двух героев

!!SN:W^opt795.11.a^/?y3;
!!FU|y3=4/y3=5:E;                       // выход, если герой сбежал/откупился

!!HEy1:O?y3;!!HEy2:O?y4;                // y3/y4 - владельцы атакующеего/защищающегося героя
!!FU&y3<0/y4<0:E;                       // выход, если оба героя погибли
!!FU&y3>=0/y4>=0:E;                     // выход, если оба героя выжили (вербовка бандита)

!!if&y3<0;                              // если победил атакующий
  !!VRy4:Sy1;
  !!VRy1:Sy2;
  !!VRy2:Sy4;                           // y1/y2 - победитель/побежденный
!!en; 
!!SN:W^opt795.11.%Y2^/?y3 W^opt795.11.%Y2^/0; // y3 - количество ассасинов у побежденного героя, обнуление
!!SN:W^opt795.11.%Y2.rebirth^/?y4 W^opt795.11.%Y2.rebirth^/0; // y4 - количество ассасинов при перерождении, обнуление
!!VRy3&y3=0/y4>0:Sy4;                   // корректировка ассасинов героя при перерождении
!!FU&y3<1:E;                            // выход, если у побежденного героя нет выживших ассасинов

!!HEy1:O?y4;
!!OW:Iy4/?y6;                           // y6 - ИИ признак победителя

!!if&y6=1;                              // ИИ всегда присоединяет ассасинов
  !!SN:W^opt795.11.%Y1^/?y5;
  !!VRy5:+y3;
  !!SN:W^opt795.11.%Y1^/y5;             // ...
!!el;                                   // Человеку дается выбор ассасины/золото
  !!SN:H^monname^/(MON_ROGUE)/0/?(storeS:z) H^monname^/(MON_ROGUE)/0/^%T(es.795.assassins.singName)^;// установка новых названий ассасинов
  !!SN:H^monname^/(MON_ROGUE)/1/?(storeP:z) H^monname^/(MON_ROGUE)/1/^%T(es.795.assassins.plurName)^;

  !!VRy7:Sy3 *200;                      // y7 - компенсация в золоте 200 за ассасина
  *!VRz1:Sz179418;                      // z1 - текст присоединения ассасинов
  !!VRy5:Sy3 *65536 +(MON_ROGUE);       // y5 - количество существ для отображения в диалоге
  !!IF:Q1/6/y7/21/y5/7^%T(es.795.assassins.capture)^;            // вывод вопроса о присоединении

  !!if&1;                               // если выбрано золото
    !!OW:Ry4/6/dy7;
    !!UN:R2;                            // добавляем золото с обновлением строки ресурсов
  !!el;                                 // иначе
    !!SN:W^opt795.11.%Y1^/?y5;
    !!VRy5:+y3;
    !!SN:W^opt795.11.%Y1^/y5;           // добавляем ассасинов
  !!en;                                 // ...
  !!SN:H^monname^/(MON_ROGUE)/0/(storeS:z);// установка новых названий ассасинов
  !!SN:H^monname^/(MON_ROGUE)/1/(storeP:z);
!!en;                                   // ...

** end

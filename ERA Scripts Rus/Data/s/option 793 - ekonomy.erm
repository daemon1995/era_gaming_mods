ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Economy
** Name rus.     : Экономика
** Options       : 793
** Dialogs       : -
** Variables     : v940-v944

!?FU(OnAfterErmInstructions);           // при старте игры
!!UN:P793/?(economy:y);
!!FU&(economy)<>(TRUE):E;

; Set up new name
!!SN:H^secskill^/(SKILL_ESTATES)/0/^%T(es.793.name)^;

!!re i/(SKILL_BASIC)/(SKILL_EXPERT);
  !!SN:T^es.793.desc%i^/?(newDesc:z);
  !!SN:H^secskill^/(SKILL_ESTATES)/i/?(currentDesc:z);
  !!FU(ES_EraseFirstLine):P(currentDesc)/?(currentDesc);
  !!VR(newDesc):+(currentDesc);
  !!SN:H^secskill^/(SKILL_ESTATES)/i/(newDesc:z);
!!en;
!!re i/0/(HERO_LAST_WOG);
  !!HEi:X?t/?s;
  !!if&t=0/s=(SKILL_ESTATES);
    !!SN:H^spec^/i/2/^%T(es.793.spec)^;
  !!en;
!!en;

// Disable standard income increase from Estates
!?FU(OnStartOrLoad);
!!UN:P793/?(wogOption:y);     [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;

; Set the power of Estate to 0 for each level
!!re i/1/3;
  !!VR(offset:y):Si -1 *4;
  !!UN:C6547996/(offset)/4/?i^es_793_patch%i^ C6547996/(offset)/4/0;
!!en;

!?FU(OnGameLeave);
!!UN:P793/?(wogOption:y);     [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;

!!UN:C6547996/4/i^es_793_patch1^;       [Basic]
!!UN:C6548000/4/i^es_793_patch2^;       [Advanced]
!!UN:C6548004/4/i^es_793_patch3^;       [Expert]

!?FU(ES_793_GetDailyIncomeOfTowns); [получаем размер дневного дохода городов игрока x1 в v943]
!!UN:U(OBJ_TOWN)/-1/-1/2;     [v1..v3 - координаты города]
!!CA2:O?y1;                   [y1 - владелец города]
!!FU&y1<>x1:E;                [выход, если город принадлежит не текущему герою]

!!CA2:B3/10;                  [проверка наличия префектуры]
!!VRy1&1:S500;                [y1=500, если есть префектура]
!!CA2:B3/11;                  [проверка наличия городского холла]
!!VRy1&1:S1000;               [y1=1000, если есть городской холл]
!!CA2:B3/12;                  [проверка наличия муниципалитета]
!!VRy1&1:S2000;               [y1=2000, если есть муниципалитет]
!!CA2:B3/13;                  [проверка наличия капитолия]
!!VRy1&1:S4000;               [y1=4000, если есть капитолий]
!!CA2:B3/26;                  [проверка наличия Грааля]
!!VRy1&1:+5000;               [y1+=5000, если есть Грааль]
!!VRv943:+y1;                 [увеличиваем суммарный доход городов от строений]
!!UN:P785/?y1;                [проверяем включена ли опция 785 Батраки в y1]
!!FU&y1=0:E;                  [выход, если опция не активна]

!!CA2:P?y2/?y3/?y4;           [y2/y3/y4 - координаты города]
!!SN:W^peons_%Y2_%Y3_%Y4^/?y5;[y5 - ежедневный доход от батраков в городе]
!!VRv943:+y5;                 [увеличиваем суммарный доход городов на доход батраков]

!?FU(OnHeroScreenMouseClick)&i^mouse_item^>=80/i^mouse_item^<=101; [клик в окне героя]
!!UN:P793/?(wogOption:y);               [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!VR(ssIndex:y):Si^mouse_item^ -79 %8 +1;[номер слота втор.навыка у героя]
!!CM:H?(hero:y)/?(rightHero:y);         [левый/правый герои]
!!HE(hero):S(ssIndex)/?(ss:y)/1;        [навык в слоте]

; Show up current bonus if clicking on a Economy skill slot or clicking on skill level/name if either Estates I and Estates II is disabled
!!if&(ss)=(SKILL_ESTATES);
  !!UN:P203/?(estatesI:y) P191/?(estatesII:y);

  !!if&i^mouse_item^<=86;
    !!FU|(estatesI)/(estatesII):E;
  !!en;

  !!FU(ES_793_ShowCurrentBonusDlg):P(hero);
!!en;

!?FU(OnHeroesMeetScreenMouseClick)&i^mouse_item^>=201/i^mouse_item^<=214; [клик в окне обмена между героями]
!!UN:P793/?(wogOption:y);               [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!CM:H?(hero:y)/?(rightHero:y);         [левый/правый герои]
!!VR(hero:y)&i^mouse_item^>207:S(rightHero); [герой, на навык которого кликнули]
!!VR(ssIndex:y):Si^mouse_item^ -200 %8 +1;[номер слота втор.навыка у героя]
!!HE(hero):S(ssIndex)/?(ss:y)/1;        [навык в слоте]

; Show up current bonus if both Estates I and Estates II are disabled
!!if&(ss)=(SKILL_ESTATES);
  !!UN:P203/?(estatesI:y) P191/?(estatesII:y);
  !!FU|(estatesI)/(estatesII):E;

  !!FU(ES_793_ShowCurrentBonusDlg):P(hero);
!!en;

!?FU(ES_793_CalcBonusIncome);           [установка расширенного описания Экономики для героя x1]
!#VA(hero:x) (bonusIncome:x);

!!HE(hero):S(SKILL_ESTATES)/?(lv:y) X?(spec:y)/?(spec2:y)/d/d/d/d/d O?(owner:y); [y1 - Уровень Экономики, y2=0,y3=13 для специалистов по Экономике, y5 - владелец героя]
!!VR(lv)&(spec)=0/(spec2)=(SKILL_ESTATES):+1; [+1 уровень экономики, если герой - спец. по Экономике]
!!VRv942:S(lv);                         [v942 - уровень Экономики героя]

!!VRv943:S0;                            [инициализация v943]
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/?v941;       [v941 Количество городов на карте]
!!VRv2:S-1;                             [v2, инициализация v1 для быстрого поиска]
!!DO(ES_793_GetDailyIncomeOfTowns)/1/v941/1&v941>=0:P(owner); [получаем размер дневного дохода городов текущего игрока в v943]
!!VR(bonusIncome):Sv943 *(lv) :20;      [реальный бонус экономики героя на текущий момент]

!?FU(ES_793_ShowCurrentBonusDlg);
!#VA(hero:x);

!!CM:R0;

!!FU(ES_793_CalcBonusIncome):P(hero)/?(bonusIncome:y); [подмена описания, если нажатие на экономике]
!!HE(hero):S(SKILL_ESTATES)/?(lv:y);
!!SN:T^es.793.desc%(lv)^/?(desc:z) T^es.793.currBonus^/?(currBonus:z)/^gold^/(bonusIncome);

!!if&i^mouse_action^=(MOUSE_LMB_PRESSED);
  !!IF:M^%(desc)%(currBonus)^;
!!el&i^mouse_action^=(MOUSE_RMB_PRESSED);
  !!IF:M0/(MSG_TYPE_POPUP)^%(desc)%(currBonus)^;
!!en;

** НАЧИСЛЕНИЯ

; Ideally this script needs to be executed after any other OnCalculateTownIncome to grant more bonuses
!?FU(OnCalculateTownIncome);
!#VA(townId:x) (gold:x);

!!UN:P793/?(economy:y);
!!FU&(economy)<>(TRUE):E;

!!VR(totalEstateLv:y):S0;
; Loop through all the hero 
!!CA0/(townId):O?(owner:y);
!!OW:H(owner)/2;

!!re i/1/v2;
  !!VR(ptr:y):Si +2;
  !!HEv(ptr):S(SKILL_ESTATES)/?(lv:y) X?(spec:y)/?(spec2:y);
  !!VR(lv)&(spec)=0/(spec2)=(SKILL_ESTATES):+1;
  !!VR(totalEstateLv):+(lv);
!!en;

!!VR(newPercent:y):S5 *(totalEstateLv) +100;
!!VR(gold):*(newPercent) :100;

** end

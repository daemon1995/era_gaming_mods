ZVSE2
** Author orig.  : Jim Vogan
** Rewritten by  : Archer30 and daemon_n
** Name          : Buckler of Beelzebub
** Name rus.     : Щит Вельзевула
** Changes rus.  : [Algor] вынос опции в отдельный erm-файл для мода ERA
**               : [Algor] вынос текстов в ert-файл
**               : [Algor] перенумерация переменных
** Options       : 281


// Create erm hook on giving spell if Dimemsion Door is not banned
!?FU(ES_CreateERMHook);
!#VA(address:x);

; Check if option is enabled
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Check if Dimension Door is banned from options
!!UN:P146/?(bannedFromGuilds:y);
!!UN:P155/?(bannedDimensionDoor:y);
!!FU&(bannedFromGuilds)/(bannedDimensionDoor):E;

; Check if Dimenstion Door is banned by the map
; These two lines would only be re-enabled if we implement banning spells from artifact feature
*!UN:J0/(SPELL_DIMENSION_DOOR)/?(bannedByMap:y);
*!FU&(bannedByMap):E;

!!SN:E(address)/1/5085308/(ES_OnArtifactGiveSpell); [Trigger when artifact with spell given is about to be equipped]

// Initialization
!?FU(OnAfterErmInstructions);
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!UN:A15/(ART_BLANK_SHIELD)/(ART_SWORD_OF_HELLFIRE)/(ART_SHIELD_OF_THE_DAMNED)/(ART_HELLSTORM_HELMET)/(ART_BREASTPLATE_OF_BRIMSTONE); new combo #15

!!SN:H^art^/(ART_BLANK_SHIELD)/0/^%T(es.281.name)^;
!!SN:H^art^/(ART_BLANK_SHIELD)/1/^%T(es.281.desc)^;

; Enable spell on the artifact
!!UN:A(ART_BLANK_SHIELD)/8/(TRUE);

// artifact #(ART_BLANK_SHIELD) put on
!?FU(OnEquipArt)&v998=(ART_BLANK_SHIELD);
; disable this script if option disabled
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; increase primary skills by 2 if v998=(ART_BLANK_SHIELD)
!!HE(CURRENT_HERO):Fd2/d2/d2/d2;

// artifact #(ART_BLANK_SHIELD) taken off
!?FU(OnUnequipArt)&v998=(ART_BLANK_SHIELD);
; disable this script if option disabled
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; decrease primary skills by 2 if v998=(ART_BLANK_SHIELD)
!!HE(CURRENT_HERO):Fd-2/d-2/d-2/d-2;

// Change the spell of AB depending on the actual artifact equipped
!?FU(ES_OnArtifactGiveSpell);
!#VA(address:x);

!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/?(art:y);

!!if&(art)=(ART_BLANK_SHIELD);
  !!UN:C5085052/1/(SPELL_DIMENSION_DOOR);
  !!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/(ART_ARMAGEDDONS_BLADE);
!!el&(art)=(ART_ARMAGEDDONS_BLADE);
  !!UN:C5085052/1/(SPELL_ARMAGEDDON);
  !!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/(ART_ARMAGEDDONS_BLADE);
!!en;

// Add no retaliation flag to the stacks with the owner having a Buckler of Beelzebub
; Set up no retaliation when the stack stats are initialised
; This works perfectly random heroes option. If Asmodeus is summoned, the stacks are only granted No Retaliation only if Asmodeus himself has Buckler of Beelzebub
!?FU(ES_BattleStack_InitParams);
!#VA(stack:x) (side:x);

!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;
!!FU&i^battle_hero_%(side)^<=(NO_HERO):E;

!!HEi^battle_hero_%(side)^:A2/(ART_BLANK_SHIELD)/?(has:y)/?(equipped:y);

!!if&(equipped)>0;
  !!BM(stack):T?(type:y);
  !!BM(stack)&(type)<>(MON_ARROW_TOWERS):Fd|(MON_FLAG_NO_RETALIATION);
!!en;

** end

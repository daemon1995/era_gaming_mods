ZVSE2
** Author orig.  : Algor, feanor
** Rewritten by  : Archer30
** Name          : Pathfinding
** Name rus.     : Поиск Пути
** Options       : 739

** Опция изменяет стоимости перемещения по разным почвам/дорогам
** для разных уровней навыка Поиск Пути, так чтобы КАЖДЫЙ уровень
** навыка хоть и в разной степени, но был полезен на КАЖДОЙ почве.


!?FU(OnAfterErmInstructions);           [перед началом новой игры]
!!UN:P739/?(esPathfinding:y);           [проверяем включена ли опция 739]
!!FU&(esPathfinding)<>(TRUE):E;         [выход если опция не включена]

; Set up new skill description
!!VR(basicDesc:z):S^%T(es.739.desc1)^;
!!VR(advancedDesc:z):S^%T(es.739.desc2)^;
!!VR(expertDesc:z):S^%T(es.739.desc3)^;

; Add in WoG Pathfinding enhancedment if the option is enabled
!!UN:P209/?(wogPathfinding:y);

!!if&(wogPathfinding);
  !!VR(newLine:z):S^
^;
  !!VR(basicDesc):+(newLine) +z175112;
  !!VR(advancedDesc):+(newLine) +z175113;
  !!VR(expertDesc):+(newLine) +z175114;
!!en;

; Change the descriptions
!!SN:H^secskill^/(SKILL_PATHFINDING)/(SKILL_BASIC)/(basicDesc);
!!SN:H^secskill^/(SKILL_PATHFINDING)/(SKILL_ADVANCED)/(advancedDesc);
!!SN:H^secskill^/(SKILL_PATHFINDING)/(SKILL_EXPERT)/(expertDesc);

; Create an array for storing the original values
!!FU(NewIntArray):P?i^es_739_patches^/(M_TEMP);

!?FU(OnStartOrLoad);
!!UN:P739/?(esPathfinding:y);           [проверяем включена ли опция 739]
!!FU&(esPathfinding)<>(TRUE):E;         [выход если опция не включена]

; Store the original values in the array
!!SN:Mi^es_739_patches^/0;

!!re (address:y)/6546704/6546908/4;
  !!UN:C(address)/(UNC_INT)/?(value:y);
  !!FU(Array_Push):Pi^es_739_patches^/(value);
!!en;

; Set up new values
** Грязь
!!UN:C6546704/(UNC_INT)/130;     [Нет ПП]
!!UN:C6546708/(UNC_INT)/120;     [Базовый ПП]
!!UN:C6546712/(UNC_INT)/110;     [Продвинутый ПП]
!!UN:C6546716/(UNC_INT)/100;     [Эксперт ПП]
** Песок
!!UN:C6546720/(UNC_INT)/160;     [Нет ПП]
!!UN:C6546724/(UNC_INT)/140;     [Базовый ПП]
!!UN:C6546728/(UNC_INT)/120;     [Продвинутый ПП]
!!UN:C6546732/(UNC_INT)/100;     [Эксперт ПП]
** Трава
!!UN:C6546736/(UNC_INT)/115;     [Нет ПП]
!!UN:C6546740/(UNC_INT)/110;     [Базовый ПП]
!!UN:C6546744/(UNC_INT)/105;     [Продвинутый ПП]
!!UN:C6546748/(UNC_INT)/100;     [Эксперт ПП]
** Снег
!!UN:C6546752/(UNC_INT)/160;     [Нет ПП]
!!UN:C6546756/(UNC_INT)/140;     [Базовый ПП]
!!UN:C6546760/(UNC_INT)/120;     [Продвинутый ПП]
!!UN:C6546764/(UNC_INT)/100;     [Эксперт ПП]
** Болото
!!UN:C6546768/(UNC_INT)/175;     [Нет ПП]
!!UN:C6546772/(UNC_INT)/150;     [Базовый ПП]
!!UN:C6546776/(UNC_INT)/125;     [Продвинутый ПП]
!!UN:C6546780/(UNC_INT)/100;     [Эксперт ПП]
** Скалы
!!UN:C6546784/(UNC_INT)/145;     [Нет ПП]
!!UN:C6546788/(UNC_INT)/130;     [Базовый ПП]
!!UN:C6546792/(UNC_INT)/115;     [Продвинутый ПП]
!!UN:C6546796/(UNC_INT)/100;     [Эксперт ПП]
** Подземелье
!!UN:C6546800/(UNC_INT)/115;     [Нет ПП]
!!UN:C6546804/(UNC_INT)/110;     [Базовый ПП]
!!UN:C6546808/(UNC_INT)/105;     [Продвинутый ПП]
!!UN:C6546812/(UNC_INT)/100;     [Эксперт ПП]
** Лава
!!UN:C6546816/(UNC_INT)/130;     [Нет ПП]
!!UN:C6546820/(UNC_INT)/120;     [Базовый ПП]
!!UN:C6546824/(UNC_INT)/110;     [Продвинутый ПП]
!!UN:C6546828/(UNC_INT)/100;     [Эксперт ПП]
** Вода
!!UN:C6546832/(UNC_INT)/100;     [Нет ПП]
!!UN:C6546836/(UNC_INT)/100;     [Базовый ПП]
!!UN:C6546840/(UNC_INT)/100;     [Продвинутый ПП]
!!UN:C6546844/(UNC_INT)/100;     [Эксперт ПП]
** Пропасть
!!UN:C6546848/(UNC_INT)/0;       [Нет ПП]
!!UN:C6546852/(UNC_INT)/0;       [Базовый ПП]
!!UN:C6546856/(UNC_INT)/0;       [Продвинутый ПП]
!!UN:C6546860/(UNC_INT)/0;       [Эксперт ПП]
** Земляная дорога
!!UN:C6546864/(UNC_INT)/80;      [Нет ПП]
!!UN:C6546868/(UNC_INT)/80;      [Базовый ПП]
!!UN:C6546872/(UNC_INT)/80;      [Продвинутый ПП]
!!UN:C6546876/(UNC_INT)/80;      [Эксперт ПП]
** Гравийная дорога
!!UN:C6546880/(UNC_INT)/65;      [Нет ПП]
!!UN:C6546884/(UNC_INT)/65;      [Базовый ПП]
!!UN:C6546888/(UNC_INT)/65;      [Продвинутый ПП]
!!UN:C6546892/(UNC_INT)/65;      [Эксперт ПП]
** Мощеная дорога
!!UN:C6546896/(UNC_INT)/50;      [Нет ПП]
!!UN:C6546900/(UNC_INT)/50;      [Базовый ПП]
!!UN:C6546904/(UNC_INT)/50;      [Продвинутый ПП]
!!UN:C6546908/(UNC_INT)/50;      [Эксперт ПП]

!?FU(OnGameLeave);
!!UN:P739/?(esPathfinding:y);           [проверяем включена ли опция 739]
!!FU&(esPathfinding)<>(TRUE):E;         [выход если опция не включена]

; Restore all the original values
!!SN:Mi^es_739_patches^/?(size:y);
!!VR(address:y):S6546704;

!!re i/0/(size)/1/-1;
  !!SN:Mi^es_739_patches^/i/?(value:y);
  !!UN:C(address)/(UNC_INT)/(value:y);
  !!VR(address):+4;
!!en;

** end

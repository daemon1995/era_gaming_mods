ZVSE2

[script name: Piercing shot v.1.12]
[author: Petyo P. Georgiev]
[version: ERA 2.9.x]
[last changed: 17.06.2020 by igrik]

** what's new:
** ver.1.11:
** 1)Crossbowmen ignore 10% of target's defence when shooting. Marksmen ignore 3+20% of target's defence when shooting;
** 2)some tweaks to the marksman stats: +2 attack, +1 defence, damage 2-5, reduced number of shots to 12.

** ver.1.1:
** 1) Crossbowmen ignore 10% of target's defence when shooting. Marksmen ignore 20% of target's defence when shooting.
** 2) New var: v2622.

** This script modifies the abilities of the level 2 castle troops archer&marksman:
** 1) Name of archer changed to crossbowman;
** 2) Marksman shoots "piercing bolt", which deals half damage to a stack behind* the targeted stack;

** *the target behind is calculated depending on the position of the marksman and target stack; if target is on the same,
** or adjastent line, it is cosidered as straight shot and the stack exactly behind the target is taken for secondary damage.
** in the other cases it is considered as diagonal shot and the secondary stack is taken like with breath attack.

**** v vars used: (v51) v2600-2622
**** z vars used: z192-195 (change igrik: old z vars: z66-z69)
**** std vars used: NONE
**** flags used: 341-366
**** FU used: NONE (change igrik: old FU31-33)
**** dialogue used: NONE
**** object used: NONE

!?FU(OnAfterErmInstructions);
!!UN:P59/?i^wog_59_enabled^;

!!if&i^wog_59_enabled^;
  !!UN:V?(wogVer:y)/?(ermVer:y)/?(humanQty:y)/?(isNetwork:y)/?(gameType:y);

  ; Disable the script for network games
  !!if&(isNetwork)<>(FALSE);
    !!VRi^wog_59_enabled^:S(FALSE);
  ; Initialize vars if local
  !!el;
    !!MA:A(MON_MARKSMAN)/d2 D(MON_MARKSMAN)/d1 E(MON_MARKSMAN)/d2 N(MON_MARKSMAN)/d:2 X(MON_MARKSMAN)/d~(MON_FLAG_ATTACKS_TWICE); Marksman
    ; Change name and descriptions
    !!SN:H^monname^/(MON_ARCHER)/0/^%T(wog.59.singular)^;
    !!SN:H^monname^/(MON_ARCHER)/1/^%T(wog.59.plural)^;
    !!SN:H^monname^/(MON_ARCHER)/2/^%T(wog.59.description)^;
    !!SN:H^monname^/(MON_MARKSMAN)/2/^%T(wog.59.description_upg)^;
  !!en;
!!en;

!?FU(WOG_CreateERMHook);
!!if&i^wog_59_enabled^;
  *!SN:Ex1/1/4465431/(WOG_59_BattleStack_OnGetDefenceAgainst);
!!en;

!?FU(WOG_59_BattleStack_OnGetDefenceAgainst);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/8/4/?(enemyStack:y);

!!if&(enemyStack); // check if there is attacker
  !!UN:C(enemyStack)/52/4/?(creatureId:y); // get atacker monId
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/?(defence:y); // get defender's defence

  !!if&(creatureId)=(MON_ARCHER); // defence percentage decreaseing 
    !!VR(decrease:y):S10;
  !!el&(creatureId)=(MON_MARKSMAN);
    !!VR(defence):-3; -3 vs marksman
    !!VR(decrease:y):S20;
  !!en;

  !!if&(decrease); //if changing defence
    !!VR(fDefence:e):S(decrease:y) *(defence):100;
    !!VR(defence):-(fDefence);

    !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/(defence); // set new defence
  !!en;
!!en;

!?FU(OnBeforeBattleAction)&i^wog_59_enabled^;
!!VRv2602:S0;
!!VRv2611:S-1;
!!BG:A?v2600;
!!BG&v2600=7:N?v2601 D?v2602;           [v2602 - target stack position]
!!BMv2601&v2600=7:T?v2604 P?v2605 I?v2608; v2604 - type of shooter stack v2605 - shooting stack position]
!!BMv2601&v2600=7/v2604=3:I?v2603;      [get shooter's side]
!!BG&v2600=7/v2604=3:Q?v2607 H?v2610;   [v2607 shooter side | v2610 attacking stack commanding hero(if any)
!!HEv2610&v2610>-1/v2600=7/v2604=3:A2/60/d/?v2615 A2/61/d/?v2616 A2/62/d/?v2617 S1/?v2618; 53-archery lvl, 50 - +5%,51 - +10%,52 - +15%
!!BU&v2602>0:Ev2602/?v2619;             [get target stack num]
!!BMv2619&v2619>-1:G28/d/?v2620;        [v2620 is the prim target behind air shield]

!!BMv2619&v2619>-1/v2604>1/v2604<4:D?v2622; get primary target defence in v2622]
!!VRy2&v2604>1/v2604<4:Sv2622;
!!VRy2&v2604=2::10;                     [defence reduction for crossbowmen]
!!VRy2&v2604=3::5;                      [defence reduction for marksmen]
!!VRy2&v2604=3:+3;
!!VRy2&y2>v2622:Sv2622;                 [do not go below zero]
!!VRy2:*-1;                             [negative value]
!!BMv2619&v2619>-1/v2604>1/v2604<4:Ddy2; [subtract value for the shot]

*********************************linear check***********************************

!!IF:V341/0 V342/0 V343/0 V344/0 V345/0 V346/0 V347/0 V348/0 V349/0;
!!IF:V350/0 V351/0 V352/0 V353/0 V354/0 V355/0 V356/0 V357/0 V358/0;
!!IF:V359/0 V360/0 V361/0 V362/0 V363/0 V364/0 V365/0 V366/0;
!!IF&v2602>0/v2602<16/v2605>0/v2605<16:V341/1;
!!IF&v2602>17/v2602<33/v2605>17/v2605<33:V342/1;
!!IF&v2602>34/v2602<50/v2605>34/v2605<50:V343/1;
!!IF&v2602>51/v2602<67/v2605>51/v2605<67:V344/1;
!!IF&v2602>68/v2602<84/v2605>68/v2605<84:V345/1;
!!IF&v2602>85/v2602<101/v2605>85/v2605<101:V346/1;
!!IF&v2602>102/v2602<118/v2605>102/v2605<118:V347/1;
!!IF&v2602>119/v2602<135/v2605>119/v2605<135:V348/1;
!!IF&v2602>136/v2602<152/v2605>136/v2605<152:V349/1;
!!IF&v2602>153/v2602<169/v2605>153/v2605<169:V350/1;
!!IF&v2602>170/v2602<186/v2605>170/v2605<186:V351/1;
!!IF|341/342/343/344/345/346/347/348/349/350/351:V352/1; [if in one line set 352-true]

****************************straight shot group check***************************

!!IF&v2602>0/v2602<33/v2605>0/v2605<16:V353/1;
!!IF&v2602>0/v2602<50/v2605>17/v2605<33:V354/1;
!!IF&v2602>17/v2602<67/v2605>34/v2605<50:V355/1;
!!IF&v2602>34/v2602<84/v2605>51/v2605<67:V356/1;
!!IF&v2602>51/v2602<101/v2605>68/v2605<84:V357/1;
!!IF&v2602>68/v2602<118/v2605>85/v2605<101:V358/1;
!!IF&v2602>85/v2602<135/v2605>102/v2605<118:V359/1;
!!IF&v2602>102/v2602<152/v2605>119/v2605<135:V360/1;
!!IF&v2602>119/v2602<169/v2605>136/v2605<152:V361/1;
!!IF&v2602>136/v2602<186/v2605>153/v2605<169:V362/1;
!!IF&v2602>153/v2602<186/v2605>170/v2605<186:V363/1;
!!IF|353/354/355/356/357/358/359/360/361/362/363:V364/1; [if one group set 364 true]

****************************chk target line only********************************

!!IF&v2602>0/v2602<16:V365/1;
!!IF&v2602>17/v2602<33:V366/1;
!!IF&v2602>34/v2602<50:V365/1;
!!IF&v2602>51/v2602<67:V366/1;
!!IF&v2602>68/v2602<84:V365/1;
!!IF&v2602>85/v2602<101:V366/1;
!!IF&v2602>102/v2602<118:V365/1;
!!IF&v2602>119/v2602<135:V366/1;
!!IF&v2602>136/v2602<152:V365/1;
!!IF&v2602>153/v2602<169:V366/1;
!!IF&v2602>170/v2602<186:V365/1;

***if in one line set sec target hex***
!!VRv2606&352/v2605<v2602:Sv2602 +1;    [set v2606 to secondary target hex (left-to-rigt)]
!!VRv2606&352/v2605>v2602:Sv2602 -1;    [set v2606 to secondary target hex (right-to-left)]

***if not in one line realize actual positions***
!!VRv2609:S0;
!!DO(WOG_59_Func1)/1/10/1&-352:Pv2602/v2605;
!!DO(WOG_59_Func2)/1/14/1&-352/-364:Pv2602/v2605;
**(v2609 values:1-shot to right, 2-shot to left, 5-shot to up-right, 6-shot to up-left,**

********* 9-shot to down-right, 10-shot to down left, 132-shot up, 136-shot down ********

!!VRv2606&v2609=1:Sv2602 +1;            [set sec target of straight shot]
!!VRv2606&v2609=2:Sv2602 -1;            [set sec target of straight shot]
!!VRv2606&v2609=5/365:Sv2602 -16;       [set sec target of diagonal shot]
!!VRv2606&v2609=5/366:Sv2602 -17;       [set sec target of diagonal shot]
!!VRv2606&v2609=6/365:Sv2602 -17;       [set sec target of diagonal shot]
!!VRv2606&v2609=6/366:Sv2602 -18;       [set sec target of diagonal shot]
!!VRv2606&v2609=9/365:Sv2602 +18;       [set sec target of diagonal shot]
!!VRv2606&v2609=9/366:Sv2602 +17;       [set sec target of diagonal shot]
!!VRv2606&v2609=10/365:Sv2602 +17;      [set sec target of diagonal shot]
!!VRv2606&v2609=10/366:Sv2602 +16;      [set sec target of diagonal shot]
!!VRv2606&v2609=132/365:Sv2602 -16;     [set sec target of vertical shot]
!!VRv2606&v2609=132/366:Sv2602 -18;     [set sec target of vertical shot]
!!VRv2606&v2609=136/365:Sv2602 +18;     [set sec target of vertical shot]
!!VRv2606&v2609=136/366:Sv2602 +16;     [set sec target of vertical shot]
!!BU&v2606>0/v2606<186:Ev2606/?v2611;
!!FU(WOG_59_Func3)&v2611>-1:Pv2615/v2616/v2617/v2618;
!!BMv2611&v2611>-1:I?v51;
!!BMv2611&v2611>-1/v2600=7/v2604=3/v2619<>v2611/v51<>v2608/v2620=0:Kv2612; [apply dmg to sec target]

!?FU(OnAfterBattleAction)&i^wog_59_enabled^/v2604>1/v2604<4;
!!BMv2619&v2619>-1:Dv2622;              [restore def value after the shot]

=====================================FUs========================================

!?FU(WOG_59_Func1);                  [realize positions (horizontally)]
!!VRx1&x1>15:-17;                       [move target position one line higher]
!!VRx2&x2>15:-17;                       [move shooter position one line higher]
!!VRx16&x1<16/x2<16:S10;
!!VRv2609&x2<x1/x1<16/x2<16:+1;         [shot to the right]
!!VRv2609&x2>x1/x1<16/x2<16:+2;         [shot to the left]
!!VRv2609&x2=x1/x1<16:+128;             [shooter&target in one vertical line]

!?FU(WOG_59_Func2);                  [realize positions (vertically)]
!!VRx1&x1<>1/x1<>18/x1<>35/x1<>52/x1<>69/x1<>86/x1<>103/x1<>120/x1<>137/x1<>154/x1<>171:-1;
!!VRx2&x2<>1/x2<>18/x2<>35/x2<>52/x2<>69/x2<>86/x2<>103/x2<>120/x2<>137/x2<>154/x2<>171:-1;
!!VRy1|x1=1/x1=18/x1=35/x1=52/x1=69/x1=86/x1=103/x1=120/x1=137/x1=154/x1=171:S10;
!!VRy2|x2=1/x2=18/x2=35/x2=52/x2=69/x2=86/x2=103/x2=120/x2=137/x2=154/x2=171:S10;
!!VRx16&y1=10/y2=10:S14;
!!VRv2609&y1=10/y2=10/x1<x2:+4;         [shot upwards]
!!VRv2609&y1=10/y2=10/x1>x2:+8;         [shot downwards]

!?FU(WOG_59_Func3);
!!BMv2601:A?y1 N?y2 G41/?y20/?y21 G42/?y22/?y23; y1 - attack skill of marksman, y2 - number of marksman
!!BMv2611:D?y3 G28/?y24/?y25;           y3 - defence of sec target
!!VRy5&y20=0:Sy2 *4;                    y5 - base damage
!!VRy5&y20>0/y21<2:Sy2 *5;              basic bless modifyer
!!VRy5&y20>0/y21>1:Sy2 *11 :2;          adv/exp bless modifyer
!!VRy5&y22>0/y23<2:Sy2 *2;              basic curse modifyer
!!VRy5&y22>0/y23>1:Sy2 *3 :2;           adv/exp curse modifyer
!!VRy6:Sy5 :20;                         y6 - bonus dmg (1)
!!VRy7:Sy1;
!!VRy7:-y3;                             attack - defence
!!VRy6:+y7;
!!VRy6&y6<0:S0;                         if defence>attack -> no bonus damage
!!VRy7:Sy5 *3;
!!VRy6&y6>y7:Sy7;                       condition bonus dmg not to excess base dmg more than 3 times
!!VRy8&x1>0:+5;                         y8 - bonus dmg (2) - archery bonus
!!VRy8&x2>0:+10;
!!VRy8&x3>0:+15;
!!VRy8&x4=1:+10;
!!VRy8&x4=2:+25;
!!VRy8&x4=3:+50;
!!VRy10:Sy5;
!!VRy10:*y8 :100;
!!VRy8:Sy10;
!!VRy9:S100;                            dmg reduction (%)
!!VRy10:Sy3;
!!VRy10:-y1;
!!VRy10:*5 :2;
!!VRy9:-y10;
!!VRe1:Sy9 :100;
!!VRe1&y1>y3:S1;
!!VRy5:+y6 +y8;
!!VRy5:*e1;
!!VRy5&y24>0/y25<2::2;                  basic air shield modifyer
!!VRy5&y24>0/y25>1::3;                  adv/exp air shield modifyer
!!VRv2612:Sy5;                          final result damage
!!VRv2612::2;
!!VRv2612&v2612<1:S1;                   no less than 1 HP

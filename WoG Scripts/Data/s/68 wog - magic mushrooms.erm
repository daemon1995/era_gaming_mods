ZVSE2

Script: Magic Mushrooms
WoGify name: script10.erm
Updated: July 29, 2004
Updated: Feb 2022 by Archer30

************************
*** Magic Mushrooms  ***
***     By Bruno     ***
***       V1.10f     ***
************************

IA support 10e
This version is working well 10.d
Thanks To Donald for his suggestions and his help

When you visit the Mushroom, your hero gains Expert level in
one of the four schools of magic for the rest of the week.
This works even if your hero has all eight skill slots filled.

Uses Functions 10-12
Uses v1-v2-v3 just for local use
Uses w20-w27 to track hero visits to mushrooms and previous skill levels
Uses z180-z188 for hint texts

!?FU(WOG68_Initialization);
!!UN:P110/?(mushroomOn:y); check if Magic Mushrooms are enabled

!!if&(mushroomOn);
  *** Init for Magic Water Mushroom
  !!VRz180:Sz110000;
  !!VRz181:Sz110001;
  !!HT63/18:W21/0/180;  Rename all Magic Water mushrooms
  !!HT63/18:W21/1/181;  call z181 string if mushroom was visited
  *** Init for Magic Fire Mushroom
  !!VRz182:Sz110002;
  !!VRz183:Sz110003;
  !!HT63/29:W23/0/182;  Rename all Magic Fire mushrooms
  !!HT63/29:W23/1/183;  call z183 string if mushroom was visited
  *** Init for Magic Earth Mushroom
  !!VRz184:Sz110004;
  !!VRz185:Sz110005;
  !!HT63/23:W25/0/184;  Rename all Magic Earth mushrooms
  !!HT63/23:W25/1/185;  call z185 string if mushroom was visited
  *** Init for Magic Air Mushroom
  !!VRz186:Sz110006;
  !!VRz187:Sz110007;
  !!HT63/14:W27/0/186;  Rename all Magic Air mushrooms
  !!HT63/14:W27/1/187;  call z187 string if mushroom was visited
!!en;

!#FU(WOG68_Initialization):P;

*** Timer
!?FU(OnEveryDay)&i^timerWeekday^=1/i^timerOnce^/i^timerDay^>1;
!!UN:P110/?(mushroomOn:y);              [check if script is enabled: y-1]
!!UN:P904/(TRUE);                       [Disable error msg - it's known that HE:S produces unknown erm error for some reason]
!!FU(WOG68_ResetVariables)&(mushroomOn):P;
!!UN:P904/(FALSE);                      [Re-enable error msg]

!?FU(WOG68_ResetVariables);
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!IF:Wi;
  *** water
  !!HEi&w21=1:S16/w20;
  !!VRw20:S0; [reinstate old skill level if visited mushroom]
  !!VRw21:S0; free the visit
  *** Fire
  !!HEi&w23=1:S14/w22;
  !!VRw22:S0; [reinstate old skill level if visited mushroom]
  !!VRw23:S0; free the visit
  *** Earth
  !!HEi&w25=1:S17/w24;
  !!VRw24:S0; [reinstate old skill level if visited mushroom]
  !!VRw25:S0; free the visit
  *** Air
  !!HEi&w27=1:S15/w26;
  !!VRw26:S0; [reinstate old skill level if visited mushroom]
  !!VRw27:S0; free the visit
!!en;

*** Magic Water Mushroom ***
!?OB63/18;
!!UN:P110/?y-1; [check if script is enabled: y-1]
!!FU&y-1=0:E; [Exit if script isn't enabled]
!!VRv1:S0;
!!IF:W-1;
!!HE-1&w21<>1/v1<>3:S16/?v1; ask skill
!!IF&v1=3/1000:Q1/20/53/1/z110008;
!!IF&w21=1/1000:M1/z110009;
!!VRw20&v1<>3/w21<>1:Sv1; w20 stock the level of the skill
!!IF&v1<>3/w21<>1/1000:Q1/20/53/1/z110010;
!!HE-1&v1<>3:S16/3; put skill in expert
!!VRw21&v1<>3:S1; heroes have visited the magic water mushroom

*** Magic Fire Mushrooms ***
!?OB63/29;
!!UN:P110/?y-1; [check if script is enabled: y-1]
!!FU&y-1=0:E; [Exit if script isn't enabled]
!!VRv1:S0;
!!IF:W-1;
!!HE-1&w23<>1/v1<>3:S14/?v1;
!!IF&v1=3/1000:Q1/20/47/1/z110011;
!!IF&w23=1/1000:M1/z110012;
!!VRw22&v1<>3/w23<>1:Sv1; w22 stock the level of the skill
!!IF&v1<>3/w23<>1/1000:Q1/20/47/1/z110013;
!!HE-1&v1<>3:S14/3; put skill in expert
!!VRw23&v1<>3:S1; heroes have visited the magic mushroom

*** Magic Earth Mushrooms ***
!?OB63/23;
!!UN:P110/?y-1; [check if script is enabled: y-1]
!!FU&y-1=0:E; [Exit if script isn't enabled]
!!VRv1:S0;
!!IF:W-1;
!!HE-1&w25<>1/v1<>3:S17/?v1;
!!IF&v1=3/1000:Q1/20/56/1/z110014;
!!IF&w25=1/1000:M1/z110015;
!!VRw24&v1<>3/w25<>1:Sv1; w22 stock the level of the skill
!!IF&v1<>3/w25<>1/1000:Q1/20/56/1/z110016;
!!HE-1&v1<>3:S17/3; put skill in expert
!!VRw25&v1<>3:S1; heroes have visited the magic mushroom

*** Magic Air Mushrooms ***
!?OB63/14;
!!UN:P110/?y-1; [check if script is enabled: y-1]
!!FU&y-1=0:E; [Exit if script isn't enabled]
!!VRv1:S0;
!!IF:W-1;
!!HE-1&w27<>1/v1<>3:S15/?v1;
!!IF&v1=3/1000:Q1/20/50/1/z110017;
!!IF&w27=1/1000:M1/z110018;
!!VRw26&v1<>3/w27<>1:Sv1; w22 stock the level of the skill
!!IF&v1<>3/w27<>1/1000:Q1/20/50/1/z110019;
!!HE-1&v1<>3:S15/3; put skill in expert
!!VRw27&v1<>3:S1; heroes have visited the magic mushroom

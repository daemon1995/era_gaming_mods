ZVSE

;Minor updates (mouse-over hint text added) and bug fixes by Timothy Pulver
;additional updating by Hermann the Weird

;upgraded buildings can be upgraded more 1 time per turn
;cost is maximum of resource type for build basic and upgraded structure
;upgraded building allows to increase number of growth in this castle by number of upgraded times
;computer builds every week 1 upgrade for each builded type
;also you can upgrade capitol or city-hall for 10000 - every day income growth by 1000.
;Used v424-v435, y, v1600-1983
;flags 432,433;
;FU859-869, FU819
;TM82
------------------------Dialog to build afterupdate-----------------------------
;x1=number of town
;x2=creature
;x3=type of town
!?FU859&432;

!!VRv430:Sx1;
!!VRv432:Sx3;

!!VRv431:Sx2-164+37;
!!CA-1:B3/v431;                       ;flag1 setted if upgraded building already built

!!CA-1&-1:T?v434;                     ;check for horde buildings
!!CA-1&-1/v434=0/x2=166:B3/19;        ;Castle - Griffins
!!CA-1&-1/v434=1/x2=165:B3/19;        ;Rampart - Dwarves
!!CA-1&-1/v434=1/x2=168:B3/25;        ;Rampart - Dendroids
!!CA-1&-1/v434=2/x2=165:B3/19;        ;Tower - Gargoiles
!!CA-1&-1/v434=3/x2=164:B3/19;        ;Inferno - Imps
!!CA-1&-1/v434=3/x2=166:B3/25;        ;Inferno - Cerberies
!!CA-1&-1/v434=4/x2=164:B3/19;        ;Necropolis - Sceletons
!!CA-1&-1/v434=5/x2=164:B3/19;        ;Dungeon - Troglodites
!!CA-1&-1/v434=6/x2=164:B3/19;        ;Stronghold - Goblins
!!CA-1&-1/v434=7/x2=164:B3/19;        ;Fortress - Gnolls
!!CA-1&-1/v434=8/x2=164:B3/19;        ;Conflux - Sprites

!!VRv431:Sx2-163;
;calculate price of afterupdate
!!FU863&1/x3=0/x2=164:P0/0/7/0/0/0/1000/5; //pikeman
!!FU863&1/x3=0/x2=165:P5/0/5/0/0/0/1500/4; //archers
!!FU863&1/x3=0/x2=166:P0/0/5/0/0/0/2500/3; //griffins
!!FU863&1/x3=0/x2=167:P0/0/5/0/5/0/2500/2; //swordman
!!FU863&1/x3=0/x2=168:P5/2/5/2/2/2/3000/2; //monk
!!FU863&1/x3=0/x2=169:P10/0/0/0/0/0/5000/1; //cavalier
!!FU863&1/x3=0/x2=170:P0/10/0/10/10/10/6000/1; //angel

!!FU863&1/x3=1/x2=164:P7/0/0/0/0/0/1000/5; //centaur
!!FU863&1/x3=1/x2=165:P0/0/5/0/0/0/2000/4; //dwarf
!!FU863&1/x3=1/x2=166:P5/0/0/0/0/0/2500/3; //elf
!!FU863&1/x3=1/x2=167:P0/0/5/0/5/0/2500/2; //pegasi
!!FU863&1/x3=1/x2=168:P5/0/0/0/0/0/4200/2; //dendroid
!!FU863&1/x3=1/x2=169:P5/0/5/0/0/5/4500/1; //unicorn
!!FU863&1/x3=1/x2=170:P0/0/15/0/10/0/10000/1; //dragon

!!FU863&1/x3=2/x2=164:P5/0/5/0/0/0/1000/5; //gremlin
!!FU863&1/x3=2/x2=165:P0/0/10/0/0/0/1200/4; //gargobli
!!FU863&1/x3=2/x2=166:P3/3/3/0/0/0/2000/3; //golem
!!FU863&1/x3=2/x2=167:P0/3/0/3/3/3/2200/2; //magi
!!FU863&1/x3=2/x2=168:P0/5/0/0/5/5/2500/2; //genie
!!FU863&1/x3=2/x2=169:P5/2/5/2/2/2/4500/1; //nagas
!!FU863&1/x3=2/x2=170:P10/0/10/0/0/15/6000/1; //titan

!!FU863&1/x3=3/x2=164:P4/0/4/0/0/0/1000/5; //imps
!!FU863&1/x3=3/x2=165:P0/5/5/0/0/0/1250/4; //gog
!!FU863&1/x3=3/x2=166:P5/0/0/5/0/0/1750/3; //cerbers
!!FU863&1/x3=3/x2=167:P5/0/5/0/0/0/2700/2; //demon
!!FU863&1/x3=3/x2=168:P0/5/0/5/0/0/3000/2; //pit
!!FU863&1/x3=3/x2=169:P0/5/10/5/5/5/3500/1; //efreet
!!FU863&1/x3=3/x2=170:P10/10/10/0/0/0/8000/1; //devils

!!FU863&1/x3=4/x2=164:P0/0/7/0/0/0/1000/5; //sceleton
!!FU863&1/x3=4/x2=165:P5/0/5/0/0/0/1500/4; //zombie
!!FU863&1/x3=4/x2=166:P3/3/3/0/0/0/2000/3; //wights
!!FU863&1/x3=4/x2=167:P5/0/5/0/10/10/2500/2; //vampire
!!FU863&1/x3=4/x2=168:P0/0/5/5/0/0/3200/2; //lich
!!FU863&1/x3=4/x2=169:P10/2/10/2/2/2/5000/1; //knights
!!FU863&1/x3=4/x2=170:P5/10/5/5/5/5/7000/1; //drg

!!FU863&1/x3=5/x2=164:P7/0/0/0/0/0/1000/5; //troglodite
!!FU863&1/x3=5/x2=165:P0/0/0/2/2/0/2000/3; //harpies
!!FU863&1/x3=5/x2=166:P0/2/0/2/2/2/2000/3; //eyes
!!FU863&1/x3=5/x2=167:P5/0/10/0/0/0/2500/2; //medusa
!!FU863&1/x3=5/x2=168:P0/0/10/0/0/10/2500/2; //minotaur
!!FU863&1/x3=5/x2=169:P5/5/5/5/0/0/4000/1; //manticore
!!FU863&1/x3=5/x2=170:P9/0/9/10/0/0/10000/1; //drg

!!FU863&1/x3=6/x2=164:P5/0/5/0/0/0/1000/5; //goblin
!!FU863&1/x3=6/x2=165:P10/0/5/0/0/0/1000/4; //wulf
!!FU863&1/x3=6/x2=166:P5/0/5/0/0/0/2200/3; //orc
!!FU863&1/x3=6/x2=167:P5/0/5/0/0/5/2200/2; //ogre
!!FU863&1/x3=6/x2=168:P5/0/10/0/0/10/2200/2; //roc
!!FU863&1/x3=6/x2=169:P5/0/10/0/10/0/3000/1; //cyclop
!!FU863&1/x3=6/x2=170:P5/0/5/0/10/0/10000/1; //beh

!!FU863&1/x3=7/x2=164:P7/0/0/0/0/0/1000/5; //gnoll
!!FU863&1/x3=7/x2=165:P5/0/0/0/0/0/2000/4; //liz
!!FU863&1/x3=7/x2=166:P0/3/0/3/0/0/2200/3; //flies
!!FU863&1/x3=7/x2=167:P0/0/10/0/0/5/2000/2; //bas
!!FU863&1/x3=7/x2=168:P10/0/10/0/0/10/2000/2; //gorgons
!!FU863&1/x3=7/x2=169:P10/10/0/0/0/0/3500/1; //wyvern
!!FU863&1/x3=7/x2=170:P10/0/10/10/0/0/7000/1; //hydra

!!FU863&1/x3=8/x2=164:P5/0/5/0/0/0/1000/6; //sprites
!!FU863&1/x3=8/x2=165:P2/2/5/0/0/2/2000/3; //air
!!FU863&1/x3=8/x2=166:P0/5/5/0/0/0/2000/3; //water
!!FU863&1/x3=8/x2=167:P5/5/5/0/0/5/2000/2; //fire
!!FU863&1/x3=8/x2=168:P0/0/10/5/0/0/3000/2; //earth
!!FU863&1/x3=8/x2=169:P5/3/5/3/3/3/4000/1; //magic
!!FU863&1/x3=8/x2=170:P10/10/10/0/0/0/5000/1; //phoenix




!?FU863&432;

!!FU862:Pv430/v431;
!!VRz405:Sz145000;
!!VRz405&x7>0:Sz405 +z145001;
!!VRz405|x1>0/x3>0:Sz405 +^
^;
!!VRz405&x1>0:Sz405 +z145002;
!!VRz405&x3>0:Sz405 +z145003;
!!VRz405|x2>0/x4>0/x5>0/x6>0:Sz405 +^
^;
!!VRz405&x2>0:Sz405 +z145004;
!!VRz405&x4>0:Sz405 +z145005;
!!VRz405&x5>0:Sz405 +z145006;
!!VRz405&x6>0:Sz405 +z145007;

!!FU862:Pv430/0;
!!VRv435:Sv435 %1000;
!!VRz405&v435>0:Sz405 +z145008;

OW:C?v434; ; v434=current player
OW:Gv434/?v434;
IF:M^v434 = %V434^;

!!VRv434: S0;                          ;if v434=7 then player can build upgrade
!!OW:Rv428/0/?v433;                    ;v433=amount of resource
!!VRv434&v433>=x1: Sd1;
!!OW:Rv428/1/?v433;
!!VRv434&v433>=x2: Sd1;
!!OW:Rv428/2/?v433;
!!VRv434&v433>=x3: Sd1;
!!OW:Rv428/3/?v433;
!!VRv434&v433>=x4: Sd1;
!!OW:Rv428/4/?v433;
!!VRv434&v433>=x5: Sd1;
!!OW:Rv428/5/?v433;
!!VRv434&v433>=x6: Sd1;
!!OW:Rv428/6/?v433;
!!VRv434&v433>=x7: Sd1;

!!VRz405&v434=7/v435=0:Sz405 +z145009;

!!VRz405&v434<7/v435=0:Sz405 +z145010;

!!IF:V433/0;
!!IF&v434=7/v435=0:Q433/405;
!!IF&v434<7/v435=0:M1/405;
!!IF&v435>0:M1/405;

!!FU861&433:Px1/x2/x3/x4/x5/x6/x7/0/v430/v431/v428/x8;


--------------------------Dialog to build gold reserve-----------------------------
!?FU860&432;
;get money
!!OW:Rv428/6/?v431;                    ;v431=amount of gold
!!OW:Rv428/7/?v430;                    ;v430=amount of mithril
!!FU862:Px1/0;                         ;get current value

!!VRv429:Sv435 %1000;                  ;v429=0 if possible to build
!!VRv435: :1000 *1000;
!!CA-1:B3/12;
!!CA-1&-1:B3/13;

!!VRz405:Sz145011;

!!VRz405&1/v429>0:Sz405 +z145012;

!!VRz405&1/v431>=7000/v430>=2/v429=0:Sz405+z145013;
!!IF&1/v431>=7000/v430>=2/v429=0:Q433/6/7000/7/2/2/405;
!!FU861&1/433/v431>=7000/v430>=2/v429=0:P0/0/0/0/0/0/7000/2/x1/0/v428/1000;

!!VRz405&-1:Sz405+z145014;
!!VRy1:S0;
!!VRy1|v431<7000/v430<2:S1;    [Set y1 to 1 if not enough resources for Gold Reserve: y1]
!!VRz405&1/y1=1/v429=0:Sz405+z145015;
!!VRz405|-1/y1=1/v429>0:Sz405+z145016;
!!IF|-1/v431<7000/v430<2/v429>0:Q433/6/7000/7/2/1/405;

-----------------------Decrease money and build upgrade-------------------------
;x1-x8 resource decrease
;x9 town number
;x10 type increasing
;x11 player color
;x12 number to add
!?FU861&432;
!!VRy1:Sx9 *8 +1600 +x10;              ;index of variable where saved
!!VRvy1:+x12;
!!OW:Rx11/0/?y1;
!!VRy1:-x1;
!!OW:Rx11/0/y1;
!!OW:Rx11/1/?y1;
!!VRy1:-x2;
!!OW:Rx11/1/y1;
!!OW:Rx11/2/?y1;
!!VRy1:-x3;
!!OW:Rx11/2/y1;
!!OW:Rx11/3/?y1;
!!VRy1:-x4;
!!OW:Rx11/3/y1;
!!OW:Rx11/4/?y1;
!!VRy1:-x5;
!!OW:Rx11/4/y1;
!!OW:Rx11/5/?y1;
!!VRy1:-x6;
!!OW:Rx11/5/y1;
!!OW:Rx11/6/?y1;
!!VRy1:-x7;
!!OW:Rx11/6/y1;
!!OW:Rx11/7/?y1;
!!VRy1:-x8;
!!OW:Rx11/7/y1;

!!VRy1:Sx9 *8 +1600;                   ;hall
!!VRvy1:Sd1;                           ;now you cannot build more
!!VRy2:Svy1;
!!UN&999:R4;                           [algor: redraw town screen (for human only)]

-----------------------------Get current values---------------------------------
;x1=number of town
;x2=index of building (0 if hall 1-7 dwellings)
;return in v435 current additional value
!?FU862&432;
!!VRy1:Sx1 *8 +1600 +x2;               ;index of variable where saved
!!VRv435:Svy1;

-------------------------------Click handling-----------------------------------
!?CM1&432/999;
!!CM:S?y-1;  //Subtype of click: y-1 (if 13, it's a "dead" area of the screen)
!!FU&y-1=13:E; //Exit if dead area clicked
!!CM:I?v425;         [Get click area]
!!CM:F?v426;         [Get type of click]
!!CA-1:T?v427 O?y-2; [Get town type: v427, Owner: y-2]
!!OW:C?v428;         [v428=color]
!!CA-1:U?v429;       [Number on town list]
!!VRv426:&512;       [Look at bit of l-click]

!!FU|v426<>0/y-2<>v428:E; [Exit if no l-click or player doesn't own town]

!!FU860&v425=158:Pv429/v425/v427; //Hall icon click

!!FU&v425<>150:E;     [Exit if l-click not on town icon]

!!VRz-8:Sz145017;      [Set strings for radio button box]
!!VRz-9:Sz145018;

!!IF:V2/0;           [Initialize Flag 2 to False]
!!DO819/1/7/1:P;     [Check for dwellings built]

!!FU859&v425>=164/v425<=170:Pv429/v425/v427; //Creatures icons


** Function to check for upgraded dwellings

!?FU819&432;
!!VRyx16:Sx16 +36;     [Check for upgraded building]
!!CA-1:B3/yx16;

!!CA-1:T?y8;     [Check town type]
!!CA-1&-1/y8=0/x16=3:B3/19;     [check for horde buildings]
!!CA-1&-1/y8=1/x16=2:B3/19;
!!CA-1&-1/y8=1/x16=5:B3/25;
!!CA-1&-1/y8=2/x16=2:B3/19;
!!CA-1&-1/y8=3/x16=1:B3/19;
!!CA-1&-1/y8=3/x16=3:B3/25;
!!CA-1&-1/y8=4/x16=1:B3/19;
!!CA-1&-1/y8=5/x16=1:B3/19;
!!CA-1&-1/y8=6/x16=1:B3/19;
!!CA-1&-1/y8=7/x16=1:B3/19;
!!CA-1&-1/y8=8/x16=1:B3/19;

!!VRyx16&1:Sx16 *-1;     [Set indexed y vars]

!!VRy9:Sx16 -1;     [Get creature names and dwelling levels]
!!UN:Ty8/y9/1/?y10;
!!UN:N3/-10/y10/1;
!!VRy9:Sx16 *-1;
!!VRzy9:Sz145019;

!!VRyx16&-1:S0;     [set to 0 if no upgr. dwelling]
!!IF&1:V2/1;        [set Flag 2 to True if any upgraded dwellings exist in town]

!!IF&x16=7/-2:M1/z145020;
!!FU&x16=7/-2:E;          [Exit if no upgraded dwellings exist in town]

!!IF&x16=7/2:G1/424/1/-8/-9/y1/y2/y3/y4/y5/y6/y7/0/0/0/0;     [Radio button box]

!!VRv425&v424=1/x16=7:S999;     [Translate selection to correct values of v425 for FU859]
!!VRv425&v424=2/x16=7:S164;
!!VRv425&v424=4/x16=7:S165;
!!VRv425&v424=8/x16=7:S166;
!!VRv425&v424=16/x16=7:S167;
!!VRv425&v424=32/x16=7:S168;
!!VRv425&v424=64/x16=7:S169;
!!VRv425&v424=128/x16=7:S170;


-------------------------Mouse Move trigger (for hint text)---------------------
!?MM1&432/999;
!!CM:I?y-1; //Area mouse moves over
!!VRz-1&y-1=158:Sz145021;
!!VRz-1&y-1=150:Sz145022;
!!CM&y-1=150|y-1=158:Mz-1; //Display mouse-over hint text
------------------------------------Timer---------------------------------------
!?TM82&432;
!!UN:U98/-1/?v425;
!!VRv425:-1;
!!DO867/0/v425/1:P;                    ;money increase

!!VRv430:Sc%7;                         ;v430=1 is monday now
!!DO865/0/v425/1&v430=1:P;             ;creatures grow only on mondays

!!OW:C?v425;
!!OW:Iv425/?v426;
!!OW:Wv425/?v427;                      ;v427=number of towns
!!FU869&v426=1/v427>0:Pv425;

--------------------------------AI constructing---------------------------------
;x1=color
!?FU869&432;
!!OW:W-1/0/?v428;
!!CA0/v428:T?v434;                     ;v434=type

!!VRv429:S0 R49;

!!CA0/v428:B3/12;                      ;city hall
!!CA0/v428&-1:B3/13;                   ;capitol
!!FU870&1/v429>1/v429<=5:P0/0/0/0/0/0/0/0/v428/0/x1/1000;

!!CA0/v428:B3/37;                      ;1st
!!CA0/v428&-1/v434>=3:B3/19;
!!FU870&1/v429=6:P0/0/0/0/0/0/0/0/v428/1/x1/5;

!!CA0/v428:B3/38;                      ;2nd
!!CA0/v428&-1/v434>=1/v434<=2:B3/19;
!!FU870&1/v429>=7/v429<=8:P0/0/0/0/0/0/0/0/v428/2/x1/4;

!!CA0/v428:B3/39;                      ;3rd
!!CA0/v428&-1/v434=0:B3/19;
!!CA0/v428&-1/v434=3:B3/25;
!!FU870&1/v429>=9/v429<=12:P0/0/0/0/0/0/0/0/v428/3/x1/3;

!!CA0/v428:B3/40;                      ;4th
!!FU870&1/v429>=13/v429<=18:P0/0/0/0/0/0/0/0/v428/4/x1/2;

!!CA0/v428:B3/41;                      ;5th
!!CA0/v428&-1/v434=1:B3/25;
!!FU870&1/v429>=19/v429<=21:P0/0/0/0/0/0/0/0/v428/5/x1/2;

!!CA0/v428:B3/42;                      ;6th
!!FU870&1/v429>=22/v429<=24:P0/0/0/0/0/0/0/0/v428/6/x1/1;

!!CA0/v428:B3/43;                      ;7th
!!FU870&v429=25:P0/0/0/0/0/0/0/0/v428/7/x1/1;

!?FU870&432;
!!VRy1&x10<>0:Sx10+36;
!!CA0/x9&x10<>0:B3/y1;
!!FU861&x10<>0/1:Px1/x2/x3/x4/x5/x6/x7/x8/x9/x10/x11/x12;

!!CA0/x9&x10=0:B3/12;
!!CA0/x9&x10=0/-1:B3/13;
!!FU861&x10=0/1:Px1/x2/x3/x4/x5/x6/x7/x8/x9/x10/x11/x12;

---------------------------Everyday money increase------------------------------
!?FU867&432;
!!CA0/x16:O?v434;
!!OW:C?v433;
!!FU862&v434=v433:Px16/0;
!!VRv435: :1000 *1000;                 ;only thousands possible to add.
!!VRy1:Sx16*8 +1600;                   ;decrease non-thousands values
!!VRvy1&v434=v433:Sv435;
!_!OW&v434=v433:Rv433/6/dv435;          [доход учитывается в секции ниже. algor]

--------------------------------Creatures grow----------------------------------
!?FU865&432;
!!CA0/x16:O?v434;
!!OW:C?v433;
!!DO866/0/6/1&v434=v433:Px16;          ;if current player is owner then cycle

!?FU866&432;
!!VRx2:Sx16+1;
!!FU862:Px1/x2;                        ;get current values for dwelling
!!CA0/x1:M1/x16/d/dv435;               ;increase monsters in dwelling

------------------------------------Init----------------------------------------
!#UN:P45/?v429;
!#IF&v429=1:V432/1;

!#TM82&432:S1/999/1/255;                   ;timer every day

----------------------Display gold bonus on town screen-------------------------
** algor
!?FU(WOG_Town_GetGoldIncome)&432; [расчет дохода города]
!!SN:W^tmp.y1^/y1 W^tmp.y2^/y2 W^tmp.y3^/y3 W^tmp.y4^/y4; [сохранение значений y-переменных]
!!SN:X?y1; !!VRy2:Sy1+24;
!!UN:Cy2/4/?y3;                 [структура текущего в функции города]
!!UN:Cy3/1/?y4;                 [получить номер города на карте]
!!VRy4:*8 +1600;                [y4 - индекс v-переменной с доп. доходом]
!!VRy3:Svy4 :1000 *1000;        [только тысячи, остаток используется по другому]
!!VRy2:Sy1+28; !!UN:Cy2/4/?y1;  [y1 - текущий доход города]
!!VRy1:+y3;                     [добавление дохода от золотого запаса]
!!UN:Cy2/4/y1;                  [обновление дохода]
!!SN:W^tmp.y1^/?y1 W^tmp.y2^/?y2 W^tmp.y3^/?y3 W^tmp.y4^/?y4; [восстановление значений y-переменных]

!?FU(WOG_CreateERMHook)&432;
!!SN:Ex1/1/6027880/(WOG_Town_GetGoldIncome);
** end

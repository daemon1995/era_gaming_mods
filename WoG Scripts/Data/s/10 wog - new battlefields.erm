ZVSE2

** NEW BATTLEFIELDS
** НОВЫЕ ПОЛЯ БИТВ

** Option 068 by Donald X. Vaccarino, Overlord, Dutch, 
** Jonas Gustafsson, and Timothy Pulver 

** Rewritten by igrik
** Updated by Archer30 to support external images

** New Battlefield pictures will often be used in combat. 
** This has no affect on the battles but adds some visual variety.


!#DC(WOG_OPT_NEW_BATTLE_FIELDS) = 68;
!#VRi^wog_68_randomBackground_enabled^:S(FALSE); [Enable random background for a type of battle instead of relying on the subtype of terrain]


!?FU(WOG_OPT_SetNewBattleFields)&i^battle_isVisible^; [Executes after OnBeforeBattleUniversal]
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                      

!!FU(WOG_Battle_GetMapItem):P?(mapItem:y);       
!!FU&(mapItem)=(FALSE):E;

!!FU(WOG_MapItem_GetRealType):P(mapItem)/?(meetType:y);
!!FU&(meetType)=(OBJ_TOWN):E;           [Exit on battling towns (even without walls)]

!!BA:P?(x:y)/?(y:y)/?(z:y); 
!!OB(x)/(y)/(z):U?(meetSubType:y);

; Check if it is a battle with hero (excluding random hero)
; Note that if ranodom hero is active, the coordinates of the hero are -1/-1/-1
!!if&i^wog_72_randHeroActive^<>(TRUE);
  !!BA:H(BATTLE_RIGHT)/?(rightHero:y);

  !!if&(rightHero)>(NO_HERO);
    !!HE(rightHero):P?(x)/?(y)/?(z);
    !!VR(meetType):S(OBJ_HERO);
  !!en;
!!en;

; Get terrain type
!!TR(x)/(y)/(z):T?(terrType:y)/?(terrSubType:y);
!!FU&(terrType)>=9:E;                   [Exit if the battle initiates on rocks]
!!FU&(terrType)=8/i^battle_hero_vs_hero^:E; [Exit if it's a battle between two boats]

; Deal with Creature Banks and Mines
!!VR(name:z):S^^;

!!if&(meetType)=(OBJ_DRAGON_UTOPIA); 
  !!VR(name):S^BnUt^;

!!el&(meetType)=(OBJ_CREATURE_BANK);
  !!VR(name):S^BnNg^;

!!el&(meetType)=(OBJ_CRYPT);            [N/A by default]
  !!VR(name):S^BnCr^;

; Abandoned Mine
!!el&(meetType)=(OBJ_MINE)/(meetSubType)=7;
  !!VR(name):S^GrMn^;

!!el;
  !!BU:G?(bonuses:y);                   [есть ли бонусы от спец.земли в битве]

  !!if&(bonuses)=1;                     [magic plains]
    !!VR(name):S^Magi^;
  !!el&(bonuses)=2;                     [cursed ground]
    !!VR(name):S^Curs^;
  !!el&(bonuses)=3;                     [holy ground]
    !!VR(name):S^Holy^;
  !!el&(bonuses)=4;                     [evil fog]
    !!VR(name):S^Evil^;
  !!el&(bonuses)=5;                     [clover field]
    !!VR(name):S^Clov^;
  !!el&(bonuses)=6;                     [lucid pools]
    !!VR(name):S^Luci^;
  !!el&(bonuses)=7;                     [fiery fields]
    !!VR(name):S^Fier^;
  !!el&(bonuses)=9;                     [magic clouds]
    !!VR(name):S^Clou^;
  ; If there is no overlay or the overlay is not valid, get the background from the type of terrain
  !!el;
    !!VR(name):S^Gr%(terrType)^;
  !!en;
!!en;

; Check out the max available background image index (can be empty)
!!FU(NewIntArray):P?(indexList:y);
!!SN:F^PcxPngExists^/^Bk%(name).pcx^;
!!FU(Array_Push)&v1:P(indexList)/-1;

!!re i/0/99;                            [23 - max subtype of Sand. Max of 101 images available for one terrain type]
  !!SN:F^PcxPngExists^/^Bk%(name)%i.pcx^;

  ; Push the found image to the list
  !!if&v1;
    !!FU(Array_Push):P(indexList)/i;
  ; Exit if no file found, only when the index is >= 1 (for normal player's habit, index could start from 1)
  ; Here we no longer exit to allow more flexible naming
  *!el&i>=1;
    *!br;
  !!en;
!!en;

; Exit if no image
!!SN:M(indexList)/?(size:y);
!!FU&(size)=0:E;

; Choose the image depending on the subtype of terrain or just random, depending on the value of i^wog_68_randomBackground_enabled^
!!if&i^wog_68_randomBackground_enabled^=(FALSE);
  !!VR(listIndex:y):S(terrSubType) %(size); [вычисляем 1 из (size) фонов по остатку от деления]
!!el;
  !!VR(listIndex):R0/1/(size) -1;
!!en;

; Get the index of the filename
!!SN:M(indexList)/(listIndex)/?(index:y);

!!if&(index)=-1;
  !!VR(suffix:z):S^^;
!!el;
  !!VR(suffix):S^%(index)^;
!!en;

; Test
*!VR(filename:z):S^Bk%(name)%(suffix).pcx^;
*!IF:M^%(filename)^;

; Change the background
!!BA:B^Bk%(name)%(suffix).pcx^;         [установить новый фон битвы]

!?FU(WOG_CreateERMHook);
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                       

!!SN:Ex1/1/4601891/(WOG_OPT_SetNewBattleFields); 

** End of Script **

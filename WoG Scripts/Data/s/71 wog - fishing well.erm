ZVSE2

**   Fishing Well ERM Script Version 1.8 created by Timothy E. Pulver
**   Requires WoG version 3.58 or later using ERM version 2.7 or later.
**   WoGify Name: script07.erm
**   Updated: Nov. 18, 2009 [JHV]

** This script enhances all magic wells on a map by adding an option for
** the hero to go fishing there. It's all a matter of pure luck as to what he
** or she will catch. It could be something silly, it could be gold or it
** could be a chest that contains resources, an artifact or a trap. But there
** are other dangers too -- there's a chance that something nasty will emerge
** from within the well and attack the hero.

** To install this script in a map, simply copy & paste the code into a global
** (timed) event. Copy the entire code, from ZVSE to the end. Make sure
** that you disable ALL colours from receiving the event and/or change
** the event day to something like 500 or later to avoid the ERM script
** popping up as if it were a regular event message. And of course, you'll
** also need to put one or more Magic Wells on your map for the heroes
** visit. Happy fishing! :-)

** Changes in version 1.2: tunnel chance lowered to 1 in 10,
** obscure result chance raised to 3 in 10, easy option for mapmaker to
** disable tunnels if desired (see below), "dead end" option added,
** loose coin value raised to 50-200, chest of gold value raised to
** 200-900, maximum monsters lowered to 5, tunnel check routine (counts !
** of wells on map) moved to occur only after a tunnel has been
** investigated. Flags 4 and 5 added, also function 1109 and
** v310-v313.

** Changes in version 1.3: tunnel routines changed to use UN:U instead
** of library functions, AI only visits a Well if it has less than 300 movement
** points, and AI no longer takes a dead end passage. Also,  variables
** 310-v313 changed to local y variables. Disabled artifacts won't show
** up in chests now but Angel Wings and Tomes can, if not disabled by
** the mapmaker. Even combo artifacts can show up if specifically
** enabled for the map. If you don't have a First Aid tent, you  can gain one
** from the "blackfly" result.

** Changes in version 1.4: minor tunnel monster bug fixed. Chest result is now
** more likely and has two extra possibilities: pink gas which increases the
** number of one of the hero's stacks by 20% (or a scroll if an empty slot
** comes up) and purple gas which changes all troops of one type to a
** downgraded type.Also, traps now kill from 10-30% of creatures instantly,
** and if you find an artifact in a chest, it's automatically equipped if you have
** a free slot. Obscure result adds the chance of a gnome riding by who will
** offer to buy some of your troops (for triple their gold value). This version
** also adds support for the WoGify options dialogue, switches from a
** regular object trigger to a post-visit object trigger, and uses the new
** HE:C2 command to give a creature if that result comes up.

** Changes in version 1.5: movement penalty removed for AI heroes visiting the
** Fishing Well. Artifacts and scrolls automatically equipped with new HE:A4
** command.

** Version 1.6 is updated to work correctly with stack experience.

** Version 1.7 adds a check for the custom picture files existing when one will
** be used, and displays a standard dialogue with no picture if the file isn't
** found.

** Version 1.8 removes Armageddon from the "Living Scroll" category and switches
** to using the new (WOG_GENERATE_SPELL) library function.

** JHV version allows one fish per well, disables loose change, &
** pre-sets random (human) choices so re-loading game won't change results

Flags Used in this Script: 1-6
Variables Used in this Script: v301-v305,v308,v998-v1000,z1-z2,z300-z303
(& z356-z357 by JHV, 10/1/2007)
Functions Used in this Script: FU1100-FU1110, (no longer uses FU14000)
Dialogues Used in this script: 7-9
!#DC(WOG_OPT_FISHING_WELL) = 107;
-------------------------------------------------------------------------------
 (setup hint texts for JHV version)
; igrik's fix (there was no check for script inclusion for descriptions)
!?FU(WOG_107_Initialization);
!!UN:P(WOG_OPT_FISHING_WELL)/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VRz356:Sz107085;
!!VRz357:Sz107086;

!!re i/0/14;
  !!HT(OBJ_MAGIC_WELL)/(ANY_OBJ):Pi/z356;
!!en;

!!HT(OBJ_MAGIC_WELL)/(ANY_OBJ):P15/z357;  (use z357 for hint text after well fished)

!#FU(WOG_107_Initialization):P;

** function to process well choices, called by script00 during wogification
!?FU5211; x1=subtype, -1 for wells
!!FU&x1<>-1:E; exit if not well
!!UN:U49/x1/-2/952; [get x/y/l of yellow square of object into v952-v954]
!!FU(WOG_GENERATE_SPELL):P0/0/0/0/1/0; [Generate random spell, excluding banned/disabled: y-99]

!!if&y-99=-1;
  !!VRy-99:S(SPELL_MAGIC_ARROW);
!!en;

!!PO952:Sy-99; save spell !

!!VRy2:S0 R6; [Set y2 to random number from 0 to 6 -- army slot number]
!!PO952:Oy2; save slot !
!!VRy1:S1 R5;  [Set y1 to random number from 1-6 for chest type]
!!VRy7:S0 R7; [Set y7-1 to random number from 0-7, +2*100-->200 to 900]
!!VRy2:Sy1*8+y7; will combine several rand !'s into PO:B0
!!VRy7:S0 R6; [Set y7-2 to random number from 0 to 6 -- resource type]
!!VRy7&y7=6:S7; [If result is 6, set it to 7 (Mithril)]
!!VRy8:S0 R5; [Set y8 to random number from 0 to 5, +3 --> resource amount]
!!VRy3:Sy2*8+y7*8+y8;
 [Chest contains artifact: y1=4]
!!VRy11:S1 R2; [y11=random number from 1-3]
!!VRy7:S1 R2; [Darts kill 10-30% of monsters - store number from 1 to 3 in y7-3]
!!VRy4:Sy3*8+y11*4+y7;
!!PO952:B0/y4;
!!DO1106/1/2/0:P; [Set v308 to random artifact number: 7 to 158 excluding disabled artifacts.]
!!PO952:V0/v308;
!!FU(WOG_GENERATE_SPELL):P0/0/0/0/1/0; [Generate random spell, excluding banned/disabled: y-99]

!!if&y-99=-1;
  !!VRy-99:S15;
!!en;

!!PO952:V1/y-99; save spell !
  [Generate random number from 0-144 for monster]
!!FU(WOG_107_GetRandomMonster):P?y30;
!!PO952:Ty30; save monster !
--------------------------------------------------------------------------------
; Reset all the fish-out wells every month
!?FU(OnEveryDay)&i^timerMonthDay^=1/i^timerDay^>1/i^timerOnce^;
!!VR(x:y):S-1;

; loop through all the Magic Wells
!!re i;
  !!UN:U(OBJ_MAGIC_WELL)/(ANY_OBJ)/-1/(x)/(y:y)/(z:y);
  !!br&(x)<0;

  !!PO(x)/(y)/(z):N0;                   [Reset]
!!en;

--------------------------------------------------------------------------------
!$OB(OBJ_MAGIC_WELL);          [Magic Well is trigger]  (post-trigger)
 (Don't check for stand-alone script--JHV)
!!UN:P(WOG_OPT_FISHING_WELL)/?y1; [Check if Fishing Well script is enabled in WoGify Options]


!!FU&y1<>1:E; [Exit trigger if script isn't enabled]

!!IF:V4/0;   set V4 to 1 to disable tunnel option

(JV version - check whether well aready fished using position N number)
(Note:  **OB998:H?z358; does not read current hint into z358)
!!PO998:N?y1;
!!FU&y1=15:E; exit if well already fished

------------------------------------------------------------------------------------------
 [Clear flags 1-5]
!!IF:R1111100000;

!!if&999; [Ask if hero wishes to fish in the well]

  !!HE(CURRENT_HERO):W?y-1/1; [Get hero's current movement points: y-1]


  !!if&y-1>0;
    !!IF:Q3/^%T(wog.107.visit.question)^;

    !!if&3;
      !!VRy-1:-5000 F0/d; [Subtract 5000 from y-1: y-1][If less than 0, set y-1 to 0: y-1]
      !!HE(CURRENT_HERO):Wy-1;  [Reduce non-AI hero's movement points by 5000 if he/she stopped to fish]
      //replaced after MP reducing to battle save compatibility
      !!FU(WOG_107_Main):P;  [If hero has enough movement and wishes to fish, call function 1100]
    !!en;
  !!el;
    !!IF:M1/^%T(wog.107.visit.noMovepoints)^; [If hero doesn't have enough movement but does want to fish, display this message]
  !!en;
!!en;



------------------------------------------------------------------------------------------

!?FU(WOG_107_Main);  [Start of well fishing Function]

!!FU(WOG_GetRandomSeedByCoord):Pv998/v999/v1000/?y-1;
!!PO998:V3/?y1 V3/d1;
!!VRy-1:+y1 Rd/y-1;

!!FU(WOG_CheckRandomMap):P?y30;
!!VRy31:Sy30 +2;
!!VRy5:R0/0/y31; random ! 0-3 (obscure, monster, chest, tunnel)

!!PO998:N?y6; bit flags
!!VRy7:Sy6%2; bit flags mod 2, =1 if bit 0 set
!!VRy8:Sy7*-1+y6%4; bit flags - bit 0 mod 4, =2 if bit 1 set
!!VRy9:Sy7+y8*-1+y6%8; bit flags - bits 0 & 1 mod 8, =4 if bit 2 set
!!VRy10:Sy7+y8+y9*-1+y6; bit flags - bits 0, 1 & 2, =8 if bit 3 set
!!VRy1:S7+y5; index to y7-y10
!!VRy2:Syy1; =0 if option y5 not done

!!if&y2<>0;
  !!VRy5&y30/y10=0:S3;
  !!VRy5&y9=0:S2;
  !!VRy5&y8=0:S1;
  !!VRy5&y7=0:S0;
!!en;

!!FU1101&y5=0:Py-1; [obscure result]
**FU1102&y1>=3/y1<=4:P; [If y1 = 3-4, call function 1102 - loose coins]
!!FU1103&y5=2:Py-1; [iron chest]
!!FU1104&y5=1:Py-1; [monster]
!!FU1107&y5=3:Py-1; [tunnel - works for random maps only]

------------------------------------------------------------------------------------------

!?FU1101;  [Start of obscure fishing result Function]
(JHV version:  set well to fished status)
!#VA(usedY[4]:y);

!!PO998:N?y1;  get "position N number" of current position
!!VRy1:+1; set "obscure fishing" done
!!PO998:Ny1;

!!VRz301:S^%T(wog.107.message.head)^;
!!VRz302:S^%T(wog.107.message.body)^;
*!VRx1:+
!!VRy1:S0 R30; [y1 = random ! from 0-30]

!!if&y1>=0/y1<=17;
  !!VRz-2&y1=0:S^..\data\s\pic\sandwich.jpg^;
  *!VRz-1&y1=1:Sz107006; [Boot]
  *!VRz-5&y1=1:Sz107063; [Old boot]
  !!VRz-2&y1=1:S^..\data\s\pic\boot.jpg^;
  *!VRz-1&y1=2:Sz107007; [Sword]
  *!VRz-5&y1=2:Sz107064; [Rusty sword]
  !!VRz-2&y1=2:S^..\data\s\pic\sword.jpg^;
  *!VRz-1&y1=3:Sz107008; [Lambchop]
  *!VRz-5&y1=3:Sz107065; [Lambchop]
  !!VRz-2&y1=3:S^..\data\s\pic\lambchop.jpg^;
  *!VRz-1&y1=4:Sz107009; [Mud]
  *!VRz-5&y1=4:Sz107066; [Slimy mud]
  !!VRz-2&y1=4:S^..\data\s\pic\mud.jpg^;
  *!VRz-1&y1=5:Sz107010; [Mossy Rocks]
  *!VRz-5&y1=5:Sz107067; [Mossy Rocks]
  !!VRz-2&y1=5:S^..\data\s\pic\mossy.jpg^;
  *!VRz-1&y1=6:Sz107011; [Trout]
  *!VRz-5&y1=6:Sz107068; [Trout]
  !!VRz-2&y1=6:S^..\data\s\pic\trout.jpg^;
  *!VRz-1&y1=7:Sz107012; [Hole]
  *!VRz-5&y1=7:Sz107069; [Dark hole]
  !!VRz-2&y1=7:S^..\data\s\pic\hole.jpg^;
  *!VRz-1&y1=8:Sz107013; [Mermaid]
  *!VRz-5&y1=8:Sz107070; [Mermaid]
  !!VRz-2&y1=8:S^..\data\s\pic\mermaid.jpg^;
  *!VRz-1&y1=9:Sz107014; [Fishing]
  *!VRz-5&y1=9:Sz107071; [Fishing]
  !!VRz-2&y1=9:S^..\data\s\pic\fishing.jpg^;
  *!VRz-1&y1=10:Sz107015; [Worms]
  *!VRz-5&y1=10:Sz107072; [Worms]
  !!VRz-2&y1=10:S^..\data\s\pic\worms.jpg^;
  *!VRz-1&y1=11:Sz107016; [Echoes]
  *!VRz-5&y1=11:Sz107073; [Spookies]
  !!VRz-2&y1=11:S^..\data\s\pic\echoes.jpg^;
  *!VRz-1&y1=13:Sz107018; [Hot Day]
  *!VRz-5&y1=13:Sz107077; [Hot sun]
  !!VRz-2&y1=13:S^..\data\s\pic\hotday.jpg^;
  *!VRz-1&y1=14:Sz107019; [Anchor]
  *!VRz-5&y1=14:Sz107078; [Anchor]
  !!VRz-2&y1=14:S^..\data\s\pic\anchor.jpg^;
  *!VRz-1&y1=15:Sz107020; [Dunking]
  *!VRz-5&y1=15:Sz107079; [Well dunking!]
  !!VRz-2&y1=15:S^..\data\s\pic\dunking.jpg^;
  *!VRz-1&y1=16:Sz107021; [Bobbing]
  *!VRz-5&y1=16:Sz107080; [Bob with an apple]
  !!VRz-2&y1=16:S^..\data\s\pic\bobbing.jpg^;
  *!VRz-1&y1=17:Sz107022; [Eel]
  *!VRz-5&y1=17:Sz107081; [Electric eel]
  !!VRz-2&y1=17:S^..\data\s\pic\eel.jpg^;

  !!VR(zInd:y):Sy1 +107005;
  !!VR(zIndSec:y):Sy1 +107062;
  !!VRz-1:Sz(zInd); 
  !!VRz-5:Sz(zIndSec);

  !!if&y1=12;
    !!VRz-6:Sz107075; [Brave dragon]
    !!VRz-7:Sz107076; [Silly warlock]
    !!VRz-2:S^..\data\s\pic\tales1.jpg^;
    !!VRz-3:S^..\data\s\pic\tales2.jpg^;
    !!VRz-4:S^..\data\s\pic\tales3.jpg^;
  !!en;
!!en;

!!VRz-1& y1=19:Sz107024; [Rainbow fish]
!!VRz-5& y1=19:Sz107082; [Rainbow fish]
!!VRz-2& y1=19:S^..\data\s\pic\fish.jpg^;

!!VRz-1& y1=22:Sz107028; [Storm]
!!VRz-5& y1=22:Sz107083; [Storm]
!!VRz-2& y1=22:S^..\data\s\pic\storm.jpg^;

!!VRz-1&y1>=25/y1<=27:Sz107032; [Gnome trader]
!!VRz-5&y1>=25/y1<=27:Sz107084; [Gnome trader]
!!VRz-2&y1>=25/y1<=27:S^..\data\s\pic\gnome.jpg^;

!!IF:V1/0; [Initialize Flag 1 to False]
!!UN&y1<=19/y1<>18:J8/2/-2; [Check if file z-2 is present in Data\s folder: Flag 1=0 if file missing]
!!UN&y1=22:J8/2/-2; [Check if file z-2 is present in Data\s folder: Flag 1=0 if file missing]
!!UN&y1>=25/y1<=27:J8/2/-2; [Check if file z-2 is present in Data\s folder: Flag 1=0 if file missing]
!!UN&y1=12/1:J8/2/-3; [Check if file z-3 is present in Data\s folder: Flag 1=0 if file missing]
!!UN&y1=12/1:J8/2/-4; [Check if file z-4 is present in Data\s folder: Flag 1=0 if file missing]

!!IF&1/y1<>12:D7/-1/^^/^^/-2/^^/^^/^^/-5/^^/^^/^^/^^/^^/^^/^^; [Set up custom dialogue box]
!!IF&1/y1=12:D8/-1/^^/^^/-2/-3/-4/^^/-5/-6/-7/^^/^^/^^/^^/^^; [Set up custom dialogue box]
!!IF&1:F7/^^/^^/^^/^^/(FALSE); [Disable Cancel button]
!!IF&1/y1<=19/y1<>12/y1<>18/1000:E1/7; [Display Custom Dialogue]
!!IF&1/y1=12/1000:E1/8; [Display Custom Dialogue for "Tales"]
!!IF&-1/y1<=19/y1<>18/1000:M^%Z-1^; [Display standard Dialogue if files missing]

!!IF&y1=18/1000:Q1/8/6/1/z107023;
!!HE-1&y1=18:A1/6/15;

!!HE-1&y1=19:R0/d1; [Rainbow fish gives +1 temporary morale to hero]

!!IF&y1=20/1000:Q1/8/2/1/z107025; [Fake Grail]
!!IF&y1=20/1000:M1/z107026;

!!VRy2&y1=21:S2 T8; [Set y2 to random ! between 2 and 10]
!!VRy3&y1=21:S0 -y2 -100000; [Create a negative number for message display purposes]
!!IF&y1=21/1000:Q1/36/y3/1/z107027;
!!VRy2&y1=21:*-1;     [Change number to a negative number]
!!OW&y1=21:R-1/6/dy2; [Remove 1 to 10 gold from player]

!!IF&1/y1=22/1000:E1/7; [Display Custom Dialogue for storm]
!!IF&-1/y1=22/1000:M^%Z-1^; [Display standard Dialogue if files missing]
!!HE-1&y1=22:R0/d-1; [Reduce morale by 1 for storm]

!!IF&y1=23/1000:Q500/21/16/1/z107029; [Friendly dwarves]

!!PO998:S?y-99; get pre-et spell !

!!UN&y1=24:N1/z301/y-99; [Assign spell name to z301]
!!IF&y1=24/1000:Q1/9/y-99/1/z107030;
!!HE-1&y1=24:My-99/1; [Give spell to hero]

!!PO998&y1>=25/y1<=27:O?y2; get pre-et sarmy lot ! 0-6
!!HE-1&y1>=25/y1<=27:C0/y2/d0/?y4; [Store ! of monsters found in hero's army slot y2 in y4]
!!HE-1&y1>=25/y1<=27:C0/y2/?y5/d0; [Store TYPE of monsters found in hero's army slot y2 in y5]
!!VRy4&y1>=25/y1<=27/y5<0:S0; [Set number of monsters to 0 if type is less than 0]
!!VRy6&y1>=25/y1<=27:Sy4 *65536 +y5; [Store type/number of monsters for message display purposes]
!!UN&y1>=25/y1<=27/y4>1/y5>=0:N3/z2/y5/1; [Store monster name (plural) in z2]
!!UN&y1>=25/y1<=27/y4=1/y5>=0:N3/z2/y5/0; [Store monster name (singular) in z2]
!!MA&y1>=25/y1<=27/y4>0:Cy5/6/?y7; [Store cost of troop type in y7]
!!VRy7&y1>=25/y1<=27/y4>0:*y4; [Multiply by number of troops in stack]
!!VRy7&y1>=25/y1<=27/y4>0:*3; [Multiply this value by 3]
!!IF&y1>=25/y1<=27/y4>0/1000:Q2/21/y6/6/y7/2/z107031;  [Ask if player will trade troops for gold - result in flag 2]

!!VRy3&y1>=25/y1<=27/y4>0/-1000:S0 R1; [If AI, generate random number between 0 and 1]
!!IF&y1>=25/y1<=27/y4>0/-1000/y3=1:V2/1; [If AI and y3=1, set flag 2 to True]

!!IF&1/y1>=25/y1<=27/y4<1/1000:E1/7; [If empty slot was selected, display this instead]
!!IF&-1/y1>=25/y1<=27/y4<1/1000:M^%Z-1^; [If empty slot selected, display standard dialogue]

!!IF&y1>=25/y1<=27/y4>0/2/1000:Q2/36/y7/1/z107033;
!!HE-1&y1>=25/y1<=27/y4>0/2:C0/y2/d0/0;  [Remove troop stack]
!!OW&y1>=25/y1<=27/y4>0/2:R-1/6/dy7;     [Give player y7 gold]

!!VRz-1&y1>=25/y1<=27/y4>0/-2:Sz107034; [Gnome trader]
!!IF&1/y1>=25/y1<=27/y4>0/-2:D7/-1/^^/^^/-2/^^/^^/^^/-5/^^/^^/^^/^^/^^/^^/^^; [Set up custom dialogue box]
!!IF&1/y1>=25/y1<=27/y4>0/-2/1000:E1/7; [If refuse to sell your troops, display this instead]
!!IF&-1/y1>=25/y1<=27/y4>0/-2/1000:M^%Z-1^; [Refuse to sell troops, standard dialogue]

!!if&y1>=28;
  !!FU(WOG_107_GetRandomMonster):P?y30;[Get monster to be rescued by hero]
  !!SN:H^monname^/y30/0/?z301; [Assign monster name to z301]

  !!if&1000;
    !!IF:Q1/21/y30/1/z107035;
    !!HE(CURRENT_HERO):C2/y30/1/1;  [Add monster for human]
  !!el;
    !!HE(CURRENT_HERO):C2/y30/1/0; [Add monster for AI]
  !!en;
!!en;

------------------------------------------------------------------------------------------

!?FU1102;  [Start of loose coins result Function]

!!VRy1:S50 T150;    [Set y1 to random ! from 50-200]
!!IF&1000:Q1/36/y1/1/z107036;
!!OW:R-1/6/dy1;  [Give player 50 to 200 gold]

------------------------------------------------------------------------------------------

!?FU1103;  [Start of iron chest result Function]
(JHV version:  set well to fished status)
!!PO998:N?y1;  get "position N number" of current position
!!VRy1:+4; set "chest fishing" done
!!PO998:Ny1;

!!PO998:B0/?y21; y1*8+y7_1*8+y7_2*8+y8*8+y11*4+y7_3
!!VRy22:Sy21%4; y7_3, 1-3
!!VRy23:Sy21:4%4; y11, 1-3
!!VRy24:Sy21:32%8+3; y8, 3-8
!!VRy25:Sy21:256%8; y7_2, 0-6
!!VRy26:Sy21:2048%8+2*100; y7_1, 200-900
!!VRy1:Sy21:16384; y1, 1-6

!!VRz-1:Sz107037; [Chest text]
!!VRz-3:Sz107060; [Do you open it?]
!!VRz-5:Sz107061; [Rusty old chest]
!!VRz-2:S^..\data\s\pic\chest.jpg^;
!!UN:J8/2/-2; [Check if file z-2 is present in Data\s folder: Flag 1=0 if file missing]
!!IF&1:D9/-1/^^/-3/-2/^^/^^/^^/-5/^^/^^/^^/^^/^^/^^/^^; [Set up custom dialogue box]
!!IF&1:F9/^^/^^/^^/^^/(TRUE); [Enable Cancel button]
!!IF&1/1000:E1/9; [Display Custom Dialogue]
!!VRz-1&-1:Sz107059; [Chest text if standard Dialogue box used]
!!IF&-1/1000:Q2^%Z-1^; [Display standard Dialogue if files missing]

!!IF&1:V2/0; [Initialize Flag 2 to False]
!!IF&1/v1=5:V2/1; [If player clicks OK, set Flag 2 to True]

!!IF&-1000:V2/1;  [If player is AI, set flag 2 to true]

!!VRy1&-2:S0; [If chest isn't opened, set y1 to 0]

!!IF&y1=0/1000:M1/z107038;

 [Set up for Pink Gas, Purple Gas and Poison Dart Trap]
!!HE-1:B0/?z303;  [Store hero's name in z303]
!!PO998:O?y2; get pre-set army slot ! 0-6
!!HE-1:C0/y2/d/?y4; [Store ! of monsters found in hero's army slot y2 in y4]
!!HE-1:C0/y2/?y5/d0; [Store TYPE of monsters found in hero's army slot y2 in y5]
!!VRy4&y5<0:S0; [Set number of monsters to 0 if type is less than 0]
!!UN&y4>0:N3/z2/y5/1; [Store monster name (plural) in z2]

 [Pink Gas: Increases one of hero's monster stacks by 20%: y1=1]
!!VRy1&y1=1/y4<1:S7; [Chest contains scroll instead]
!!VRy7&y1=1/y4>0:Sy4 :5; [Store 20% of number of monsters (y4) in y7]
!!VRy7&y1=1/y4>0:+1;  [Add one to y7]
!!VRy6&y1=1/y4>0:Sy7 *65536 +y5; [Store type/number of monsters for message display purposes]
!!IF&y1=1/y4>0/1000:Q1/21/y6/1/z107039;
!!HE-1&y1=1/y4>0:C0/y2/d/dy7/0/3; [Add y7 troops to hero's slot number y2, no stack exp. loss]

 [Chest contains gold: y1=2]
!!VRy7&y1=2:Sy26; [Set y7 to random number from 200 to 900]
!!IF&y1=2/1000:Q1/6/y7/1/z107040;
!!OW&y1=2:R-1/6/dy7;  [Give player gold]

 [Chest contains resources: y1=3]
!!VRy7&y1=3:Sy25; [Set y7 to random number from 0 to 5 or 7 -- resource type]
!!VRy8&y1=3:Sy24; [Set y8 to random number from 3 to 8 -- resource amount]
!!IF&y1=3/1000:Q1/y7/y8/1/z107041;
!!OW&y1=3:R-1/y7/dy8; [Give player resources]

 [Chest contains artifact: y1=4]
!!VRy11&y1=4:Sy23; [y11=random number from 1-3]
!!VRy1&y1=4/y11=3:S7; [33% chance that an artifact will be a scroll]
!!PO998&y1=4:V0/?v308; get pre-set random artifact number: 7 to 158 excluding disabled artifacts
!!HE-1&y1=4:A4/v308; [Equip on hero artifact ! stored in v308]
!!IF&y1=4/1000:Q2/8/v308/1/z107042;

 [Purple Gas: Downgrades one type of Hero's creatures: y1=5]
!!IF&y1=5/y4<1/1000:M1/z107043;

!!VRy7:Sy5 %2;  [Store remainder of dividing monster type by 2 in y7]

 [If monster is an upgraded monster, set y8 to number of downgraded monster type]
!!VRy8:S-1; [Initialize y8 to -1]
!!VRy8&y7=1/y5<132/y5<>113/y5<>115:Sy5 -1; [If upgraded and less than 132, subtract 1]
!!VRy8&y5=123:S115; [Ice Elemental to Water Elemental]
!!VRy8&y5=125:S113; [Magma Elemental to Earth Elemental]
!!VRy8&y5=127:S112; [Storm Elemental to Air Elemental]
!!VRy8&y5=129:S114; [Energy Elemental to Fire Elemental]
!!VRy8&y5=136:S35; [Enchanter to Arch Mage]
!!VRy8&y5=137:S19; [Sharpshooter to Grand Elf]
!!VRy8|y5=170/y5=171:S137; [Arctic Sharpshooter or Lava Sharpshooter to Sharpshooter]
!!VRy8&y5=168:S111; [Gorynych to Chaos Hydra]
!!VRy8&y5=169:S9; [War Zealot to Zealot]
!!VRy8&y5=173:S29; [Santa Gremlin to Master Gremlin]
!!VRy8&y5=150:S13; [Supreme Archangel to Archangel]
!!VRy8&y5=151:S27; [Diamond Dragon to Gold Dragon]
!!VRy8&y5=152:S41; [Lord of Thunder to Titan]
!!VRy8&y5=153:S55; [Antichrist to Arch Devil]
!!VRy8&y5=154:S69; [Blood Dragon to Ghost Dragonn]
!!VRy8&y5=155:S83; [Darkness Dragon to Black Dragon]
!!VRy8&y5=156:S97; [Ghost Behemoth to Ancient Behemoth]
!!VRy8&y5=157:S111; [Hell Hydra to Chaos Hydra]
!!VRy8&y5=158:S131; [Sacred Phoenix to Phoenix]

!!VRy1&y1=5/y4>0/y8=-1:S6; [If monster doesn't have an upgraded version, make the result a trap instead]

!!UN&y1=5/y4>0/y8<>-1:N3/z1/y8/1; [Store downgraded monster name (plural) in z1]
!!IF&y1=5/y4>0/y8<>-1/1000:Q1/21/y5/21/y8/1/z107044;
!!HE-1&y1=5/y4>0/y8<>-1:C1/y5/y8/d/0/3; [Change y5 troop type to y8 troop type with no stack exp. change]

 [Chest contains trap: y1=6]
!!VRy3&y1=6/y4>0:Sy4 :10; [Store 10% of number of monsters (y4) in y3]
!!VRy7&y1=6/y4>0:Sy22; [Darts kill 10-30% of monsters - store number from 1 to 3 in y7]
!!VRy3&y1=6/y4>0/y3<1:S1;  [If y3 is less than 1, make it 1]
!!VRy3&y1=6/y4>0:*y7; [Multiply y3 by y7]
!!VRy3&y1=6/y4>0/y3>y4:Sy4; [If ! killed is greater than ! of monsters, make it ! of monsters]
!!VRy6:Sy3 *65536 +y5; [Store type/number of monsters for message display purposes in y6]
!!IF&y1=6/y4<1/1000:M1/z107045;
!!IF&y1=6/y4>0/1000:Q1/21/y6/1/z107046;
!!VRy3&y1=6:*-1; [Change y3 into a negative value]
!!HE-1&y1=6/y4>0:C0/y2/d0/dy3/0/3; [Remove y3 troops killed by darts without changing stack exp.]

 [Chest contains scroll: y1=7]
!!PO998&y1=7:V1/?y-99; random spell, excluding banned/disabled: y-99
!!VRy8&y1=7:S1001 +y-99; [Number of scroll - store in y8]
!!UN&y1=7:N1/z301/y-99; [Assign spell name to z301]
!!UN&y1=7:P33/?y9; [Check if Living Scrolls script is active]
!!VRy9|y-99<10/y-99=24/y-99=25/y-99=26/y-99=35/y-99=38/y-99=39/y-99=40/y-99>=65:S0;
!!VRz2&y1=7/y9=0:Sz107047;
!!VRz2&y1=7/y9=1:Sz107048;
!!IF&y1=7/1000:Q2/9/y-99/1/z107049;
!!HE-1&y1=7:A4/y8; [Equip hero with scroll]

------------------------------------------------------------------------------------------

!?FU1104;  [Start of monster result Function]
(JHV version:  set well to fished status)
!!PO998:N?y1;  get "position N number" of current position
!!VRy1:+2; set "monster fishing" done
!!PO998:Ny1;

!!PO998:T?y30; get pre-set monster !
!!VRy1:S1 R4;  [Set y1 to random number from 1 to 5 - number of monsters that appear]
!!VRy2:Sy1 *65536 +y30; [Set y2 to monster type/number for message display purposes]
!!IF&1000:Q1/21/y2/1/z107050;
!!HE-1:Tv998/v999/v1000/y30/y1;  [Start a battle with hero vs y1 of type y30 monsters]

------------------------------------------------------------------------------------------

!?FU(WOG_107_GetRandomMonster);  [Start of get monster ! Function]
!#VA(result:x);

  [Generate random number from 0-144 for monster]
!!VR(result):R0/(MON_PIKEMAN)/(MON_TROLL); [Random number 0..144: (result)]

; Add 1 if 122/124/126/128
!!if|(result)=122/(result)=124/(result)=126/(result)=128;
  !!VR(result):+1; 
; Add 4 if neutral dragons
!!el&(result)>=(MON_AZURE_DRAGON)/(result)<=(MON_RUST_DRAGON);
  !!VR(result):+4;
!!en;

------------------------------------------------------------------------------------------

!?FU1106;  [Start of Artifact-number Function]

!!UN:P3/?y1;      [Check if Commanders are enabled. y1=0 if yes]
!!VRv308:S7 R149; [Choose a random Artifact]
!!VRv308&v308>=144:+2; [Skip "artifact lock" and "highlight"]
!!UN:Av308/?y2;   [Test enabled/disabled value of artifact: y2]
!!VRx16&y2=0:S99; [Exit loop when enabled artifact is found]
------------------------------------------------------------------------------------------

!?FU1107;  [Start of tunnel result Function]
(JHV version:  set well to fished status)
!!PO998:N?y1;  get "position N number" of current position
!!VRy1:+8; set "tunnelling" done
!!PO998:Ny1;
!!IF&1000:Q6/z107051;

!!IF&-1000:V6/1;  [If player is AI, set flag 6 to true]

 [If hero explores, locate another Well for tunnel to lead to]
!!FU1108&6:P;

 [Display message and move Hero to new location]
!!HE-1&6/v304>0:Pv301/v302/v303;
!!IF&6/1000/v304>0:M1/z107052;

 [If Hero doesn't explore tunnel, display this message]
!!IF&-6/1000:M1/z107053;

------------------------------------------------------------------------------------------

!?FU1108;  [Start of tunnel ! Function]
!!PO998:O?y2; random ! 0-6 (slot ! for upgrades--reused)
!!FU1110&y2>1:P; only try to tunnel 2/7 of the time (too much of an advantage)
!!FU&y2>1:E;
!!UN:U49/-1/?v304;  [Calculate number of Magic Wells on map - store in v304]
!!VRv304:-1;  [Number of Wells minus one]
!!FU1109:P;   [Function 1109 chooses a random Well]

------------------------------------------------------------------------------------------

!?FU1109;  [Start of random Well selection Function]

 [Random Well number - store in v305]
!!VRv305:S1 Rv304;

 [Location of Magic Well - store in v301-v303]
!!UN:U49/-1/v305/301;

 [Checks object type at Well location - store in y1]
!!OB301:T?y1;

 [If object is a hero, call dead-end function unless AI]
!!FU1110&y1=34/1000:P;

 [As above, but if AI, just set v304 to 0]
!!VRv304&y1=34/-1000:S0;

 [If Well is same one hero is fishing in, call dead-end function unless AI]
!!FU1110&v301=v998/v302=v999/v303=v1000/1000:P;

 [As above, but if AI, just set v304 to 0]
!!VRv304&v301=v998/v302=v999/v303=v1000/-1000:S0;

 [If AI and object isn't a hero, set AI's movement points to 0]
!!HE-1&y1<>34/-1000:W0;

------------------------------------------------------------------------------------------

!?FU1110;  [Start of "dead end" Function]

!!VRv304:S0; [Set v304 to 0 to indicate dead end function reached]
!!VRy1:S1 T9;  [Random ! from 1 to 10]

!!VRy2&y1<10:Sy1 +149;
!!VRy2&y1=10:S168;
!!VRy1:R0/0/5;  [Random resource type - from 0 to 5]
!!VRy3:R0/4/8;  [4-8 of Random resource]
!!VRy4:S2000 T2000; [Random gold - from 2000 to 4000]
!!UN:N3/301/y2/0; [Get name of monster - store in z301]

[Display text message and ask if Hero wants to fight]
!!IF:Q5/21/y2/2/z107054;

!!HE-1&5:Tv998/v999/v1000/y2/1; [Provoke a battle with monster]
!!HE-1&5:O?y2;  [Check if hero won or lost battle]
!!IF&5/y2>-1:Q1/y1/y3/6/y4/1/z107055;
!!OW&5/y2>-1:R-1/y1/dy3 R-1/6/dy4;  [Give player resource plus gold]
!!IF&5/y2>-1:M1/z107056;
!!IF&-5:M1/z107057;
!!IF&5/y2=-1:M1/z107058;

** End of Script **

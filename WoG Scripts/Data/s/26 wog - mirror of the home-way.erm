ZVSE2

** MIRROR OF THE HOME-WAY
** ЗЕРКАЛО ПУТИ ДОМОЙ

** Option 052 by Sir Four
** Rewritten by igrik (02.10.2020)
** Updated by Archer30 (May 2022)

** This building works as a 'Town Portal' spell:
**   - for player 1000 coins.
**   - for AI 0 coins. But is disabled for now.


!#DC(WOG_OPT_MIRROR_HOME_WAY) = 52;


!?FU(OnAfterErmInstructions);
!!UN:P(WOG_OPT_MIRROR_HOME_WAY)/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!SN:T^wog.52.objName^/?(objName:z);
!!SN:H^object^/(OBJ_NEW_WOG_OBJECTS)/66/(objName);

!?OB(OBJ_NEW_WOG_OBJECTS)/66;
!!UN:P(WOG_OPT_MIRROR_HOME_WAY)/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!HE(ANY_HERO):O?(owner:y);

; Human player - pay 1000 to teleport to a selected owned town
!!if&(ERM_FLAG_IS_HUMAN);
  !!OW:R(owner)/(RES_GOLD)/?(gold:y);

  !!if&(gold)<1000;
    !!SN:T^wog.52.noMoney^/?(pauper:z);
    !!IF:M1/(pauper);
    !!FU:E;
  !!en;

  !!UN:C4314462/1/?(patch1:y);    [патч: сохраняем значения]
  !!UN:C4315628/2/?(patch2:y);    [патч: сохраняем значения]

  !!UN:C4314462/1/235;            [патч: временно убираем проверку на мувпоинты]
  !!UN:C4315628/2/15851;          [патч: временно убираем стоимость мувов и маны]
  !!UN:C6918840/4/?(advMgr:y);    [менеджер КП]

  !!UN:R7/0/0 R5/0/0;             [включаем курсор мыши]  
  !!SN:E4314336/2/(advMgr)/3;     [открыть диалог городского портала]

  !!if&v1>100;
    !!OW:R(owner)/(RES_GOLD)/d-1000; [only if town was chosen] @daemon_n
  !!en;

  !!UN:C4314462/1/(patch1);       [патч: возвращаем проверку на мувпоинты]
  !!UN:C4315628/2/(patch2);       [патч: возвращаем параметры стоимости мувов и маны]
; AI player - teleoport to a random owned town
!!el;
  ; Now it's completely disabled for AI as it's reported that AI could stuck in loops.
  !!FU:E;

  ; 25% chance as most of the time this is meaningless
  !!VR(random:y):R0/0/3;
  !!FU&(random)>0:E;

  !!FU(NewIntArray):P?(aiTowns:y);
  !!VR(x:y):S-1;

  !!re i;
    !!UN:U(OBJ_TOWN)/(ANY_OBJ)/-1/(x)/(y:y)/(z:y);
    !!br&(x)<0;

    !!CA(x)/(y)/(z):O?(townOwner:y) H1/?(visitHero:y);

    !!if&(owner)=(townOwner)/(visitHero)=(NO_HERO);
      !!CA(x)/(y)/(z):U?(townId:y);
      !!FU(Array_Push):P(aiTowns)/(townId);
    !!en;
  !!en;

  !!SN:M(aiTowns)/?(size:y);

  !!if&(size)>0;
    !!VR(lastIndex:y):S(size) -1;
    !!VR(random:y):R0/0/(lastIndex);
    !!SN:M(aiTowns)/(random)/?(townId);
    !!CA0/(townId):P?(x)/?(y)/?(z);
    !!HE(CURRENT_HERO):P(x)/(y)/(z);
  !!en;
!!en;

** End of Script **

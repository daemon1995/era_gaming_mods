ZVSE2

** Metamorphs by Timothy Pulver
** Version 1.3
** Script56.erm
** Updated by rennyo 26.02.2012
** Updated August 23, 2004
** Updated by Archer30 on Sep 2024

** This script changes all Earth Messengers into Metamorphs. Metamorphs stacks transform
** in battle on their first action into another type of creature. The type is determined
** randomly but their total Health will be equal or lower in the new form. They'll change
** again each time they completely kill a stack of enemy monsters, unless the Health of
** one creature of that type exceeds the total Health of the (transformed) Metamorph
** stack. After battle, transformed Metamorphs will change back into their normal
** (Metamorph) form. Metamorphs won't transform on Cursed Ground.

** Note: this script won't operate in MP (network) human-to-human battles.

Permanent Variables used:  v4500-v4602, z206-z209
Temporary Variables used:  v1-v10, v13-v14, v24

--------------------------------------------------------------------------------------------------------------------

 [Check if script is enabled: v1]
!?FU(OnAfterErmInstructions);
!!UN:P56/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

 [Set Name and Description for Metamorph (Earth Messenger)]
!!SN:H^monname^/(MON_EARTH_MESSENGER)/0/z156000;
!!SN:H^monname^/(MON_EARTH_MESSENGER)/1/z156001;
!!SN:H^monname^/(MON_EARTH_MESSENGER)/2/?(desc:z);
!!VR(desc):+z156002;
!!SN:H^monname^/(MON_EARTH_MESSENGER)/2/(desc);

 [Set new bonus lines for Metamorphs (but only when in Metamorph form)]
!!EA(MON_EARTH_MESSENGER):F97/10/?(slot:y); [Bonus line number for Quicksand Post-Attack Spell: v2]
!!EA(MON_EARTH_MESSENGER):B(slot)/1/87/43/0/5/10/15/20/25/30/35/40/45/50; [5% Magic Resistance per Rank]
!!EA(MON_EARTH_MESSENGER):F112/67/?(slot); [Bonus line number for Summon Earth Elemental Pre-Attack Spell: v3]
!!EA(MON_EARTH_MESSENGER):B(slot)/1/114/75/0/35/40/45/50/55/60/65/70/75/80; [75% Chance of Regeneration: Rank 1]

--------------------------------------------------------------------------------------------------------------------

 [Store information about creature type (Metamorphs) in v variables)]
; Note: OnBeforeBattleUniversal doesn't work for some reason
!?FU(OnSetupBattlefield)&1000/-998;
!!UN:P56/?(wogOption:y);                [Check if script is enabled: y-1]
!!FU&(wogOption)<>(TRUE):E;             [Exit if script isn't enabled]

!!DO(WOG_56_StoreMetamorphInfo)/0/41/1:P; [Cycle through all battlefield stacks]

--------------------------------------------------------------------------------------------------------------------

 [Store information about creature type (Metamorphs) in v variables]
!?FU(WOG_56_StoreMetamorphInfo);
!!VRy1:S4500 +x16; [Index number for monster Type: y1]
!!VRy2:S4550 +x16; [Index number for number in stack: y2]
!!BMx16:T?vy1 B?vy2; [Store monster Type in vy1, Number in vy2]

--------------------------------------------------------------------------------------------------------------------

 [Battle Stack Obtain Turn trigger]
!?FU(OnBattleStackObtainsTurn)&1000/-998/i^wog_tacticsIsEnded^;
!!UN:P56/?y-1; [Check if script is enabled: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!BMi^battle_current_stack^:T?v2 N?v5; [Type of monster: v2, Number in stack: v5]

!!FU(WOG_56_TransformMetamorphInit)&v2=(MON_EARTH_MESSENGER)/v5>=1:P; [Initial Transformation of Metamorph]

--------------------------------------------------------------------------------

 [Initial Transformation of Metamorph]
!?FU(WOG_56_TransformMetamorphInit);
!!FU(GetMaxMonsterId):P?y32;
!!VRv3:R0/(MON_FIRST)/y32;              [Random number from 0..last mon for monster: v3]
!!VRv4:S(NO_MON);                       [Initialize v4 to -1]
!!MA:Pv2/?v6;                           [Health of one Metamorph: v6]
!!VRv6:*v5;                             [Total health of Metamorph stack: v6]
!!FU&v6<=0:E;                           [Overflow check]

; Determine the new creature
; Infinite loop until we find an ideal creature
!!re i;
  ; Search from level 2 to 6 creatures only for the initial transformation
  !!FU(WOG_GetRandomMonster):P?v4/1/5/(INT_MAX)/(MON_EARTH_MESSENGER);

  ; Skip if not enough to generate a full creature
  !!MA:Pv4/?y1;
  !!VRv7:Sv6 :y1;                       [Number of creatures it can transform to: v7]
  !!co&v7<=0;

  !!FU(WOG_56_CheckIfPassabilityIsCorrect):Pi^battle_current_stack^/v4/?y34;
  !!co&y34<>(TRUE);

  !!br;
!!en;

!!VRv1:Si^battle_current_stack^;

!!BMi^battle_current_stack^:P?v8 O?v14; [Position of Metamorph: v8, Hero's slot: v14]
!!VRy-4:Si^battle_current_stack^ *-1 -1; [Negative of Metamorph stack number: y-4]
!!EAy-4:E?y-5/d/d/d; [Experience of Metamorph stack: y-5]
!!BMi^battle_current_stack^:I?v9; [Side that owns Metamorph: v9]

!!BMi^battle_current_stack^:F?y-8; [Read Metamorph's Flags: y-8]
!!VRy31:Sy-8 &(MON_FLAG_ACTED);
!!VRy-9:Sy-8 &(MON_FLAG_CLONE); [See if Metamorph is a clone: y-9=8388608 if yes]
!!VRy-10:Sy-8 &(MON_FLAG_SUMMONED); [See if Metamorph was summoned: y-10=4194304 if yes]
!!BMi^battle_current_stack^:Fd|549453824; [Write Metamorph's Flags: y-8]
!!BMi^battle_current_stack^:R?y-12;

; For some reason, BM:M/F/R results in broken sound here. Thus we set the volume to 0 temporarily
!!if&i^battle_isVisible^;
  !!UN:C6916020/4/?y33;
  !!UN:C6916020/4/0;
!!en;

!!BMi^battle_current_stack^:K1;                              [Kill the Metamorph]

!!if&i^battle_isVisible^;
  !!SN:D;
  !!UN:C6916020/4/y33;
!!en;

!!FU(NewIntArray):P?i^wog_56_metamorphStatsList^/(M_TEMP);
!!FU(Array_Push):Pi^wog_56_metamorphStatsList^/y31/y-9/y-10/y-12/y-5;

!!BU:Sv4/v7/v8/v9/v14/1; [Summon new creature stack: Type is v4]

!!SN:Mi^wog_56_metamorphStatsList^;
!!VRi^wog_56_metamorphStatsList^:S0;

!!if&i^battle_isVisible^;
  !!VRz1&v5=1:Sz156009;
  !!VRz1&v5>1:Sz156010;
  !!SN:H^monname^/v4/1/?z2; [New creature name (plural): z2]
  !!VRz3:Sz156011;
  !!MM:Sz3; [Display scroll bar message]
!!en;

--------------------------------------------------------------------------------------------------------------------

 [Determine if victorious Metamorphed stack transforms again]
; Retaliation doesn't trigger the transformation
!?FU(WOG_OnAfterAttack)&1000/-998/i^battle_acting_stack^=x1;
!#VA(atkStack:x) (defStack:x);

!!UN:P56/?(wogOption:y); [Check if script is enabled]
!!FU&(wogOption)<>(TRUE):E; [Exit if script isn't enabled]

; Exit if not Metamorph
!!VR(index:y):S(atkStack) +4500;
!!FU&v(index)<>(MON_EARTH_MESSENGER):E;

; Exit if the attacker is killed or the defender is not killed
!!BM(atkStack):N?(atkNum:y);
!!BM(defStack):N?(defNum:y);
!!FU|(atkNum)<=0/(defNum)>0:E;

; Exit if the killed stack is Metamorph
!!BM(defStack):T?y32;
!!FU&y32=(MON_EARTH_MESSENGER):E;

!!BM(atkStack):T?v2 N?v3; [Type that Metamorph is currently: v2, Number in stack: v3]
!!MA:Pv2/?v4; [Health of one (Metamorph): v4]
!!VRv4:*v3; [Total health of (Metamorph) stack: v4]
!!MA:Py32/?v5; [Health of one target creature: v5]
!!FU&v2=y32:E; [Exit if target creature is the same type as Metamorph's current type]
!!FU&v4<v5:E; [Exit if target creature's health is higher than (Metamorphs)]
!!FU(WOG_CheckIfMonsterIsValid):Py32/?y30; [Exit if target creature is a War Machine, commander or emissary]
!!FU&y30<>(TRUE):E;

!!FU(WOG_56_CheckIfPassabilityIsCorrect):P(atkStack)/y32/?y35;
!!FU&y35<>(TRUE):E;

; Exit if the monster cannot be generated
!!MA:Hy32/?y34;
!!FU&y34<=0:E;

!!VRy-4:S(atkStack) *-1 -1; [Negative of Metamorph stack number: y-4]
!!EAy-4:E?y-5/d/d/d; [Experience of Metamorph stack: y-5]
!!VRv9:Sv4 :v5; [Number of creatures (Metamorphs) can transform to: v9]
!!VRv9&v9<1:S1; [Minimum 1]
!!BM(atkStack):P?v6 O?v7 I?v8; [Position: v6, Hero's slot: v7, Side: v8]

!!BM(atkStack):F?y-8; [Read Metamorph's Flags: y-8]
!!VRy31:Sy-8 &(MON_FLAG_ACTED);
!!VRy-9:Sy-8 &(MON_FLAG_CLONE); [See if Metamorph is a clone: y-9=8388608 if yes]
!!VRy-10:Sy-8 &(MON_FLAG_SUMMONED); [See if Metamorph was summoned: y-10=4194304 if yes]
!!BM(atkStack):Fd|549453824; [Write Metamorph's Flags: y-8]
!!BM(atkStack):R?y-12;

; For some reason, BM:M/F/R results in broken sound here. Thus we set the volume to 0 temporarily
!!if&i^battle_isVisible^;
  !!UN:C6916020/4/?y33;
  !!UN:C6916020/4/0;
!!en;

!!BM(atkStack):K1;                      [Kill the Metamorph]

!!if&i^battle_isVisible^;
  !!SN:D;
  !!UN:C6916020/4/y33;
!!en;

!!FU(WOG_56_ResetStackStats):P(atkStack);[For 3rd party script compatibilities]

!!FU(NewIntArray):P?i^wog_56_metamorphStatsList^/(M_TEMP);
!!FU(Array_Push):Pi^wog_56_metamorphStatsList^/y31/y-9/y-10/y-12/y-5;

!!BU:Sy32/v9/v6/v8/v7/1; [Summon new (Metamorph) creature stack: type=y32]

!!SN:Mi^wog_56_metamorphStatsList^;
!!VRi^wog_56_metamorphStatsList^:S0;

!!if&i^battle_isVisible^;
  !!VRz1&v5=1:Sz156003;
  !!VRz1&v5>1:Sz156004;
  !!SN:H^monname^/y32/1/?z2; [New creature name (plural): z2]
  !!VRz3:Sz156005;
  !!MM:Sz3; [Display scroll bar message]
!!en;

--------------------------------------------------------------------------------

// Set up Metamorph parameters
!?FU(WOG_BattleStack_InitParams)&i^wog_56_metamorphStatsList^;
!#VA(stack:x) (side:x);

; Flags
!!SN:Mi^wog_56_metamorphStatsList^/0/?(isActed:y) Mi^wog_56_metamorphStatsList^/1/?(isClone:y) Mi^wog_56_metamorphStatsList^/2/?(isSummoned:y);
!!BM(stack):F?(flags:y);
!!VR(flags):|(isActed) |(isClone) |(isSummoned);
!!BM(stack):F(flags);

; Retaliations
!!SN:Mi^wog_56_metamorphStatsList^/3/?(retals:y);
!!BM(stack):R(retals);

; Stack exp
; Currently we don't change the stack exp after transformation to avoid the complication of restoration
!!FU:E;

!!VR(eaStack:y):S(stack) +1 *-1;
!!BM(stack):T?(type:y);                 [Copy Bonus Lines for new type to (Metamorph) stack]
!!EA(eaStack):O(type);                  [Set Experience of (Metamorph) stack]

!!SN:Mi^wog_56_metamorphStatsList^/4/?(exp:y);
!!EA(eaStack):E(exp)/2/(type)/d;

--------------------------------------------------------------------------------------------------------------------

!?FU(WOG_56_CheckIfPassabilityIsCorrect);
!#VA(stack:x) (mon2:x) (result:x);

!!VR(result):S(TRUE);

; Exit if monster #2 is not wide
!!MA:X(mon2)/?(mon2Flags:y);
!!VR(mon2IsWide:y):S(mon2Flags) &(MON_FLAG_WIDE);
!!FU&(mon2IsWide)=(FALSE):E;

; Exit if monster #1 is wide (we don't need to worry about passability if the monster is wide before transformation)
!!BM(stack):F?(mon1Flags:y);
!!VR(mon1IsWide:y):S(mon1Flags) &(MON_FLAG_WIDE);
!!FU&(mon1IsWide):E;

; Get the second tile of the new creature
!!BM(stack):I?(side:y) P?(pos:y);

!!if&(side)=(BATTLE_LEFT);
  !!VR(secPos:y):S(pos) +1;
!!el;
  !!VR(secPos):S(pos) -1;
!!en;

; Check if it's valid
!!FU(WOG_CheckIfHexIsFree):P(secPos)/?(result);

--------------------------------------------------------------------------------------------------------------------

 [After battle, set correct Metamorphs in hero's army]
!?FU(OnAfterBattleUniversal)&1000/-998;
!!UN:P56/?(wogOption:y);                [Check if script is enabled]
!!FU&(wogOption)<>(TRUE):E;             [Exit if script isn't enabled]

; Set correct Metamorphs in hero's army (if the hero has retreated then it's not required, as the hero lose all the troops)
!!DO(WOG_56_RestoreMetamorph)/(BATTLE_ATTACKER_STACK_FIRST)/(BATTLE_ATTACKER_STACK_LAST)/1&i^wog_isRetreated_0^<>(TRUE):P;
!!DO(WOG_56_RestoreMetamorph)/(BATTLE_DEFENDER_STACK_FIRST)/(BATTLE_DEFENDER_STACK_LAST)/1&i^wog_isRetreated_1^<>(TRUE):P;

--------------------------------------------------------------------------------------------------------------------

 [After battle, set correct Metamorphs in hero's army]
!?FU(WOG_56_RestoreMetamorph);
!!VRy1:S4500 +x16; [Stack number index: y1]
!!VRy10:Svy1; [Type of creature at start of battle: y10]
!!BMx16:N?y2 O?y3; [Number of creatures in stack: y2, Hero slot #: y3]

 [Exit if not a Metamorph at start of battle,]
 [all killed, or a summoned creature there now]
!!FU|y10<>(MON_EARTH_MESSENGER)/y2<1/y3<0:E;

!!MA:P(MON_EARTH_MESSENGER)/?y4; [Health of one Metamorph: y4]
!!BMx16:T?y5; [Type of creature: y5]
!!MA:Py5/?y6; [Health of one creature: y6]
!!VRy7:Sy6 *y2; [Total health of remaining stack of creatures: y7]
!!VRy8:Sy7 :y4; [Number of Metamorphs to convert to: y8]
!!VRy8&y8<1:S1; [If less than 1, make it 1]
!!BMx16:B?y12; [Starting number of summoned stack: y12]
!!VRy13:S4550 +x16; [Starting Metamorph index number]
!!VRy8&y12=y2:Svy13; [Set to starting Metamorph number if none lost: y8]
!!BMx16:I?y11; [Check which side owns it: y11]
!!BMx16:Ny8; [Set to correct number]
!!BA:My11/y3/(MON_EARTH_MESSENGER)/y8; [Set Metamorphs of correct number]
!!VRy14:Sx16 *-1 -1; [Negative of stack number: y14]
!!EAy14:E?y15/d/d/d; [Experience of stack: y15]
!!BA:Hy11/?y16; [Hero number: y16]
!!HEy16&y16>0/y15>0:C0/y3/d/d/y15/3; [Add back any experience from before the battle]

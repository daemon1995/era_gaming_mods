ZVSE2

** ALL CREATURES WITHOUT WOG EXPIRIENCE ABILITIES
** бяе ясыеярбю аег бнц яонянамняреи нр ношрю

** Option 742 by igrik (29.09.2020)
** Original option number was 019 (changed by Archer30 due to the conflict with Master of Life)

** The script disables all WoG abilities received by creatures of the experience
** (except: attack, defense, damage, speed, health).


!#DC(WOG_OPT_CREATURE_NO_WOGABILITIES) = 742;


!?FU(OnAfterErmInited);
!!UN:P(WOG_OPT_STACK_EXPERIENCE)/?(stackExp:y) P(WOG_OPT_CREATURE_NO_WOGABILITIES)/?(noAbilities:y);  
!!FU|(stackExp)<>(TRUE)/(noAbilities)<>(TRUE):E;

!!FU(GetMaxMonsterId):P?(monLastID:y);
 
!!re i/(MON_FIRST)/(monLastID);  
  !!re k/0/19;
    !!EAi:Bk/d/?(abilitiy:y)/d/d/d/d/d/d/d/d/d/d/d/d;
    ; Disable the ability if it is not Attack, Defense, Health, Max Damage, Speed or Min Damage
    !!EAi&(abilitiy)<>65/(abilitiy)<>68/(abilitiy)<>72/(abilitiy)<>77/(abilitiy)<>83/(abilitiy)<>109:Bk/0/0/0/0/0/0/0/0/0/0/0/0/0/0;
  !!en;
!!en;

** End of Script **

ZVSE2

** HERO SPECIALIZATION BOOST (HSB)
** by Alexis Koz
**
** updated by Tobyn on 2004 October 26
** (added debug msg at negative attack for monster specialists)
**
** (Updated by samot on Oct 2004: added to FU850, it may be given a 3rd parameter for duration (if not given duration = power))
** (Updated by Fnord on August 23, 2004 to add additional disabling for MP battles)
** (Updated by Tobyn on Aug 2004: to fit Neutral Town script67 Planeswalkers and Rebalanced Heroes script37)
** (Updated by Archer30 on Apr 2022: Rewrote the function of Calculating Monster Spec Bonuses)

;Thanks to: Fnord the Wizard who enchanted me
;Thanks to: All WoG command
;USED:
;v425-v441 dont use these variables in all scripts before I agreed.   ( edit by igrik (06/05/2019) v425-v441 -> v3925-v3941 )
;w28-w32
;z405-406
;FU803-808, 850-852
;Flags 428, 429, 430
;Timer 77 [TOBYN: this timer isn't used here. Check if unused by other scripts and update usage358.txt]

;-------------------------------Description-------------------------------------
;New abilities:
;    - All heroes with monster specialities improve their unit stats (Vampire and Skeleton specialists not so high as the others)
;    - Most spell-casters can randomly cast their spells in a battle at no cost (Animate Dead,
;        Resurrection and some other cause problems, therefore those heroes use other auto-spells.
;        Thant and Jeddite get nothing from HSB at all.)
;    - All heroes with Intelligence specialization have commander with DeathStare ability
;    - All heroes with Navigation specialization have commander with flying ability
;    - All heroes with Ballista specialization have commander which can shoot
;    - All heroes with Archery specialization cast Precision spell on all allied units at the beginning of a battle (even if hero doesn't know this spell or has no spellbook)
;    - All heroes with First Aid specialization cast StoneSkin spell on all allied units except undeads at the beginning of a battle (even if hero doesn't know this spell or has no spellbook)
;    - All heroes with Fire Magic specialization cast Curse spell on all enemy units at the beginning of a battle (even if hero doesn't know this spell or has no spellbook)
;    - All heroes with Sorcery specialization cast Misfortune spell on all enemy units at the beginning of a battle (even if hero doesn't know this spell or has no spellbook)
;    - All heroes with Eagle Eye specialization let all own 1st and 2nd level units fly in a battle
;    - All heroes with Mysticism specialization add 12+4*Herolevel mana to hero's mana-pool in the beginning of battle. (Remarks: After battle amount of mana cannot be more than before battle).
;    - All heroes with Resistance specialization decrease 12+4*Herolevel mana from enemy hero's mana-pool in the beginning of battle.

;---------------------Function that sets W28, W29, W30--------------------------
;if 0<w28<1000 then hero is spell-caster
;w28 = number of spell                  
;w29 = chance to cast (1 of w29)
;w30 = to ally (0) or to enemy (1)
;w31 = chance  improver
;w32 = type of spell nature: 0 all, 14 fire, 15 air, 16 water, 17 earth

;if 1000<=w28<2000 then hero is monster-specialist
;w28-1000 = additional damage to monster
;w29 = number of monster
;w30 = percent of additional hitpoints
;w31 = amount of additional Attack
;w32 = amount of additional Defense

;w28=2000 -  2 Logistics
;w28=2001 - (AntiScouting, Nagash)
;w28=2002 -  5 Navigation
;w28=2003 - 13 Estates
;w28=2004 - 23 Armorer
;w28=2005 - 22 Offense
;w28=2006 -  1 Archery
;w28=2007 - (speed +2, Sir Mullich)
;w28=2008 - 20 Artillery (Ballista spec)
;w28=2009 - 27 First Aid
;w28=2010 - 25 Sorcery
;w28=2011 - 11 Eagle Eye
;w28=2012 - 24 Intelligence
;w28=2013 -  8 Mysticism,  w29 - amount of mana added to friendly hero in the beginning of game [TOBYN: really?]
;w28=2014 - 26 Resistance, w29 - amount of mana deducted from enemy hero in the beginning of game [TOBYN: really?]
;w28=2015 - 14 Fire Magic
;w28=2016 - 12 Necromancy

;w28=210x - Adds resources (x= number of resource), w29 - number for a day [TOBYN: how is w29 used?]
;              [TOBYN: I think he meant "adds daily resources (x= resource type, w29=daily amount)"]

;w28=2200 - Behemoth specialist
;w28=2201 - Lord Haart Black Knight
;w28=2202 - Xeron Devils specialist
;w28=2203 - Mutare Dragon spec
;w28=2204 - Gelu Sharpshooter spec
;w28=2205 - Dracon Enchanter spec

; TOBYN SUGGESTS:
; w28= -1 init
; w28= xxx with xxx = creature (and w29 att/def bonus, w30 damage bonus then)
; w28=10xx with  xx = spell
; w28=20xx with  xx = secondary skill (00 Pathfinding .. 27 First Aid)
; w28=210x with   x = resource can stay, but add x=7 Mithril, x=8 Wood+Ore, x=9 each_precious
; w28=2200 - Darkness (Nagash)
; w28=2201 - Builder (Jeddite)
; w28=2202 - Speed+2 (Mullich)
; w28=2203 - Dragons (Mutare/Drake)
; w28=3xxx - Upgrade (to creature type xxx), perhaps here own msgbox with three pics
; and integrate the Behemoth (Kilgor), BlackKnight (LordHaart), Devil (Xeron) specialists
;   in std creature type specialists, and in general:
;   RoE style if no further parameter, AB style if att/def/dam parameter

--------------------------------------------------------------------------------

 [Check for network battle: sets to 1 or 2 if it's a network battle]
 [and sets to 0 if it's not a network battle]
 
!?FU(OnBeforeBattleUniversal);
!!BA:E?i^wog_39_isRemote^;

-----------------------------------Init-----------------------------------------

!#UN:P39/?i^wog_39_enabled^;
!#VRi^wog_39_MonSpecEnabled^&i^wog_39_enabled^:S(TRUE); [Switch for monster specialists]

!?FU(OnAfterErmInstructions)&i^wog_39_enabled^; [initialize of heroes w28-w32]

 note by Tobyn: FU(WOG_39_SetUpHeroSpec):P#1/#2/#3/#4/#5 seems to be
   #1 = w28 (lead variable for spec desc. See above how w29-32 depend on this one.)
   #2 = w29 (chance to cast spell | monster number)
   #3 = w30 (spell to ally or enemy | percentage of HitPoint bonus for monsters)
   #4 = hero number, all other calculations effectively rely on this one, sometimes only indirectly (via FU(WOG_39_SetUpHeroSpec) parameter here)
   #5 = w32 (which spell school, needed for friendly beneficial spells | Def bonus for monsters)

!!UN:P67/?(neutralTownEnabled:y);                check if Neutral Town script67 active

;Castle
!!FU(WOG_39_SetUpHeroSpec):P2006/0/0/0/0;       Orrin - Archery
!!FU(WOG_39_SetUpHeroSpec):P1002/2/2/1/0;       Valeska - Archers (+2 dam / creature 2 / +2 HP / hero 2 / +0 def) [TOBYN: creature lvl x always adds x dam and x HP, although x HP doesn't seem to be used later on. The att and def bonus will come otherwise.]
!!FU(WOG_39_SetUpHeroSpec):P1003/4/3/2/0;       Edric  - Griffins +3 (= +3 dam, +3 HP because this is a lvl 3 creature)
!!FU(WOG_39_SetUpHeroSpec):P2002/0/0/3/0;       Sylvia - Navigation (Commander can fly)
!!FU(WOG_39_SetUpHeroSpec):P2003/0/0/4/0;       L.Haart - Estates
!!FU(WOG_39_SetUpHeroSpec):P1004/6/4/5/0;       Sorsha - Swordsmen +4
!!FU(WOG_39_SetUpHeroSpec):P2008/0/0/6/0;       Christian - Ballista (Commanders can shoot)
!!FU(WOG_39_SetUpHeroSpec):P1006/10/6/7/0;      Tyris - Cavaliers +6
!!FU(WOG_39_SetUpHeroSpec):P2009/0/0/8/0;       Rion - First Aid (allied army has Stone Skin)
!!FU(WOG_39_SetUpHeroSpec):P41/7/0/9/16;        Adela - Bless (spell 41 / 1 of 7 / ally / hero 9 / water)
!!FU(WOG_39_SetUpHeroSpec):P45/7/1/10/16;       Cuthbert - Weakness (1 of 7, enemy, water)
!!FU(WOG_39_SetUpHeroSpec):P15/11/1/11/0;     + Adelaide gets auto Magic Arrow (1 of 11, enemy) instead of her Frost Ring (20)
!!FU(WOG_39_SetUpHeroSpec):P1005/8/5/12/0;      Ingham - Monks +5
!!FU(WOG_39_SetUpHeroSpec):P2011/0/0/13/0;      Sanya - Eagle Flying
!!FU(WOG_39_SetUpHeroSpec):P48/15/0/14/16;      Loynis - Prayer (1 of 15, ally, water)
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/15/0;    Caitlin - 350 Gold

;Rampart
!!FU(WOG_39_SetUpHeroSpec):P2004/0/0/16/0;      Mephala - Armorer
!!FU(WOG_39_SetUpHeroSpec):P1002/16/2/17/0;     Ufretin - Dwarves +2
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/18/0;    Jenova - 350 Gold
!!FU(WOG_39_SetUpHeroSpec):P1005/22/5/19/0;     Ryland - Dendroid Guards +5
!!FU(WOG_39_SetUpHeroSpec):P2014/10/0/20/0;     Thorgrim - Resistance
!!FU(WOG_39_SetUpHeroSpec):P1003/18/3/21/0;     Ivor - Wood Elves +3
!!FU(WOG_39_SetUpHeroSpec):P1006/24/6/22/0;     Clancy - Unicorns +6
!!FU(WOG_39_SetUpHeroSpec):P2000/0/0/23/0;      Kyrre - Logistics
!!FU(WOG_39_SetUpHeroSpec):P55/9/0/24/14;       Coronius - Slayer (1 of 9, ally, fire)
!!FU(WOG_39_SetUpHeroSpec):P37/7/0/25/16;       Uland - Cure (1 of 7, ally, water)
!!FU(WOG_39_SetUpHeroSpec):P2012/0/0/26/0;      Elleshar - Intelligence (Commander has DeathStare)
!!FU(WOG_39_SetUpHeroSpec):P2009/0/0/27/0;      Gem - First Aid (allied army has Stone Skin)
!!FU(WOG_39_SetUpHeroSpec):P2011/0/0/28/0;      Malcom - Eagle Flying
!!FU(WOG_39_SetUpHeroSpec):P51/6/0/29/15;       Melodia - Fortune (1 of 6, ally, air)
!!FU(WOG_39_SetUpHeroSpec):P16/14/1/30/16;      Alagar - Ice Bolt (1 of 14, enemy, water)
!!FU(WOG_39_SetUpHeroSpec):P1004/20/4/31/0;     Aeris - Pegasus +4 dam, +4  HP

;Tower
!!FU(WOG_39_SetUpHeroSpec):P1002/30/2/32/0;     Piquedram - Stone Gargoyles +2
!!FU(WOG_39_SetUpHeroSpec):P1005/36/5/33/0;     Thane - Genies +5
!!FU(WOG_39_SetUpHeroSpec):P1003/32/3/34/0;     Josephine - Stone Golem +3
!!FU(WOG_39_SetUpHeroSpec):P2004/0/0/35/0;      Neela - Armorer
!!FU(WOG_39_SetUpHeroSpec):P2008/0/0/36/0;      Torosar - Ballista (Commanders can shoot)
!!FU(WOG_39_SetUpHeroSpec):P1006/38/6/37/0;     Fafner - Nagas + 6
!!FU(WOG_39_SetUpHeroSpec):P2101/1/0/38/0;      Rissa - 1 Mercury
!!FU(WOG_39_SetUpHeroSpec):P1005/36/5/39/0;     Iona - Genies +5
!!FU(WOG_39_SetUpHeroSpec):P60/17/1/40/15;      Astral - Hypnotize (1 of 17, enemy, air)
!!FU(WOG_39_SetUpHeroSpec):P2013/10/0/41/0;     Halon - Mysticism
!!FU(WOG_39_SetUpHeroSpec):P2011/0/0/42/0;      Serena - Eagle Flying
!!FU(WOG_39_SetUpHeroSpec):P51/6/0/43/15;       Daremyth - Fortune (1 of 6, ally, air)
!!FU(WOG_39_SetUpHeroSpec):P1004/34/4/44/0;     Theodorus - Magi +4
!!FU(WOG_39_SetUpHeroSpec):P15/11/1/45/0;     + Solmyr gets auto Magic Arrow (15) (1 of 11, enemy, all) instead of his Chain Lightning (19)
!!FU(WOG_39_SetUpHeroSpec):P53/7/0/46/15;       Cyra - Haste (1 of 7, ally, air)
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/47/0;    Aine - 350 Gold

;Inferno
!!FU(WOG_39_SetUpHeroSpec):P1003/46/3/48/0;     Fiona - Hell Hounds + 3
!!FU(WOG_39_SetUpHeroSpec):P1006/52/6/49/0;     Rashka - Efreet +6
!!FU(WOG_39_SetUpHeroSpec):P1004/48/4/50/0;     Marius - Demons +4
!!FU(WOG_39_SetUpHeroSpec):P1001/42/1/51/0;     Ignatius - Imps +1
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/52/0;    Octavia - 350 Gold
!!FU(WOG_39_SetUpHeroSpec):P1002/44/2/53/0;     Calh - Gogs +2
!!FU(WOG_39_SetUpHeroSpec):P2008/0/0/54/0;      Pyre - Ballista (Commanders can shoot)
!!FU(WOG_39_SetUpHeroSpec):P1005/50/5/55/0;     Nymus - Pit Fiends +5
!!FU(WOG_39_SetUpHeroSpec):P2012/0/0/56/0;      Ayden - Intelligence (Commander has Death Stare)
!!FU(WOG_39_SetUpHeroSpec):P29/11/0/57/14;    + Xyron gets auto Fire Shield (29) (1 of 11, ally, fire) instead of his Inferno (22)
!!FU(WOG_39_SetUpHeroSpec):P2013/10/0/58/0;     Axsis - Mysticism
!!FU(WOG_39_SetUpHeroSpec):P45/7/1/59/16;       Olema - Weakness (1 of 7, enemy, water)
!!FU(WOG_39_SetUpHeroSpec):P2103/1/0/60/0;      Calid - 1 Sulfur
!!FU(WOG_39_SetUpHeroSpec):P43/7/0/61/14;       Ash - Bloodlust (1 of 7, ally, fire)
!!FU(WOG_39_SetUpHeroSpec):P2010/0/0/62/0;      Zydar - Sorcery (enemy army Misfortune)
!!FU(WOG_39_SetUpHeroSpec):P29/11/0/63/14;    + Xarfax gets auto Fire Shield (29) instead of his Fireball (21)

;Necropolis
!!FU(WOG_39_SetUpHeroSpec):P1002/58/2/64/0;     Straker - Walking Dead +2
!!FU(WOG_39_SetUpHeroSpec):P1002/62/2/65/0;     Vokial - Vampires (+2 only)
!!FU(WOG_39_SetUpHeroSpec):P1005/64/5/66/0;     Moandor - Liches +5
!!FU(WOG_39_SetUpHeroSpec):P1003/60/3/67/0;     Charna - Wights +3
!!FU(WOG_39_SetUpHeroSpec):P1006/66/6/68/0;     Tamika - Black Knights +6
!!FU(WOG_39_SetUpHeroSpec):P2016/0/0/69/0;      Isra - Necromancy
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/70/0;    Clavius - 350 Gold
!!FU(WOG_39_SetUpHeroSpec):P1001/56/1/71/0;     Galthran - Skeletons +1
!!FU(WOG_39_SetUpHeroSpec):P47/9/1/72/15;     + Septienna gets auto Disrupting Ray (47) (1 of 9, enemy, air) instead of her Death Ripple (24)
!!FU(WOG_39_SetUpHeroSpec):P15/11/1/73/0;     + Aislinn gets auto Magic Arrow (15) (1 of 11, enemy, all) instead of Meteor Shower (23)
!!FU(WOG_39_SetUpHeroSpec):P2010/0/0/74/0;      Sandro - Sorcery (enemy army Misfortune)
!!FU(WOG_39_SetUpHeroSpec):P2011/0/0/75/0;      Nimbus - Eagle Flying
*!FU(WOG_39_SetUpHeroSpec):P54/7/1/76/17;     + Thant gets auto Slow (1 of 7, enemy, earth) instead of Animate Dead (39)
!!FU(WOG_39_SetUpHeroSpec):P-1/0/0/76/0;  MOD:+ Thant gets nothing from HSB, leaves standard description (script37 may give him Skel Upgr instead)
!!FU(WOG_39_SetUpHeroSpec):P46/7/0/77/17;       Xsi really has Stone Skin (1 of 7, ally, earth)
!!FU(WOG_39_SetUpHeroSpec):P2016/0/0/78/0;      Vidomina - Necromancy
*!FU(WOG_39_SetUpHeroSpec):P2001/0/0/79/0;      Nagash - Darkness
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/79/0;    Nagash - 350 Gold

;Dungeon
!!FU(WOG_39_SetUpHeroSpec):P1002/72/2/80/0;     Lorelei - Harpies +2
!!FU(WOG_39_SetUpHeroSpec):P2008/0/0/81/0;      Arlach - Ballista (Commanders can shoot)
!!FU(WOG_39_SetUpHeroSpec):P1005/78/5/82/0;     Dace - Minotaurs +5
!!FU(WOG_39_SetUpHeroSpec):P1003/74/3/83/0;     Ajit - Beholders +3
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/84/0;    Damacon - 350 Gold
!!FU(WOG_39_SetUpHeroSpec):P2000/0/0/85/0;      Gunnar - Logistics
!!FU(WOG_39_SetUpHeroSpec):P1006/80/6/86/0;     Synca - Manticores +6
!!FU(WOG_39_SetUpHeroSpec):P1001/70/1/87/0;     Shakti - Troglodytes +1
!!FU(WOG_39_SetUpHeroSpec):P54/7/1/88/17;     + Alamar gets auto Slow (54) (1 of 7, enemy, earth) instead of his Resurrection (38)
!!FU(WOG_39_SetUpHeroSpec):P2013/10/0/89/0;     Jaegar - Mysticism
!!FU(WOG_39_SetUpHeroSpec):P2010/0/0/90/0;      Malekith - Sorcery (enemy army Misfortune)
*!FU(WOG_39_SetUpHeroSpec):P54/7/1/91/17;     + Jeddite gets auto Slow (54) (1 of 7, enemy, earth) instead of his Resurrection (38)
!!FU(WOG_39_SetUpHeroSpec):P-1/0/0/91/0;  MOD:+ Jeddite gets nothing from HSB, leaves standard description and effects
!!FU(WOG_39_SetUpHeroSpec):P2011/0/0/92/0;      Geon - Eagle Flying
!!FU(WOG_39_SetUpHeroSpec):P15/11/1/93/0;     + Deemer gets auto Magic Arrow (15) (1 of 11, enemy, all) instead of his Meteor Shower (23)
!!FU(WOG_39_SetUpHeroSpec):P2104/1/0/94/0;      Sephinroth - 1 Crystal
!!FU(WOG_39_SetUpHeroSpec):P46/7/0/95/17;       Darkstorn really has Stone Skin (1 of 7, ally, earth)

;Stronghold
!!FU(WOG_39_SetUpHeroSpec):P1006/94/6/96/0;     Yog - Cyclopes +6
!!FU(WOG_39_SetUpHeroSpec):P2008/0/0/97/0;      Gurnisson - Ballista (Commanders can shoot)
!!FU(WOG_39_SetUpHeroSpec):P1003/88/3/98/0;     Jarbakas - Orcs +3
!!FU(WOG_39_SetUpHeroSpec):P1005/92/5/99/0;     Shiva - Rocs +5
!!FU(WOG_39_SetUpHeroSpec):P1001/84/1/100/0;    Gretchin - Goblins +1
!!FU(WOG_39_SetUpHeroSpec):P1004/90/4/101/0;    Krellion - Ogres +4
!!FU(WOG_39_SetUpHeroSpec):P2005/0/0/102/0;     Crag Hack - Offense
!!FU(WOG_39_SetUpHeroSpec):P1002/86/2/103/0;    Tyraxor - Wolf Riders +2
!!FU(WOG_39_SetUpHeroSpec):P2010/0/0/104/0;     Gird - Sorcery (enemy army Misfortune)
!!FU(WOG_39_SetUpHeroSpec):P1004/90/4/105/0;    Vey - Ogres +4
!!FU(WOG_39_SetUpHeroSpec):P2000/0/0/106/0;     Dessa - Logistics
!!FU(WOG_39_SetUpHeroSpec):P53/7/0/107/15;      Terek - Haste (1 of 7, ally, air)
!!FU(WOG_39_SetUpHeroSpec):P28/8/0/108/15;    + Zubin gets auto Air Shield (28) instead of his Precision (44)
!!FU(WOG_39_SetUpHeroSpec):P2005/0/0/109/0;     Gundula - Offense
!!FU(WOG_39_SetUpHeroSpec):P2011/0/0/110/0;     Oris - Eagle Flying
!!FU(WOG_39_SetUpHeroSpec):P2105/1/0/111/0;     Saurug - 1 Gem

;Fortress
!!FU(WOG_39_SetUpHeroSpec):P1004/106/4/112/0;   Bron - Basilisks +4
!!FU(WOG_39_SetUpHeroSpec):P1001/98/1/113/0;    Drakon - Gnolls +1
!!FU(WOG_39_SetUpHeroSpec):P1002/100/2/114/0;   Wystan - Lizardmen +2
!!FU(WOG_39_SetUpHeroSpec):P2004/0/0/115/0;     Tazar - Armorer
!!FU(WOG_39_SetUpHeroSpec):P1005/102/5/116/0;   Alkin - Gorgon +5
!!FU(WOG_39_SetUpHeroSpec):P1003/104/3/117/0;   Korbac - Serpent Flies +3
!!FU(WOG_39_SetUpHeroSpec):P2008/0/0/118/0;     Gerwulf - Ballista (Commanders can shoot)
!!FU(WOG_39_SetUpHeroSpec):P1006/108/6/119/0;   Broghild - Wyverns +6
!!FU(WOG_39_SetUpHeroSpec):P45/7/1/120/16;      Mirlanda - Weakness (1 of 7, enemy, water)
!!FU(WOG_39_SetUpHeroSpec):P2013/10/0/121/0;    Rosic - Mysticism
!!FU(WOG_39_SetUpHeroSpec):P2002/0/0/122/0;     Voy - Navigation (Commander can fly)
!!FU(WOG_39_SetUpHeroSpec):P2009/0/0/123/0;     Verdish - First Aid (allied army has Stone Skin)
!!FU(WOG_39_SetUpHeroSpec):P46/7/0/124/17;      Merist - Stone Skin (1 of 7, ally, earth (was fire))
!!FU(WOG_39_SetUpHeroSpec):P2010/0/0/125/0;     Styg - Sorcery (enemy army Misfortune)
!!FU(WOG_39_SetUpHeroSpec):P2012/0/0/126/0;     Andra - Intelligence (Commander has DeathStare)
!!FU(WOG_39_SetUpHeroSpec):P2011/0/0/127/0;     Tiva - Eagle Flying

;Confluxers
!!if&(neutralTownEnabled);
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/128/0;     Pasis - none if Neutral Town active
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/129/0;     . Thunar - none
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/130/0;     . Ignissa - none
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/131/0;     . Lacus - none
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/132/0;     . Monere - none
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/133/0;     . Erdamon - none
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/134/0;     . Fiur - none
  !!FU(WOG_39_SetUpHeroSpec):P-1/0/0/135/0;     . Kalt - none
!!el;
  !!FU(WOG_39_SetUpHeroSpec):P1006/120/6/128/0; Pasis - Psychic Elementals +6
  !!FU(WOG_39_SetUpHeroSpec):P1005/113/5/129/0; Thunar - Earth Elementals +5
  !!FU(WOG_39_SetUpHeroSpec):P1004/114/4/130/0; Ignissa - Fire Elementals +4
  !!FU(WOG_39_SetUpHeroSpec):P1003/115/3/131/0; Lacus - Water Elementals +3
  !!FU(WOG_39_SetUpHeroSpec):P1006/120/6/132/0; Monere - Psychic Elementals +6
  !!FU(WOG_39_SetUpHeroSpec):P1005/113/5/133/0; Erdamon - Earth Elementals +5
  !!FU(WOG_39_SetUpHeroSpec):P1004/114/4/134/0; Fiur - Fire Elementals +4
  !!FU(WOG_39_SetUpHeroSpec):P1003/115/3/135/0; Kalt - Water Elementals +3
!!en;

!!FU(WOG_39_SetUpHeroSpec):P27/7/0/136/17;    + Luna gets auto Shield (27) (1 of 7, ally, earth) instead of her Fire Wall (13)
!!FU(WOG_39_SetUpHeroSpec):P53/8/0/137/15;      Brissa - Haste (1 of 8, ally, air)
!!FU(WOG_39_SetUpHeroSpec):P15/11/1/138/0;      Ciele - Magic Arrow (1 of 11, enemy, all)
!!FU(WOG_39_SetUpHeroSpec):P46/7/0/139/17;      Labetha - Stone Skin (1 of 7, ally, earth)
!!FU(WOG_39_SetUpHeroSpec):P43/7/0/140/14;      Inteus - Bloodlust (1 of 7, ally, fire)
!!FU(WOG_39_SetUpHeroSpec):P47/9/1/141/15;      Aenain - Disrupting Ray (1 of 9, enemy, air)
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/142/0;   Gelare - 350 Gold
!!FU(WOG_39_SetUpHeroSpec):P2106/350/0/143/0;   Grindan - 350 Gold

;Others
!!FU(WOG_39_SetUpHeroSpec):P2007/0/0/144/0;     Sir Mullich - 2 Speed
!!FU(WOG_39_SetUpHeroSpec):P2015/0/0/145/0;     Adrienne - Expert Fire Magic
!!FU(WOG_39_SetUpHeroSpec):P1000/6/4/146/0;     Catherine - Swordsmen (+0 dam, +4 HP)
!!FU(WOG_39_SetUpHeroSpec):P2205/0/0/147/0;     Dracon - upg to Enchanter
!!FU(WOG_39_SetUpHeroSpec):P2204/0/0/148/0;     Gelu - upg to Sharpshooter
!!FU(WOG_39_SetUpHeroSpec):P2200/0/0/149/0;     Kilgor - Behemoths
!!FU(WOG_39_SetUpHeroSpec):P2201/0/0/150/0;     Lord Haart - Black Knights
!!FU(WOG_39_SetUpHeroSpec):P2203/0/0/151/0;     Mutare - Dragons
!!FU(WOG_39_SetUpHeroSpec):P1000/6/4/152/0;     Roland - Swordsmen (+0 dam, +4 HP)
!!FU(WOG_39_SetUpHeroSpec):P2203/0/0/153/0;     Mutare Drake - Dragons
!!FU(WOG_39_SetUpHeroSpec):P1000/90/4/154/0;    Boragus - Ogres (+0 dam, +4 HP)
!!FU(WOG_39_SetUpHeroSpec):P2202/0/0/155/0;     Xeron - Devils

!?FU(WOG_39_SetUpHeroSpec);
!#VA(var1:x) (var2:x) (var3:x) (hero:x) (spell:x);

!!IF:W(hero);
!!VRw28:S(var1);
!!VRw29:S(var2);
!!VRw30:S(var3);

!!if&w28>0/w28<1000;
  !!HE(hero):Mw28/1;          //give spell
  !!VRw31:S0;
  !!VRw32:S(spell);
!!el&w28>=1000/w28<2000/i^wog_39_MonSpecEnabled^;
  !!FU(WOG_39_UpdateMonSpecBonus):P(hero);      //monster specialists boost
!!en;

!!UN:P(WOG_OPT_DISABLE_COMMANDERS)/?(commanderDisabled:y);               TOBYN: changed unused v3930 with check for commanders enabled and added control below

!!if&(commanderDisabled)<>(TRUE);
  !!CO(hero)&w28=2012:B1/12/1;    //commanders of Intelligence heroes have DeathStare
  !!CO(hero)&w28=2008:B1/4/1;     //commanders of Ballista heroes can shoot
  !!CO(hero)&w28=2002:B1/14/1;    //commanders of Navigation heroes can fly
!!en;

; Set new spec text. Don't set text for Monster Specialist if Mon Spec is disabled
!!FU&w28>=1000/w28<2000/i^wog_39_MonSpecEnabled^<>(TRUE):E;

!!if&w28>=0;
  !!FU(WOG_39_SetUpSpecDesc):P(hero)/0;      //init hint for speciality
  !!SN:H^spec^/(hero)/2/z405; TOBYN: added check to be able to circumvent HSB (if lead var w28<0)
!!en;

---------------------------Hero level increasing--------------------------------
!?FU(OnAfterHeroGainLevel)&i^wog_39_enabled^;
!!HE(CURRENT_HERO):N?(hero:y);               get hero number

!!IF:W(hero);
; Update the HE:X data of creature specialists
!!FU(WOG_39_UpdateMonSpecBonus)&w28>=1000/w28<2000/i^wog_39_MonSpecEnabled^:P(hero);

!!VRw29&w28=2013:+3;                    [Mysticism and Resistance]
!!VRw29&w28=2014:+3;

-------------------------Monsters specialists boost-----------------------------
!?FU(WOG_39_UpdateMonSpecBonus);
!#VA(hero:x);

!!IF:W(hero);
; Calculate Attack and Defense bonuses (Half of the hero's raw attack/defense as bonus)
!!FU(GetHeroPrimarySkillsWithoutArts):P(hero)/?(atk:y)/?(def:y)/d/d;
!!VRw31:S(atk) :2 F1/99;
!!VRw32:S(def) :2 F1/99;

; Calculate Damage and Health Bonuses
!!HE(hero):Ed/?(exp:y);

!!if&w29<>(MON_SKELETON)/w29<>(MON_VAMPIRE);
  !!MA:Lw29/?(lv:y);
  !!VR(lvCoef:y):S8 -(lv);
  !!VRw28:S(exp) :(lvCoef) +1;          [TOBYN: w28 set here]

  !!VRw30:S(exp) *3 +12;                [Additional percent to HP = 12+3*level of hero]
!!el;
  !!VRw28:S0;                           [no damage bonus for Skeletons and Vampires]

  !!VRw30:S0;                           [TOBYN: Skeletons and Vampires specialists dont give health bonus]
!!en;

!!HE(hero):X4/w29/w31/w32/w28;
!!VRw28:+1000;

!!FU(WOG_39_SetUpSpecDesc):P(hero)/0;
!!SN:H^spec^/(hero)/2/z405;

-------------------------------After battle-------------------------------------
!?FU(OnAfterBattleUniversal)&i^wog_39_enabled^/i^wog_39_isRemote^=0/-998;

!!BA:H0/?v3926;                          //set v3926 is number of left hero - used in BG section
!!BA:H1/?v3927;                          //set v3927 is number of right hero - used in BG section

!!if&v3926>=0;
  !!IF:Wv3926;

  !!if&w28=2013;
    !!HEv3926:I?v3933/1; //mysticism
    !!VRv3933&v3933>w30:Sw30;
    !!HEv3926:Iv3933/1;
  !!en;
!!en;

!!if&v3927>=0;
  !!IF:Wv3927;

  !!if&w28=2013;
    !!HEv3927:I?v3933/1;
    !!VRv3933&v3933>w30:Sw30;
    !!HEv3927:Iv3933/1;
  !!en;
!!en;

----------------------------------Before battle---------------------------------
!?FU(OnSetupBattlefield)&i^wog_39_enabled^/i^wog_39_isRemote^=0/-998;
!!VRv3937:S0R8;                           //v3937 = random value generator
!!BH0:N?v3926;                          //set v3926 is number of left hero - used in BG section
!!HEv3926&v3926>=0:F?v3938/?v3939/?v3934/?v3931;     ;set variables of attack and defense (v3938-441)
!!BH1:N?v3927;                          //set v3927 is number of right hero - used in BG section
!!HEv3927&v3927>=0:F?v3940/?v3941/?v3934/?v3931;

!!IF&v3926>=0:Wv3926;
!!DO851/0/19/1&w28=2011/v3926>=0:P0/v3926; //eagle eye

!!if&v3926>=0;
  !!DO(WOG_39_SetUpMonsterBonuses)/0/19/1&w28>=1000/w28<2000:P0/v3926/0/404; //monster specs add bonuses
  !!COv3926&w28=2012:B1/12/1;     //commanders of Intelligence heroes have DeathStare
  !!COv3926&w28=2008:B1/4/1;      //commanders of Ballista heroes can shoot
  !!COv3926&w28=2002:B1/14/1;     //commanders of Navigation heroes can fly
!!en;

!!if&v3927>=0;
  !!IF:Wv3927;
  !!DO851/20/40/1&w28=2011:P1/v3927;
  !!DO(WOG_39_SetUpMonsterBonuses)/20/40/1&w28>=1000/w28<2000:P1/v3927/0/411;
  !!COv3927&w28=2012:B1/12/1;
  !!COv3927&w28=2008:B1/4/1;
  !!COv3927&w28=2002:B1/14/1;
!!en;

;Code below - Mysticism and Resistance
!!if&v3926>=0;
  !!IF:Wv3926;
  !!HEv3926&w28=2013:I?v3933/1;    //mysticism
  !!VRw30&w28=2013:Sv3933;        //w30 contains initial mana's amount
  !!VRv3933:+w29;
  !!HEv3926&w28=2013:Iv3933/1;
!!en;

!!if&v3927>=0;
  !!IF:Wv3927;
  !!HEv3926&w28=2014/v3926>=0:I?v3933/1; //resistance
  !!VRv3933:-w29;
  !!VRv3933&v3933<0:S0;
  !!HEv3926&w28=2014/v3926>=0:Iv3933/1;

  !!IF:Wv3927;
  !!HEv3927&w28=2013:I?v3933/1;
  !!VRw30&w28=2013:Sv3933;
  !!VRv3933:+w29;
  !!HEv3927&w28=2013:Iv3933/1;
!!en;

!!IF&v3926>=0:Wv3926;

!!if&v3927>=0;
  !!HEv3927&w28=2014/v3926>=0:I?v3933/1;
  !!VRv3933:-w29;
  !!VRv3933&v3933<0:S0;
  !!HEv3927&w28=2014/v3926>=0:Iv3933/1;
!!en;

!!IF&v3926>=0:Wv3926;

!!if&v3926>=0;
  !!DO850/0/20/1&w28=2006:P44/5;
  !!DO850/0/20/1&w28=2009:P46/5;
  !!DO850/21/41/1&w28=2010:P52/5;
  !!DO850/21/41/1&w28=2015:P42/5;
!!en;

!!IF&v3927>=0:Wv3927;

!!if&v3927>=0;
  !!DO850/21/41/1&w28=2006:P44/5;
  !!DO850/21/41/1&w28=2009:P46/5;
  !!DO850/0/20/1&w28=2010:P52/5;
  !!DO850/0/20/1&w28=2015:P42/5;
!!en;

!?FU850;
!!VRx3&x3=0:Sx2; // set x3 (duration) to x2 (Power) if not set
!!BMx16:Mx1/x3/x2;

------------------------When next turn of battle started------------------------
!?FU(OnBattleRound)&i^wog_39_enabled^/i^wog_39_isRemote^=0/-998;
!!IF:V428/0;

!!BH0:N?v3926;                          //set v3926 is number of left hero - used in BG section
!!BH1:N?v3927;                          //set v3927 is number of right hero - used in BG section

!!IF&v997>=0:V428/1;                   //if tactics phase finished and script is enabled set flag428=1

-----------------------When next monster gain control---------------------------
;v3926 is number of left hero - initialized in BR section above
;v3927 is number of right hero - initialized in BR section above
;v3926 and v3928 do not change in code for CM trigger
;All v3929-... can be modified outside later
;flag428 must be set in BR section above if script enabled and tactics turn finished
;flag429 must be set in BF section to set up first spells procedures
;v3932 do not change in BG sections.
!?FU(OnBeforeBattleAction)&i^wog_39_enabled^/i^wog_39_isRemote^=0/-998;

!!VRv3937: +1;                          //random value randomizing
!!BG&428:Q?v3925;                       //v3925 is attacking side
!!VRv3933&v3925=0:Sv3926;                 //v3933 is number of casting hero
!!VRv3933&v3925=1:Sv3927;
!!IF&428/v3933>=0:Wv3933;
!!FU805&428/v3933>=0/w28>0/w28<1000:P;  //call spellcaster procedure if hero is spellcaster

!!BG:A?v3929;
!!VRv3925:S0;
!!BG&v3929=(BATTLE_ACTION_HERO_CAST):S?v3925;
!!VRv3925&v3929=(BATTLE_ACTION_MONSTER_CAST)/v3933>=0/v3933<=15:S(SPELL_CURE);
!!BG&v3925=(SPELL_CURE):E?v3929;
!!BG:Q?v3936;

-------------------------------Spell Casters------------------------------------
!?FU805;
!!VRv3928&v3925=0/w30=0:S0;              //v3928 is side which will under spell
!!VRv3928&v3925=0/w30=1:S1;
!!VRv3928&v3925=1/w30=0:S1;
!!VRv3928&v3925=1/w30=1:S0;

!!VRv3937: *5 +3 %8;
!!VRv3930:Sv3928*20 +v3937;               //v3930 - target monster            TOBYN: new local 430
!!BMv3930:N?v3929;                       //v3929 - number of monsters
!!BMv3930:T?v3935;
!!VRv3929&v3935>=145/v3935<=149:S0;       //tower arrows and ballistas cannot be target
!!VRv3937: *5 +3 %8;
!!VRv3930&v3929<=0:Sv3928*20 +v3937;                 TOBYN: possible new local 430
!!BMv3930&v3929<=0:N?v3929;
!!BMv3930:T?v3935;
!!VRv3929&v3935>=145/v3935<=149:S0;
!!VRv3937: *5 +3 %8;
!!VRv3930&v3929<=0:Sv3928*20 +v3937;                 TOBYN: possible new local 430
!!BMv3930&v3929<=0:N?v3929;
!!BMv3930:T?v3935;
!!VRv3929&v3935>=145/v3935<=149:S0;
!!VRv3937: *5 +3 %8;
!!VRv3930&v3929<=0:Sv3928*20 +v3937;                 TOBYN: possible new local 430
!!BMv3930&v3929<=0:N?v3929;
!!BMv3930:T?v3935;
!!VRv3929&v3935>=145/v3935<=149:S0;

!!VRv3931:S0;                           //v3931 - position to cast if 0 then do not cast
!!BMv3930&v3929>0:P?v3931;

!!VRv3929:Sw29+w31;                     //v3929 - chance is 1 of (w29+w31)
!!VRv3937: *5 +3 %v3929;
!!VRv3932&v3931>0/v3929>0:S0 +v3937;       //randomize v3932

!!VRv3932&v3931>0/v3929<1:S1;             //if (w29+w31)<=1 that means the time to cast

!!HEv3933:I?v3929/1;
!!HEv3933:I100/1;
!!HEv3933:Sw32/?v3935;
!!HEv3933:Sw32/0;
!!BHv3925:M?v3934;

!!BG:A?v3930;                           //if hero or monster casts spell do not randomly spell
!!VRv3932|v3930=1/v3930=10:S2;

!!BHv3925&v3932=1/v3931>0:Cw28/v3931/0/0;  //v3932=1 means that time to cast
!!VRw31&v3932=1/v3931>0/w28>0/w28<1000:+w29; //if hero casts spell then w31 increasing by w29 value
!!VRw31|v3932<>1/v3931<=0/w28>0/w28<1000:-1; //w43 keeps addition value that added to w29. w31 decrement every turn and the chance is increasing

!!BHv3925:Mv3934;
!!HEv3933:Sw32/v3935;
!!HEv3933:Iv3929/1;

!!VRv3929:Sw29+w31;
!!VRw31&v3929<2:S-1*w29+2;              //if chance is more than 1/2 then decrease to 1/2


------------------------------Eagle eye flying----------------------------------
;do not change v3932
!?FU851;
!!VRv3934:S-1;
!!BMx16:T?v3934;
!!BMx16:I?v3931;
!!MA&v3934>=0/x1=v3931:Lv3934/?v3935;
!!BMx16&v3934>=0/x1=v3931:F?v3930;                  TOBYN: possible new local 430
!!VRv3933&v3935>=0/x1=v3931/v3935<=1/v3934>=0:Sv3930|2;
!!BMx16&v3935>=0/x1=v3931/v3935<=1/v3934>=0:Fv3933;

----------------------------When Cure spell was cast----------------------------
;After action
!?FU(OnAfterBattleAction)&i^wog_39_enabled^/v3925=(SPELL_CURE)/i^wog_39_isRemote^=0/-998;
!!IF:Wv3926;
!!FU&w28<1000|w28>2000:E; igrik: this function only to monster specs

!!DO(WOG_39_SetUpMonsterBonuses)/v3929/v3929/1&v3936=(BATTLE_LEFT)/v3929>=0:P0/v3926/1/559; igrik: line 559 to DEBUG
!!DO(WOG_39_SetUpMonsterBonuses)/(BATTLE_ATTACKER_STACK_FIRST)/(BATTLE_ATTACKER_STACK_LAST)/1&v3936=(BATTLE_LEFT)/v3929=-1:P0/v3926/1/560;
!!DO(WOG_39_SetUpMonsterBonuses)/v3929/v3929/1&v3936=(BATTLE_RIGHT)/v3929>=0:P1/v3927/1/561;
!!DO(WOG_39_SetUpMonsterBonuses)/(BATTLE_DEFENDER_STACK_FIRST)/(BATTLE_DEFENDER_STACK_LAST)/1&v3936=(BATTLE_RIGHT)/v3929=-1:P1/v3927/1/562;

-------------------------Monster specs add bonuses------------------------------
;do not change v3932
;x1=side
;x2=number of hero (or v3926)
;x3=1 if non first call of the function
;x4=line calling in script for debug (by igrik)
!?FU(WOG_39_SetUpMonsterBonuses)&i^wog_39_MonSpecEnabled^;
!!VRv3930&x1=0:Sv3938;                   //restore hero parameters      TOBYN: new local 430
!!VRv3931&x1=0:Sv3939;                                                     |
!!VRv3930&x1=1:Sv3940;                                                  TOBYN: new local 430
!!VRv3931&x1=1:Sv3941;
!!BMx16:T?v3934;                        //v3934=type of creature
!!BMx16:I?v3935;                        //v3935=left or right
;Standard
!!FU852&w29=v3934/x1=v3935:Px16/v3934/v3930/v3931/x3/587;
;Non-confluxers upgraded creatures
!!VRv3933:Sv3934-1;
!!FU852&w29=v3933/x1=v3935/w29<>112/w29<>113/w29<>114/w29<>115:Px16/v3934/v3930/v3931/x3/590;
;Conflux upgraded
!!FU852&w29=112/v3934=127/x1=v3935:Px16/v3934/v3930/v3931/x3/585;    igrik: line 585 to DEBUG
!!FU852&w29=113/v3934=125/x1=v3935:Px16/v3934/v3930/v3931/x3/586;
!!FU852&w29=114/v3934=129/x1=v3935:Px16/v3934/v3930/v3931/x3/587;
!!FU852&w29=115/v3934=123/x1=v3935:Px16/v3934/v3930/v3931/x3/588;


!?FU852;
;x1=monster stack ID
;x2=monster type
;x3=hero attack skill (or v3930)
;x4=hero defense skill  (or v3931)
;x5=1 if non first call of the function
;x6=line in script for debug (by igrik)
;v3935=side 
;v3926=hero id
!!BMx1&x5<>1:Sd1;                      //speed +1
!!MA:Px2/?v3933;                       //hitpoints bonus percentage = 12 + 3 * herolevel
!!VRv3933:*w30:100;
!!VRy96:Sv3933;
!!BMx1:Hdv3933;
!!MA:Ax2/?v3933;                        //attack for each specified monster = Standard+HeroBonus+CleanHeroBonus
!!BA:H0/?y97 H1/?y98;
!!VRw31&w31<0:Sv3930 +v3931;    set correct w31 instead   [DEBUGGING]
!!VRv3933:+x3+w31;
!!BMx1:Av3933;
!!MA:Dx2/?v3933;                        //defense
!!VRv3933:+x4+w32;
!!BMx1:Dv3933;

-----------------------Right mouse click in battle info-------------------------
; VRv425-v428 cannot touch   [SECTION EDITED by Tobyn]
!?FU(OnBattleScreenMouseClick)&i^wog_39_enabled^/i^mouse_action^=(MOUSE_RMB_PRESSED)/i^mouse_battleHex^>=252/i^mouse_battleHex^<=253/i^wog_39_isRemote^=0/-998;
!!VR(side:y):Si^mouse_battleHex^ -252;
!!FU&i^battle_hero_%(side)^=(NO_HERO):E;

!!CM:R0;
!!VR(hero:y):Si^battle_hero_%(side)^;
!!IF:W(hero);

!!ifw28>=1000/w28<2000/i^wog_39_MonSpecEnabled^<>(TRUE);
  !!SN:H^spec^/(hero)/2/?z405;
!!el;
  !!FU(WOG_39_SetUpSpecDesc):Pv3926/1;
!!en;

!!IF:M0/(MSG_TYPE_POPUP)/z405;

-----------------------------Setting heroes info--------------------------------
;sets to z405 text for hero's info
;x1 = hero number                                                [SECTION EDITED by Tobyn]
;x2 = 1 if was called from battle
!?FU(WOG_39_SetUpSpecDesc);
!!IF:Wx1;
!!FU&w28<0:E;      just exit if lead var w28 is negative (this might be useful for custom specialty descriptions)

!!HEx1:B0/?z-1; ?z406;
!!VRz405:Sz139087;
;                          init spell strings
!!VRz-2&w28=15:Sz139088;     Magic Arrow
!!VRz-2&w28=16:Sz139089;     Ice Bolt
!!VRz-2&w28=17:Sz139090;     Lightning Bolt
!!VRz-2&w28=27:Sz139091;     Shield
!!VRz-2&w28=28:Sz139092;     Air Shield
!!VRz-2&w28=29:Sz139093;     Fire Shield
!!VRz-2&w28=37:Sz139094;     Curse
!!VRz-2&w28=41:Sz139095;     Bless
!!VRz-2&w28=43:Sz139096;     Bloodlust
!!VRz-2&w28=45:Sz139097;     Weakness
!!VRz-2&w28=46:Sz139098;     Stone Skin
!!VRz-2&w28=47:Sz139099;     Disrupting Ray
!!VRz-2&w28=48:Sz139100;     Prayer
!!VRz-2&w28=51:Sz139101;     Fortune
!!VRz-2&w28=53:Sz139102;     Haste
!!VRz-2&w28=54:Sz139103;     Slow
!!VRz-2&w28=55:Sz139104;     Slayer
!!VRz-2&w28=60:Sz139105;     Hypnotize
!!VRz-3:Sz-2;              init string for original spell names
!!VRz-3&x1=11:Sz139106;      Adelaide
!!VRz-3&x1=45:Sz139107;      Solmyr
!!VRz-3&x1=57:Sz139108;      Xyron
!!VRz-3&x1=63:Sz139109;      Xarfax
!!VRz-3&x1=72:Sz139110;      Septienna
!!VRz-3&x1=73:Sz139111;      Aislinn
!!VRz-3&x1=76:Sz139112;      Thant
!!VRz-3&x1=88:Sz139113;      Alamar
*!VRz-3&y1=0/x1=91:Sz139113; Jeddite [somewhat incorrect: it's just Resurrection, not *stronger* Resurrection... no more Res spec but Rebuild spec instead]
*!VRz-3&y1=1/x1=91:Sz139114; Jeddite if Rebalanced Heroes active (Blind) [somewhat incorrect: it's just Blind, not *stronger* Blind]
!!VRz-3&x1=93:Sz139115;      Deemer
!!VRz-3&x1=108:Sz139116;     Zubin
!!VRz-3&x1=136:Sz139117;     Luna

!!HEx1:R2/?v3932;

!!if&v3932=0;
  !!VRz-4:Sz139118;
  !!VRz-5:Sz139120;
!!el&v3932=1;
  !!VRz-4:Sz139119;
  !!VRz-5:Sz139121;
!!en;

;Spellcasters
!!if&w28>0/w28<1000;
  !!VRz405:+z139122;
  !!FU807:Px1;
!!en;

;Monstermasters
!!if&w28>=1000/w28<2000;
  !!VRz405:+z139123;
  !!FU808:Px1/x2;
!!en;

;Logistics
!!VRz405&w28=2000:+z139124;
;AntiScouting
!!VRz405&w28=2001:+z139125;
;Navigators
!!VRz405&w28=2002:+z139126;
;Estates
!!VRz405&w28=2003:+z139127;
;Armorer
!!VRz405&w28=2004:+z139128;
;Offence
!!VRz405&w28=2005:+z139129;
;Archery
!!VRz405&w28=2006:+z139130;
;Sir Mullich
!!VRz405&w28=2007:+z139131;
;Ballista
!!VRz405&w28=2008:+z139132;
;First Aid
!!VRz405&w28=2009:+z139133;
;Sorcery
!!VRz405&w28=2010:+z139134;
;Eagle Eye
!!VRz405&w28=2011:+z139135;
;Intelligence
!!VRz405&w28=2012:+z139136;
;Mysticism
!!VRz405&w28=2013:+z139137;
;Resistance
!!VRz405&w28=2014:+z139138;
;Fire Magic
!!VRz405&w28=2015:+z139139;
;Necromancy
!!VRz405&w28=2016:+z139140;
;Resource
!!VRz405&w28=2100:+z139141;
!!VRz405&w28=2101:+z139142;
!!VRz405&w28=2102:+z139143;
!!VRz405&w28=2103:+z139144;
!!VRz405&w28=2104:+z139145;
!!VRz405&w28=2105:+z139146;
!!VRz405&w28=2106:+z139147;
!!VRz405&w28=2107:+z139148;
;Behemoths
!!VRz405&w28=2200:+z139149;
;Black Knights
!!VRz405&w28=2201:+z139150;
;Devils
!!VRz405&w28=2202:+z139151;
;Dragon
!!VRz405&w28=2203:+z139152;
;Gelu
!!VRz405&w28=2204:+z139153;
;Enchanter
!!VRz405&w28=2205:+z139154;

!?FU807;                                //spell
!!VRv3932:Sw29+w31;
!!VRz405:+z139155+^ %W29.^;
!!VRz405&x2=1:+z139156+^ %V432^;

!?FU808;                                //monsters
!!UN:N3/406/w29/1;
!!VRz405:+z139157;
!!VRz405&w31>0:+z139158;
!!VRz405&w32>0:+z139159;
!!VRv3932:Sw28-1000;
!!UN:N3/406/w29/0;
!!VRz405&v3932>0:+z139160;
!!VRz405&w30>0:+z139161;
!!VRz405&x1<128:+z139162;

*!VRz405:+^%Z-1's stats and level determine the bonus (except for speed).^;   disabled to have a nicer msgbox

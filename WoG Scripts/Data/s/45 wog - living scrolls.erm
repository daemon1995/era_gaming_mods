ZVSE2

** Living Scrolls Script Version 1.7 Created by Timothy Pulver
** Requires WoG version 3.58 or later using ERM version 2.7 or later.
** WoGify Name: script33.erm
** Updated: October 21, 2004

** This script enhances most Scrolls in the game, changing them from
** passive Spell Scrolls to active Living Scrolls that may cast the
** spell themselves each round in combat after the first.

** For each Living Scroll equipped, there is a 20% chance that one
** of the equipped Living Scrolls will cast its spell. Thus, if a hero has
** three Living Scroll equipped, there's a 60% chance each round
** that one of the three will randomly cast its spell. Equipping five
** Living Scrolls virtually guarantees that one will cast its spell
** its spell. However, if a creature is immune to a spell, the spell won't
** be cast upon it.

** In most cases, the scroll will cast the spell at Basic skill level only,
** regardless of the hero's level. However, the hero's Power will be used
** for determining spell effect where applicable. A spell scroll spell
** will not add to the duration of an existing spell, but may cast the
** same spell on a creature before the duration has expired.

** All Spell Scrolls are changed into Living Scrolls EXCEPT for the
** following: Adventure spells, Elemental Summoning spells, Sacrifice,
** Death Ripple, Destroy Undead, Animate Dead, Dispel, Clone, Resurrection,
** and Armageddon.

** Version 1.1 fixes some bugs.
** Version 1.2 removes "Clone" as a Living Scroll spell because it
** seems to be unstable and often crashes.
** Version 1.3 fixes a problem with Magic Plains and the issue of
** area-effect spells wiping out a friendly stack that was temporarily
** changed to a single unit (for message-display purposes). The stack
** size changing is no longer used in this manner.
** Version 1.4 fixes a problem with Anti-Magic garrisons
** Version 1.5 adds right-click info for scrolls in backpacks and
** also in the hero trading screen. It also fixes a problem with Magic Plains.
** Version 1.6 fixes some miscellaneous bugs.
** Version 1.7 removes Armageddon as a Living Scroll.

------------------------------------------------------------------------

Variables Used: v1-v6, z1-z2, v1220, v1222-v1235, v1257, v9300
Functions Used: FU5244-FU5426, FU5248-FU5255, FU5260-FU5261

------------------------------------------------------------------------

 [Start of Battle Trigger]
!?FU(OnBeforeBattle)&-997/i^battle_hero_1^=(NO_HERO);

!!UN:P33/?y-1; [Check if Living Scroll script is enabled in WoGify Options: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!VRv1257:S0;
!!VRv1218:S0; [Set v1218 to 0 - modifier for choosing random scroll]

; Check if living scroll can be used
; Archer30's note: This is only a rough check. It has to be replaced by BattleMgr_CanCast (0041FA10) soon, same with hero spec boost
!!OB998:T?v2 U?v3; [Check object type and subtype where battle is occurring]
!!VRv1257&v2=33/v3=1:S1; [If normal horizontal anti-magic garrison, set v1257 to 1]
!!VRv1257&v2=33/v3=3:S1; [If special vertical anti-magic garrison, set v1257 to 1]
!!VRv1257&v2=219/v3=1:S1; [If normal vertical anti-magic garrison, set v1257 to 1]

!!HEi^battle_hero_0^:A2/(ART_ORB_OF_INHIBITION)/?y2/?y3; Check 126    Orb of Inhibition
!!VRv1257&y3>0:S1;

!!FU5253&v1257=0:P; [Set up at start of battle]

------------------------------------------------------------------------

 [Combat turn trigger: every turn after the first one]
!?FU(OnBattleRound)&i^battle_round^>0/v1257=0/-997/i^battle_hero_1^=(NO_HERO);

!!UN:P33/?y-1; [Check if Living Scroll script is enabled in WoGify Options: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!BH1:N?v1; [Hero number of defender]
!!VRv1229:S0; [Counter for number of casting attempts this round]
!!FU5244&v1=-1:P; [Check for casting a Living Scroll spell--only if no defending hero]

------------------------------------------------------------------------

 [Check for casting a Living Scroll spell]
!?FU5244&v1230>0;

!!VRy1:Sv1230 *20; [Percent chance of a spell being cast]
!!VRy8:S0 R3; [Generate number to help randomizer be more random]
!!VRy2&y8=0:S1 R24; [1-25]
!!VRy2&y8=1:S26 R24; [26-50]
!!VRy2&y8=2:S51 R24; [51-75]
!!VRy2&y8=3:S76 R24; [76-100]
!!VRy3&y2<=y1:S1; [Set y3 to 1 if a spell is cast]
!!VRy3&v1229>0:S1; [Set y3 to 1 if 2nd or later attempt this round]

 [Select a random spell from all Living Scrolls equipped]
!!VRy4:Sv1230 -1;
!!VRy8:S0 R3; [Generate number to help randomizer be more random]
!!VRy5&y8>=0:S1 Ry4;
!!VRy5&y8>=1:S1 Ry4;
!!VRy5&y8>=2:S1 Ry4;
!!VRy5&y8=3:S1 Ry4;

!!VRv1218&v1218>=v1230:S0; [Reset v1218 to 0 if equals/exceeds # of scrolls]
!!VRy5:+v1218; [Add v1218 modifier to y5]
!!VRv1218:+1; [Increment v1218 modifier for random scroll selection]
!!VRy5&y5>v1230:S1 Ry4; [Don't let y5 exceed # of equipped scrolls]

!!VRy6:S1230 +y5;
!!VRy7:Svy6;

!!FU5245&y3=1:Py7; [Call casting function, passing spell number as x1 parameter]

------------------------------------------------------------------------

 [Cast a Living Scroll spell]

!?FU5245;
!!VRv1220:S1; [Initialize casting position]
!!VRv1225:S1; [Initialize casting skill to Basic]

!!VRy1:Sx1; [Set y1 to x1 (spell scroll spell chosen in function 5244)]

!!VRy5:S-1; [Initialize y5 to -1]

!!VRy5&y1>=15/y1<=19:S21; [Enemy target]
!!VRy5&y1>=21/y1<=23:S21; [Enemy target]
!!VRy5|y1=42/y1=45/y1=47/y1=50/y1=52/y1=54/y1=57:S21; [Enemy target]
!!VRy5|y1=59/y1=60/y1=61/y1=62:S21; [Enemy target]

!!VRy5&y1>=27/y1<=34:S0; [Friendly target]
!!VRy5|y1=20/y1=36/y1=37/y1=41/y1=43/y1=44/y1=46/y1=48:S0; [Friendly target]
!!VRy5|y1=49/y1=51/y1=53/y1=55/y1=56/y1=58/y1=63:S0; [Friendly target]

!!VRy5|y1=10/y1=11/y1=12/y1=13/y1=14/y1=64:S99; [No specific target]

!!VRv1223:S0; [Initialize loop check counter to 0]

!!FU5246&y5<>99:Py5/y1; [Find a valid stack to cast spell on]

!!FU5248|y1=12/y1=13:P; [Find a valid position (v1220) for a Forcefield or Fire Wall]
!!VRv1224&y1=64:S99; [Assume no valid position found for Remove Obstacle]
!!FU5251&y1=64:P; [Find a valid position (v1220) for Remove Obstacle]

!!VRy6:Sv1226 +1; [Hero's Power for spell duration +1 because it's usually 1 turn less]

!!BU:G?y7; [Battlefield: y7 (1=Magic Plains)]
!!BU&y7=1:G-1; [Set Battlefield to -1 (no modifier) if it's Magic Plains]

!!VRy8:S20; [Set stack number to 20: y8]

!!BMy8:T?y9 N?y10; [Store stack's type (y9) and number (y10)]
!!BMy8:T(MON_NOT_USED_1) N1; [Set stack #20 to "Not Used (1)" and number of 1]
!!SN:H^monname^/(MON_NOT_USED_1)/0/z133000; [Change "Not Used (1)" name to "Living Scroll"]

 [Stack #20 casts spell y1 on target at position v1220]
 [v1225 is spell skill level, y6=spell power, v1223 is loop-check counter]
!!BMy8&v1223<100/y1<>63:Cy1/v1220/v1225/y6/0;

 [Teleport spell (y1=63): v1228 is teleport destination, v1227 is target stack number]
!!FU5252&y1=63:P; [Find a valid Teleport destination: v1228]

!!BMv1227&v1223<100/y1=63:Pv1228; [Teleport stack to destination]
!!BMv1227&v1223<100/y1=63:G72/0/0; [Remove dendroid's "Bind" from teleported stack]

!!BMy8:Ty9 Ny10; [Restore stack y8's type and number after spell is cast]
!!BU&y7=1:G1; [Restore Battlefield if it was Magic Plains]

 [Try again if no target found for 1st spell and Hero has multiple Living Scrolls]
 [Maximum 20 tries]
!!VRv1229&v1223>99:+1;
!!FU5244&v1223>99/v1229<20/v1230>1:P;

------------------------------------------------------------------------

 [Select a random valid target stack: x1=starting stack number, x2=spell number]

!?FU5246;
!!VRv1227:S0; [Make certain v1227 is set to 0]
!!VRy5:S0 R3; [Random number to help randomizer work better]
!!VRv1227&y5=0:Sx1 R4; [Choose a random target stack]
!!VRv1227&y5=1:Sx1 +5 R4; [Choose a random target stack]
!!VRv1227&y5=2:Sx1 +10 R4; [Choose a random target stack]
!!VRv1227&y5=3:Sx1 +15 R4; [Choose a random target stack]
!!BMv1227:N?y2 P?v1220 T?y3; [y2=number in stack, v1220=position of stack, y3=type of monster]
!!VRy4:S0;
!!VRy4|v1220<1/v1220>185:S-1; [If invalid position, set y4 to -1]
!!BU&y4=0:Ev1220/?y4; [y4=-1 if no living monster at this position]
!!VRv1223:+1; [Increment v1223 loop counter]
!!VRv1224:S0; [Initialize v1224 spell immunity/validity check variable to 0]
!!VRv1224&v1223<100/y4=-1:S99; [Set v1224 to 99 if no living monster and loop under 100]

 [Call spell immunity check function as long as number in stack is above 0 and a living]
 [stack is at this position: y3=type of monster, x2=spell number,v1227=stack number]
 [y2=number of monsters in stack]
!!FU5249&v1223<100/y2>0/y4<>-1:Py3/x2/v1227/y2;

 [Call function again if loop is less than 100 *and* number in stack is less than 0]
 [OR: call it if v1224 is set to 99: x1=starting stack number, x2=spell number]
!!FU5246&v1223<100/y2<1|v1224=99:Px1/x2;

------------------------------------------------------------------------

 [Find a valid location for a Force Field or Fire Wall]

!?FU5248;
!!VRy1:S1 R184; [Random position]
!!VRy4:Sy1 -16; [2nd hex of Force Field]
!!BU:Ey1/?y2 Oy1/?y3; [y2=-1 if no monster, y3=0 if no obstacle]
!!BU&y4>=0:Ey4/?y5 Oy4/?y6; [y5=-1 if no monster, y6=0 if no obstacle]
!!VRv1220:Sy1; [If valid, set position (v1220) to y1]

!!FU5248|y2>=0/y5>=0:P; [Call function again if monster there]
!!FU5248|y3<>0/y6<>0:P; [Call function again if object there]

 [Call function again if out of field position]
!!FU5248|y1=16/y1=17/y1=33/y1=34/y1=50/y1=51/y1=67/y1=68:P;
!!FU5248|y1=84/y1=85/y1=101/y1=102/y1=118/y1=119/y1=135:P;
!!FU5248|y1=136/y1=152/y1=153/y1=169/y1=170:P;

------------------------------------------------------------------------

 [Check for spell immunity: select appropriate categories for spell]
 [x1=type of monster, x2=spell number, x3=stack number, x4=number of monsters in stack]

!?FU5249;
0=black dragons/magic elementals/war machines,
1=undead,2=trogs,3=red/green/azure dragons,
4=efreet/phoenix/fire elementals,5=golems,6=gargoyles,
7=double wide creatures,8=gold dragons,9=air elementals,
10=earth elementals,11=water elementals,12=shooting creatures,13=healthy (for Cure),
14=elementals,15=mind spell immunity (undead,titans,golems,elementals),
16=total hit health (for Hypnotize)

!!VRv2:C-1/-1/-1/-1/-1; [Initialize variables]
!!VRv2|x2=15/x2=21/x2=31/x2=52:C3/4/8; [Magic Arrow, Fire Ball, Protection from Fire, Misfortune]
!!VRv2&x2=16:C3/8/11; [Ice Bolt]
!!VRv2&x2=20:C3/7/8/11; [Frost Ring]
!!VRv2&x2=17:C3/8/10; [Lightning]
!!VRv2|x2=18/x2=35/x2=36:C-1; [Implosion, Dispel*, Magic Mirror]
!!VRv2&x2=19:C8/10; [Chain Lightning]
!!VRv2|x2=22/x2=26/x2=29/x2=55:C4/8; [Inferno, Armageddon, Fire Shield, Slayer]
!!VRv2&x2=23:C8/9; [Meteor Shower]
!!VRv2|x2=27/x2=28/x2=30/x2=32/x2=33/x2=34/x2=45/x2=46/x2=47/x2=51/x2=53/x2=54:C3/8; [Shield,Air Shield, Protection from Air, Protection from Water, Protection from Earth, Anti-Magic, Weakness, Stone Skin, Disrupting Ray, Fortune, Haste, Slow]
!!VRv2&x2=37:C3/8/13; [Cure]
!!VRv2&x2=41:C1/3/8; [Bless]
!!VRv2&x2=42:C1/3/4/8; [Curse]
!!VRv2&x2=43:C4/15; [Bloodlust]
!!VRv2&x2=44:C1/3/8/12; [Precision]
!!VRv2|x2=48/x2=58/x2=65:C8; [Prayer, Counterstrike, Clone]
!!VRv2|x2=49/x2=59:C3/8/15; [Mirth, Berserk]
!!VRv2&x2=60:C3/8/15/16; [Hypnotize]
!!VRv2&x2=61:C3/8/12/15; [Forgetfulness]
!!VRv2&x2=63:C3/8; [Teleport]
!!VRv2&x2=50:C8/15; [Sorrow]
!!VRv2&x2=56:C4/15; [Frenzy]
!!VRv2&x2=57:C10; [Titan's Lightning Bolt]
!!VRv2&x2=62:C2/3/4/8/15; [Blind]

 [Check for spell immunities: v1224=99 if any found]
!!FU5250&x1>=0:Pv2/v3/v4/v5/v6/x1/x3/x4/x2;

------------------------------------------------------------------------

 [Check for spell immunities: called from Function 5249]

!?FU5250;
!!VRy1:S0;
 [Black Dragons/Darkness Dragon/Diamond Dragon/Magic Elementals: Category 0]
 [Also War Machines]
!!VRv1224|x6=83/x6=121/x6=151/x6=155:S99;
!!VRv1224&x6>=145/x6<=149:S99;

 [Undead: Category 1]
!!VRy1|x1=1/x2=1/x3=1/x4=1/x5=1:S1;
!!VRv1224&y1=1/x6>=56/x6<=69:S99;
!!VRv1224&y1=1/x6=141:S99;
!!VRv1224&y1=1/x6=154:S99;
!!VRv1224&y1=1/x6=178:S99;
!!VRv1224&y1=1/x6=187:S99;

 [Troglodytes: Category 2]
!!VRy1|x1=2/x2=2/x3=2/x4=2/x5=2:S2;
!!VRv1224&y1=2/x6>=70/x6<=71:S99;

 [Red/Green/Azure Dragons: Category 3]
!!VRy1|x1=3/x2=3/x3=3/x4=3/x5=3:S3;
!!VRv1224&y1=3/x6=26:S99;
!!VRv1224&y1=3/x6=82:S99;
!!VRv1224&y1=3/x6=132:S99;

 [Efreet/Fire Bird/Fire Elementals: Category 4]
!!VRy1|x1=4/x2=4/x3=4/x4=4/x5=4:S4;
!!VRv1224&y1=4/x6>=52/x6<=53:S99;
!!VRv1224&y1=4/x6>=130/x6<=131:S99;
!!VRv1224&y1=4/x6=114:S99;
!!VRv1224&y1=4/x6=129:S99;
!!VRv1224&y1=4/x6=158:S99;

 [Golems/Messengers: Category 5]
!!VRy1|x1=5/x2=5/x3=5/x4=5/x5=5:S5;
!!VRv1224&y1=5/x6>=32/x6<=33:S99;
!!VRv1224&y1=5/x6>=116/x6<=117:S99;
!!VRv1224&y1=5/x6>=163/x6<=164:S99;
!!VRv1224&y1=5/x6>=165/x6<=166:S99;

 [Gargoyles: Category 6]
!!VRy1|x1=6/x2=6/x3=6/x4=6/x5=6:S6;
!!VRv1224&y1=6/x6>=30/x6<=31:S99;

 [Double Wide: Category 7]
!!VRy1|x1=7/x2=7/x3=7/x4=7/x5=7:S7;
!!MA&y1=7:Xx6/?y2; [Get monster attributes]
!!VRy2&y1=7:&1; [AND the bits to check for Double Wide attribute: 1 if True, 0 if False]
!!VRv1224&y1=7/y2=1:S99;

 [Gold Dragons: Category 8]
!!VRy1|x1=8/x2=8/x3=8/x4=8/x5=8:S8;
!!VRv1224&y1=8/x6=27:S99;

 [Air Elementals: Category 9]
!!VRy1|x1=9/x2=9/x3=9/x4=9/x5=9:S9;
!!VRv1224&y1=9/x6=112:S99;
!!VRv1224&y1=9/x6=127:S99;

 [Earth Elementals: Category 10]
!!VRy1|x1=10/x2=10/x3=10/x4=10/x5=10:S10;
!!VRv1224&y1=10/x6=113:S99;
!!VRv1224&y1=10/x6=125:S99;

 [Water Elementals: Category 11]
!!VRy1|x1=11/x2=11/x3=11/x4=11/x5=11:S11;
!!VRv1224&y1=11/x6=115:S99;
!!VRv1224&y1=11/x6=123:S99;

 [Shooting Creatures: Category 12]
!!VRy1|x1=12/x2=12/x3=12/x4=12/x5=12:S12;
!!MA&y1=12:Xx6/?y2; [Get monster attributes]
!!VRy2&y1=12:&4; [AND the bits to check for Shooter attribute: 4 if True, 0 if False]
!!VRv1224&y1=12/y2=0:S99;

 [Healthy Creatures: Category 13]
 [x6=monster type, x7=stack number]
!!VRy1|x1=13/x2=13/x3=13/x4=13/x5=13:S13;
!!MA&y1=13:Px6/?y2; [Get monster type's starting health: store in y2]
!!BMx7&y1=13:L?y3; [Get top monster's lost hit points: store in y3]
!!VRv1224&y1=13/y3=0:S99;

 [Elementals/Ghosts/Emissaries: Category 14]
!!VRy1|x1=14/x2=14/x3=14/x4=14/x5=14:S14;
!!VRv1224&y1=14/x6>=112/x6<=115:S99;
!!VRv1224&y1=14/x6>=120/x6<=129:S99;
!!VRv1224&y1=14/x6>=159/x6<=163:S99;
!!VRv1224&y1=14/x6=182:S99;
!!VRv1224&y1=14/x6=191:S99;

 [Mind Spell Immunity: Undead/Titans/Golems/Elementals/Ghosts/Emissaries: Category 15]
!!VRy1|x1=15/x2=15/x3=15/x4=15/x5=15:S15;
!!MA&y1=15:Xx6/?y2; [Get monster attributes]
!!VRy2&y1=15:&1024; [AND the bits to check for Mind Spell Immunity attribute: 1024 if True, 0 if False]
!!VRv1224&y1=15/y2=1024:S99;


 [Total Hit Points (for Hypnotize): Category 16]
 [x6=monster type, x8=number in stack]
!!VRy1|x1=16/x2=16/x3=16/x4=16/x5=16:S16;
!!MA&y1=16:Px6/?y2; [Get monster type's starting health: store in y2]
!!VRy3&y1=16:Sx8 *y2; [Total hit points of stack]
!!HE-1&y1=16:Fd/d/?y4/d; [Hero's Power: store in y4]
!!VRy5&y1=16:Sy4 *25 +50; [Maximum hit points that can be Hypnotized]
!!VRv1224&y1=16/y5<y3:S99;

------------------------------------------------------------------------

 [Find a valid location for Remove Obstacle]

!?FU5251;
!!VRy1:S1 R184; [Random position]
!!BU:Oy1/?y2; [y2=0 if no obstacle]

!!VRv1220&y2>0:Sy1; [If valid, set position (v1220) to y1]
!!VRv1225&y2>0:S3; [Set casting skill to Expert]
!!VRv1224&y2>0:S0; [Valid position found for Remove Obstacle]

!!VRv1223:+1; [Increment loop check counter]

 [Set y3 to 99 if position is out of combat field]
!!VRy3:S0;
!!VRy3|y1=16/y1=17/y1=33/y1=34/y1=50/y1=51/y1=67/y1=68:S99;
!!VRy3|y1=84/y1=85/y1=101/y1=102/y1=118/y1=119/y1=135:S99;
!!VRy3|y1=136/y1=152/y1=153/y1=169/y1=170:S99;

 [Set y2 to 0 if y3=99]
!!VRy2&y3=99:S0;

 [Call function again if no object there or position out of combat field]
!!FU5251&y2=0/v1223<100:P;

------------------------------------------------------------------------

 [Find a valid location for Teleporting]

!?FU5252;
!!VRv1228:S1 R184; [Random teleport destination position (v1228)]

!!BU:Ev1228/?y1 Ov1228/?y2; [y1=-1 if no monster, y2=0 if no obstacle]

!!VRv1223:+1; [Increment loop check counter]
!!VRy3:S0;    [Reset y3 to 0]

 [Set y3 to 99 if monster or obstacle present at destination]
!!VRy3|y1>=0/y2<>0:S99;

 [Check for "out of field" position]
!!VRy3|v1228=16/v1228=17/v1228=33/v1228=34/v1228=50/v1228=51/v1228=67/v1228=68:S99;
!!VRy3|v1228=84/v1228=85/v1228=101/v1228=102/v1228=118/v1228=119/v1228=135:S99;
!!VRy3|v1228=136/v1228=152/v1228=153/v1228=169/v1228=170:S99;

 [Call function again if a monster or obstacle is at destination position]
 [or if it's and "out of field" position -- don't call if loop check has]
 [exceeded 99 attempts]
!!FU5252&y3=99/v1223<100:P;

------------------------------------------------------------------------

 [Set up at start of battle]
!?FU5253;

!!BA:H0/?y1; [Hero number of attacker]
!!HEy1:Fd/d/?v1226/d B0/?z3; [Hero's Power for spell duration]
!!VRv1230:S0; [Initialize equipped Living Scroll counter to zero]

 [Determine how many and which Living Scrolls attacking Hero has equipped]
!!DO5254/9/13/1:Py1;

!!VRy2:Sv1230 *20; [Percent chance of casting a spell]

 [Build a string of spell names]
!!DO5255/1/v1230/1&v1230>0:P;

 [Check if Living Scroll Start of Battle messages are disabled]
!!OW:C?y3; [Current player: y3]
!!VRy3:+1; [Add 1 to y3]
!!FU$bit$:Py3; [Convert to bit number: y-100]
!!VRy4:Sv3321; [Set y4 to v3321]
!!VRy4:&y-100; [Check if message is disabled for current player: y4]
!!FU&y4=y-100:E; [Exit Function if messages are disabled for current player]

!!IF&v1230>0/1000:M1/z133001;

------------------------------------------------------------------------

 [Determine how many and which Living Scrolls attacking Hero has equipped]
!?FU5254;

!!VRy1:Sx16; [y1=misc. slot number]
!!VRy1&x16=13:S18; [Convert number for misc. slot 5]

!!HEx1:A1/?y2/y1; [Check artifact number in misc. slot #y1 - store artifact # in y2]
!!VRy3&y2>1000:Sy2 -1001; [Convert to spell number: store in y3]

 [If artifact isn't a Living Scroll, set y4 to 1]
!!VRy4:S0;
!!VRy4|y3<10/y3=24/y3=25/y3=26/y3=35/y3=38/y3=39/y3=40/y3=65/y3=66/y3=67/y3=68/y3=69:S1;
!!VRy4&y2<1001:S1; [If not a scroll, set y4 to 1]

 [If scroll is Earthquake, only use it if battle is in a town with a fort, citadel or castle]
!!BA&y3=14:S?y5; [Check for Siege Battle: y5>0 if it's a siege]
!!VRy4&y3=14/y5=0:S1; [If not a siege battle, set y4 to 1]

!!VRv1230&y4=0:+1; [Count each equipped Living Scroll found]
!!VRy5&y4=0:S1230 +v1230; [Set index number]
!!VRvy5&y4=0:Sy3; [Store spell scroll numbers at v1231-1235]

------------------------------------------------------------------------

 [Build a string of spell names]
!?FU5255;

!!VRy1:S1230 +x16; [Spell index]
!!VRy2:Svy1; [Spell number]

!!UN:N1/1/y2; [Store spell name in z1]

 [Add spell name to z2 string]
!!VRz2&x16=1:Sz1;
!!VRz2&x16>1/x16<>v1230:Sz2+^, ^+z1;
!!VRz2&x16>1/x16=v1230:Sz2+z133002+z1;

------------------------------------------------------------------------

 [Spell Scroll is object trigger]
!?OB93;

!!UN:P33/?y-1; [Check if Living Scroll script is enabled in WoGify Options: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!OB998:C?v1; [Check control word (spell number) - store in v1]
!!VRv2:S0;

 [Set v2 to 1 if not a Living Scroll]
!!VRv2|v1<10/v1=24/v1=25/v1=26/v1=35/v1=38/v1=39/v1=40/v1=65/v1=66/v1=67/v1=68/v1=69:S1;
!!OB998&v2=0:M-1/1/0; [Disable standard message]
!!UN&v2=0:N1/1/v1; [Store spell name in z1]
!!IF&v2=0/1000:Q2/9/v1/1/z133003;

------------------------------------------------------------------------

 [Hero screen click trigger]
!?CM2;

!!UN:P33/?y-1; [Check if Living Scroll script is enabled in WoGify Options: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!CM:S?v1 I?v2; [v1=type of click, v2=area clicked]
!!VRv3:Sv2 -2;  [v3=equip position for use with HE:A1 command]
!!VRv4:Sv2 -21; [v4=backpack position for use with HE:A1 command]
!!FU5260&v1=14/v2>=2/v2<=20:Pv3/-1; [Equipped slot]
!!FU5260&v1=14/v2>=40/v2<=44:Pv4/-1; [Backpack slot]

------------------------------------------------------------------------

 [Hero Meeting Screen click trigger]
!?CM3;

!!UN:P33/?y-1; [Check if Living Scroll script is enabled in WoGify Options: y-1]
!!FU&y-1<>1:E; [Exit if script isn't enabled]

!!CM:S?v1 I?v2; [v1=type of click, v2=area clicked]
!!VRv3:Sv2 -27;  [v3=equip position for left hero to use with HE:A1 command]
!!VRv4:Sv2 -70; [v4=backpack position for left hero to use with HE:A1 command]
!!VRv5:Sv2 -46;  [v5=equip position for right hero to use with HE:A1 command]
!!VRv6:Sv2 -75; [v6=backpack position for right hero to use with HE:A1 command]

!!CM:H?v7/?v8; [Left hero: v7, Right hero: v8]

!!FU5260&v1=14/v2>=27/v2<=45:Pv3/v7; [Equipped slot for left hero]
!!FU5260&v1=14/v2>=89/v2<=93:Pv4/v7; [Backpack slot for left hero]
!!FU5260&v1=14/v2>=46/v2<=64:Pv5/v8; [Equipped slot for right hero]
!!FU5260&v1=14/v2>=94/v2<=98:Pv6/v8; [Backpack slot for right hero]


------------------------------------------------------------------------

 [Spell is an equipped spell scroll: check if it's a Living Scroll]
!?FU5260;
!!HEx2:A1/?y1/x1;
!!VRy2:Sy1 -1001;
!!VRy3:S0;

 [Set y3 to 1 if not a Living Scroll]
!!VRy3|y2<10/y2=24/y2=25/y2=26/y2=35/y2=38/y2=39/y2=40/y2=65/y2=66/y2=67/y2=68/y2=69:S1;
!!FU5261&y1>1000:Py1/y3;

------------------------------------------------------------------------

 [Display appropriate right-click text for Living or Regular Spell Scrolls]
!?FU5261;
!!VRy1:Sx1 -1001;
!!UN:N1/1/y1;
!!IF&x2=0:Q1/9/y1/4/z133004;

!!IF&x2=1:Q1/9/y1/4/z133005;

!!CM:R0; [Disable standard right-click info. for Living Scroll]

** End of Script **

